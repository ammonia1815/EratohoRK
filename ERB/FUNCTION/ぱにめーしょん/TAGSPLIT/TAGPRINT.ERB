;-------------------------------------------------------------------------------
;
;	TAGPRINT 함수군
;
;===============================================================================
;	태그를 이용했다색·폰트·스타일·버튼화자유자재인 표시용 함수
;	레이어용으로 만들었지만 별로 레이어가 아니어도 사용할 수 있습니다
;	CALL TAGPRINT(LOCALS)
;	CALL TAGPRINTL(LOCALS)
;	CALL TAGPRINTW(LOCALS)와 직접 두드려
;-------------------------------------------------------------------------------
;	■단락 문자
;	  디폴트로"#"입니다만 ARGS:1으로 변경이 가능한 것으로, 보기 쉬운 것을 사용해 주세요
;	  이하"#"로 설명합니다
;
;	  블록Ａ#← 여기 →#블록Ｃ  ※왼쪽의 예의 단락 문자로 단락지어진 부분을 1 블록
;
;	  이 블록내에서 폰트를 바꾸거나 색을 바꾸거나 하고 있습니다
;	  블록은 1문자로부터 가능해, 1행 당 100 블록까지 사용 가능
;-------------------------------------------------------------------------------
;	■캐릭터 라인의 사양은 태그 방식
;	  <COLOR_0xFFFF00><FONT_나눔명조><BUTTON_이로하>, 색은 로#
;	  이 예에서는 "색은 로"를 세키색·폰트 내일 아침·버튼치 "이로하"로 표시합니다
;
;	■이용할 수 있는 태그
;		<COLOR_XX>	<색_XX> 			캐릭터 라인의 색을 바꾸어 표시, 프롭퍼티는 16진RGB(덧붙여 씀 하면 단축 가능)
;										기초로려했다 있고 때는<COLOR><색>등 프롭퍼티 없음으로 선언
;		<FONT_XX>	<폰트 _XX> 		캐릭터 라인의 폰트를 바꾸어 표시, 프롭퍼티는 폰트명(덧붙여 씀 하면 단축 가능)
;										기초로려했다 있고 때는<FONT><폰트>등 프롭퍼티 없음으로 선언
;		<STYLE_XX>	<스타일 _XX>		캐릭터 라인의 스타일 0～0 x0F의 비트 적산( 1 =굵은 글씨  2 =이탤릭  4 =부정선  8 =밑줄)
;										기초로려했다 있고 때는<STYLE><스타일>등 프롭퍼티 없음으로 선언
;		<BUTTON_XX>	<버튼 _XX>			캐릭터 라인의 버튼화, 프롭퍼티는 돌아가 값(RESULTS)
;
;	·프롭퍼티의 XX는 캐릭터 라인, Y는 수치
;	·일영 표기를 억지로 양대응시켰다의 것으로, 동일 블록내에서 동시에 사용하지 말아줘
;	·태그의 순번은 적당해 OK, 당연 사용하지 않는 것은 생략해 OK
;	·색은 로,<COLOR_0xFFFF00><FONT_나눔명조><BUTTON_이로하>#  이런 형식도 가능
;	  대응시킨다에는 SYSTEM_TAGSPLIT.ERB의 @TAGSPLIT_TAGEND() 함수의 돌아가 값을 0에서 1으로 바꾸어 주세요
;-------------------------------------------------------------------------------
;	■색과 폰트는 단축 표기가 가능
;	  <색_세키>라든지<FONT_내일 아침>이라든지 하고 싶으면 이 파일의 하부에 있다
;	  AUTOSETCOLOR/FONT에 추가해 이용이군요가 있습니다
;===============================================================================
@TAGPRINT(ARGS, ARGS:1)
#LOCALSIZE 4
#LOCALSSIZE 2
#DIM LCOUNT,1
SIF ARGS == ""
	RETURN RESULT
VARSET LOCAL
; RESULT 퇴피
LOCAL = RESULT, RESULT:1, RESULT:2
; 단락 문자를 세트
CALL SET_NEST_SEPARATOR(ARGS:1)
; 블록 마다 분할
SPLIT ARGS, NEST_SEPARATOR, TAGPRINT_NEST
; 1 블록씩
FOR LCOUNT , 0 , RESULT
	; 하늘이라면 아무것도 하지 않는다
	SIF TAGPRINT_NEST:LCOUNT == ""
		CONTINUE
	; 캐릭터 라인 세트
	LOCALS = %TAGSPLIT_ONE(TAGPRINT_NEST:LCOUNT)%
	; 색태그
	LOCALS:1 = %TAGSPLIT_ONE(,"COLOR") + TAGSPLIT_ONE(,"색")%
	SIF LOCALS:1 != ""
		CALL AUTOSETCOLOR( LOCALS:1 )
	; 폰트 태그
	LOCALS:1 = %TAGSPLIT_ONE(,"FONT") + TAGSPLIT_ONE(,"폰트")%
	SIF LOCALS:1 != ""
		CALL AUTOSETFONT( LOCALS:1 )
	; 스타일 태그
	LOCALS:1 = %TAGSPLIT_ONE(,"STYLE") + TAGSPLIT_ONE(,"스타일")%
	SIF LOCALS:1 != ""
		CALL AUTOSETSTYLE( LOCALS:1 )
	; 버튼 태그
	LOCALS:1 = %TAGSPLIT_ONE(,"BUTTON") + TAGSPLIT_ONE(,"버튼")%
	IF LOCALS:1 != ""
;OriginalString : LOCALS, LOCALS:1
		PRINTBUTTON LOCALS, LOCALS:1
	ELSE
;OriginalString : %LOCALS%
		PRINTFORM %LOCALS%
	ENDIF
	CALL AUTOSETCOLOR_SYS(-1)
	CALL AUTOSETFONT( "RESET" )		;	무게의 5할 이상은 이 녀석이다아
	CALL AUTOSETSTYLE( "RESET" )
NEXT
RESULT = LOCAL, LOCAL:1, LOCAL:2
RETURN RESULT

@TAGPRINTL(ARGS,ARGS:1)
CALL TAGPRINT(ARGS,ARGS:1)
;OriginalString : 
PRINTL 
RETURN RESULT

@TAGPRINTW(ARGS,ARGS:1)
CALL TAGPRINT(ARGS,ARGS:1)
WAITANYKEY
RETURN RESULT

@SET_NEST_SEPARATOR(ARGS)
IF (NEST_SEPARATOR != "" ) && (ARGS != "")
	NEST_SEPARATOR = %ARGS%
ELSE
	NEST_SEPARATOR = #
ENDIF
RETURN RESULT

;-------------------------------------------------------------------------------
;	TAGPRINT로 사용했다오제키수
;-------------------------------------------------------------------------------
;	AUTOSETCOLOR(ARGS) 
;	ARGS에 던져 넣은 캐릭터 라인을 해석해 SETCOLOR, RESET로 색을 되돌린다.
;-------------------------------------------------------------------------------
;	AUTOSETFONT(ARGS)
;	ARGS에 던져 넣은 캐릭터 라인을 해석해 SETFONT, RESET로 폰트 되돌린다.
;-------------------------------------------------------------------------------
;===============================================================================
@AUTOSETCOLOR(ARGS = "RESET")
#LOCALSIZE 1
LOCAL = RESULT
IF ISNUMERIC(ARGS)
	CALL AUTOSETCOLOR_SYS(TOINT(ARGS))
ELSE
	SELECTCASE ARGS
		CASE "","RESET","COLOR","색"		; 초기화
			CALL AUTOSETCOLOR_SYS(-1)
		CASE "빨강"							; --- "빨강"라든지 하고 싶은 경우 이런 느낌으로 추가 할 것  ---
			CALL AUTOSETCOLOR_SYS(0xFF0000)
		CASE "초록"
			CALL AUTOSETCOLOR_SYS(0x00FF00)
		CASE "파랑"
			CALL AUTOSETCOLOR_SYS(0x0000FF)
		CASE "노랑"
			CALL AUTOSETCOLOR_SYS(0xFFFF00)
		CASE "수"
			CALL AUTOSETCOLOR_SYS(0x00FFFF)
		CASE "유카리"
			CALL AUTOSETCOLOR_SYS(0xFF00FF)
	ENDSELECT
ENDIF
RETURN RESULT

;-------------------------------------------------------------------------------
@AUTOSETCOLOR_SYS(ARG = -1)
#DIM LCOLOR,1
#LOCALSIZE 1
SELECTCASE ARG
	CASE 0 TO 0xFFFFFF						; 현재색퇴피했다상에서 변경
		LCOLOR = GETCOLOR()
		SETCOLOR ARG
		LOCAL = 1
	CASE -1									; 인수 없음으로 되돌린다
		IF LOCAL == 1
			SIF LCOLOR == GETDEFBGCOLOR()	; 만일 최초로-1으로 불러 버렸을 경우 시커멓게 되기 (위해)때문에 피해 둔다
				LCOLOR = GETDEFCOLOR()
			;SIF GETCOLOR() != LCOLOR
				SETCOLOR LCOLOR
			LOCAL = 0
		ENDIF
ENDSELECT


;-------------------------------------------------------------------------------
;===============================================================================
@AUTOSETFONT(ARGS = "RESET")
#LOCALSIZE 2
#DIMS LFONTS,1
LOCAL = RESULT
IF GROUPMATCH(ARGS, "","RESET","FONT","폰트")
	; 변경되어 있지 않으면 즉돌아가기
	IF LOCAL:1 == 0
		RETURN LOCAL
	ELSE
		SIF CHKFONT(LFONTS)
			SETFONT LFONTS
		LOCAL:1 = 0
	ENDIF
ELSE
	$RESTART
	SELECTCASE ARGS
		CASE "고딕"						; 단축시켰다 있고 경우 이런 느끼고로 추가 할 것
			ARGS = "나눔바른고딕"
			GOTO RESTART
		CASE "내일 아침"
			ARGS = "나눔명조"
			GOTO RESTART
		CASEELSE
			IF CHKFONT(ARGS)
				LFONTS = %GETFONT()%
				SETFONT ARGS
				LOCAL:1 = 1
			ENDIF
	ENDSELECT
ENDIF
RETURN LOCAL


;-------------------------------------------------------------------------------
;===============================================================================
@AUTOSETSTYLE(ARGS = "RESET")
#LOCALSIZE 2
#DIM LSTYLE,1
LOCAL = RESULT
IF GROUPMATCH(ARGS,"","RESET","STYLE","스타일")
	IF LOCAL:1 == 0
		RETURN LOCAL
	ELSE
		FONTSTYLE LSTYLE
		LOCAL:1 = 0
	ENDIF
ELSEIF ISNUMERIC(ARGS)
	LSTYLE = GETSTYLE()
	FONTSTYLE TOINT(ARGS)
	LOCAL:1 = 1
ENDIF
RETURN LOCAL
