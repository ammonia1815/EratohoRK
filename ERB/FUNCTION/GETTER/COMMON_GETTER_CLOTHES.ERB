;==================================================
;
;	범용 함수 두는 곳＠참조편
;		참조에 이용해 특정의 수치나 명칭 따위를반환의
;		혹은, 상황에 따른 접두어 따위를반환의
;
;		구상전용의 것도 있거나 않기도 하고.
;
;==================================================

;==================================================
;
;	의상관련의 참조
;
;==================================================

;-------------------------------------------------
;함수명:CLOTHES
;개  요점:복장명취득함수(종합)
;인  수:ARG:0… 지정 부위
;　　　:ARG:1… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:2… 생략가능. 진정한 경우, 단축명을반환
;　　　:ARG:3… 생략가능. 진정한 경우, 상하겉옷판정시 입지 않으면전신겉옷의 이름을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 지정 부위 복장의 이름을반환
;-------------------------------------------------
@CLOTHES(ARG:0, ARG:1, ARG:2 = 0, ARG:3 = 0)
#FUNCTIONS
SELECTCASE ARG:0
	;특수 장식품
	CASE 0
		RETURNF GLOVES(ARG:1, ARG:2)
	;신발안
	CASE 1
		RETURNF SOCKS(ARG:1, ARG:2)
	;하반신속옷
	CASE 2
		RETURNF INNER_B(ARG:1, ARG:2)
	;상반신속옷
	CASE 3
		RETURNF INNER_T(ARG:1, ARG:2)
	;하반신겉옷
	CASE 4
		RETURNF OUTER_B(ARG:1, ARG:2, ARG:3)
	;상반신겉옷
	CASE 5
		RETURNF OUTER_T(ARG:1, ARG:2, ARG:3)
	;전신겉옷
	CASE 6
		RETURNF DRESS(ARG:1, ARG:2)
	;커스텀의상
	CASE 9
		RETURNF COSPLAY(ARG:1, ARG:2)
ENDSELECT

;-------------------------------------------------
;함수명:COSPLAY
;개  요점:복장명취득함수(커스텀의상)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 커스텀의상의 이름을반환
;-------------------------------------------------
@COSPLAY(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE EQUIP:ARG:코스프레
	CASE 1
		RETURNF "노예복"
	CASE 2
		RETURNF "기모노"
	CASE 3
		RETURNF "메이드복"
	CASE 4
		RETURNF "중국의류"
	CASE 5
		RETURNF "세라복"
	CASE 6
		RETURNF "스쿠미즈"
	CASE 7
		RETURNF "바니걸복"
	CASE 8
		RETURNF "체육복"
ENDSELECT

;-------------------------------------------------
;함수명:GLOVES
;개  요점:복장명취득함수(특수)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 특수 장식품의 이름을반환
;-------------------------------------------------
@GLOVES(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE EQUIP:ARG:악세서리
	;장갑
	CASE 1
		RETURNF "장갑"
	;소매
	CASE 2
		RETURNF "소매"
	;우의
	CASE 3
		RETURNF "우의"
ENDSELECT

;-------------------------------------------------
;함수명:SOCKS
;개  요점:복장명취득함수(신발안)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 신발안의 이름을반환
;-------------------------------------------------
@SOCKS(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE EQUIP:ARG:신발안
	;양말
	CASE 1
		RETURNF  \@ ARG:1 ? 신발안 # 양말 \@
	;니삭스
	CASE 2
		RETURNF  \@ ARG:1 ? 니삭스 # 니삭스 \@
	;가터벨트
	CASE 3
		RETURNF  \@ ARG:1 ? 가터 # 가터벨트 \@
	;버선
	CASE 4
		RETURNF "버선"
ENDSELECT

;-------------------------------------------------
;함수명:INNER_B
;개  요점:복장명취득함수(하반신속옷)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 하반신속옷의 이름을반환
;-------------------------------------------------
@INNER_B(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE EQUIP:ARG:하반신속옷
	;드로와즈
	CASE 1
		RETURNF \@ ARG:1 ? 드로와 # 드로와즈 \@
	;팬티
	CASE 2
		RETURNF \@ ARG:1 ? 팬츠 # 팬티 \@
	;트렁크
	CASE 3
		RETURNF "트렁크"
	;팬티스타킹
	CASE 4
		RETURNF \@ ARG:1 ? 팬티스타킹 # 팬티스타킹 \@
	;곤
	CASE 5
		RETURNF "곤"
ENDSELECT

;-------------------------------------------------
;함수명:INNER_T
;개  요점:복장명취득함수(상반신속옷)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 상반신속옷의 이름을반환
;-------------------------------------------------
@INNER_T(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE EQUIP:ARG:상반신속옷
	;브래지어
	CASE 1
		RETURNF \@ ARG:1 ? 브라 # 브래지어 \@
	;캐미숄
	CASE 2
		RETURNF \@ ARG:1 ? 캐미 # 캐미숄 \@
	;붕대
	CASE 3
		RETURNF "붕대"
	;Ｔ셔츠
	CASE 4
		RETURNF "Ｔ셔츠"
ENDSELECT

;-------------------------------------------------
;함수명:OUTER_B
;개  요점:복장명취득함수(하반신겉옷)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;      :ARG:2… 생략가능. 진정한 경우, 입지 않으면전신겉옷의 이름을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 상반신속옷의 이름을반환
;-------------------------------------------------
@OUTER_B(ARG:0, ARG:1, ARG:2)
#FUNCTIONS
SELECTCASE EQUIP:ARG:하반신겉옷
	;스커트
	CASE 1
		RETURNF "스커트"
	;치마바지
	CASE 2
		RETURNF "치마바지"
	;바지
	CASE 3
		RETURNF "바지"
	;하카마
	CASE 4
		RETURNF "하카마"
	;없음
	CASE 0
		RETURNF \@ ARG:2 ? %DRESS(ARG:0, ARG:1)% # \@
ENDSELECT

;-------------------------------------------------
;함수명:OUTER_T
;개  요점:복장명취득함수(상반신겉옷)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;      :ARG:2… 생략가능. 진정한 경우, 입지 않으면전신겉옷의 이름을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 상반신겉옷의 이름을반환
;-------------------------------------------------
@OUTER_T(ARG:0, ARG:1, ARG:2)
#FUNCTIONS
SELECTCASE EQUIP:ARG:상반신겉옷
	;겉옷
	CASE 1
		RETURNF "겉옷"
	;없음
	CASE 0
		RETURNF \@ ARG:2 ? %DRESS(ARG:0, ARG:1)% # \@
ENDSELECT

;-------------------------------------------------
;함수명:DRESS
;개  요점:복장명취득함수(전신겉옷)
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET)
;　　　:ARG:1… 생략가능. 진정한 경우, 단축명을반환
;반환값:복장명(캐릭터 라인)
;비  고:식 안의 함수
;캐릭터등록번호로부터, 전신겉옷의 이름을반환
;-------------------------------------------------
@DRESS(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE EQUIP:ARG:전신겉옷
	;기모노
	CASE 1
		RETURNF "기모노"
	;도사복
	CASE 2
		RETURNF "도사복"
	;원피스
	CASE 3
		RETURNF  \@ ARG:1 ? 원피 # 원피스 \@
ENDSELECT

;-------------------------------------------------
;방해 복장 함수@CLOTHESTYPE
;식중에서 사용하는 함수입니다. 강도라고 말해도 찢을 것은 아니고, 어느 정도 행위의 방해가 될까를 가리킵니다.
;인수 1이 0이라면하반신속옷
;반환값	1=리 있고하며 Ｖ성교 할 수 있다				(팬티/코스프레)
;		2=신고 있으면삽입 하기 어렵다			(드로와즈/트렁크/팬티스타킹/곤)
;			·팬츠않고들 쉿이라고 털을 말려들게 해 아프지요. 스치고
;			·그리고, 팬티에서도 되고는 무리입니다. 그것은 그렇지만
;인수가 1이라면하반신겉옷
;반환값	1=가랑이 부분이 없고, 거의 방해가 되지 않는다	(스커트/원피스/기모노/도사복/코스프레)
;		2=방해가 된다						(바지/치마바지)
;			·뭐, 보통으로 생각해스커트에서도 방해입니다만.
;			·야마메짱의 원피 정도 밖에 보이지 않지만, 타이트스커트 어떻게 하지
;인수가 2라면상반신속옷
;반환값	1=부드럽게 만지작거리기 쉽다				(캐미숄/Ｔ셔츠/코스프레)
;		2=만지작거리기 어렵다						(브래지어/붕대)
;			·캐미 껴에 동글동글 하는 것 일어나고 가져 좋습니다
;인수가 3이라면상반신겉옷
;반환값	1=옷자락으로부터손가 들어간다					(겉옷/기모노/코스프레)
;		2=손가 들어가지 않는다						(도사복/원피스)
;			·겉옷는 전이 열리지 않고소매가 좁은 것도 있지만, 옷자락을 걸으면 만인 좋은 찬다고 판단
;모두, 반환값 0이라고 입지 않은 것을 나타낸다
;인수 0은 0이라면 TARGET, 1이라면 MASTER
;

;↓코스프레는미구현입니다만 처리는 남기고 있습니다 by.

;코스프레는 의견의 나뉘는 곳이라고 생각합니다만…
;일부러 만지작거리기 어려운 옷 입히고 나서 만지작거린다는 것은, 개인적으로는 조금 합이성에 빠뜨릴 생각이했다의 것으로…
;(합이성로 결론지을 수 없는 면이 있는 것은 거듭거듭 감안하고 가)
;반드시 만지작거리기 쉬운 커스텀의상야라고 하는 일로 합니다. 특히노예복는.
;이론은 받아들입니다.
;-------------------------------------------------
@CLOTHESTYPE(ARG, ARG:1)
#FUNCTION
;인수가 정상적이지 않은 경우는-1을반환
SIF ARG < 0 || 1 < ARG || ARG:1 < 0 || 3 < ARG:1
	RETURNF -1
SELECTCASE ARG:1
	;하반신속옷
	CASE 0
		RETURNF LOWERUNDER(ARG)
	;하반신겉옷
	CASE 1
		RETURNF LOWERJACKET(ARG)
	;상반신속옷
	CASE 2
		RETURNF UPPERUNDER(ARG)
	;상반신겉옷
	CASE 3
		RETURNF UPPERJACKET(ARG)
ENDSELECT

;-------------------------------------------------
;하반신속옷함수@LOWERUNDER
;반환값	0=입지 않았다
;		1=리 있고하며 Ｖ성교 할 수 있다				(팬티/코스프레)
;		2=신고 있으면삽입 하기 어렵다			(드로와즈/트렁크/팬티스타킹/곤)
;인수는 0이라면 TARGET, 1이라면 MASTER
;-------------------------------------------------
@LOWERUNDER(ARG)
#FUNCTION
#LOCALSIZE 1
LOCAL = ARG ? MASTER # TARGET
;인수가 정상적이지 않은 경우는-1을반환
SIF ARG < 0 || 1 < ARG
	RETURNF -1
;코스프레
SELECTCASE TEQUIP:LOCAL:코스프레
	;노예복
	CASE 1
		RETURNF 0
	;그 외
	CASE IS != 0
		RETURNF 1
ENDSELECT
;팬티만 1으로 돌려준다
SIF TEQUIP:LOCAL:하반신속옷 == 2
	RETURNF 1
;외를 입고 있으면 2, 없으면 0
RETURNF TEQUIP:LOCAL:하반신속옷 ? 2 # 0

;-------------------------------------------------
;상반신속옷함수@UPPERUNDER
;반환값	0=입지 않았다
;		1=부드럽게 만지작거리기 쉽다				(캐미숄/Ｔ셔츠/코스프레)
;		2=만지작거리기 어렵다						(브래지어)
;인수는 0이라면 TARGET, 1이라면 MASTER
;-------------------------------------------------
@UPPERUNDER(ARG)
#FUNCTION
#LOCALSIZE 1
LOCAL = ARG ? MASTER # TARGET
;인수가 정상적이지 않은 경우는-1을반환
SIF ARG < 0 || 1 < ARG
	RETURNF -1
;코스프레
SELECTCASE TEQUIP:LOCAL:코스프레
	;노예복
	CASE 1
		RETURNF 0
	;그 외
	CASE IS != 0
		RETURNF 1
ENDSELECT
;캐미와Ｔ셔츠는 1으로 돌려준다
SIF TEQUIP:LOCAL:상반신속옷 == 2 || TEQUIP:LOCAL:상반신속옷 == 4
	RETURNF 1
;상반신속옷
RETURNF TEQUIP:LOCAL:상반신속옷 ? 2 # 0

;-------------------------------------------------
;하반신겉옷함수@LOWERJACKET
;반환값	0=입지 않았다
;		1=가랑이 부분이 없고, 거의 방해가 되지 않는다	(스커트/원피스/기모노/도사복)
;		2=방해가 된다						(바지, 하카마)
;인수는 0이라면 TARGET, 1이라면 MASTER
;-------------------------------------------------
@LOWERJACKET(ARG)
#FUNCTION
#LOCALSIZE 1
LOCAL = ARG ? MASTER # TARGET
;인수가 정상적이지 않은 경우는-1을반환
SIF ARG < 0 || 1 < ARG
	RETURNF -1
;코스프레
SELECTCASE TEQUIP:LOCAL:코스프레
	;노예복
	CASE 1
		RETURNF 0
	;그 외
	CASE IS != 0
		RETURNF 1
ENDSELECT
;바지와 하카마는 2로 돌려준다
SIF TEQUIP:LOCAL:하반신겉옷 == 3 || TEQUIP:LOCAL:하반신겉옷 == 4
	RETURNF 2
;다른하반신겉옷
SIF TEQUIP:LOCAL:하반신겉옷
	RETURNF 1
;전신겉옷
SIF TEQUIP:LOCAL:전신겉옷
	RETURNF 1
;입지 않았다
RETURNF 0

;-------------------------------------------------
;상반신겉옷함수@UPPERJACKET
;반환값	0=입지 않았다
;		1=옷자락 따위로부터손가 들어간다				(겉옷/기모노/코스프레)
;		2=손가 들어가지 않는다						(원피스/도사복)
;인수는 0이라면 TARGET, 1이라면 MASTER
;-------------------------------------------------
@UPPERJACKET(ARG)
#FUNCTION
#LOCALSIZE 1
LOCAL = ARG ? MASTER # TARGET
;인수가 정상적이지 않은 경우는-1을반환
SIF ARG < 0 || 1 < ARG
	RETURNF -1
;코스프레
SELECTCASE TEQUIP:LOCAL:코스프레
	;노예복
	CASE 1
		RETURNF 0
	;그 외
	CASE IS != 0
		RETURNF 1
ENDSELECT
;기모노만 1으로 돌려준다
SIF TEQUIP:LOCAL:전신겉옷 == 1
	RETURNF 1
;다른전신겉옷
SIF TEQUIP:LOCAL:전신겉옷
	RETURNF 2
;상반신겉옷
RETURNF TEQUIP:LOCAL:상반신겉옷 ? 1 # 0

;-------------------------------------------------
;하반신복장 판정 함수@PANTSCHECK
;식중에서 사용하는 함수입니다. 하반신의 복장이 현재 어떻게 되어 있는지 판정합니다
;「입고 있을까」는 아니고 「애무의 방해가 될까」을반환의 것으로, 예를 들어스커트는 신어도 through됩니다
;반환값은 0이라면 손대어 마음껏, 1이라면속옷, 2라면겉옷, 3이라면 양쪽 모두가 방해를 합니다.
;인수는 0이라면 TARGET, 1이라면 MASTER. 인수 1을 정말로 하면 방해하지 않아도 판정에 넣습니다
;-------------------------------------------------
@PANTSCHECK(ARG, ARG:1)
#FUNCTION
#LOCALSIZE 1
;인수가 정상적이지 않은 경우는-1을반환
SIF ARG < 0 || 1 < ARG
	RETURNF -1
ARG:1 = ARG:1 ? 1 # 0
LOCAL = 0
LOCAL |= LOWERUNDER(ARG) >= (2 - ARG:1) ? 1 # 0
LOCAL |= LOWERJACKET(ARG) >= (2 - ARG:1) ? 2 # 0
RETURNF LOCAL

;-------------------------------------------------
;상반신복장 판정 함수@BRACHECK
;식중에서 사용하는 함수입니다. 상반신의 복장이 현재 어떻게 되어 있는지 판정합니다
;역시 방해가 되지 않는 것은 through됩니다
;반환값은 0이라면 손대어 마음껏, 1이라면속옷, 2라면겉옷, 3이라면 양쪽 모두가 방해를 합니다.
;인수는 0이라면 TARGET, 1이라면 MASTER. 인수 1을 정말로 하면 방해하지 않아도 판정에 넣습니다
;-------------------------------------------------
@BRACHECK(ARG, ARG:1)
#FUNCTION
#LOCALSIZE 1
;인수가 정상적이지 않은 경우는-1을반환
SIF ARG < 0 || 1 < ARG
	RETURNF -1
LOCAL = 0
LOCAL |= UPPERUNDER(ARG) >= (2 - ARG:1) ? 1 # 0
LOCAL |= UPPERJACKET(ARG) >= (2 - ARG:1) ? 2 # 0
RETURNF LOCAL

;-------------------------------------------------
;전라판정 함수@ZENRACHECK
;식중에서 사용하는 함수입니다. 탈의액션구상에서만 사용합니다
;이탈의로전라가 될지 어떨지를 판정해, 되는 경우만 1을 돌려줍니다.
;인수는 0이라면 TARGET, 1이라면 MASTER
;-------------------------------------------------
@ZENRACHECK(ARG)
#FUNCTION
#LOCALSIZE 1
LOCAL = ARG ? MASTER # TARGET
;인수가 정상적이지 않은 경우는 0을반환
SIF ARG < 0 || 1 < ARG || (!ARG && ISSWAP())
	RETURNF 0
;탈의실패
SIF ARG && TFLAG:강제탈의 < 0
	RETURNF 0
SELECTCASE ARG ? TFLAG:강제탈의 # TFLAG:자주탈의
	;상반신겉옷
	CASE 1
		RETURNF !(TEQUIP:LOCAL:악세서리 || TEQUIP:LOCAL:신발안 || TEQUIP:LOCAL:하반신속옷 || TEQUIP:LOCAL:상반신속옷 || TEQUIP:LOCAL:하반신겉옷)
	;하반신겉옷
	CASE 2
		RETURNF !(TEQUIP:LOCAL:악세서리 || TEQUIP:LOCAL:신발안 || TEQUIP:LOCAL:하반신속옷 || TEQUIP:LOCAL:상반신속옷 || TEQUIP:LOCAL:상반신겉옷)
	;상＋하겉옷
	CASE 3
		RETURNF !(TEQUIP:LOCAL:악세서리 || TEQUIP:LOCAL:신발안 || TEQUIP:LOCAL:하반신속옷 || TEQUIP:LOCAL:상반신속옷)
	;상반신전부
	CASE 4
		RETURNF !(TEQUIP:LOCAL:악세서리 || TEQUIP:LOCAL:신발안 || TEQUIP:LOCAL:하반신속옷 || TEQUIP:LOCAL:하반신겉옷)
	;하반신전부
	CASE 5
		RETURNF !(TEQUIP:LOCAL:악세서리 || TEQUIP:LOCAL:신발안 || TEQUIP:LOCAL:상반신속옷 || TEQUIP:LOCAL:상반신겉옷)
	;악세서리─
	CASE 6
		RETURNF !(TEQUIP:LOCAL:하반신속옷 || TEQUIP:LOCAL:상반신속옷 || TEQUIP:LOCAL:하반신겉옷 || TEQUIP:LOCAL:상반신겉옷 || TEQUIP:LOCAL:전신겉옷)
	;악세서리－까지
	CASE 7
		RETURNF TEQUIP:LOCAL:악세서리 !| TEQUIP:LOCAL:신발안
	;전라까지
	CASE 8
		RETURNF 1
	;속옷만
	CASE 9
		RETURNF !(TEQUIP:LOCAL:악세서리 || TEQUIP:LOCAL:신발안 || TEQUIP:LOCAL:하반신겉옷 || TEQUIP:LOCAL:상반신겉옷 || TEQUIP:LOCAL:전신겉옷)
ENDSELECT
RETURNF 0

;-------------------------------------------------
;가슴 보이고 판정 함수@B_DATUICHECK
;식중에서 사용하는 함수입니다. 탈의액션구상에서만 사용합니다
;이탈의로가슴를 보일지 어떨지를 판정해, 브라까지 보이는 경우는 2, 말하는 p의 경우는 3을 돌려줍니다
;최초부터가슴가 보이고 있는 경우는 1이 됩니다
;인수는 0이라면 TARGET, 1이라면 MASTER
;-------------------------------------------------
@B_DATUICHECK(ARG)
#FUNCTION
#LOCALSIZE 1
LOCAL = ARG ? MASTER # TARGET

;인수가 정상적이지 않은 경우는 0을반환
SIF ARG < 0 || 1 < ARG || (!ARG && ISSWAP())
	RETURNF 0
;탈의실패
SIF ARG && TFLAG:강제탈의 < 0
	RETURNF 0

;최초부터가슴가 보이고 있는 경우
SIF !(TEQUIP:LOCAL:상반신속옷 || TEQUIP:LOCAL:상반신겉옷 || TEQUIP:LOCAL:전신겉옷) || (TEQUIP:착의노출 & (ARG ? 8 # 2))
	RETURNF 1
SELECTCASE ARG ? TFLAG:강제탈의 # TFLAG:자주탈의
	;상반신겉옷
	CASE 1
		RETURNF TEQUIP:LOCAL:상반신속옷 ? 2 # 3
	;상＋하겉옷(상반신겉옷/전신겉옷를 이미 벗고 있는 경우는노출가 끝난 상태인 것으로 스르)
	CASE 3
		SIF TEQUIP:LOCAL:상반신겉옷 || TEQUIP:LOCAL:전신겉옷
			RETURNF TEQUIP:LOCAL:상반신속옷 ? 2 # 3
	;상반신전부/악세서리－까지/전라까지
	CASE 4, 7, 8
		RETURNF 3
	;속옷만(상반신겉옷/전신겉옷를 이미 벗고 있는 경우가 보고 말하는 p)
	CASE 9
		SIF TEQUIP:LOCAL:상반신겉옷 !| TEQUIP:LOCAL:전신겉옷
			RETURNF 3
ENDSELECT
RETURNF 0

;-------------------------------------------------
;성기 보이고 판정 함수@P_DATUICHECK
;식중에서 사용하는 함수입니다. 탈의액션구상에서만 사용합니다
;이탈의로 팬츠·성기를 보일지 어떨지를 판정해, 팬티까지 보이는 경우는 2, 성기까지 보이는 경우는 3을 돌려줍니다
;최초부터성기가 보이고 있는 경우는 1이 됩니다
;인수는 0이라면 TARGET, 1이라면 MASTER
;-------------------------------------------------
@P_DATUICHECK(ARG)
#FUNCTION
#LOCALSIZE 1
LOCAL = ARG ? MASTER # TARGET
;인수가 정상적이지 않은 경우는 0을반환
SIF ARG < 0 || 1 < ARG || (!ARG && ISSWAP())
	RETURNF 0
;탈의실패
SIF ARG && TFLAG:강제탈의 < 0
	RETURNF 0
;최초부터성기가 보이고 있는 경우
SIF !(TEQUIP:LOCAL:하반신속옷 || TEQUIP:LOCAL:하반신겉옷 || TEQUIP:LOCAL:전신겉옷) || (TEQUIP:착의노출 & (ARG ? 4 # 1))
	RETURNF 1
SELECTCASE ARG ? TFLAG:강제탈의 # TFLAG:자주탈의
	;하반신겉옷
	CASE 2
		RETURNF TEQUIP:LOCAL:하반신속옷 ? 2 # 3
	;상＋하겉옷(하반신겉옷/전신겉옷를 이미 벗고 있는 경우는노출가 끝난 상태인 것으로 스르)
	CASE 3
		SIF TEQUIP:LOCAL:하반신겉옷 || TEQUIP:LOCAL:전신겉옷
			RETURNF TEQUIP:LOCAL:하반신속옷 ? 2 # 3
	;하반신전부/악세서리－까지/전라까지
	CASE 5, 7, 8
		RETURNF 3
	;속옷만(하반신겉옷/전신겉옷를 이미 벗고 있는 경우만성기p)
	CASE 9
		SIF TEQUIP:LOCAL:하반신겉옷 !| TEQUIP:LOCAL:전신겉옷
			RETURNF 3
ENDSELECT
RETURNF 0

;-------------------------------------------------
;함수명:CLOTHE_SAVE
;개  요점:탈의용, 복장 보존·취득함수
;인  수:ARG:0… 캐릭터등록번호(MASTER나 TARGET. 생략했다묘한 수치가 들어가면 TARGET)
;　　　:ARG:1… 생략=-1으로 하면 현재의복장을 보존. 수치를 넣으면 보존했다복장과 현재의 복장의 차이을반환.
;반환값:차이가 난다=벗어 착용하고 있으면 정. 일단, 벗으면 1, 입고 있으면 2로 돌려준다.
;비  고:식 안의 함수
;보존해 둔 복장과 현재의 복장의 차이을반환. MASTER와 TARGET에 밖에 대응하고 있지 않다
;-------------------------------------------------
@CLOTHE_SAVE(ARG:0, ARG:1 = -1)
#FUNCTION
#LOCALSIZE 21
#DIM LCOUNT, 1
IF ARG != MASTER
	ARG = TARGET
	LOCAL:20 = 10
ELSE
	ARG = MASTER
	LOCAL:20 = 0
ENDIF

;SAVE
IF ARG:1 < 0
	FOR LCOUNT, 0, 10
		LOCAL:(LCOUNT+LOCAL:20) = TEQUIP:ARG:LCOUNT
	NEXT
	RETURNF 0
;LOAD
ELSEIF LOCAL:(ARG:1+LOCAL:20) == TEQUIP:ARG:(ARG:1)
	RETURNF 0
ELSE
	RETURNF TEQUIP:ARG:(ARG:1) ? 2 # 1
ENDIF
