;---------------------------------------------------------------------
;
;	Module		:FUNCITON.ERB
;	Facility	:편입 함수군
;
;	Licence		:프리
;
;	Modification Data:시스템 전체로 사용되는 편입 함수군
;
;	Edit	Date				Author		ver			Reason
;	001		2012/05/30			kap			X.XX		신규 작성
;	002		2012/07/30			kap			0.300		KEYMARK2종을 추가
;			2012/08/04			kap			0.300		캐릭터 삭제 함수를 추가
;			2012/08/12			kap			0.300		수치보정함수를 추가
;	003		2012/09/08			kap			0.350		SET_STR계를 추가
;	004		2012/10/04			kap			0.400		IS_EXIST_HERE를 추가
;								kap			0.400		GET_CNUM를 추가
;	005		2012/10/04			kap			0.400		FINDCHARA_ID와 GET_ID를 추가
;	006		2012/10/26			kap			0.420		FRIEND 캐릭터 생성시에 탤런트와체력·기력의 현재지를 조정하는 조치를 추가
;														@ADD_FRIEND_POINT를 추가
;	007		2012/12/01			Ris			0.420		@SET_STRF의 정의를#FUNCTION로부터#FUNCTIONS에 수정
;	008		2013/09/02			kap			0.480		@MAXWIDTH를 추가
;	009		2013/12/10			kap			0.510		@ADD_FREIND에 채집 장소의 초기치의 처리를 추가
;---------------------------------------------------------------------
;@ADD_LINE			행 수추가 함수
;@EQUATE_LINE		행 수추가 함수
;@SHOW_BLANK		화면 암전 함수
;@MAXLINE			최대행수정의 함수
;@MAXWIDTH			최대폭정의 함수
;@PRINTSBUTTON_C	물이 들어 버튼 작성 함수
;@CALC				보정계산의 기본 함수
;@EQUALCHECK_TURN	등치 체크 함수
;@GET_KEYMARK		키보드의 줄 순서의 알파벳을 수치로부터 취한다
;@GET_KEYMARK_NUM	키보드의 줄 순서의 알파벳을 수치에 고친다
;@SET_STR			STR 세트 함수
;@SET_STRF			STR 세트 함수의 식 안의 함수판
;@GET_STR			STR취득함수
;@IS_EXIST_HERE		존재 판정 함수
;@GET_CNUM			NICKNAME가 ARGS의 CHARA의등록번호을반환
;@ADD FRIEND		프렌드 캐릭터의 생성 함수
;@GAIN_FRIEND_POINT	교우치의 가산 함수
;@GET_FRIEND_POINT	교우치의 표시 함수(실제의 1/100)
;@SET_ID			ID의 설정 함수
;@FINDCHARA_ID		ID가 ARGS의 CHARA의등록번호을반환
;@GET_ID			대상의 캐릭터의 ID을반환
;----------------------------------------------------------
;@DELETE_CHARA
;캐릭터 삭제 함수
;캐릭터 삭제와 거기에 부수 하는 여러가지 처리를 실시한다
;ARG	삭제하는 캐릭터의등록번호
;----------------------------------------------------------
@DELETE_CHARA, ARG
IF TARGET == ARG
	TARGET = -1
ELSEIF TARGET > ARG
	TARGET -= 1
ENDIF
IF ASSI == ARG
	ASSI = -1
ELSEIF ASSI > ARG
	ASSI -= 1
ENDIF
DELCHARA ARG

;----------------------------------------------------------
;ARG의 수만큼 라인을 추가
;----------------------------------------------------------
@ADD_LINE, ARG
FOR LOCAL, 0, ARG
;OriginalString : 
	PRINTL 
NEXT

;----------------------------------------------------------
;ARG행이 될 때까지 라인을 추가
;----------------------------------------------------------
@EQUATE_LINE, ARG
WHILE LINECOUNT < ARG
;OriginalString : 
	PRINTL 
WEND

;-------------------------------------------------
;화면을 암전시킨다
;ARG		암전하고 있는시간
;-------------------------------------------------
@SHOW_BLANK, ARG = 100
#LOCALSIZE 2
LOCAL = CURRENTREDRAW()
LOCAL:1 = LINECOUNT
REDRAW 0
CALL ADD_LINE(50)
TWAIT ARG, 0
REDRAW LOCAL
CLEARLINE LINECOUNT - LOCAL:1

;-------------------------------------------------
;최대행수의 정의 함수
;-------------------------------------------------
@MAXLINE
#FUNCTION
RETURNF 51

;-------------------------------------------------
;최대폭의 정의 함수
;-------------------------------------------------
@MAXWIDTH
#FUNCTION
RETURNF STRLENS(DRAWLINESTR)

;-------------------------------------------------
;표시
;버튼 내용
;조건
;조건(ARG)이 진정한 때의 색
;조건(ARG:1)이 반대때의 색
;-------------------------------------------------
@PRINTSBUTTON_C, ARGS, ARGS:1, ARG, ARG:1 = -1, ARG:2 = -1
#LOCALSIZE 1
LOCAL = GETCOLOR()
ARG:1 = ARG:1 == -1 ? GETDEFCOLOR() # ARG:1
ARG:2 = ARG:2 == -1 ? GETDEFCOLOR() # ARG:2
SETCOLOR ARG ? ARG:1 # ARG:2
;IF IS_WINDOW
;	PRINTBUTTON ARGS, ""
;ELSE
;OriginalString : ARGS, ARGS:1
	PRINTBUTTON ARGS, ARGS:1
;ENDIF
SETCOLOR LOCAL

;-------------------------------------------------
;보정함수@CALCF
;ARG	보정원의 수치
;ARG:1	보정계수
;-------------------------------------------------
@CALCF, ARG, ARG:1
#FUNCTION
RETURNF ARG:1 >= 0 ? ARG * (100 + ARG:1) / 100 # ARG *  100 / (100 - ARG:1)

;-------------------------------------------------
;@SET_STRF
;STR 세트 함수의 식 안의 함수판
;처리의 형편상, 이쪽에서 부르는 것이 기본
;본체는 GET_STR이지만 이름을 바꾸어 알기 쉽게 해 둔다
;-------------------------------------------------
@SET_STRF, ARGS, ARGS:1
#FUNCTIONS
CALLF GET_STR, ARGS, ARGS:1, 1

;-------------------------------------------------
;@GET_STR
;STR취득함수
;ARGS의 STR의 내용을반환
;요컨데 인수로서 변수명을 건네주는데 사용한다
;-------------------------------------------------
@GET_STR, ARGS, ARGS:1, ARG
#FUNCTIONS
SELECTCASE AUTO_SPLIT(ARGS, ":", 0)
	CASE "STR"
		SIF ARG
			STR:AUTO_SPLIT_INT(ARGS, ":", 1) = %ARGS%
		RETURNF STR:AUTO_SPLIT_INT(ARGS, ":", 1)
	CASE "RESULTS"
		SIF ARG
			RESULTS:AUTO_SPLIT_INT(ARGS, ":", 1) = %ARGS%
		RETURNF RESULTS:AUTO_SPLIT_INT(ARGS, ":", 1)
	CASE "CSTR"
		SELECTCASE AUTO_SPLIT(ARGS, ":", 2)
			CASE "TARGET"
				LOCAL = TARGET
			CASE "MASTER"
				LOCAL = MASTER
			CASE "ASSI"
				LOCAL = ASSI
			CASE "PLAYER"
				LOCAL = PLAYER
			CASEELSE
				LOCAL = ISNUMERIC(AUTO_SPLIT(ARGS, ":", 2)) ? AUTO_SPLIT_INT(ARGS, ":", 2) # GETNUM(CSTR, AUTO_SPLIT(ARGS, ":", 2))
		ENDSELECT
		SIF ARG
			CSTR:AUTO_SPLIT_INT(ARGS, ":", 1):LOCAL = %ARGS:1%
		RETURNF CSTR:AUTO_SPLIT_INT(ARGS, ":", 1):LOCAL
	CASE "TSTR"
		LOCAL = ISNUMERIC(AUTO_SPLIT(ARGS, ":", 1)) ? AUTO_SPLIT_INT(ARGS, ":", 1) # GETNUM(TSTR, AUTO_SPLIT(ARGS, ":", 1))
		SIF ARG
			TSTR:LOCAL = %ARGS:1%
		RETURNF TSTR:LOCAL
	CASE "SAVESTR"
		LOCAL = ISNUMERIC(AUTO_SPLIT(ARGS, ":", 1)) ? AUTO_SPLIT_INT(ARGS, ":", 1) # GETNUM(SAVESTR, AUTO_SPLIT(ARGS, ":", 1))
		SIF ARG
			SAVESTR:LOCAL = %ARGS:1%
		RETURNF SAVESTR:LOCAL
	CASE "GLOBALS"
		LOCAL = ISNUMERIC(AUTO_SPLIT(ARGS, ":", 1)) ? AUTO_SPLIT_INT(ARGS, ":", 1) # GETNUM(GLOBALS, AUTO_SPLIT(ARGS, ":", 1))
		SIF ARG
			GLOBALS:LOCAL = %ARGS%
		RETURNF GLOBALS:LOCAL
	CASEELSE
		THROW @GET_STRF의 인수 ARGS(%ARGS%)가이상입니다!
ENDSELECT
