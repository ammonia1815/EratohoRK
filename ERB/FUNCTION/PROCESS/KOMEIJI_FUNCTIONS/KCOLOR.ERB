;-------------------------------------------------
;KCOLOR 함수(R#전용)
;-------------------------------------------------
;·구상내에서 별캐릭터의 대사를 기술할 때의 위화감을 저감 하는목적이며, 이상하지는 않다. 안전합니다(기만)
;·KOJO_COLOR와의 차이는 색치만 돌려주는 식 안의 함수인 것으로, 캐릭터명으로부터 당기므로 없는 캐릭터라도 아무렇지도 않은 일
;·없는 캐릭터분을 온전히 하려고 하면 일일이 상세하게 TRYCCALLFORM KOJO_COLOR_K{NO:ASSI} 녹여 없으면 안 되는 분위기였으므로 어쩔 수 없다
;·구상로부터이벤트함수 손대는 것은 취미가 아닌 것이지만, 본체 동고라면 아무튼 좋은가적인 김
;·그렇게 말한 이유로부터 왠지 모르게 선행 투입적인 분위기
;·테이블의 사양은 정직 미묘한 느낌도 부정할 수 없다
;-------------------------------------------------

;-------------------------------------------------
;함수명:KCOLOR
;개  요점:메인구상색취득함수
;인  수:ARGS:0… 캐릭터명(NAME) 또는 통칭(CALLNAME). 캐릭터 라인으로 캐릭터번호(NO)를 건네주는 일도 가능
;반환값:지정 캐릭터의구상색. 미정도리의 경우는 0 xA0A0A0가 돌아간다
;비  고:식 안의 함수
;지정 캐릭터의구상색(특히 조건이 없는 경우)을취득 한다
;즉일상이벤트중점인
;※타이틀 화면이나로드화면에서는 사용할 수 없는 풍미
;-------------------------------------------------
@KCOLOR(ARGS:0)
#FUNCTION
#LOCALSIZE 1
LOCAL:0 = ISNUMERIC(ARGS:0) ? TOINT(ARGS:0) # GETNUM(RELATION, ARGS:0)
SIF !EXISTCSV(LOCAL:0)
	THROW @KCOLOR로 예외가 발생 섬했다. 존재하지 않는 캐릭터명 혹은 캐릭터번호입니다（%ARGS:0%）
RETURNF _KCOLOR_TBL(LOCAL:0)


;-------------------------------------------------
;함수명:_LOAD_KCOLOR
;개  요점:메인구상색주사 함수
;인  수:없음
;반환값:없음(RESULT 보호)
;비  고:내부 함수
;내부 함수인 것으로 다른 장소로부터 호출해서는 안 된다. 좋다?
;확실히 사용하려면 요소 요소에서 부르지 않으면 안 되는 것 같지만,
;우선 이하의 2 지점에서 불러 두면 대개는 커버 할 수 있을 것 같은 분위기
;KCOLOR 호출시에 테이블 없으면 만드는 방식은 TRYCCALLFORM 사용하는 형편상 식 안의 함수화 할 수 없게 되므로 각하했다
;-------------------------------------------------
@_LOAD_KCOLOR
#LOCALSIZE 3
LOCAL:0 = RESULT
LOCAL:1 = GETCOLOR()
LOCAL:2 = 0
WHILE EXISTCSV(LOCAL:2)
	TRYCCALLFORM KOJO_COLOR_K{LOCAL:2}
		CALLF _KCOLOR_TBL(LOCAL:2++, GETCOLOR())
	CATCH
		CALLF _KCOLOR_TBL(LOCAL:2++, 0xA0A0A0)	;미정도리의 경우는 디폴트색
	ENDCATCH
WEND
SETCOLOR LOCAL:1
RETURN LOCAL:0

@EVENTFIRST
#PRI
SIF !KCOLOR("0")
	CALL _LOAD_KCOLOR()

@EVENTLOAD
#PRI
SIF !KCOLOR("0")
	CALL _LOAD_KCOLOR()


;-------------------------------------------------
;함수명:_KCOLOR_TBL
;개  요점:메인구상색테이블
;인  수:ARG:0… 캐릭터번호(NO)
;　　　 ARG:1…[생략가능]0이상의 경우는 SET모드로서 동작, 지정 캐릭터의구상색을 보유 한다
;반환값:지정 캐릭터의구상색. 미정도리의 경우는 0 xA0A0A0가 돌아간다
;비  고:내부 함수/식 안의 함수
;내부 함수인 것으로 다른 장소로부터 호출해서는 안 된다. 좋다?
;256인 것이 뭐라고도 미묘. 뭐 좋아
;-------------------------------------------------
@_KCOLOR_TBL(ARG:0, ARG:1 = -1)
#FUNCTION
#LOCALSIZE 256
LOCAL:(ARG:0) = ARG:1 >= 0 ? ARG:1 # LOCAL:(ARG:0)
RETURNF LOCAL:(ARG:0)
