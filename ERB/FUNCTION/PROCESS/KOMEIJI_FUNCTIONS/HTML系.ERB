;-------------------------------------------------
;HTML_PRINT 관련 함수
;-------------------------------------------------
;Emuera1810 이후 향해라
;우선 이런 느낌에. 생각보다는구상전용의 생각
;대개 좋은 느낌되었다일까?
;-------------------------------------------------
;주된 사용법(썩둑. 졸릴 때에 썼으므로 조금 의미 불명)
;우선@HTMLPRINT(|D)(L|W)의 사용법을 기억합니다
;예 1) CALL HTMLPRINTW(@" 「드모, %NAME:MASTER%=산. 고메이치코이시입니다」")
;다음에,<b><i><s><u>태그의 사용법을 기억합니다
;예 2) CALL HTMLPRINTW(@" 「드모, %NAME:MASTER%=산. <b>고메이치코이시</b>입니다」")
;<font>태그는 귀찮은 것으로 대신에@HTMLFONT의 사용법을 기억합니다
;예 3) CALL HTMLPRINTW(@" 「드모, %NAME:MASTER%=산. %HTMLFONT("<b>고메이치코이시</b>", "나눔명조", 0 xFFFFFF)%입니다」")
;여유가 있으면 문자 참조의 사용법을 기억하면 더 좋다(이 예라면"&#x2661;"대신에 UNICODE(0 x2661)에서도 같음)
;예 4) CALL HTMLPRINTW(@" 「드모, %NAME:MASTER%=산. <b>고메이치코이시</b>입니다%HTMLFONT("&#x2661;", "Verdana", 0 xC07070)%」")
;자주(잘) 사용하는 기호라든지는<font>태그마다 함수에 모아 두면 편리할지도
;마지막으로,<나>나&등의 기호는 문자 참조 사용하고 쓰지 않으면 이상한 일이 되기 때문에 조심해
;이것으로 대개의 일이 뒤탈 없게 일행으로 기술 할 수 있습니다. 해냈군요!
;-------------------------------------------------
;·대전제로서 HTML를 신규에 습득해서까지 무리하게 사용하는필요는 전혀 없다
;·HTML_PRINT 명령은 기본적으로 HTML 사양에 준거. 이것들의 함수도 비교적 HTML를 베어문 것이 있는 사람용일지도 모른다
;·HTML미경험에서도, 여기서 취급하는 범위를 베어물 뿐(만큼)이라면 간단한 것으로 성욕을 주체 못하는 (분)편은 아무쪼록
;-------------------------------------------------
;함수의 개요
;～～PRINT 함수～～
;·HTMLPRINT(|D)(L|W)…PRINT(|FORM)(|D)(L|W) 명령에 가까운 거동의 함수. HTML_PRINT 명령 같은 태그가 거의 사용 가능
;·HTMLPRINTSINGLE(|D)…PRINTSINGLE(|D) 명령에 가까운 거동의 함수. HTML_PRINT 명령 같은 태그가 거의 사용 가능
;·HTMLBIGPRINT(L|W)…BIGPRINT 함수 같은 성질을 가지는 HTMLPRINT 함수. HTML_PRINT 명령 같은 태그가 거의 사용 가능
;·HTMLSMALLPRINT(L|W)…SMALLPRINT 함수 같은 요소를 가지는 HTMLPRINT 함수. HTML_PRINT 명령 같은 태그가 거의 사용 가능
;～～태그 처리 함수～～
;·HTMLAUTOBUTTON…<button>,<nonbutton>, 태그 없음을 자동으로 교체되는 식 안의 함수. 버튼의 유효/무효상태를관리 하는 경우에
;·HTMLBUTTON…<button>태그를 부가하는 식 안의 함수. 버튼을 만들 때에필요. 버튼의 값, 툴 팁 캐릭터 라인에 관한 태그
;·HTMLFONT…<font>태그를 부가하는 식 안의 함수. SETFONT, SETCOLOR 명령 상당히 더해, 버튼선택색도 정의할 수 있다
;·HTMLNOBR…<nobr>태그를 부가하는 식 안의 함수. 화면단으로 되풀이하지 않게 되는, PRINTSINGLE 명령 상당
;·HTMLNONBUTTON…<nonbutton>태그를 부가하는 식 안의 함수. 비버튼에 툴 팁 캐릭터 라인을 설정할 수 있다
;·HTMLP…<p>태그를 부가하는 식 안의 함수. 문자의 표시 위치를 지정하는 ALIGNMENT 명령 상당
;·HTMLSTYLE…<b><i><s><u>태그를 부가하는 식 안의 함수. FONTSTYLE 명령 상당
;～～유틸리티 함수～～
;·CHKFACE…'/'단락의 폰트명중에서 사용할 수 있는 것을반환식 안의 함수. <font>태그의 face 속성으로 복수 지정 할 수 없는 사양을 커버
;·HTMLCOLOR… 색치를#RRGGBB 형식의 캐릭터 라인으로 변경해 돌려주는 식 안의 함수. <font>태그의 color 속성에 대응
;-------------------------------------------------

;*************************************************
;PRINT 함수
;*************************************************
;·FORM 캐릭터 라인을 건네줄 때는""의 전에@를 붙여 주세요
;　　　캐릭터 라인 → "당신는사정했다"
;  FORM 캐릭터 라인 → @"%CALLNAME:MASTER%는사정했다"
;-------------------------------------------------

;-------------------------------------------------
;함수명:HTMLPRINT(|D)(L|W)
;개  요점:HTML_PRINT 명령을 PRINT 명령풍으로 하는 래퍼 함수
;인  수:ARGS:0…HTML 캐릭터 라인. FORM 캐릭터 라인을 건네주는 경우는""의 전에@를 붙여 주세요
;　　　 ARG:0…[생략가능]문자의 표시색. 생략했다경우는 SETCOLOR의 지정 그대로
;　　　         ※키워드"D"를 사용했다경우는 ARG:0을 지정할 수 없습니다
;　　　 ARGS:1…[생략가능]캐릭터 라인의 표시 위치. (left/왼쪽/center/중앙/right/오른쪽)의 어느쪽이든을 지정가능
;　　　         생략했다경우는, ALIGNMENT의 지정 그대로. 대문자 소문자의 구별은 하지 않습니다
;반환값:없음(아마 RESULT 보호)
;비  고:이 함수에서는<p>태그를 사용할 수 없습니다. <nobr>태그는 문두 문말에서만 사용 가능
;·HTML_PRINT에 의한 묘화는 ALIGNMENT, SETFONT, SETCOLOR, FONTSTYLE와 그 유사 명령의영향를 받지 않는다
;·HTML_PRINT에는 WAIT시킨다기능이 없다
;상기는구상로 사용할 때에 조금 귀찮은 부분인 것으로, 영향를 받았는지와 같이 태그를 부가하도록(듯이) 세공
;이 처리에 의해, 일부 태그를 사용 할 수 없게 됩니다만 양해해 주십시오
;또, HTML_PRINT는 자동적으로 개행하므로 PRINT나 PRINTD는 만들 수 없습니다. 아무쪼록
;(그 중 Buffer 기능부의 HTMLPRINTB계라든지 HTMLPRINTBC계 만들지도 모르지만…)
;표시 위치와 색을 제어할 수 있으면구상적으로 한층 더 사용하기 쉬운 기분이했다의 것으로 조금 세공해 두었다
;반드시구상로 사용하는 분에는 이 정도로 좋은 생각이 든다. 아무것도 생각하지 않아도 괜찮다
;-------------------------------------------------
@HTMLPRINTL(ARGS:0, ARG:0 = -1, ARGS:1)
HTML_PRINT HTMLP(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT(), ARG:0 < 0 ? GETCOLOR() # ARG:0), ARGS:1)
RETURN RESULT

@HTMLPRINTW(ARGS:0, ARG:0 = -1, ARGS:1)
;OriginalString : (ARGS:0, ARG:0, ARGS:1)
CALL HTMLPRINTL(ARGS:0, ARG:0, ARGS:1)
WAIT
RETURN RESULT

@HTMLPRINTDL(ARGS:0, ARGS:1)
HTML_PRINT HTMLP(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT()), ARGS:1)
RETURN RESULT

@HTMLPRINTDW(ARGS:0, ARGS:1)
CALL HTMLPRINTDL(ARGS:0, ARGS:1)
WAIT
RETURN RESULT


;-------------------------------------------------
;함수명:HTMLPRINTSINGLE(|D)
;개  요점:HTML_PRINT 명령을 PRINTSINGLE 명령풍으로 하는 래퍼 함수
;인  수:ARGS:0…HTML 캐릭터 라인. FORM 캐릭터 라인을 건네주는 경우는""의 전에@를 붙여 주세요
;　　　 ARG:0…[생략가능]문자의 표시색. 생략했다경우는 SETCOLOR의 지정 그대로
;　　　         ※키워드"D"를 사용했다경우는 ARG:0을 지정할 수 없습니다
;　　　 ARGS:1…[생략가능]캐릭터 라인의 표시 위치. (left/왼쪽/center/중앙/right/오른쪽)의 어느쪽이든을 지정가능
;　　　         생략했다경우는, ALIGNMENT의 지정 그대로. 대문자 소문자의 구별은 하지 않습니다
;반환값:없음(아마 RESULT 보호)
;비  고:이 함수에서는<p><nobr>태그를 사용할 수 없습니다. (<nobr>태그는 자동으로 부가됩니다)
;본가 PRINTSINGLE 명령과 같이, 자동적으로 개행이 부가되기 (위해)때문에, (|L|W) 키워드는 없습니다
;아마 전혀 사용하지 않는다. 어째서 만들어 버렸는지
;-------------------------------------------------
@HTMLPRINTSINGLE(ARGS:0, ARG:0 = -1, ARGS:1)
HTML_PRINT HTMLP(HTMLNOBR(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT(), ARG:0 < 0 ? GETCOLOR() # ARG:0)), ARGS:1)
RETURN RESULT

@HTMLPRINTSINGLED(ARGS:0, ARGS:1)
HTML_PRINT HTMLP(HTMLNOBR(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT())), ARGS:1)
RETURN RESULT


;-------------------------------------------------
;함수명:HTMLBIGPRINT(L|W)
;개  요점:오오키이 HTMLPRINT 함수
;인  수:ARGS:0…HTML 캐릭터 라인. FORM 캐릭터 라인을 건네주는 경우는""의 전에@를 붙여 주세요
;　　　 ARG:0…[생략가능]문자의 표시색. 생략했다경우는 SETCOLOR의 지정 그대로
;　　　 ARGS:1…[생략가능]캐릭터 라인의 표시 위치. (left/왼쪽/center/중앙/right/오른쪽)의 어느쪽이든을 지정가능
;　　　         생략했다경우는, ALIGNMENT의 지정 그대로. 대문자 소문자의 구별은 하지 않습니다
;반환값:없음(아마 RESULT 보호)
;비  고:이 함수에서는<p><br>태그를 사용할 수 없습니다. <nobr>태그는 문두 문말에서만 사용 가능
;우선 준비했다HTML_PRINT판 BIGPRINT 함수
;태그를 사용 할 수 있다고는 해도, 보다신중함인 운용이 요구될지도
;예를 들면 도중에 색을 바꾸어도 문제는 없겠지만, 폰트를 바꾸면 엉망에
;BIGPRINT는 특히 2행분 사용하므로, \n라든지<br>넣을 때 와 이상해진다
;-------------------------------------------------
@HTMLBIGPRINTL(ARGS:0, ARG:0 = -1, ARGS:1)
#LOCALSIZE 1
#LOCALSSIZE 1
IF CHKFONT("Raavi")
	LOCAL:0 = CURRENTREDRAW()
	LOCALS:0 = %GETFONT()%
	REDRAW 0
	SETFONT "Raavi"
;OriginalString : (" " + ARGS:0 + " ", ARG:0, ARGS:1)
	CALL HTMLPRINTL(" " + ARGS:0 + " ", ARG:0, ARGS:1)
	SETFONT LOCALS:0
	PRINTL 
	REDRAW LOCAL:0
ELSE
;OriginalString : (ARGS:0, ARG:0, ARGS:1)
	CALL HTMLPRINTL(ARGS:0, ARG:0, ARGS:1)
ENDIF
RETURN RESULT

@HTMLBIGPRINTW(ARGS:0, ARG:0 = -1, ARGS:1)
CALL HTMLBIGPRINTL(ARGS:0, ARG:0, ARGS:1)
WAIT
RETURN RESULT


;-------------------------------------------------
;함수명:HTMLSMALLPRINT(L|W)
;개  요점:치이사이 HTMLPRINT 함수
;인  수:ARGS:0…HTML 캐릭터 라인. FORM 캐릭터 라인을 건네주는 경우는""대신에@""로 묶어 주세요
;　　　 ARG:0…[생략가능]문자의 표시색. 생략했다경우는 SETCOLOR의 지정 그대로
;　　　 ARGS:1…[생략가능]캐릭터 라인의 표시 위치. (left/왼쪽/center/중앙/right/오른쪽)의 어느쪽이든을 지정가능
;　　　         생략했다경우는, ALIGNMENT의 지정 그대로. 대문자 소문자의 구별은 하지 않습니다
;반환값:없음(아마 RESULT 보호)
;비  고:이 함수에서는<p>태그를 사용할 수 없습니다. <nobr>태그는 문두 문말에서만 사용 가능
;우선 준비했다HTML_PRINT판 SMALLPRINT 함수
;태그를 사용 할 수 있다고는 해도, 보다신중함인 운용이 요구될지도
;예를 들면 도중에 색을 바꾸어도 문제는 없겠지만, 폰트를 바꾸면 엉망에
;-------------------------------------------------
@HTMLSMALLPRINTL(ARGS:0, ARG:0 = -1, ARGS:1)
#LOCALSSIZE 1
IF CHKFONT("Estrangelo Edessa")
	LOCALS:0 = %GETFONT()%
	SETFONT "Estrangelo Edessa"
;OriginalString : (" " + ARGS:0 + " ", ARG:0, ARGS:1)
	CALL HTMLPRINTL(" " + ARGS:0 + " ", ARG:0, ARGS:1)
	SETFONT LOCALS:0
ELSE
;OriginalString : (ARGS:0, ARG:0, ARGS:1)
	CALL HTMLPRINTL(ARGS:0, ARG:0, ARGS:1)
ENDIF
RETURN RESULT

@HTMLSMALLPRINTW(ARGS:0, ARG:0 = -1, ARGS:1)
CALL HTMLSMALLPRINTL(ARGS:0, ARG:0, ARGS:1)
WAIT
RETURN RESULT


;*************************************************
;태그 처리 함수
;*************************************************
;·<b><i><s><u><br>태그는 속성치 없고 스스로 써, 아무쪼록
;·스스로 쓴 (분)편이, 함수 상자로 하는 것보다는 짧게 쓸 수 있어 처리도 빠를 것
;·이것들의 함수는 HTML_PRINT 명령을 직접적으로 부르는 경우도 사용할 수 있고, PRINT 함수내에서도 사용할 수 있다
;·대개의 일은, 이것들의 함수를 상자로 하면 어떻게든 되는… 일지도
;-------------------------------------------------

;-------------------------------------------------
;함수명:HTMLAUTOBUTTON
;개  요점:HTML_PRINT 명령 및 관련 함수로 사용하는<button><nonbutton><font>태그를 자동으로 부가하는 함수
;인  수:ARGS:0… 버튼으로 하는 캐릭터 라인
;　　　 ARGS:1… 버튼의 값(수치 변수 따위는 짜넣어 함수 TOSTR로 캐릭터 라인화해 주세요)
;　　　 ARGS:2…[생략가능][Emuera1812beta001 이후 마셔 지정가능]툴 팁에 표시하는 캐릭터 라인
;　　　 ARG:0…[생략가능]표시색. 생략했다경우는 현재의 문자색
;　　　 ARG:1…[생략가능]버튼의선택뒤집어서 접음시색. 생략했다경우는 디폴트의선택색
;　　　 ARG:2…[생략가능]버튼이 유효한가 어떤가를 지정하는 진위치. 생략했다경우는 유효(1)
;　　　 ARGS:3…[생략가능]버튼이 무효의 경우의 캐릭터 라인. 생략했다경우는 ARGS:0
;　　　 ARGS:4…[생략가능][Emuera1812beta001 이후 마셔 지정가능]버튼이 무효의 경우의 툴 팁 캐릭터 라인. 생략했다경우는 ARGS:2
;　　　 ARG:3…[생략가능]버튼이 무효의 경우의 표시색. 생략했다경우는 ARG:0
;반환값:ARGS:0을<button><nonbutton><font><b><i><s><u>태그로 둘러싸거나 둘러싸지 않았다거나 하는 HTML 캐릭터 라인
;비  고:식 안의 함수
;★★★툴 팁 기능은 Emuera1812alpha001 이후에 사용할 수 있습니다★★★
;★★★Button 무효 Tooltip유로 사용할 수 있는 것은 Emuera1812beta001 이후★★★
;보탄가 value 가라고 하는 전파를 수신했다의 것으로 만든 것이지만…
;Emuera1812beta001로 value 속성의 사양 바뀌었으므로 생각보다는 필요없게 되었다. 하는 김에<nonbutton>도 대부분 필요하지 않은 아이에게
;alpha판은 살짝 잘라 버려, beta판의 사양에 따라손수선
;기본은<button>에 통째로 맡김 해, 각 속성이나 태그 그 자체의필요성을 판별
;하는 김에 유효/무효시에 표시 캐릭터 라인/툴 팁 캐릭터 라인/표시색도 바꿀 수 있도록(듯이)
;인수 나누지 않아도 3항연산자 사용하면 끝나는 이야기일지도 모르지만, 사용손가 쓰기 쉬운 (분)편을 선택해 사용하면 좋다고 생각한다
;SETFONT/FONTSTYLE는 승손에 자동 계승하는 고맙지 않은 친절인 설계. 이유는 귀찮았기 때문에
;기분에 필요없으면 개조할까 별로 비슷한 함수 짤까 하면 좋다고 생각한다
;정직 이 함수 단체는, 미리 만들어 둔 함수를 짜맞춘 것 뿐인 것으로 대했다 것은 없는 것이지만…
;우선 나는 사용하지 않는다… 라고 생각한다. 인수의 종류가 많으면 IDE도 아닌 한 의미 불명하게 되기 십상
;-------------------------------------------------
@HTMLAUTOBUTTON(ARGS:0, ARGS:1, ARGS:2, ARG:0 = -1, ARG:1 = -1, ARG:2 = 1, ARGS:3, ARGS:4, ARG:3 = -1)
#FUNCTIONS
IF !ARG:2	;버튼이 무효인 경우, 사용하는 인수를 바꾼다
	ARG:0 = ARG:3 >= 0 ? ARG:3 # ARG:0
	ARGS:0 = \@ ARGS:3 != "" ? %ARGS:3% # %ARGS:0% \@
	ARGS:2 = \@ ARGS:4 != "" ? %ARGS:4% # %ARGS:2% \@
ENDIF
RETURNF HTMLBUTTON(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT(), ARG:0 < 0 ? GETCOLOR() # ARG:0, ARG:1), ARGS:1, ARGS:2, ARG:2)


;-------------------------------------------------
;함수명:HTMLBUTTON
;개  요점:HTML_PRINT 명령 및 관련 함수로 사용하는<button>태그를 부가하는 함수
;인  수:ARGS:0… 버튼으로 하는 캐릭터 라인
;　　　 ARGS:1… 버튼의 값(수치 변수 따위는 짜넣어 함수 TOSTR로 캐릭터 라인화해 주세요)
;　　　 ARGS:2…[생략가능][Emuera1812alpha001 이후 마셔 지정가능]툴 팁에 표시하는 캐릭터 라인
;　　　 ARG:0…[생략가능]버튼이 유효한가 어떤가를 지정하는 진위치. 생략했다경우는 유효(1)
;반환값:ARGS:0을<button>태그로 둘러싼 HTML 캐릭터 라인
;비  고:식 안의 함수
;★★★툴 팁 기능은 Emuera1812alpha001 이후에 사용할 수 있습니다★★★
;★★★Button 무효 Tooltip유로 사용할 수 있는 것은 Emuera1812beta001 이후★★★
;일단 공문자열 돌려주는 버튼은 개미인 것으로, 만들 수 있다
;공문자열의 툴 팁 내도 어쩔 수 없기 때문에, 그러한 때는 title 속성 생략한다
;버튼이 무효인 경우는 value 속성 생략한다. 툴 팁도 공문자라면 태그 그 자체를 붙이지 않는다
;-------------------------------------------------
@HTMLBUTTON(ARGS:0, ARGS:1, ARGS:2, ARG:0 = 1)
#FUNCTIONS
#LOCALSSIZE 1
SIF !ARG:0 && ARGS:2 == ""
	RETURNF ARGS:0
LOCALS:0 = <button
SIF ARG:0
	LOCALS:0 += @" value='%ARGS:1%'"
SIF ARGS:2 != ""
	LOCALS:0 += @" title='%ARGS:2%'"
RETURNF LOCALS:0 + @">%ARGS:0%</button>"


;-------------------------------------------------
;함수명:HTMLFONT
;개  요점:HTML_PRINT 명령 및 관련 함수로 사용하는<font>태그를 부가하는 함수
;인  수:ARGS:0… 부가하는대상의 캐릭터 라인
;　　　 ARGS:1…[생략가능]폰트명. 생략했다경우 및, 지정했다폰트가 존재하지 않는 경우는 태그에 포함되지 않는다
;　　　 ARG:0…[생략가능]표시색. 생략했다경우는 태그에 포함되지 않는다
;　　　 ARG:1…[생략가능]버튼의선택뒤집어서 접음시색. 생략했다경우는 태그에 포함되지 않는다
;　　　 ※ARGS:1/ARG:0/ARG:1의 모두를 생략했다경우, GETFONT()와 GETCOLOR()의 정보를 바탕으로 태그가 생성됩니다
;반환값:ARGS:0을<font>태그로 둘러싼 HTML 캐릭터 라인
;비  고:식 안의 함수
;<font>태그는 SETFONT 명령과 SETCOLOR 명령에 상당합니다
;무심코 버튼의선택뒤집어서 접음시색을 바꿀 수 있는 것은 신기능. HTML_PRINT만!
;-------------------------------------------------
@HTMLFONT(ARGS:0, ARGS:1, ARG:0 = -1, ARG:1 = -1)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = <font
SIF !CHKFONT(ARGS:1) && ARG:0 < 0 && ARG:1 < 0
	RETURNF @"<font face='%GETFONT()%' color='%HTMLCOLOR()%'>%ARGS%</font>"
SIF CHKFONT(ARGS:1)
	LOCALS:0 += @" face='%ARGS:1%'"
SIF ARG:0 >= 0
	LOCALS:0 += @" color='%HTMLCOLOR(ARG:0)%'"
SIF ARG:1 >= 0
	LOCALS:0 += @" bcolor='%HTMLCOLOR(ARG:1)%'"
RETURNF @"%LOCALS:0%>%ARGS:0%</font>"


;-------------------------------------------------
;함수명:HTMLNOBR
;개  요점:HTML_PRINT 명령 및 관련 함수로 사용하는<nobr>태그를 부가하는 함수
;인  수:ARGS:0… 부가하는대상의 캐릭터 라인
;반환값:ARGS:0을<nobr>태그로 둘러싼 HTML 캐릭터 라인
;비  고:식 안의 함수
;<nobr>태그를 사용하면 묘화 영역을 넘은 것에 의한 암묵의 개행을 하지 않게 됩니다(PRINTSINGLE 명령에 상당)
;그 경우에서도,<br>태그에 의한 명시적인 개행은 가능합니다
;<nobr>태그는 최초의 테키스트보다 외측에 두는필요가 있습니다
;이 함수도 그처럼 사용해 주세요
;필요하지 않다고 생각하지만… 기분의 문제라는 녀석
;-------------------------------------------------
@HTMLNOBR(ARGS:0)
#FUNCTIONS
RETURNF @"<nobr>%ARGS:0%</nobr>"


;-------------------------------------------------
;함수명:HTMLNONBUTTON
;개  요점:HTML_PRINT 명령 및 관련 함수로 사용하는<nonbutton>태그를 부가하는 함수
;인  수:ARGS:0… 툴 팁 첨부로 하는 캐릭터 라인
;　　　 ARGS:1…[Emuera1812alpha001 이후 마셔 지정가능]툴 팁에 표시하는 캐릭터 라인
;반환값:ARGS:0을<nonbutton>태그로 둘러싼 HTML 캐릭터 라인
;비  고:식 안의 함수 Emuera1812alpha001 이후에서만 사용가능
;★★★툴 팁 기능은 Emuera1812alpha001 이후에 사용할 수 있습니다★★★
;공문자열의 툴 팁 내도 어쩔 수 없기 때문에, 그러한 때는 태그 붙이지 않아
;Emuera1812beta001로 필요하지 않게 된 감이 강하다. 사라질까나?
;-------------------------------------------------
@HTMLNONBUTTON(ARGS:0, ARGS:1)
#FUNCTIONS
SIF ARGS:1 == ""
	RETURNF ARGS:0
RETURNF @"<nonbutton title='%ARGS:1%'>%ARGS:0%</nonbutton>"


;-------------------------------------------------
;함수명:HTMLP
;개  요점:HTML_PRINT 명령 및 관련 함수로 사용하는<p>태그를 부가하는 함수
;인  수:ARGS:0… 부가하는대상의 캐릭터 라인
;　　　 ARGS:1…[생략가능]캐릭터 라인의 표시 위치. (left/왼쪽/center/중앙/right/오른쪽)의 어느쪽이든을 지정가능
;　　　         생략했다경우는, CURRENTALIGN()의 정보를 바탕으로 태그가 생성됩니다. 대문자 소문자의 구별은 하지 않습니다
;반환값:ARGS:0을<p>태그로 둘러싼 HTML 캐릭터 라인
;비  고:식 안의 함수
;<p>태그를 사용하면 캐릭터 라인의 표시 위치를 제어할 수 있습니다(ALIGNMENT 명령에 상당)
;<p>태그는 제일 외측에 두는필요가 있습니다
;이 함수도 제일 외측에서 사용해 주세요
;-------------------------------------------------
@HTMLP(ARGS:0, ARGS:1)
#FUNCTIONS
#DIMS CONST ALIGN = "", "left", "center", "right", "왼쪽", "중앙", "오른쪽"
SIF !MATCH(ALIGN, TOLOWER(ARGS:1))	;만약을 위해 인수 체크
	THROW @HTMLP에 불명한 align 속성치가 도 사라질 수 있는 했다（%ARGS:1%）
ARGS:1 '= MATCH(ALIGN, ARGS:1, 4) ? ALIGN:(FINDELEMENT(ALIGN, ARGS:1, , , 1) - 3) # ARGS:1
RETURNF "<p align='" + TOLOWER(\@ ARGS:1 == "" ? %CURRENTALIGN()% # %ARGS:1% \@) + @"'>%ARGS:0%</p>"


;-------------------------------------------------
;함수명:HTMLSTYLE
;개  요점:HTML_PRINT 명령 및 관련 함수로 사용하는<b><i><s><u>태그를 부가하는 함수
;인  수:ARGS:0… 부가하는대상의 캐릭터 라인
;　　　 ARG:0…[생략가능]문자의 스타일. 굵은 글씨(1)/이탤릭(2)/부정선(4)/밑줄(8) 의 편성을 지정 가능
;　　　         지정 방법은 FONTSTYLE 명령과 같아, 각 숫자의 화로 복수 지정할 수 있습니다
;　　　         생략했다경우는 GETSTYLE()의 정보를 바탕으로 태그가 생성됩니다
;반환값:ARGS:0을필요인<b><i><s><u>태그로 둘러싼 HTML 캐릭터 라인
;비  고:식 안의 함수
;FONTSTYLE 명령 상당히 날조한 함수. 정리하고 있는 것이 편리한 때도 있는… 일지도
;이 함수로 부가되는 태그의 효과는 이하와 같다
;<b>태그… 굵은 글씨. FONTBOLD, 또는 FONTSTYLE 1에 상당
;<i>태그… 이탤릭. FONTITALIC, 또는 FONTSTYLE 2에 상당
;<s>태그… 부정. FONTSTYLE 4에 상당
;<u>태그… 밑줄. FONTSTYLE 8에 상당
;-------------------------------------------------
;FONTSTYLE 명령과 달리, 태그내에서 이미 적용중의 효과를 지울 수 없습니다
;예 1) HTML_PRINT @"TEST0<b>TEST1%HTMLSTYLE("TEST2", 2) %TEST3</b>TEST4"
;　　이 경우, TEST2에는 굵은 글씨와 이탤릭의 양쪽 모두가 적용됩니다. 이탤릭에만 하려면,
;예 2) HTML_PRINT @"TEST0<b>TEST1</b>%HTMLSTYLE("TEST", 2)%<b>TEST3</b>TEST4"
;　　와 같이, 일단<b>태그를 닫는필요가 있습니다
;-------------------------------------------------
@HTMLSTYLE(ARGS:0, ARG:0 = -1)
#FUNCTIONS
#LOCALSSIZE 2
VARSET LOCALS
ARG:0 = ARG:0 < 0 ? GETSTYLE() # ARG:0
IF GETBIT(ARG:0, 0)
	LOCALS:0 = <b>%LOCALS:0%
	LOCALS:1 += "</b>"
ENDIF
IF GETBIT(ARG:0, 1)
	LOCALS:0 = <i>%LOCALS:0%
	LOCALS:1 += "</i>"
ENDIF
IF GETBIT(ARG:0, 2)
	LOCALS:0 = <s>%LOCALS:0%
	LOCALS:1 += "</s>"
ENDIF
IF GETBIT(ARG:0, 3)
	LOCALS:0 = <u>%LOCALS:0%
	LOCALS:1 += "</u>"
ENDIF
RETURNF LOCALS:0 + ARGS:0 + LOCALS:1


;*************************************************
;유틸리티 함수
;*************************************************
;·바로<font>태그 쓰고 싶은 사람이라든지
;-------------------------------------------------

;-------------------------------------------------
;함수명:CHKFACE
;개  요점:복수 폰트 사용 가부 판정 함수
;인  수:ARGS:0… 폰트명(/단락으로 복수 지정가능. 우선 10종까지)
;반환값:전부터 순서에 CHKFONT 해 최초로 사용 가능과 판단당했다폰트명
;　　　 사용할 수 있는 폰트가 없는 경우는 현재의 폰트명
;비  고:식 안의 함수
;<font>태그에 있어서의 face 속성의, 단락(우선폰트 지정) 기능의 대체
;일 것이었지만… 사용하는 곳은 그 밖에도 있는지도…?
;표준 이외의 폰트이지만, 있으면 사용하는 경우라든지…
;후는… 그…WinXP/VISTA/7/8이외의…
;, 별로 Mac 대응을 위해서(때문에) 만들었을 것이 아니기 때문에… 차차차착각 하지 말아요!
;-------------------------------------------------
@CHKFACE(ARGS:0)
#FUNCTIONS
#LOCALSIZE 2
#LOCALSSIZE 10
SPLIT ARGS:0, "/", LOCALS, LOCAL:0
FOR LOCAL:1, 0, LOCAL:0
	SIF CHKFONT(LOCALS:(LOCAL:1))
		RETURNF LOCALS:(LOCAL:1)
NEXT
RETURNF GETFONT()


;-------------------------------------------------
;함수명:HTMLCOLOR
;개  요점:HTML_PRINT 명령의 font 태그에 건네주는 색치로 변환하는 함수
;인  수:ARG:0…[생략가능]색치
;　　　        생략했다경우는 GETCOLOR()의 정보를 바탕으로 자동 생성
;반환값:#RRGGBB 형식의 16진색치를 표현하는 캐릭터 라인
;비  고:식 안의 함수
;보통으로 10진 건네줄 수 있으면 이런 것 필요하지 않은데…HTML 사양 이유 방법 없음
;있을 수 없는 수치가 와도 하 24 bit 밖에 보지 않기 때문에 문제 없음
;TOSTR 사용하고 있는 것은성격의 문제. 자리수 맞댐과 대문자화를했다 샀다
;실제는 CONVERT라도 좋겠지만
;-------------------------------------------------
@HTMLCOLOR(ARG:0 = -1)
#FUNCTIONS
RETURNF "#" + TOSTR((ARG:0 < 0 ? GETCOLOR() # ARG:0) & 0xFFFFFF, "X6")
