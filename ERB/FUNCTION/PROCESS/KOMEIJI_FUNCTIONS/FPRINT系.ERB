;-------------------------------------------------
;FPRINT계 함수
;-------------------------------------------------
;·기본적인 것이 없으면 체계적으로 어떨까라고 생각했으므로 부록으로
;·F는 함수(FUNCTION)의 F. 보통 F가 아니야… 아마
;·SETFONT 명령 상당한 인수를 마련할 생각은 없는 것 같다. 내가 우선 사용하지 않기 때문에
;·익숙해지면 HTMLPRINT가 편리. 사용법은 대개 함께. 태그도 사용할 수 있고
;·우선 HTMLPRINT보다는 처리가 가볍다고 생각되므로 사용구분도 가능
;-------------------------------------------------

;-------------------------------------------------
;함수명:FPRINT(|D)(|L|W)
;개  요점:보통 PRINT 함수
;인  수:ARGS:0… 표시 캐릭터 라인. FORM 캐릭터 라인을 건네주는 경우는""의 전에@를 붙여 주세요
;　　　 ARG:0…[생략가능]문자의 표시색. 생략했다경우는 SETCOLOR의 지정 그대로
;　　　         ※키워드"D"를 사용했다경우는 ARG:0을 지정할 수 없습니다
;　　　 ARGS:1…[생략가능]캐릭터 라인의 표시 위치. (LEFT/왼쪽/CENTER/중앙/RIGHT/오른쪽)의 어느쪽이든을 지정가능
;　　　         생략했다경우는, ALIGNMENT의 지정 그대로. 대문자 소문자의 구별은 하지 않습니다
;　　　         ※키워드"L""W"를 사용하지 않는 경우는 ARGS:1을 지정할 수 없습니다
;반환값:없음(아마 RESULT 보호)
;CALL로 호출해 사용하는 PRINT 명령과 같은 것
;그 중에서도 기본적인 부류. 색지정 기능과 표시 위치 지정 기능만 존재한다
;다만, 키워드에 의해 일부 기능을 사용할 수 없는 함수도 있으므로 주의
;구상를 쓸 때, 기본은통상대로PRINTFORM(|D)(|L|W)를 사용해,
;이 함수는구상중에 3인째의 캐릭터(조수등)를 등장시킨다경우 따위에 이용하면 좋다
;(우선은 색을 바꾸는데 사용한다. 오른쪽 공격까지 할지 어떨지는 임의)
;-------------------------------------------------
;예 1) 다음의 2행의 표시 결과는 동일함(다만, 전자가 처리는 빠른… 이라고 해도구상내에서 서두를 것도 없지만)
;PRINTFORMW 「안녕하세요, %CALLNAME:MASTER%」
;CALL FPRINTW(@" 「안녕하세요, %CALLNAME:MASTER%」")
;-------------------------------------------------
;예 2)조수의 대사를 날조 하는 케이스
;CALL FPRINTW(" 「안녕 자리지금… 선」", 0 xCCFFCC, "RIGHT")
;-------------------------------------------------
@FPRINT(ARGS:0, ARG:0 = -1)
#LOCALSIZE 1
LOCAL:0 = GETCOLOR()
SETCOLOR ARG:0 < 0 ? LOCAL:0 # (ARG:0 & 0xFFFFFF)
;OriginalString : ARGS:0
PRINTS ARGS:0
SETCOLOR LOCAL:0
RETURN RESULT

@FPRINTL(ARGS:0, ARG:0 = -1, ARGS:1)
#LOCALSSIZE 1
LOCALS:0 = %CURRENTALIGN()%
CALL ALIGNMENTS(ARGS:1)
CALL FPRINT(ARGS:0, ARG:0)
PRINTL 
CALL ALIGNMENTS(LOCALS:0)
RETURN RESULT

@FPRINTW(ARGS:0, ARG:0 = -1, ARGS:1)
CALL FPRINTL(ARGS:0, ARG:0, ARGS:1)
WAIT
RETURN RESULT

@FPRINTD(ARGS:0)
CALL FPRINT(ARGS:0, GETDEFCOLOR())
RETURN RESULT

@FPRINTDL(ARGS:0, ARGS:1)
CALL FPRINTL(ARGS:0, GETDEFCOLOR(), ARGS:1)
RETURN RESULT

@FPRINTDW(ARGS:0, ARGS:1)
CALL FPRINTL(ARGS:0, GETDEFCOLOR(), ARGS:1)
WAIT
RETURN RESULT
