;────────────────────────────────────
;숍 메뉴의 표시
;────────────────────────────────────
@SHOW_SHOP
CALL VERSION_UP

DRAWLINE
PRINTFORML {FLAG:연수+1}년 {FLAG:일수+1}일째 (%GET_TIME()%) %GET_SEASON()% %GET_DAY()%요일   남편님:%CALLNAME:1%
SIF TARGET >= 0
	PRINTFORML %CALLNAME:MASTER%(을)를 %CALLNAME:TARGET%이(가) 조교중
SIF ASSI:1 >= 0
	PRINTFORML （조수 1:%CALLNAME:(ASSI:1)%）\@ ASSI:2 >= 0 ? （조수 2:%CALLNAME:(ASSI:2)%） # \@
HTML_PRINT HTML_BASE("체력", MASTER, 0, 16) + HTML_BASE("기력", MASTER, 1, 16)
SIF TARGET >= 0
	PRINTFORML (%CALLNAME%의 마력　{CFLAG:마력})
[SKIPSTART]
필요하지 않다고 생각하므로 comment out 해 둡니다@/L
IF TARGET >= 0
	PRINTFORML 【의존도  test판】  (%CALLNAME%의 %CALLNAME:MASTER%에의 의존도:{CFLAG:의존도}  %CALLNAME:MASTER%의 %CALLNAME:(FLAG:주인님)%들에게로의 의존도:{CFLAG:MASTER:의존도})
	PRINTL   송곳이 좋은 날짜에서의 수치를, 남편님의 이름·주인공의 성별, 할 수 있으면 대범한 플레이 방침도 붙여
	PRINTL   스레 등에 보고하실 수 있으면 다행입니다. 엔딩 분기등에 사용할 예정의 값입니다.
ENDIF
[SKIPEND]
DRAWLINE
;죠교개시
;OriginalString : (HTMLAUTOBUTTON("[100] - 조교의 시간이다", "100", "죠교を開始します", , , TARGET >= 0, 
;CALL HTMLPRINTL(HTMLAUTOBUTTON("[100] - 조교의 시간이 왔다", "100", "조교를 개시합니다", , , TARGET >= 0,  
;"[---] - 조교자를 결정해 주세요", "조교를 개시하기 위해서는, 조교자를 결정할 필요가 있습니다", 0 xA0A0A0))
{
CALL HTMLPRINTL(HTMLAUTOBUTTON("[100] - 조교의 시간이다", "100", "죠교를 개시합니다", , , TARGET >= 0, 
	"[---] - 죠교자를 결정해 주세요", "죠교를 개시하기 위해서는, 죠교자를 결정하는필요가 있습니다", 0xA0A0A0))
}
;능력표시
;OriginalString : (HTMLBUTTON("[101] - 능력의 표시", "101", "죠교자と죠교대상の各種경험치等を確認します"))
CALL HTMLPRINTL(HTMLBUTTON("[101] - 능력의 표시", "101", "죠교자(와)과죠교대상의 각종경험치등을 확인합니다"))
;마력소비
SIF TARGET >= 0
;OriginalString : (HTMLBUTTON("[102] - 마력을 소비한다", "102", "마력を使ってアイテムや能力等の취득、新たな죠교자の追加を行います"))
	CALL HTMLPRINTL(HTMLBUTTON("[102] - 마력을 소비한다", "102", "마력를 사용해 아이템이나 능력등의취득, 새로운죠교자의 추가를 실시합니다"))
;죠교자변환
SIF CHARANUM > 2
;OriginalString : (HTMLBUTTON("[110] - 죠교자/조수를 변경한다", "110", "現在の죠교자/조수を他の죠교자に変更します"))
	CALL HTMLPRINTL(HTMLBUTTON("[110] - 죠교자/조수를 변경한다", "110", "현재의죠교자/조수를 다른죠교자로 변경합니다"))
;의상 설정
SIF TARGET > 0
;OriginalString : (HTMLBUTTON("[155] - 의상 설정", "155", "죠교자と죠교대상の의상を設定します"))
	CALL HTMLPRINTL(HTMLBUTTON("[155] - 의상 설정", "155", "죠교자(와)과죠교대상노의상를 설정합니다"))
;데이타베이스
;OriginalString : (HTMLBUTTON("[177] - 데이터베이스 보기", "177", "各種죠교データを確認します"))
CALL HTMLPRINTL(HTMLBUTTON("[177] - 데이터베이스 보기", "177", "각종죠교데이터를 확인합니다"))
;콘피그
;OriginalString : (HTMLBUTTON("[178] - 구성", "178", "ゲームの設定を行います"))
CALL HTMLPRINTL(HTMLBUTTON("[178] - 구성", "178", "게임의 설정을 실시합니다"))
;세이브
;OriginalString : (HTMLBUTTON("[200] - 세이브", "200", "現在のデータを保存します"))
CALL HTMLPRINTL(HTMLBUTTON("[200] - 세이브", "200", "현재의 데이터를 보존합니다"))
;로드
;OriginalString : (HTMLBUTTON("[300] - 로드", "300", "保存했다データを読み込みます"))
CALL HTMLPRINTL(HTMLBUTTON("[300] - 로드", "300", "보존했다데이터를 읽어들입니다"))

;Emuera디버그모드기동시
[IF_DEBUG]
{
;OriginalString : (HTMLBUTTON("[777] - 디버그모드" + (FLAG:디버그 ? "를 종료" # ""), "777", 
	CALL HTMLPRINTL(HTMLBUTTON("[777] - 디버그모드" + (FLAG:디버그 ? "를 종료" # ""), "777", 
		FLAG:디버그 ? "통상화면으로 돌아갑니다" # "변종 디버그모드를 사용합니다"))
}
	IF FLAG:디버그
		PRINTFORML \n--디버그 커멘드는 간편하게 상황을 바꿀 뿐(만큼)을 위해서(때문에) 만들었으므로,\n이하의 커멘드에 의한 이상은 동작 보장외로 합니다--\n
		;마력증가
		SIF TARGET >= 0
;OriginalString : (HTMLBUTTON("[778] - 마력増加", "778", "죠교자の마력を増やします"))
			CALL HTMLPRINTL(HTMLBUTTON("[778] - 마력증가", "778", "죠교자노마력를 늘립니다"))
		;전생의 의식
;OriginalString : (HTMLBUTTON("[779] - 転生の儀式", "779", "플레이ヤーキャラの再設定画面へ進みます"))
		CALL HTMLPRINTL(HTMLBUTTON("[779] - 전생의 의식", "779", "플레이야캐라의 재설정 화면으로 나아갑니다"))
		;플래그체크
;OriginalString : (HTMLBUTTON("[780] - 플래그チェック", "780", "CFLAGとFLAGの状況を確認します"))
		CALL HTMLPRINTL(HTMLBUTTON("[780] - 플래그체크", "780", "CFLAG(와)과FLAG의 상황을 확인합니다"))
		;오토모드
;OriginalString : (HTMLBUTTON("[781] - 오토모드", "781", "죠교대상の행동を自動で行う모드の設定画面を呼び出します"))
		CALL HTMLPRINTL(HTMLBUTTON("[781] - 오토모드", "781", "죠교대상노행동를 자동으로 실시한다모드의 설정 화면을 호출합니다"))
		;소질보정확인
;OriginalString : (HTMLBUTTON("[782] - 素質보정確認", "782", "各キャラのコマンド선택경향を確認します"))
		CALL HTMLPRINTL(HTMLBUTTON("[782] - 소질보정확인", "782", "각 캐릭터의 커맨드선택경향를 확인합니다"))
	ENDIF
[ENDIF]

PRINTL 
;OriginalString : (HTMLBUTTON("[888] - 구상표시설정", "888", "구상の表示/非表示を設定します"))
CALL HTMLPRINTL(HTMLBUTTON("[888] - 구상표시설정", "888", "구상의 표시/비표시를 설정합니다"))
DRAWLINE


;────────────────────────────────────
;숍 메뉴
;────────────────────────────────────
@USERSHOP
IF RESULT == 100 && TARGET >= 0
	BEGIN TRAIN
	RETURN 1
ELSEIF RESULT == 101
	CALL SHOW_CHARADATA
ELSEIF RESULT == 102 && TARGET >= 0
	CALL BONUS_GAIN
	RETURN 1
ELSEIF RESULT == 110 && CHARANUM > 2
	CALL CHANGE_TARGET_ASSI
ELSEIF RESULT == 155 && TARGET >= 0
	CALL CLOTHES_SETTING
ELSEIF RESULT == 177
	CALL DATABASE_OUTPUT
ELSEIF RESULT == 178
	CALL CONFIGURE
ELSEIF RESULT == 200
	;SAVEGAME
	;→SYSTEM_DATAMENU.ERB
	CALL DATAMENU("SAVE")
ELSEIF RESULT == 300
	;LOADGAME
	;→SYSTEM_DATAMENU.ERB
	CALL DATAMENU("LOAD")
ELSEIF RESULT == 888
	CALL KOJO_SET
ENDIF
;Emuera디버그모드기동시
[IF_DEBUG]
	IF 0
	ELSEIF RESULT == 777
		PRINTFORML 디버그용의 메세지를 표시\@ FLAG:디버그 ? 하지 않는다 # 한다 \@같게 설정했습니다
		FLAG:디버그 = !FLAG:디버그 ? 1 # 0
		WAIT
	ELSEIF RESULT == 778 && FLAG:디버그 && TARGET >= 0
		CFLAG:마력 += 10000
		PRINTFORML %CALLNAME%의 마력+10000되었습니다
		WAIT
	ELSEIF RESULT == 779 && FLAG:디버그
		PRINTW 조교 대상（주인공）를 재설정할 수 있습니다
		CALL MASTER_CUSTOM
		CALL BASE_MASTER_SETUP
	ELSEIF RESULT == 780 && FLAG:디버그
		CALL FLAGCHECK
	ELSEIF RESULT == 781 && FLAG:디버그
		CALL SET_AUTO
	ELSEIF RESULT == 782 && FLAG:디버그
		CALL CHECK_TALENT_CRI
	ENDIF
[ENDIF]


@KOJO_SET
PRINTL 캐릭터 말을 어떻게 표시합니까?
PRINTL [0] - 표시OFF
PRINTL [1] - 표시ON　※추천
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
		PRINTW 캐릭터 말을 표시하지 않습니다
	CASE 1
		PRINTW 캐릭터 말을 표시합니다
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT
FLAG:구상 = RESULT
PRINTL 

;────────────────────────────────────
;죠교자/조수변경
;────────────────────────────────────
@CHANGE_TARGET_ASSI
#DIM OLD_T, 3

OLD_T = TARGET, (ASSI:1), (ASSI:2)

$CHANGE_LOOP
PRINTFORML 조교자/조수를 변경할 수 있습니다
CALL KOJO_COLOR(TARGET)
PRINTBUTTON @"[  0] 조교자 - \@ TARGET >= 0 ? [{TARGET,2}] %CALLNAME:TARGET% # 조교자는 없습니다 \@", 0
PRINTL 
RESETCOLOR
CALL KOJO_COLOR(ASSI:1)
PRINTBUTTON @"[  1] 조수1  - \@ ASSI:1 >= 0 ? [{ASSI:1,2}] %CALLNAME:(ASSI:1)% # 조수1(은)는 없습니다 \@", 1
PRINTL 
IF CHARANUM > 3
	RESETCOLOR
	CALL KOJO_COLOR(ASSI:2)
	PRINTBUTTON @"[  2] 조수2  - \@ ASSI:2 >= 0 ? [{ASSI:2,2}] %CALLNAME:(ASSI:2)% # 조수2(은)는 없습니다 \@", 2
	PRINTL 
ENDIF
IF TARGET < 0
	SETCOLOR 160, 160, 160
	PRINTFORML [---] 조교자를 결정해 주세요
	RESETCOLOR
ELSE
	RESETCOLOR
	PRINTFORML [  9] 변경 종료
ENDIF
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
		CALL CHANGE_TARGET
		GOTO CHANGE_LOOP
	CASE 1
		CALL CHANGE_ASSI(1)
		GOTO CHANGE_LOOP
	CASE 2
		IF CHARANUM > 3
			CALL CHANGE_ASSI(2)
			GOTO CHANGE_LOOP
		ENDIF
	CASE 9
		IF TARGET >= 0
			;조수 1가 하늘의 경우, 조수 2를조수 1로 한다
			SIF ASSI:1 == -1
				SWAP ASSI:1, ASSI:2
			CALL CHANGE_TARGET_ASSI_MESSAGE(OLD_T)
			RETURN RESULT
		ENDIF
ENDSELECT
CLEARLINE 1
GOTO INPUT_LOOP

@CHANGE_TARGET_ASSI_MESSAGE(OLD_T)
#DIM LCOUNT
#DIM REF OLD_T, 0
#DIM NEW_T, 3

NEW_T = TARGET, (ASSI:1), (ASSI:2)

;변경이 전혀 행해지지 않은 경우
IF OLD_T:0 == NEW_T:0 && OLD_T:1 == NEW_T:1 && OLD_T:2 == NEW_T:2
	;죠교자가주인님의 경우
	IF TALENT:TARGET:주인님
		PRINTFORMW %CALLNAME:TARGET%(은)는 이대로 스스로%CALLNAME:MASTER%(을)를 조교하기로 했다…
	;그 이외
	ELSE
		PRINTFORMW 이대로%CALLNAME:TARGET%에 %CALLNAME:MASTER%(을)를 조교시키기로 했다…
	ENDIF
ENDIF
;죠교자의 변경을 하고 있는 경우
IF OLD_T:0 != NEW_T:0
	;구상를 위해서(때문에) 전용 변수에 교대원·선의등록번호를 넣는다
	SHOP_OLDTARGET = OLD_T:0
	SHOP_NEWTARGET = NEW_T:0
	;신죠교자컷 인
	PRINTL
	CALL CHARA_CUTIN(CSTR:10, CSTR:11, CSTR:로마자)
	PRINTL
	;일시적으로 TARGET를 구죠교자에 되돌린다
	TARGET = OLD_T:0
	;구죠교자구상호출
	CALL KOJO_EVENT(102)
	;주인님→주인님 이외에의 변경
	IF TARGET >= 0 && TALENT:TARGET:주인님
		PRINTFORMW %CALLNAME:TARGET%(은)는 %CALLNAME:SHOP_NEWTARGET%에 %CALLNAME:MASTER%의 조교를 위탁했다…
	;주인님 이외→주인님에의 변경
	ELSEIF TALENT:SHOP_NEWTARGET:주인님
		PRINTFORMW %CALLNAME:SHOP_NEWTARGET%(은)는 스스로%CALLNAME:MASTER%(을)를 조교하기로 했다…
	;주인님 이외→주인님 이외에의 변경
	ELSE
		PRINTFORMW \@ TARGET >= 0 ? %CALLNAME:TARGET%에 대신해, # \@%CALLNAME:SHOP_NEWTARGET%하지만 %CALLNAME:MASTER%(을)를 조교하게 되었다…
	ENDIF
	;TARGET를 신죠교자에
	TARGET = NEW_T:0
	;신죠교자구상호출
	CALL KOJO_EVENT(102, 1)
ENDIF
;신조수전원을 조사
FOR LCOUNT, 1, 3
	;조수LCOUNT가 신규 임명자의 경우
	IF FINDLASTELEMENT(OLD_T, NEW_T:LCOUNT, 1) == -1
		;ASSI에 신규 임명자를 지정
		ASSI = NEW_T:LCOUNT
		;조수임명구상호출
		CALL KOJO_EVENT_ASSI(102)
		CALL VOIDLINE_IF(RESULT)
		;ASSI를 원래대로 되돌린다
		ASSI = ASSI:1
	ENDIF
NEXT

;죠교자선택
@CHANGE_TARGET(ARG)
LOCALS = 
LOCALS += "<식별>CHANGE_TARGET"
LOCALS += "<상부>CHANGE_TARGET_SHOW"
LOCALS += "<조건>CHANGE_TARGET_REQU"
LOCALS += "<구상>죠교자"
LOCALS += ARG ? "<취소>취소불가" # "<취소>돌아가기"
LOCALS += "<항목>이름/체력·기력/경험치"
CALL SELECT_CHARA_LIST(LOCALS)
SIF RESULT != -1
	CALL SET_TARGET_ASSI(TARGET, 0, RESULT)

@CHANGE_TARGET_SHOW
PRINTFORML 현재 \@ TARGET >= 0 ? [{TARGET,2}] %CALLNAME:TARGET%가 조교중 # 조교자는 없습니다 \@
PRINTL 이번 조교자는 누구입니까?

@CHANGE_TARGET_REQU(ARG)
RETURN ARG == 0 ? 0 # 1

;조수선택
@CHANGE_ASSI(ARG)
LOCALS = 
LOCALS += "<식별>CHANGE_TARGET"
LOCALS += @"<상부>CHANGE_ASSI_SHOW_{ARG}"
LOCALS += "<조건>CHANGE_TARGET_REQU"
LOCALS += "<구상>죠교자"
LOCALS += "<선택>CHANGE_ASSI_CHOI"
LOCALS += "<취소>돌아가기"
LOCALS += "<항목>이름/체력·기력/경험치"
CALL SELECT_CHARA_LIST(LOCALS)
IF RESULT != -1
	SIF RESULT == -2
		RESULT = -1
	CALL SET_TARGET_ASSI(ASSI, ARG, RESULT)
ENDIF

@CHANGE_ASSI_SHOW_1
CALL CHANGE_ASSI_SHOW(1)
@CHANGE_ASSI_SHOW_2
CALL CHANGE_ASSI_SHOW(2)
@CHANGE_ASSI_SHOW(ARG)
IF ASSI:1 >= 0
	PRINTFORML 현재 [{ASSI:1,3}] %CALLNAME:(ASSI:1)%\@ ASSI:2 >= 0 ? 、[{ASSI:2,3}] %CALLNAME:(ASSI:2)% # \@가조수
ELSE
	PRINTL 현재 조수는 없습니다
ENDIF
PRINTFORML 이번 조수{ARG}는 누구입니까?
PRINTFORML 　[0]「없음」※조수{ARG}(을)를 지정하지 않습니다

@CHANGE_ASSI_CHOI(ARGS)
IF ARGS == "0"
	RESULTS '= "-2"
	RETURN 0
ENDIF
RETURN 1

;죠교자/조수의 임명에 응해 적당 이동·해임을 실시한다
@SET_TARGET_ASSI(LREF, ARG, ARG:1)
#DIM REF LREF,0
;전임자와 역할의 교환을 실시한다
LOCAL = LREF:ARG
IF ARG:1 != -1
	TARGET = TARGET == ARG:1 ? LOCAL # TARGET
	ASSI:1 = ASSI:1 == ARG:1 ? LOCAL # ASSI:1
	ASSI:2 = ASSI:2 == ARG:1 ? LOCAL # ASSI:2
ENDIF
;역할의 임명을 실시한다
LREF:ARG = ARG:1
;ASSI를 설정
ASSI = ASSI:1

;────────────────────────────────────
;캐릭터의 스테이터스 표시
;────────────────────────────────────
@SHOW_CHARADATA(ARG)
LOCALS = 
LOCALS += "<식별>SHOW_CHARADATA"
LOCALS += "<구상>죠교자"
LOCALS += "<취소>돌아가기"
LOCALS += "<항목>이름/체력·기력/경험치"
CALL SELECT_CHARA_LIST(LOCALS)
;돌아가기선택시 탈출
SIF RESULT < 0
	RETURN 0
LOCAL = RESULT
;스테이터스 표시
CALL PRINT_STATUS(RESULT)
;재묘화
RESTART

@SHOW_CHARADATA_PAD
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM L_SELECT
#DIM L_CHARA, 4
#DIM LINE
LINE = LINECOUNT
L_SELECT = 0
L_CHARA = MASTER, TARGET, ASSI:1, ASSI:2
$START
CLEARLINE LINECOUNT - LINE

DRAWLINE
FOR LOCAL, 0, 4
	SIF L_CHARA:LOCAL == -1
		CONTINUE
	SIF LOCAL == L_SELECT
		SETCOLOR 0x7070C0
	LOCALS = 
	LOCALS += @" %CALLNAME:LOCAL,16,LEFT% 체력({MAX(BASE:LOCAL:0,0),4}/{MAXBASE:LOCAL:0,4}) "
	LOCALS += @" [LV {CFLAG:LOCAL:0,2} NEXT {CFLAG:LOCAL:필요경험치 - CFLAG:LOCAL:1,6}]"
	LOCALS += AUTO_SPLIT("_죠교자_조수 1_조수 2", "_", LOCAL)
	SIF TALENT:(L_CHARA:LOCAL):주인님
		LOCALS += "<주인님>"
	PRINTBUTTON LOCALS, UNICODE(10000 + LOCAL)
	RESETCOLOR
	PRINTL 
	SIF LOCAL == 0
		DRAWLINE
NEXT
DRAWLINE
PRINTBUTTON "[" + KEYMARK_B+ "] - 돌아온다", KEY_B
ONEINPUTS
SELECTCASE RESULTS
	CASE KEY_A
		CALL PRINT_STATUS, L_CHARA:L_SELECT
	CASE KEY_B
		RETURN 0
	CASE KEY_UP
		DO 
			SIF --L_SELECT == -1
				L_SELECT = 3
		LOOP L_CHARA:L_SELECT == -1
	CASE KEY_DOWN
		L_SELECT++
		SIF L_CHARA:L_SELECT == -1
			L_SELECT = 0
ENDSELECT
GOTO START
@FLAGCHECK
PRINTL [0] - 돌아온다
PRINTL [1] - FLAG
PRINTL [2] - CFLAG
PRINTL [3] - FLAG의 덤프
PRINTL [4] - CFLAG의 덤프
$INPUT_LOOP1
INPUT
SELECTCASE RESULT
	CASE 0
	CASE 1
		VARSIZE FLAG
		LOCAL = RESULT
		PRINTFORML FLAG번호(0～{LOCAL-1})(을)를 입력해 주세요.
		$INPUT_LOOP2
		INPUT
		IF RESULT < 0 || LOCAL <= RESULT
			CLEARLINE 1
			REUSELASTLINE FLAG의 범위는(0～{LOCAL-1})입니다.
			GOTO INPUT_LOOP2
		ENDIF
		PRINTFORMW FLAG:{RESULT} = {FLAG:RESULT}
		RESTART
	CASE 2
		PRINTFORML 대상 캐릭터의 등록 번호(0～{CHARANUM-1})(을)를 입력해 주세요.
		$INPUT_LOOP3
		INPUT
		IF RESULT < 0 || CHARANUM <= RESULT
			CLEARLINE 1
			REUSELASTLINE 존재하지 않습니다.
			GOTO INPUT_LOOP3
		ENDIF
		LOCAL = RESULT
		VARSIZE CFLAG
		LOCAL:1 = RESULT
		PRINTFORML %CALLNAME:LOCAL%하지만 선택되었습니다.
		PRINTFORML CFLAG번호(0～{LOCAL:1-1})(을)를 입력해 주세요.
		$INPUT_LOOP4
		INPUT
		IF RESULT < 0 || LOCAL:1 <= RESULT
			CLEARLINE 1
			REUSELASTLINE CFLAG의 범위는(0～{LOCAL:1-1})입니다
			GOTO INPUT_LOOP4
		ENDIF
		PRINTFORMW %CALLNAME:LOCAL%의CFLAG:{RESULT} = {CFLAG:LOCAL:RESULT}
		RESTART
	CASE 3
		PRINTL FLAG일람(값이0의 것은 생략)
		FOR LOCAL, 0, VARSIZE("FLAG")
			SIF FLAG:LOCAL
				PRINTFORMLC [FLAG:{LOCAL,4}={FLAG:LOCAL,10}]
		NEXT
		WAIT
		RESTART
	CASE 4
		PRINTFORML 대상 캐릭터의 등록 번호(0～{CHARANUM-1})(을)를 입력해 주세요.
		$INPUT_LOOP5
		INPUT
		IF RESULT < 0 || CHARANUM <= RESULT
			CLEARLINE 1
			REUSELASTLINE 존재하지 않습니다.
			GOTO INPUT_LOOP5
		ENDIF
		PRINTFORML %CALLNAME:RESULT%의CFLAG일람(값이0의 것은 생략)
		FOR LOCAL, 0, VARSIZE("CFLAG")
			SIF CFLAG:RESULT:LOCAL
				PRINTFORMLC [CFLAG:{LOCAL,4}={CFLAG:RESULT:LOCAL,10}]
		NEXT
		WAIT
		RESTART
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP1
ENDSELECT

@SET_AUTO
#DIM SELECT
#DIM LINE
#DIMS 세트명, 5
#LOCALSSIZE 100
REDRAW 0
SELECT = -1
VARSET LOCALS
LINE = LINECOUNT
FOR LOCAL, 0, 5
	SIF !STRCOUNT(GLOBALS:@"오토모드등록{LOCAL}", "_")
		GLOBALS:@"오토모드등록{LOCAL}" = 등록{LOCAL}_%GLOBALS:@"오토모드등록{LOCAL}"%
	SPLIT GLOBALS:@"오토모드등록{LOCAL}", "_", RESULTS
	세트명:LOCAL = %RESULTS%
NEXT
SPLIT GLOBALS:@"오토모드등록{FLAG:선택세트}", "_", RESULTS
SPLIT RESULTS:1, "/", LOCALS
$START
;삭제 커맨드 및이상치의 체크
FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL == ""
		CONTINUE
	SIF GETNUM(TRAINNAME, LOCALS:LOCAL) >= 0 || LOCALS:LOCAL == "이루는대로"
		CONTINUE
	ARRAYREMOVE LOCALS, LOCAL, 1
	LOCAL--
NEXT
;공행의 정리
FOR LOCAL, 0, 100 - MATCH(LOCALS, "")
	SIF LOCALS:LOCAL != ""
		CONTINUE
	ARRAYREMOVE LOCALS, LOCAL, 1
	LOCAL--
NEXT
;신규 추가급당했다커맨드의 체크
FOR LOCAL, 0, 100
	SIF TRAINNAME:LOCAL == ""
		CONTINUE
	;사양상통상선택 할 수 있으면 문제가 있으므로 컷 해 둔다
	SIF TRAINNAME:LOCAL == "이루는대로"
		CONTINUE
	SIF FINDELEMENT(LOCALS, TRAINNAME:LOCAL) >= 0
		CONTINUE
	LOCALS:(100 - MATCH(LOCALS, "")) = %TRAINNAME:LOCAL%
NEXT
CLEARLINE LINECOUNT - LINE
DRAWLINE
PRINTBUTTON @"오토모드<%\@FLAG:오토모드 > 0?ＯＮ#ＯＦＦ\@%>", 1000
IF FLAG:오토모드 > 0
	PRINT 　
	PRINTBUTTON @"경과턴수<{FLAG:오토모드}>", 1001
ENDIF
PRINTL 
DRAWLINE
PRINTFORML <%세트명:(FLAG:선택세트)%>
FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL == ""
		BREAK
	SIF LOCAL == SELECT
		SETCOLOR 0xC07070
	PRINTBUTTON @"%"["+LOCALS:LOCAL+"]",24,LEFT% > ", LOCAL
	SIF LOCAL % 4 == 3
		PRINTL 
	RESETCOLOR
NEXT
PRINTL 
DRAWLINE
PRINTL <세트>
FOR LOCAL, 0, 5
	SIF LOCAL == FLAG:선택세트
		SETCOLOR 0xC07070
	PRINTBUTTON @"%"["+세트명:LOCAL+"]",25,LEFT%", 2000 + LOCAL
	RESETCOLOR
	SIF LOCAL % 3 == 2
		PRINTL 
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTL <세트 조작>
PRINTBUTTON "[명칭 변경]　", 2010
PRINT 　
PRINTBUTTON "[등록]　　", 2011
PRINT 　
PRINTBUTTON "[리셋트]", 2012
PRINTL 
DRAWLINE
PRINTL <디폴트 세트>
PRINTBUTTON "[번호순서]　　", 2020
PRINT 　
PRINTBUTTON "[순종]　　", 2021
PRINT 　
PRINTBUTTON "[반항적]　", 2022
PRINTL 
DRAWLINE
PRINTL <표시 설정>
SIF FLAG:오토모드표시설정 == 0
	SETCOLOR 0xC07070
PRINTBUTTON "[통상]　　", 2030
RESETCOLOR
PRINT 　
SIF FLAG:오토모드표시설정 == 1
	SETCOLOR 0xC07070
PRINTBUTTON "[간략]　　", 2031
PRINT 　
RESETCOLOR
SIF FLAG:오토모드표시설정 == 2
	SETCOLOR 0xC07070
;PRINTBUTTON "[디버그비표시]", 2032
RESETCOLOR
PRINTL 
DRAWLINE
PRINTL <우선 대만족 보너스>
PRINTBUTTON "["+AUTO_SPLIT("언제나 대로/어널을 괴롭혀/징계 기사라고/격렬하게 해/응석부리게 해/망가질 때까지 해/밖에 나가고 싶다/잉세대/쉬게 해/안기고 싶다/봉사하고 싶다/귀여워해/랜덤", "/", FLAG:우선대만족보너스 % 100)+"]", 3000
PRINTL 
PRINTBUTTON "["+AUTO_SPLIT("언제나 대로/어널을 괴롭혀/징계 기사라고/격렬하게 해/응석부리게 해/망가질 때까지 해/밖에 나가고 싶다/잉세대/쉬게 해/안기고 싶다/봉사하고 싶다/귀여워해/랜덤", "/", FLAG:우선대만족보너스 / 100)+"]", 3001
PRINTL (예비)
DRAWLINE
PRINTBUTTON "[돌아온다]", 999
INPUT
SELECTCASE RESULT
	CASE 2000 TO 2004
		IF FLAG:선택세트 != RESULT - 2000
			FLAG:선택세트 = RESULT - 2000
			SPLIT GLOBALS:@"오토모드등록{FLAG:선택세트}", "_", RESULTS
			SPLIT RESULTS:1, "/", LOCALS
		ENDIF
	CASE 2010
		PRINTL 명칭을 입력해 주세요
		INPUTS
		SIF RESULTS == ""
			RESULTS = 등록{FLAG:선택세트}
		세트명:(FLAG:선택세트) = %RESULTS%
	CASE 2011, 999
		PRINTW 현재세트를 등록합니다
		GLOBALS:@"오토모드등록{FLAG:선택세트}" = %세트명:(FLAG:선택세트)%_
		FOR LOCAL, 0, 100 - MATCH(LOCALS, "")
			SIF LOCALS:LOCAL == ""
				BREAK
			GLOBALS:@"오토모드등록{FLAG:선택세트}" += LOCALS:LOCAL
			GLOBALS:@"오토모드등록{FLAG:선택세트}" += "/"
		NEXT
		;말미의 단락 기호를 깎는다
		GLOBALS:@"오토모드등록{FLAG:선택세트}" = %SUBSTRING(GLOBALS:@"오토모드등록{FLAG:선택세트}", 0, STRLENS(GLOBALS:@"오토모드등록{FLAG:선택세트}") - 1)%
		SAVEGLOBAL
		IF RESULT == 999
			CALL AUTO_MODE, 1
			REDRAW 1
			RETURN 0
		ENDIF
	CASE 2012
		SPLIT GLOBALS:@"오토모드등록{FLAG:선택세트}", "_", RESULTS
		SPLIT RESULTS:1, "/", LOCALS
	CASE 2020
		VARSET LOCALS
	CASE 2021
		VARSET LOCALS
		LOCALS += "애정을 담아 한다/허리를 흔든다/가게해줘!/안에 내!/적극적으로 한다/쾌감을 받아들인다/기분좋게 해/상냥하게 응답한다/더 하드하게 해/"
		LOCALS += "소극적으로 한다/용서를 빌다/부끄러워한다/마음 약하게 응답한다/침묵한다/무뚝뚝하게 응답한다/강하게 응답한다/빈정거린다/쾌감을 참는다/"
		LOCALS += "비명지른다/고통을 참는다/자위를 시작한다/싫어한다/서투르다고 모욕한다/겁없이 웃는다/무서워한다/"
		LOCALS += "안에 내면 안돼!/거부한다/날뛴다/도구를 제거한다"
		SPLIT LOCALS, "/", LOCALS
	CASE 2022
		VARSET LOCALS
		LOCALS += "날뛴다/도구를 제거한다/거부한다/안에 내면 안돼!/"
		LOCALS += "서투르다고 모욕한다/싫어한다/겁없이 웃는다/비명지른다/고통을 참는다/자위를 시작한다/무서워한다/"
		LOCALS += "소극적으로 한다/용서를 빌다/부끄러워한다/마음 약하게 응답한다/침묵한다/무뚝뚝하게 응답한다/강하게 응답한다/빈정거린다/쾌감을 참는다/"
		LOCALS += "안에 내!/적극적으로 한다/쾌감을 받아들인다/기분좋게 해/상냥하게 응답한다/더 하드하게 해/애정을 담아 한다/허리를 흔든다/가게해줘!"
		SPLIT LOCALS, "/", LOCALS
	CASE 2030, 2031, 2032
		FLAG:오토모드표시설정 = RESULT - 2030
	CASE 3000
		IF FLAG:우선대만족보너스 % 100 == 12
			FLAG:우선대만족보너스 -= 12
		ELSE
			FLAG:우선대만족보너스++
		ENDIF
	CASE 3001
		IF FLAG:우선대만족보너스 / 100 == 12
			FLAG:우선대만족보너스 -= 900
		ELSEIF FLAG:우선대만족보너스 / 100 == 5
			FLAG:우선대만족보너스 += 300
		ELSE
			FLAG:우선대만족보너스 += 100
		ENDIF
		
	CASE 1001
		IF FLAG:오토모드 > 0
			CALL INPUT_MANY, 1, 100
			FLAG:오토모드 = RESULT
		ENDIF
	CASE 1000
		IF !FLAG:오토모드
			FLAG:오토모드 = 1
		ELSE
			FLAG:오토모드 *= -1
		ENDIF
	CASEELSE
		IF SELECT == -1
			SELECT = RESULT
		ELSE
			SWAP LOCALS:SELECT, LOCALS:RESULT
			SELECT = -1
		ENDIF
ENDSELECT
GOTO START

@CHECK_TALENT_CRI
#LOCALSIZE 10
#LOCALSSIZE 200
#DIM LINE
#DIM LINENUM, 2
#DIM LCHARANUM
#DIM LMASTER
#DIM LTARGET
#DIM LTEMP
#DIMS LTEMPS

#DIM LVAR, 30
#DIM LSEX_CRI, 30
#DIM LMAIDEN_CRI, 30

#DIM LTALENT_CRI_EX, 1000
#DIM LTALENT_MENU_CRI, 1000, 20
#DIM LTALENT_CRI, 1000, 20, 30

#DIMS LSEX_OPTION
#DIM LFAVOR_OPTION
#DIM LMAIDEN_OPTION
#DIM LFUTANARI_OPTION

LCHARANUM = CHARANUM
LMASTER = MASTER
LTARGET = TARGET
LINENUM = 15, 35

LSEX_OPTION = 성별무시
LMAIDEN_OPTION = 1

VARSET LTALENT_MENU_CRI:0:0
VARSET LTALENT_CRI:0:0:0
FOR LOCAL, 0, VARSIZE("TALENT")
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	ADDVOIDCHARA
	TARGET = CHARANUM - 1
	TALENT:LOCAL = 1
	;변수를 초기화 ;변수 초기화
	CALL SET_CRI_VAR
	FOR LOCAL:1, 0, 11
		;휴식를 튕긴다
		;사역는 처리하지 않는다
		SIF LOCAL:1 == 8 || LOCAL:1 == 9
			CONTINUE
		CALL SET_CRI_VAR, MENU(LOCAL:1), 1
	NEXT
	CALL ACT_MENU_TALENT_T
	VARSET LVAR
	VARSET RESULT
	CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
	FOR LOCAL:1, 0, 20
		LTALENT_MENU_CRI:LOCAL:(LOCAL:1) += RESULT:(LOCAL:1) * 2
	NEXT
	VARSET RESULT
	CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
	FOR LOCAL:1, 0, 20
		LTALENT_MENU_CRI:LOCAL:(LOCAL:1) += RESULT:(LOCAL:1)
		SIF LTALENT_MENU_CRI:LOCAL:(LOCAL:1)
			LTALENT_CRI_EX:LOCAL = 1
	NEXT
	FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
		CALL RES_CRI_VAR, 1
		CALL ACT_M_INIT(LOCAL:1)
		TRYCCALLFORM ACT_ACTM{LOCAL:1}_TALENT_T
		CATCH
			CONTINUE
		ENDCATCH
		VARSET LVAR
		VARSET RESULT
		CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
		FOR LOCAL:2, 0, 30
			LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2) += RESULT:(LOCAL:2) * 2
		NEXT
		CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
		FOR LOCAL:2, 0, 30
			LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2) += RESULT:(LOCAL:2)
			SIF LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2)
				LTALENT_CRI_EX:LOCAL = 1
		NEXT
	NEXT
	DELCHARA TARGET
NEXT
TARGET= LTARGET
$START
DRAWLINE
LOCALS = 소질 해석/캐릭터 지정/종료
LOCAL = 0
WHILE AUTO_SPLIT(LOCALS, "/", LOCAL) != ""
	PRINTBUTTON "[" + AUTO_SPLIT(LOCALS, "/", LOCAL) +"]", AUTO_SPLIT(LOCALS, "/", LOCAL) 
	PRINTL 
	LOCAL++
WEND
CALL INPUT_SPLIT, LOCALS
SELECTCASE RESULTS
	CASE "소질 해석"
		$START_TALENT
		LOCALS:1 = 
		LOCAL:1 = 0
		FOR LOCAL, 0, VARSIZE("TALENT") + 2
			IF LOCAL == VARSIZE("TALENT") + 1
				LOCALS:1 += "END"
				PRINTBUTTON @"%"[돌아온다]", 16, LEFT%", "END"
			ELSEIF LOCAL == VARSIZE("TALENT")
				LOCALS:1 += "ALL/"
				PRINTBUTTON @"%"[전소질]", 16, LEFT%", "ALL"
			ELSE
				SIF !LTALENT_CRI_EX:LOCAL
					CONTINUE
				LOCALS:1 += TALENTNAME:LOCAL + "/"
				PRINTBUTTON @"%"[" + TALENTNAME:LOCAL +"]", 16, LEFT%", TALENTNAME:LOCAL
			ENDIF
			SIF LOCAL:1 % 6 == 0
				PRINTL 
			LOCAL:1++
		NEXT
		SIF !LINEISEMPTY()
			PRINTL 
		CALL INPUT_SPLIT, LOCALS:1
		SIF RESULTS == "END"
			GOTO START
		FOR LOCAL, 0, VARSIZE("TALENT")
			IF RESULTS == "ALL"
				SIF !LTALENT_CRI_EX:LOCAL
					CONTINUE
			ELSE
				SIF TALENTNAME:LOCAL != RESULTS
					CONTINUE
			ENDIF
			DRAWLINE
			PRINTFORML %TALENTNAME:LOCAL%
			FOR LOCAL:1, 0, 20
				LTEMP = LTALENT_MENU_CRI:LOCAL:(LOCAL:1)
				SIF !LTEMP
					CONTINUE
				PRINTFORM %MENU(LOCAL:1), 24, LEFT%
				IF LTEMP > 0
					SETCOLOR 0x7070C0
				ELSE
					SETCOLOR 0xC07070
				ENDIF
				PRINTFORML {LTEMP,4}
				RESETCOLOR
			NEXT
			FOR LOCAL:1, 0, 20
				FOR LOCAL:2, 0, 30
					LTEMP = LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2)
					SIF !LTEMP
						CONTINUE
					PRINTFORM %GET_ACTABBNAME(ACTM_NUM(LOCAL:1, LOCAL:2)) + "(" + MENU(LOCAL:1) + ")", 24, LEFT%
					IF LTEMP > 0
						SETCOLOR 0x7070C0
					ELSE
						SETCOLOR 0xC07070
					ENDIF
					PRINTFORML {LTEMP,4}
					RESETCOLOR
				NEXT
			NEXT
		NEXT
		WAIT
		GOTO START_TALENT
	CASE "캐릭터 지정"
		$START_CHARA
		LOCALS:1 = END/죠교자를후타나리로 한다/성별 무시/죠교대상가 남자/죠교대상가 여자/죠교대상가후타나리/첫체험 무시/죠교대상가처녀/호의소/호의중/호의다
		DRAWLINE
		FOR LOCAL, 1, FLAG:Cha총수 + 1
			LOCALS:1 += "/" + CSVCALLNAME(LOCAL, 0)
			PRINTBUTTON @"%"[" + CSVCALLNAME(LOCAL, 0) +"]", 24, LEFT%", CSVCALLNAME(LOCAL, 0)
			SIF LOCAL % 4 == 0
				PRINTL 
		NEXT
		SIF !LINEISEMPTY()
			PRINTL 
		DRAWLINE
		FOR LOCAL, 0 ,4
			LTEMPS = %AUTO_SPLIT("성별 무시/죠교대상가 남자/죠교대상가 여자/죠교대상가후타나리", "/", LOCAL)%
			SIF LTEMPS == LSEX_OPTION
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		SIF LFUTANARI_OPTION
			SETCOLOR 0x70C070
		PRINTBUTTON @"%"[조교자를 뚜껑나름대로 한다]", 24, LEFT%", "조교자를 뚜껑나름대로 한다"
		RESETCOLOR
		PRINTL 
		DRAWLINE
		FOR LOCAL, 0 ,2
			LTEMPS = %AUTO_SPLIT("첫체험 무시/죠교대상가처녀", "/", LOCAL)%
			SIF GETBIT(LMAIDEN_OPTION, LOCAL)
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		PRINTL 
		FOR LOCAL, 0 ,3
			LTEMPS = %AUTO_SPLIT("호의소/호의중/호의다", "/", LOCAL)%
			SIF LFAVOR_OPTION == LOCAL
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		PRINTL 
		DRAWLINE
		PRINTBUTTON @"%"[돌아온다]", 24, LEFT%", "END"
		PRINTL 
		CALL INPUT_SPLIT, LOCALS:1
		SIF RESULTS == "END"
			GOTO START
		IF GROUPMATCH(RESULTS, "성별 무시", "죠교대상가 남자", "죠교대상가 여자", "죠교대상가후타나리")
			LSEX_OPTION = %RESULTS%
			GOTO START_CHARA
		ENDIF
		IF GROUPMATCH(RESULTS, "죠교자를후타나리로 한다", "첫체험 무시", "죠교대상가처녀", "호의소", "호의중", "호의다")
			SIF RESULTS == "죠교자를후타나리로 한다"
				INVERTBIT LFUTANARI_OPTION, 0
			SIF RESULTS == "첫체험 무시"
				INVERTBIT LMAIDEN_OPTION, 0
			SIF RESULTS == "죠교대상가처녀"
				INVERTBIT LMAIDEN_OPTION, 1
			SIF RESULTS == "호의소"
				LFAVOR_OPTION = 0
			SIF RESULTS == "호의중"
				LFAVOR_OPTION = 1
			SIF RESULTS == "호의다"
				LFAVOR_OPTION = 2
			GOTO START_CHARA
		ENDIF
		FOR LOCAL, 0, FLAG:Cha총수 + 1
			SIF RESULTS != CSVCALLNAME(LOCAL, 0)
				CONTINUE
			ADDCHARA LOCAL
			BREAK
		NEXT
		TARGET = CHARANUM - 1
		ADDVOIDCHARA
		MASTER = CHARANUM - 1
		SIF LSEX_OPTION == "죠교대상가 남자"
			TALENT:MASTER:남자 = 1
		SIF LSEX_OPTION == "죠교대상가후타나리"
			TALENT:MASTER:후타나리 = 1
		SIF LFUTANARI_OPTION
			TALENT:후타나리 = 1
		SIF GETBIT(LMAIDEN_OPTION, 1)
			TALENT:MASTER:처녀 = 1
		CFLAG:호의 = LFAVOR_OPTION * 1000
		DRAWLINE
		PRINTFORML 　%CALLNAME:TARGET%
		;변수를 초기화 ;변수 초기화
		CALL SET_CRI_VAR
		FOR LOCAL:1, 0, 11
			;휴식를 튕긴다
			;사역는 처리하지 않는다
			SIF LOCAL:1 == 8 || LOCAL:1 == 9
				CONTINUE
			CALL SET_CRI_VAR, MENU(LOCAL:1), 1
		NEXT
		LINE = LINECOUNT
		DRAWLINE
		PRINTFORML 　메뉴 선택
		DRAWLINE
		VARSET LVAR
		VARSET RESULT
		VARSET LSEX_CRI
		VARSET LMAIDEN_CRI
		IF LSEX_OPTION != "성별 무시"
			CALL CHA_CRI_VAR, "성별"
			CALL ACT_MENU_SEX
			CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
			FOR LOCAL:1, 0, 30
				LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
			FOR LOCAL:1, 0, 30
				LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
		ENDIF
		IF !GETBIT(LMAIDEN_OPTION, 0)
			VARSET RESULT
			CALL CHA_CRI_VAR, "죠교자가처녀"
			CALL ACT_MENU_MAIDEN_T
			CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
			VARSET RESULT
			CALL CHA_CRI_VAR, "죠교대상가처녀"
			CALL ACT_MENU_MAIDEN_M
			CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
		ENDIF
		CALL CHA_CRI_VAR, "소질"
		VARSET RESULT
		CALL ACT_MENU_TALENT_T
		CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
		FOR LOCAL:1, 0, 30
			LVAR:(LOCAL:1) += RESULT:(LOCAL:1) * 2
		NEXT
		CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
		FOR LOCAL:1, 0, 30
			LVAR:(LOCAL:1) += RESULT:(LOCAL:1)
		NEXT
		FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
			SETCOLOR 0xC07070
			PRINTFORM %UNICODE(0x25AE) * ABS(MIN(LVAR:(LOCAL:1), 0)),50,RIGHT%
			RESETCOLOR
			PRINTFORM  %MENU(LOCAL:1)%({LVAR:(LOCAL:1), 4}) 
			SETCOLOR 0x7070C0
			PRINTFORM %UNICODE(0x25AE) * MAX(LVAR:(LOCAL:1), 0),50,LEFT%
			PRINTFORML 
		NEXT
		WHILE LINECOUNT < LINE + LINENUM
			PRINTFORML 
		WEND
		RESETCOLOR
		LOCAL:3 = 0
		FOR LOCAL:1, 0, VARSIZE("TALENT")
			SIF !TALENT:(LOCAL:1)
				CONTINUE
			LTEMPS = 
			FOR LOCAL:2, 0, STRCOUNT(MENULIST(), "/") + 1
				IF LTALENT_MENU_CRI:(LOCAL:1):(LOCAL:2)
					SIF LTEMPS != ""
						LTEMPS += "/"
					LTEMPS += MENU(LOCAL:2) + "_" + TOSTR(LTALENT_MENU_CRI:(LOCAL:1):(LOCAL:2))
				ENDIF
			NEXT
			IF LTEMPS != ""
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %TALENTNAME:(LOCAL:1), 16, LEFT%
				SPLIT LTEMPS, "/", LOCALS
				FOR LOCAL:2, 0, RESULT
					LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
		NEXT
		IF MATCH(LMAIDEN_CRI, 0) != VARSIZE("LMAIDEN_CRI")
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %"첫체험보정", 16, LEFT%
			FOR LOCAL:2, 0, 30
				SIF !LMAIDEN_CRI:(LOCAL:2)
					CONTINUE
				LTEMP = LMAIDEN_CRI:(LOCAL:2)
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%MENU(LOCAL:2)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		ENDIF
		IF MATCH(LSEX_CRI, 0) != VARSIZE("LSEX_CRI")
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %"성별보정", 16, LEFT%
			FOR LOCAL:2, 0, 30
				SIF !LSEX_CRI:(LOCAL:2)
					CONTINUE
				LTEMP = LSEX_CRI:(LOCAL:2)
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%MENU(LOCAL:2)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		ENDIF
		LOCAL:3 = 0
		FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
			LTEMPS = 
			FOR LOCAL:2, 0, VARSIZE("TALENT")
				SIF !TALENT:(LOCAL:2)
					CONTINUE
				LTEMP = LTALENT_MENU_CRI:(LOCAL:2):(LOCAL:1)
				SIF !LTEMP
					CONTINUE
				LTEMPS += "/" + TALENTNAME:(LOCAL:2) + "_" + TOSTR(LTEMP)
			NEXT
			SIF LMAIDEN_CRI:(LOCAL:1)
				LTEMPS += "/" + "첫체험보정" + "_" + TOSTR(LMAIDEN_CRI:(LOCAL:1))
			SIF LSEX_CRI:(LOCAL:1)
				LTEMPS += "/" + "성별보정" + "_" + TOSTR(LSEX_CRI:(LOCAL:1))
			SIF LTEMPS == ""
				CONTINUE
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %MENU(LOCAL:1), 16, LEFT%
			SPLIT LTEMPS, "/", LOCALS
			FOR LOCAL:2, 1, RESULT
				LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		NEXT
		WHILE LINECOUNT < LINE + LINENUM:1
			PRINTFORML 
		WEND
		WAIT
		FOR LOCAL, 0, STRCOUNT(MENULIST(), "/") + 1
			CALL RES_CRI_VAR, 1
			CALL ACT_M_INIT(LOCAL)
			VARSET LVAR
			VARSET LSEX_CRI
			VARSET LMAIDEN_CRI
			VARSET RESULT
			IF !GETBIT(LMAIDEN_OPTION, 0)
				CALL CHA_CRI_VAR, "죠교대상가처녀"
				TRYCCALLFORM ACT_ACTM{LOCAL}_MAIDEN_M
					CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
				CALL CHA_CRI_VAR, "죠교자가처녀"
				TRYCCALLFORM ACT_ACTM{LOCAL}_MAIDEN_T
					CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
			ENDIF
			IF LSEX_OPTION != "성별 무시"
				CALL CHA_CRI_VAR, "성별"
				TRYCCALLFORM ACT_ACTM{LOCAL}_GENDER_T
					CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
					FOR LOCAL:1, 0, 30
						LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
					FOR LOCAL:1, 0, 30
						LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
			ENDIF
			CALL CHA_CRI_VAR, "소질"
			TRYCCALLFORM ACT_ACTM{LOCAL}_TALENT_T
			CATCH
			ENDCATCH
			CALLF VARIABLE_OF_CRI, "GET", "고정 누계"
			FOR LOCAL:1, 0, 30
				LVAR:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "변동 누계"
			FOR LOCAL:1, 0, 30
				LVAR:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
			LINE = LINECOUNT
			DRAWLINE
			PRINTFORML 　%MENU(LOCAL)%
			DRAWLINE
			FOR LOCAL:1, 0, ACTM_NUM(LOCAL)
				SETCOLOR 0xC07070
				PRINTFORM %UNICODE(0x25AE) * ABS(MIN(LVAR:(LOCAL:1), 0)),50,RIGHT%
				RESETCOLOR
				PRINTFORM  %GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:1))%({LVAR:(LOCAL:1), 4}) 
				SETCOLOR 0x7070C0
				PRINTFORM %UNICODE(0x25AE) * MAX(LVAR:(LOCAL:1), 0),50,LEFT%
				PRINTFORML 
			NEXT
			WHILE LINECOUNT < LINE + LINENUM
				PRINTFORML 
			WEND
			RESETCOLOR
			LOCAL:3 = 0
			FOR LOCAL:1, 0, VARSIZE("TALENT")
				SIF !TALENT:(LOCAL:1)
					CONTINUE
				LTEMPS = 
				FOR LOCAL:2, 0, ACTM_NUM(LOCAL)
					IF LTALENT_CRI:(LOCAL:1):LOCAL:(LOCAL:2)
						SIF LTEMPS != ""
							LTEMPS += "/"
						LTEMPS += GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2)) + "_" + TOSTR(LTALENT_CRI:(LOCAL:1):LOCAL:(LOCAL:2))
					ENDIF
				NEXT
				IF LTEMPS != ""
					SIF LOCAL:3++ == 0
						DRAWLINE
					PRINTFORM %TALENTNAME:(LOCAL:1), 16, LEFT%
					SPLIT LTEMPS, "/", LOCALS
					FOR LOCAL:2, 0, RESULT
						LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
						SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
						PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
						RESETCOLOR
					NEXT
					PRINTL 
				ENDIF
			NEXT
			IF MATCH(LMAIDEN_CRI, 0) != VARSIZE("LMAIDEN_CRI")
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %"첫체험보정", 16, LEFT%
				FOR LOCAL:2, 0, 30
					SIF !LMAIDEN_CRI:(LOCAL:2)
						CONTINUE
					LTEMP = LMAIDEN_CRI:(LOCAL:2)
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2))% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
			IF MATCH(LSEX_CRI, 0) != VARSIZE("LSEX_CRI")
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %"성별보정", 16, LEFT%
				FOR LOCAL:2, 0, 30
					SIF !LSEX_CRI:(LOCAL:2)
						CONTINUE
					LTEMP = LSEX_CRI:(LOCAL:2)
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2))% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
			DRAWLINE
			FOR LOCAL:1, 0, ACTM_NUM(LOCAL)
				LTEMPS = 
				FOR LOCAL:2, 0, VARSIZE("TALENT")
					SIF !TALENT:(LOCAL:2)
						CONTINUE
					LTEMP = LTALENT_CRI:(LOCAL:2):LOCAL:(LOCAL:1)
					SIF !LTEMP
						CONTINUE
					LTEMPS += "/" + TALENTNAME:(LOCAL:2) + "_" + TOSTR(LTEMP)
				NEXT
				SIF LMAIDEN_CRI:(LOCAL:1)
					LTEMPS += "/" + "첫체험보정" + "_" + TOSTR(LMAIDEN_CRI:(LOCAL:1))
				SIF LSEX_CRI:(LOCAL:1)
					LTEMPS += "/" + "성별보정" + "_" + TOSTR(LSEX_CRI:(LOCAL:1))
				SIF LTEMPS == ""
					CONTINUE
				PRINTFORM %GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:1)), 16, LEFT%
				SPLIT LTEMPS, "/", LOCALS
				FOR LOCAL:2, 1, RESULT
					LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			NEXT
			WHILE LINECOUNT < LINE + LINENUM:1
				PRINTFORML 
			WEND
			WAIT
		NEXT
		WHILE CHARANUM > LCHARANUM
			DELCHARA CHARANUM -  1
		WEND
		GOTO START_CHARA
	CASE "종료"
		TARGET = LTARGET
		MASTER = LMASTER
		WHILE CHARANUM > LCHARANUM
			DELCHARA CHARANUM -  1
		WEND
		RETURN 1
ENDSELECT
;────────────────────────────────────
;세이브인포메이션		→SYSTEM_DATAMENU.ERB에서 대체
;────────────────────────────────────
;@SAVEINFO
;PUTFORM {DAY+1}일째%GET_TIME()% \@ TARGET >= 0? %CALLNAME:TARGET%가 %CALLNAME:MASTER%를조교중 # \@


;────────────────────────────────────
;버전 업
;────────────────────────────────────
@VERSION_UP

