;-------------------------------------------------------------------------------
;	DATAMENU로 사용하는 magic number-
;-------------------------------------------------------------------------------
;	우선 SAVE와 AUTO로 90+10로 100범위, 옵션으로 변경할 수 있도록(듯이)한다면
;	magic number-를 적당한 FLAG라도 하면 k
;===============================================================================
@SAVE_AREA(ARG)
#FUNCTION
RETURNF LIMIT(100 + ARG, 0, 1000)

@AUTOSAVE_AREA(ARG)
#FUNCTION
RETURNF LIMIT(10 + ARG, 0, SAVE_AREA())

;COPYDATA용 GLOBAL:x/GLOBALS:x
;	현단계에서는코비─원 = GLOBAL:998, 코비─선 = GLOBAL:999를 사용
;	대화용일시사용 = GLOBALS:99를 사용, TEMP세이브 범위에 1000을 사용
;	여기를 바꾸면 변경가능
@COPYDATA_BY()
#FUNCTION
RETURNF 998

@COPYDATA_TO()
#FUNCTION
RETURNF 999

@COPYDATA_TEMP()
#FUNCTION
RETURNF 1000

@COPYDATA_TAG()
#FUNCTION
RETURNF 99

;-------------------------------------------------------------------------------
;	TITLE_LOADGAME	타이틀 화면으로부터로드를 선택했을 경우의 독자 처리
;-------------------------------------------------------------------------------
;===============================================================================
@TITLE_LOADGAME
CALL DATAMENU("TITLELOAD")

;-------------------------------------------------------------------------------
;	EVENTLOAD
;-------------------------------------------------------------------------------
;	Rev#로 EVENTLOAD가 눈에 띄지 않았기 때문에 이런 곳에
;	네, 진심으로?
;===============================================================================
@EVENTLOAD
LOADGLOBAL
CALLF ANIME_CONFIG("SET", GLOBAL)
SIF GLOBALS:COPYDATA_TAG() != ""
	CALL DATAMENU("NOWCOPY")

REDRAW 0
CALL DAY_EFFECT

;FLAG:2의 갱신
FLAG:주인님 = FINDTALENT("주인님")
; 플레이시간용
CALLF PLAYTIME("SET")

;-------------------------------------------------------------------------------
;	세이브시 용 일시 변수 초기화
;-------------------------------------------------------------------------------
;	본 그대로
;	Rev#의세이브시스템으로 어떤 것을 지워도 좋은 것인지 모르기 때문에 줄머리 RETURN
;===============================================================================
@VAR_INITIALIZE
#LOCALSIZE 1
RETURN RESULT

;↓이하 처리-------------------------------------------------
VARSET TFLAG
VARSET TSTR
FOR LOCAL, 0, VARSIZE("TCVAR")
	CVARSET TCVAR, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("BASE")
	CVARSET DOWNBASE, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("PALAM")
	CVARSET PALAM, LOCAL
	CVARSET GOTJUEL, LOCAL
	CVARSET CUP, LOCAL
	CVARSET CDOWN, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("EX")
	CVARSET EX, LOCAL
	CVARSET NOWEX, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("SOURCE")
	CVARSET SOURCE, LOCAL
NEXT
FOR LOCAL, 0, VARSIZE("TEQUIP")
	CVARSET TEQUIP, LOCAL
NEXT
RETURN RESULT

;-------------------------------------------------------------------------------
;	SAVEINFO		(사용하지 않고)
;-------------------------------------------------------------------------------
;===============================================================================
; →SHOP.ERB에서 comment out

;-------------------------------------------------------------------------------
;	SAVEINFOF(식 안의 함수)
;-------------------------------------------------------------------------------
;	SAVEINFO의 식 안의 함수판  PUTFORM?   그게 뭐야 맛있는거야?
;===============================================================================
@SAVEINFOF
#FUNCTIONS
#LOCALSSIZE 1
GETTIME
LOCALS = %RESULTS% %CALLNAME:MASTER% {DAY+1}일째 %GET_TIME()% \@ TARGET >= 0 ? %CALLNAME:TARGET%이(가) %CALLNAME:MASTER%을(를) 조교 중 # \@【%PLAYTIME("GET")%】
RETURNF LOCALS
;LOCALS = %RESULTS% %CALLNAME:MASTER% {DAY+1}일째%GET_TIME()% \@ TARGET >= 0 ? %CALLNAME:TARGET%가%CALLNAME:MASTER%를조교중 # \@【%PLAYTIME("GET")%】
;-------------------------------------------------------------------------------
;	CHKDATA의 RESULTS로부터세이브일시를취득 한다(식 안의 함수)
;-------------------------------------------------------------------------------
;	예  2010/04/01 12:34:56  →  20100401123456
;	식 안의 함수판 CHKDATA의 RESULT에의 일시취득는 없었다…!
;===============================================================================
;	ARGS = CHKDATA 직후의 RESULTS만 대응
@CHKDATA_DATE(ARGS)
#FUNCTION
RETURNF TOINT(SUBSTRING(ARGS,0,4))*10000000000 + TOINT(SUBSTRING(ARGS,5,2))*100000000 + TOINT(SUBSTRING(ARGS,8,2))*1000000 + TOINT(SUBSTRING(ARGS,11,2))*10000 + TOINT(SUBSTRING(ARGS,14,2))*100 + TOINT(SUBSTRING(ARGS,17,2))

;-------------------------------------------------------------------------------
;	세이브데이터를 전검색해 가장 새로운세이브데이터번호를취득(식 안의 함수)
;-------------------------------------------------------------------------------
;===============================================================================
;	ARGS = "NOAUTO"오토세이브 포함하지 않는/그 외 오토세이브 포함한다
@ALLCHKDATA_DATE_LATEST(ARGS)
#FUNCTION
#LOCALSIZE 4
#LOCALSSIZE 1
VARSET LOCAL
LOCAL:2 = -1
LOCAL:3 = ARGS == "NOAUTO" ? SAVE_AREA()-AUTOSAVE_AREA() # SAVE_AREA()
FOR LOCAL, 0, LOCAL:3
	CHKDATA LOCAL
	IF RESULT != 0
		CONTINUE
	ELSEIF CHKDATA_DATE(RESULTS) > LOCAL:1 
		LOCAL:1 = CHKDATA_DATE(RESULTS)
		LOCAL:2 = LOCAL
		LOCALS = %RESULTS%
	ENDIF
NEXT
RESULTS = %LOCALS%
RETURNF LOCAL:2


;-------------------------------------------------------------------------------
;	범위 확대오토세이브
;-------------------------------------------------------------------------------
;	SAVE_AREA()-1번으로부터 역산해서까지 AUTOSAVE_AREA() 개
;	「겐, 오토세이브덧쓰기되어 버렸다!」사고 방지용
;===============================================================================
;	LOCAL = COUNT  LOCAL:1 = DATE 일시 보존  LOCAL:2 =비교용 DATE  LOCAL:3 =최종 
@SYSTEM_AUTOSAVE
#LOCALSIZE 4
VARSET LOCAL
;오토세이브 범위가 0 이하라면 아무것도 하지 않는다
SIF AUTOSAVE_AREA() <= 0
	RETURN 0
;디폴트 비교용으로 있을 수 없는 DATE를(2999/12/31 23:59:59) 세트
LOCAL:2 = 29991231235959
;역산
;예:SAVE범위 100, AUTO범위 5 99,                       99-5(94), = 95번에서 99번까지
FOR LOCAL, SAVE_AREA(-1), SAVE_AREA(-1)-AUTOSAVE_AREA(), -1
	CHKDATA LOCAL
	;기존세이브데이터가 존재한다면 DATE를, 그렇지 않으면-1을 일시 보존
	LOCAL:1 = RESULT == 0 ? CHKDATA_DATE(RESULTS) # -1
	;상과 비교용 DATE를 비교해보다 낡은 or 비어 있는 경우
	IF LOCAL:1 < LOCAL:2
		;비교용 DATE 갱신
		LOCAL:2 = LOCAL:1
		;가장 낡은 것도 깔고는 빈세이브데이터 갱신
		LOCAL:3 = LOCAL
	ENDIF
NEXT
;다른 장소님의 바리안트로 무섭기 때문에 comment out
;CALL VAR_INITIALIZE
SAVEDATA LOCAL:3, @"%SAVEINFOF()%"
RETURN 1

;-------------------------------------------------------------------------------
;	범위 확대 퀵로드
;-------------------------------------------------------------------------------
;	가장 새로운세이브데이터를로드
;===============================================================================
@QUICKLOAD
#LOCALSIZE 1
VARSET LOCAL
LOCAL = ALLCHKDATA_DATE_LATEST()
IF LOCAL == -1
	CALL PRINT_DIALOG("세이브데이터가로드 할 수 없습니다",2,2,2)
	RETURN 0
ELSE
	CALL PRINT_DIALOG(@"{LOCAL}번을 로드했다",2,2,0)
	CALL PRINT_DIALOG(RESULTS,0,2,2)
	LOADDATA LOCAL
ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;	세이브데이터 종합
;-------------------------------------------------------------------------------
;	세이브, 코멘트세이브, 로드, 삭제, 카피의 각세이브데이터관리를 통합
;
;	사용법
;			↓이런 함수를 만든다
;	@TITLE_LOADGAME
;	CALL DATAMENU("TITLELOAD")
;
;	SAVEGAME를 CALL DATAMENU("SAVE")에 치환
;	LOADGAME를 CALL DATAMENU("LOAD")에 치환
;
;	@EVENTLOAD 에↓의 2행을 추가
;	SIF GLOBALS:COPYDATA_TAG() != ""
;		CALL DATAMENU("NOWCOPY")
;																		ENJOY!
;===============================================================================
@DATAMENU(ARGS)
#DIM NOWPAGE,1
#DIM MAXPAGE,1
#DIM NOWMODE,1
#DIM COPYSELECT,1
#DIM SCREEN,1
#LOCALSIZE 2
#LOCALSSIZE 1
COPYSELECT = -1
;초기모드선택
SELECTCASE ARGS
	;TITLE로부터 날아 왔다(LOAD 의외 불가)
	CASE "TITLELOAD"
		NOWMODE = 0
	;세이브모드
	CASE "SAVE"
		NOWMODE = 1
	;코멘트 첨부세이브모드
	CASE "SAVECOMMENT"
		NOWMODE = 2
	;로드모드
	CASE "LOAD"
		NOWMODE = 3
	;삭제모드
	CASE "DELETE"
		NOWMODE = 4
	;카피모드
	CASE "COPY"
		NOWMODE = 5
	;카피중 EVENTLOAD로부터 날아 왔다
	CASE "NOWCOPY"
		CALL COPYDATA
		NOWMODE = 5
	;인수 캐릭터 라인이 이상했으면 RETURN
	CASEELSE
		RETURN -1
ENDSELECT
;초기 페이지는오토세이브를 포함하지 않는 최신 데이터가 있는 곳
NOWPAGE = ALLCHKDATA_DATE_LATEST("NOAUTO") / 20

;재묘사는 여기로부터-------------------------------------------
$PRINT_LIST
SCREEN = LINECOUNT
;최신 데이터의취득
LOCAL:1 = ALLCHKDATA_DATE_LATEST()
;다이얼로그 표시
CALL PRINT_DIALOG(DATAMENU_LABEL(NOWMODE,1,COPYSELECT),1,1,1)
;리스트 표시
FOR LOCAL, 0, SAVE_AREA()
	;20개씩의 표시
	SIF !RANGE(LOCAL, NOWPAGE * 20, (NOWPAGE + 1) * 20 - 1)
		CONTINUE

	CHKDATA LOCAL
	;선택 할 수 없는 것을 암색화
	SIF RESULT != 0 && !( NOWMODE == 1 || NOWMODE == 2 || (NOWMODE == 5 && COPYSELECT != -1) )
		SETCOLOR 0x808080
	;줄머리 정보	COPY/NEW!/AUTO
	IF LOCAL == COPYSELECT
		SETCOLOR 0xFFFF00
;OriginalString : %" COPY "%
		PRINTFORM %" COPY "%
	ELSEIF LOCAL == LOCAL:1
		SETCOLOR 0xFFFFFF
;OriginalString : %" NEW! "%
		PRINTFORM %" NEW! "%
	ELSEIF RANGE(LOCAL,SAVE_AREA()-AUTOSAVE_AREA(),SAVE_AREA(-1))
;OriginalString : %" AUTO "%
		PRINTFORM %" AUTO "%
	ELSE
;OriginalString : %"      "%
		PRINTFORM %"      "%
	ENDIF
;OriginalString : [{LOCAL,2}] - %RESULTS%
	PRINTFORML [{LOCAL,2}] - %RESULTS%
	RESETCOLOR
NEXT
CALL NEWLINE

;최대 페이지수
MAXPAGE = (LOCAL - 1) / 20
;페이지 실렉터
CALL PRINT_PAGESELECT(NOWPAGE,MAXPAGE)
;타이틀로부터 날아 왔을 경우는 타기능 불가
IF NOWMODE
	CALL NEWLINE
	CALL PRINTC_COMMAND(1010,@"%UNICODE(0x25C0)%%DATAMENU_LABEL(TURNPAGE(NOWMODE, -1, 1, 5))%")
	CALL PRINTC_COMMAND(1011,@"%UNICODE(0x25B6)%%DATAMENU_LABEL(TURNPAGE(NOWMODE, +1, 1, 5))%")
ENDIF
CALL PRINT_RETURN(,,2)
INPUT
SELECTCASE RESULT
	CASE IS < 0
		RETURN 0
	CASE 0 TO SAVE_AREA()
		LOCAL = RESULT
		CHKDATA LOCAL
		GOTO DO_PROCESS
	CASE 1001
		NOWPAGE = TURNPAGE(NOWPAGE, -1, 0, MAXPAGE)
	CASE 1003
		NOWPAGE = TURNPAGE(NOWPAGE, +1, 0, MAXPAGE)
	CASE 1010
		NOWMODE = NOWMODE ? TURNPAGE(NOWMODE, -1, 1, 5) # 0
	CASE 1011
		NOWMODE = NOWMODE ? TURNPAGE(NOWMODE, +1, 1, 5) # 0
ENDSELECT
;카피모드 빗나가면코비─원리셋트
SIF NOWMODE != 5 && COPYSELECT != -1
	COPYSELECT = -1
$REPRINT
CLEARLINE LINECOUNT - SCREEN
GOTO PRINT_LIST


;열매 처리 여기로부터----------------------------------------------
$DO_PROCESS
;다 보고
IF NOWMODE < 0

;세이브(코비─선선택) 처리-------------------------
ELSEIF NOWMODE == 1 || NOWMODE == 2 || ( NOWMODE == 5 && ( COPYSELECT != -1 && COPYSELECT != LOCAL) )
	SELECTCASE RESULT
		CASE 0,2,3,4
			CALL PRINT_DIALOG(@"\@ !RESULT ? 세이브데이터가 존재합니다 덮어 쓰시겠습니까?  #  알 수 없는 데이터가 존재합니다 덮어 쓰시겠습니까?  \@",1,1,1)
			CALL PRINT_CHOICE(,"예",1,"아니오",2,)
			SIF RESULT == 2
				GOTO REPRINT
		CASE 1
	ENDSELECT
	;카피모드시 2회째는 CALL처에서코비─원를로드 하므로 여기서 종료
	SIF NOWMODE == 5 && ( COPYSELECT != -1 )
		CALL COPYDATA(COPYSELECT,LOCAL)

	;변수 초기화  무섭기 때문에 comment out
	;CALL VAR_INITIALIZE
	LOCALS = %SAVEINFOF()%
	;코멘트세이브모드시는 코멘트 입력
	IF NOWMODE == 2
		CALL PRINT_DIALOG("코멘트를 입력해 주세요",1,1,1)
		INPUTS
		LOCALS += @" %RESULTS%"
	ENDIF
	SAVEDATA LOCAL, LOCALS
	CALL PRINT_DIALOG(@"{LOCAL}번째 슬롯에 데이터를 세이브했다",2,2,0)
	CALL PRINT_DIALOG(LOCALS,0,2,2)
	RETURN 1

;로드처리---------------------------------------
ELSEIF NOWMODE == 0 || NOWMODE == 3
	SELECTCASE RESULT
		CASE 0
			CALL PRINT_DIALOG(@"{LOCAL}번을 로드했다",2,2,0)
			CALL PRINT_DIALOG(RESULTS,0,2,2)
			LOADDATA LOCAL
		CASE 2,3,4
			CALL PRINT_DIALOG("로드 가능한 데이터가 아닙니다",2,2,2)
			GOTO REPRINT
	ENDSELECT

;삭제 처리-----------------------------------------
ELSEIF NOWMODE == 4 && RESULT != 1
	CALL PRINT_DIALOG(@"{LOCAL}차례를 삭제해도 좋습니까?",1,1,0)
	CALL PRINT_DIALOG(RESULTS,0,1,1)
	CALL PRINT_CHOICE(,"예",1,"아니오",2,)
	IF RESULT == 1
		DELDATA LOCAL
		CALL PRINT_DIALOG(@"{LOCAL}차례를 삭제 섬했다",2,2,0)
		CALL PRINT_DIALOG(RESULTS,0,2,2)
		GOTO PRINT_LIST
	ENDIF

;코비─원선택처리---------------------------------
ELSEIF NOWMODE == 5 && COPYSELECT == -1 && RESULT == 0
	COPYSELECT = LOCAL
	GOTO PRINT_LIST
ENDIF

GOTO REPRINT

;-----------------------------------------------------------
;	DATAMENU로 사용하는 라벨 표시용 함수(식 안의 함수)
;===========================================================
;	ARG = NOWMODE  ARG:1 = HEADER용  ARG:2 =카피모드의 HEADER용
@DATAMENU_LABEL(ARG,ARG:1,ARG:2)
#FUNCTIONS
SELECTCASE ARG
	CASE 1, 2
		SIF ARG:1
			RETURNF @"세이브 - 어느 슬롯에 세이브합니까?  \@ ARG == 2 ? (코멘트모드) # \@"
		RETURNF @"\@ARG == 1 ? %FORMCELL("세이브",PRINTCPERLENGTH(-10),2)% # %FORMCELL("코멘트세이브",PRINTCPERLENGTH(-10),2)% \@"
	CASE 0, 3
		SIF ARG:1
			RETURNF "로드 - 어느 데이터를 로드합니까?"
		RETURNF FORMCELL("로드",PRINTCPERLENGTH(-10),2)
	CASE 4
		SIF ARG:1
			RETURNF "삭제 - 어느 데이터를 삭제합니까?"
		RETURNF FORMCELL("삭제",PRINTCPERLENGTH(-10),2)
	CASE 5
		SIF ARG:1
			RETURNF @"\@ ARG:2 == -1 ? 복사 - 어느 데이터를 복사합니까?  # 복사 - {ARG:2}번 데이터를 어느 슬롯에 복사합니까?  \@"
		RETURNF FORMCELL("카피",PRINTCPERLENGTH(-10),2)
	CASEELSE
		RETURNF @"ERROR!{ARG,2}"
ENDSELECT


;-----------------------------------------------------------
;	세이브데이터 카피중용
;-----------------------------------------------------------
;	그러나 보기 어렵구나w
;===========================================================
;	ARG:0/COPYDATA_BY() = 코비─원  
;	ARG:1/COPYDATA_TO() = 코비─선
@COPYDATA(ARG:0, ARG:1)
;처음으로 날아 왔다
;현재의 데이터를 TEMP범위에 보존해, GLOBAL/S에 원/선/태그 보존해코비─원를 LOAD
IF !GLOBAL:COPYDATA_BY() && !GLOBAL:COPYDATA_TO() && GLOBALS:COPYDATA_TAG() == ""
	SAVEDATA COPYDATA_TEMP(), @"TEMP"
	GLOBAL:COPYDATA_BY() = ARG:0
	GLOBAL:COPYDATA_TO() = ARG:1
	GLOBALS:COPYDATA_TAG() = {ARG,2}번 데이터를 {ARG:1,2} 슬롯에 복사했다
	SAVEGLOBAL
	LOADDATA GLOBAL:COPYDATA_BY()
;2회째
;LASTLOAD_NO와코비─원가 동일해 카피용 태그가 진은코비─선에 SAVE 해 TEMP 데이터를 LOAD
ELSEIF LASTLOAD_NO == GLOBAL:COPYDATA_BY() && GLOBALS:COPYDATA_TAG() != ""
	GLOBAL:COPYDATA_BY() = 0
	SAVEGLOBAL
	SAVEDATA GLOBAL:COPYDATA_TO(), LASTLOAD_TEXT
	LOADDATA COPYDATA_TEMP()
;3회째(최후)
;LASTLOAD_NO와 TEMP범위가 동일해 카피용 태그가 진은 태그를 표시해 TEMP범위를 삭제
ELSEIF LASTLOAD_NO == COPYDATA_TEMP() && GLOBALS:COPYDATA_TAG() != ""
	CALL PRINT_DIALOG(GLOBALS:COPYDATA_TAG(),2,2,2)
	DELDATA COPYDATA_TEMP()
	GLOBAL:COPYDATA_TO() = 0
	GLOBALS:COPYDATA_TAG() =
	SAVEGLOBAL
ENDIF

