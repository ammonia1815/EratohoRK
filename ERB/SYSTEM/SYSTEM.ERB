;────────────────────────────────────
;새로운 게임의 처리
;────────────────────────────────────
@EVENTFIRST
#DIM LCOUNT

;GLOBAL 변수로부터애니메─숀 설정을 읽어들인다
LOADGLOBAL
CALLF ANIME_CONFIG("SET", GLOBAL)
SAVEGLOBAL
LOADGLOBAL
;캐릭터 역할 변수를 초기화
TARGET = -1
ASSI = -1
ASSI:1 = -1
ASSI:2 = -1
ASSI:3 = -1
;제변수를 초기화
FLAG:모드 = 0
FLAG:TXT빈도 = 1
FLAG:추가조교자수 = 0

;테스트용 기술. 언제라도 지워도 좋아
;CALL GET_REVISION_SQRT_PROP_TEST(, 1, 5, 12, 50)
;PRINTVL LINE_CALC(-2000, "-2000→-12, 2000→0, LIMIT/-12～0")
;PRINTV IS_NOWACTNAME("똥이")
;CALL MAKE_CSV
;SKIPDISP 1
;CALL INPUTINT(0, 1, 2, 3, 4)
;SKIPDISP 0

;플레이시간의 초기화  →SYSTEM_DATAMENU.ERB
CALLF PLAYTIME("SET")

;「당신」이외의 캐릭터의 총수.
DO
	FLAG:Cha총수++
LOOP EXISTCSV(FLAG:Cha총수 + 1, 0)
;등장플래그를 세트
VARSET FLAG, 1, 100, 100 + FLAG:Cha총수
;바리안트주의 의향으로 린노스케를 철거
FLAG:155 = 0
;모드선택
PRINTL ★★모드를 선택해 주세요★★
PRINTL [ 0]NORMAL  ―― 캐릭터 메이킹으로부터 시작하는, 통상의 게임 모드
PRINTL [ 1]SANDBOX ―― NORMAL + 시험적으로 도입된 신기능(동작 보장 없음)
PRINTL [ 9]EXTRA   ―― 스토리가 없는, 좋아하는 캐릭터로 조교되는 모드
PRINTL [-1]돌아온다
CALL INPUTINT(0, 1, 9, -1)
;옵션을 설정
FLAG:소질표시방법 = 1
FLAG:일상 = 1
FLAG:AI자유도 = 15
;모드확정
FLAG:모드 = RESULT
;NORMAL라면 유카링에 초대
IF FLAG:모드 == 0
	CALL CHARA_MAKER
	;죠교대상의의상 설정
	CALL CLOTHES_SETUP(MASTER)
;SANDBOX, EXTRA라면 주인공(죠교대상)선택
ELSEIF FLAG:모드 == 1 || FLAG:모드 == 9
	DELCHARA 0
	CALL START_CHARA_SELECT
;뉴 게임할 생각이 아니었다면 타이틀에돌아가기
ELSE
	RESETDATA
	BEGIN TITLE
ENDIF

;CALL CONFIGURE

CALL START_CHARA_SELECT_T
CALL BASE_TRAINER_SETUP

;주인님인정
TALENT:주인님 = 1
FLAG:주인님 = TARGET

CFLAG:MASTER:처녀상실된 = !TALENT:MASTER:처녀
CFLAG:MASTER:동정상실된 = !TALENT:MASTER:동정

CFLAG:처녀상실된 = !TALENT:처녀
CFLAG:동정상실된 = !TALENT:동정

ITEM:505 = 1
FLAG:구상 = 1
PRINTL 
DRAWLINE
;오프닝
CALL KOJO_EVENT(1)
;오프닝이 없는 경우는 범용
IF !RESULT
	CALL WAIT_3
	PRINTFORMW 어느 정도 자고 있었을 것인가.
	PRINTL 
	CALL CHARA_CUTIN(CSTR:두명, CSTR:등장명, CSTR:로마자)
	PRINTL 
	PRINTFORMW 눈을 뜬 %CALLNAME:MASTER%(을)를 맞이한 것은, %CALLNAME%의 웃는 얼굴이었다.
	PRINTL 
	PRINTFORMW ――%CNAME()%(을)를, 조교한다.
	PRINTL 
	PRINTFORMW %CALLNAME%(은)는 조용하게 그렇게 고하면, 설명을 계속했다.
	PRINTFORMW 뭐든지, %CALLNAME:MASTER%(이)가 성적으로 느끼거나 절정 하거나 했을 때에 방출되는 정력――
	PRINTFORMW 그것이, %CALLNAME%에 있어 귀중한 마력이 되는 것 같다.
	PRINTFORMW 그 마력을 사용해, 다양한 방법이나 의식을 실시할 수 있다고 한다.
	PRINTL 
	PRINTFORMW 탈출의 전망은 없다.
	PRINTFORMW %CALLNAME%(이)가 지만 이 바보스러운 실험에 질려, %CALLNAME:MASTER%(을)를 해방해 줄 때까지.
	CALL BGM_CUTIN(CSTR:곡1)
	PRINTL 
	PRINTFORMW %CALLNAME:MASTER%(은)는, 다만 참을 뿐의 불쌍한 노예다――.
	PRINTL 
ENDIF

REDRAW 0
SETFONT "나눔명조"
FONTSTYLE 3

;RESULT가 2라면 로고를 스킵
IF RESULT != 2
	;재료. 타이틀 로고
;OriginalString : (40, 17, "eratohoЯeverse――","　","　","　　　　　　――죠교が、始まる")
	CALL FULLCUTIN(40, 17, "eratohoЯeverse――"," "," ","      ――죠교가, 시작된다")
ENDIF
FOR LOCAL, 0, 100
	PRINTL
NEXT
CALL DAY_EFFECT
CLEARLINE 100
;Emuera를디버그모드로 기동했다경우에게만 표시하도록(듯이)
;라고 생각했지만, 디버그시에는 보통으로[777]가 표시되므로 역시 필요하지 않다고 판단@/L
[SKIPSTART]
[IF_DEBUG]
PRINTL 
PRINTL 
PRINTW SHOP화면 777입력으로 디버그 모드가 되어 정보의 출력, TARGET의 행동 지정, MASTER의 소질 변경등을 실시할 수가 있습니다
[ENDIF]
[SKIPEND]
;데이타베이스 초기화
CALL DATABASE_INPUT_INIT
BEGIN SHOP

