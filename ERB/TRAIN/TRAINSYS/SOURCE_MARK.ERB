
;────────────────────────────────────
;각인취득의 체크, 그리고각인에 의한 소질 변동（조건 설정은 꽤 적당… 조정이필요라고 생각한다면 연락해 주세요）
;────────────────────────────────────
@MARK_CHECK_SOURCE
#DIM LTEMP, 5

;각인는 1회에 1단계까지 오르는 사양입니다
VARSET LOCAL
VARSET LTEMP

;각인:고통각인
;고통가 규정량을 돌파하면각인취득
SIF CUP:MASTER:고통 > (400 * (MARK:MASTER:고통각인 + 1))
	LOCAL++
;10 LV가 상한
SIF LOCAL && (MARK:MASTER:고통각인 < 10)
	LTEMP:0 = ++MARK:MASTER:고통각인

;LOCAL:0 초기화
LOCAL = 0

;각인:쾌락각인 지금의 사양에서는쾌락각인가 와작와작 오르므로 조정  요점 검토
;굴복각인보다 먼저는 오르지 않는다(굴복각인이하의 레벨이 된다)
;쾌감이 규정량을 돌파하면각인취득
SIF (CUP:MASTER:쾌Ｃ + CUP:MASTER:쾌Ｖ + CUP:MASTER:쾌Ａ + CUP:MASTER:쾌Ｂ) > (800 * (MARK:MASTER:쾌락각인 + 1))
	LOCAL ++
;굴복각인LV를 넘지 않는다. 또 10 LV가 상한
SIF LOCAL && (MARK:MASTER:쾌락각인 < MARK:굴복각인) && (MARK:MASTER:쾌락각인 < 10)
	LTEMP:1 = ++MARK:MASTER:쾌락각인

;굴복각인취득
CALL MARK_CHECK_SOURCE_SURRENDER
LTEMP:2 = RESULT

;각인:반항각인
;(이번죠교대상의 반응이순종이면 판정하지 않는다)
IF !IS_COMGRONAME("소극적으로 한다") && !IS_COMGRONAME("태연하게 한다/적극적으로 한다/받아들인다")
	LOCAL = CUP:MASTER:반항 > (MARK:반항각인 + 1) * 400
	;죠교자의 평시아라이멘도가 충분히 높면(S튼)각인취득를 중단
	SIF MARK:반항각인 > 10 - MARK:5
		LOCAL = 0
	;10-굴복+고통/2를 넘지 않는다
	SIF LOCAL && (MARK:반항각인 < 10-((MARK:굴복각인+MARK:MASTER:고통각인)/2))
		LTEMP:3 = ++MARK:반항각인
ENDIF

;각인:트라우마
LOCAL = CUP:MASTER:공포 > (MARK:MASTER:트라우마 + 1) * 400
;10 LV가 상한
IF LOCAL && (MARK:MASTER:트라우마 < 10)
	LTEMP:4 = ++MARK:MASTER:트라우마
	;이번죠교자행동를트라우마의 원으로서 덧쓰기 기억
	CFLAG:MASTER:트라우마 = TFLAG:ACT
ENDIF

CALL MARK_CHECK_SOURCE_MESSAGE(LTEMP:0, LTEMP:1, LTEMP:2, LTEMP:3, LTEMP:4)

;────────────────────────────────────
;각인취득구상와 표시
;────────────────────────────────────
@MARK_CHECK_SOURCE_MESSAGE(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4)
;각인취득시 개행
SIF ARG:0 || ARG:1 || ARG:2 || ARG:3 || ARG:4
;OriginalString : 
	PRINTL
;각인미취득시라도각인취득시구상는 부른다
RESULT = 0
CALL KOJO_MARK(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4)

FOR LOCAL, 0, 5
	;각인취득를 개별적으로 확인
	IF ARG:LOCAL
		;각인취득표시
;OriginalString : %MARKNAME:LOCAL%は{ARG:LOCAL}되었다
		PRINTFORML %MARKNAME:LOCAL%하{ARG:LOCAL}(이)가 되었다
		;굴복각인취득시, 굴복P2가굴복각인의 제곱에 응해 상승
		SIF LOCAL == 2
			CFLAG:MASTER:굴복P2 += POWER(LOCAL:LOCAL, 2) - POWER(LOCAL:LOCAL - 1, 2)
		;고통각인Lv9취득시, 굳셈를 잃는다
		IF LOCAL == 0 && ARG:0 == 9 && TALENT:MASTER:굳셈
			TALENT:MASTER:굳셈 = 0
;OriginalString : そして%CALLNAME:MASTER%は[굳셈]を失った
			PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [기장](을)를 잃었다
		;쾌락각인Lv5취득시, 쾌감을부정를 잃는다
		ELSEIF LOCAL == 1 && ARG:1 == 5 && TALENT:MASTER:쾌감을부정
			TALENT:MASTER:쾌감을부정 = 0
;OriginalString : そして%CALLNAME:MASTER%は[쾌감을부정]を失った
			PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [쾌감의 부정](을)를 잃었다
		;쾌락각인Lv7취득시, 억압를 잃는다
		ELSEIF LOCAL == 1 && ARG:1 == 7 && TALENT:MASTER:억압
			TALENT:MASTER:억압 = 0
;OriginalString : そして%CALLNAME:MASTER%は[억압]を失った
			PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [억압](을)를 잃었다
		;쾌락각인Lv10취득시, 쾌감에솔직를 얻는다
		ELSEIF LOCAL == 1 && ARG:1 == 10 && !TALENT:MASTER:쾌감에솔직
			TALENT:MASTER:쾌감에솔직 = 1
;OriginalString : そして%CALLNAME:MASTER%は[쾌감에솔직]を得た
			PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [쾌감에 순수](을)를 얻었다
		;굴복각인Lv7취득시, 반항적를 잃는다
		ELSEIF LOCAL == 2 && ARG:2 == 7 && TALENT:MASTER:반항적
			TALENT:MASTER:반항적 = 0
;OriginalString : そして%CALLNAME:MASTER%は[반항적]を失った
			PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [반항적](을)를 잃었다
		;굴복각인Lv10취득시, 프라이드높음를 잃어, 엔딩용플래그가 선다
		ELSEIF LOCAL == 2 && ARG:2 == 10
			FLAG:굴복Lv10 += 1
			IF TALENT:MASTER:프라이드높음
				TALENT:MASTER:프라이드높음 = 0
;OriginalString : そして%CALLNAME:MASTER%は[프라이드높음]を失った
				PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [프라이드 높다](을)를 잃었다
			ENDIF
		;트라우마Lv10취득시, 겁쟁이, 유치를 이득,
		;원래유치를 가지고 있을까 1/3의 확률로광기를 얻는다
		ELSEIF LOCAL == 4 && ARG:4 == 10
			IF !TALENT:MASTER:겁쟁이
				TALENT:MASTER:겁쟁이 = 1
;OriginalString : そして%CALLNAME:MASTER%は[겁쟁이]を得た
				PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [겁장이](을)를 얻었다
			ELSEIF !TALENT:MASTER:광기 && (TALENT:MASTER:유치 || !RAND:3)
				TALENT:MASTER:광기 = 1
;OriginalString : そして%CALLNAME:MASTER%は[광기]を得た
				PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 [광기](을)를 얻었다
			ELSEIF !TALENT:MASTER:유치
				TALENT:MASTER:유치 = 1
;OriginalString : そして%CALLNAME:MASTER%は幼児退行했다
				PRINTFORMW 그리고 %CALLNAME:MASTER%(은)는 유아 퇴행 했다
			ENDIF
		ENDIF
	ENDIF
NEXT

;반항각인Lv 상한의 처리
;반항각인는 10-(고통각인+굴복각인)/2를 넘지 않는다
IF MARK:반항각인 > 10-((MARK:굴복각인+MARK:MASTER:고통각인)/2)
	MARK:반항각인 = 10-((MARK:굴복각인+MARK:MASTER:고통각인)/2)
	;구상호출
	CALL KOJO_EVENT(29)
;OriginalString : %CALLNAME:TARGET%의 与える쾌락\@ MARK:MASTER:고통각인 ? と고통 # \@が%CALLNAME:MASTER%から반항の기력を奪っていく…
	PRINTFORML %CALLNAME:TARGET%(이)가 주는 쾌락\@ MARK:MASTER:고통각인 ? 와고통 # \@하지만 %CALLNAME:MASTER%(으)로부터 반항의 기력을 빼앗아 간다…
;OriginalString : 반항각인は{MARK:반항각인}되었다
	PRINTFORMW 반항 각인은{MARK:반항각인}(이)가 되었다
ENDIF

;────────────────────────────────────
;각인:굴복각인
;────────────────────────────────────
;뽑지않고두발와뽑지않고세발는 SOURCE_1. ERB에 이동. 결단적으로 신사양에 목수일＋α@revkoishi(14/05/28)
;뽑지않고두발와뽑지않고세발의 처리를 환송해. SOURCE_1의 처리를 고쳤으므로 여기서 좋다고 말하는 일로@/L(2014/06/05)
@MARK_CHECK_SOURCE_SURRENDER
#DIM LCOUNT

;-------------------------------------------------
;협력적죠교대상행동에 의한굴복
;-------------------------------------------------
SIF IS_COMGRONAME("적극적으로 한다/받아들인다/태연하게 한다") && !IS_NOWCOMNAME("이루는대로") && !IS_COMIMPNAME("악인상대")
	CALL MARK_CHECK_GET("호의적반응")	;【굴복:0】호의적반응(죠교대상가적극적으로 한다계를선택)
;발핥기강제/이라마치오가 다른 ACT분류로부터 파생했다경우에서도 MENUMATCH라면 줍는 것이 가능@/L
SIF MENUMATCH(TFLAG:ACT, "봉사") && IS_COMGRONAME("적극적으로 한다")
	CALL MARK_CHECK_GET("적극봉사")		;【굴복:29】적극봉사(ACT분류가봉사계중에적극적으로 따른다)
SIF ((SEX_INITIATIVE(MASTER) && IS_COMGRONAME("적극적으로 한다")) || (SEX_INITIATIVE(TARGET) && IS_COMGRONAME("받아들인다"))) ;&& ! IS_NOWCOMNAME("이루는대로")
	CALL MARK_CHECK_GET("허리흔듦")		;【굴복:31】허리흔듦(성교중적극적으로 따른다)
SIF IS_NOWCOMNAME("가게해줘!")
	CALL MARK_CHECK_GET("가게해줘")		;【굴복:49】가게해줘!

;-------------------------------------------------
;죠교대상쾌감에 의한굴복
;-------------------------------------------------
SIF TCVAR:MASTER:발기도 >= 1000
	CALL MARK_CHECK_GET("발기")			;【굴복:2】발기시켰다(발기도 1000 돌파)
FOR LCOUNT, 0, 4
	SIF PALAM:MASTER:LCOUNT >= 1000
		CALL MARK_CHECK_GET(TOSTR(3 + LCOUNT))	;【굴복:3】【굴복:4】【굴복:5】【굴복:6】쾌Ｘ를 1000이상 주었다
NEXT
SIF IS_CONDITION(MASTER, "정욕")
	CALL MARK_CHECK_GET("정욕")			;【굴복:32】정욕(죠교대상의상태가정욕)
SIF TCVAR:MASTER:애태움도 > 1
	CALL MARK_CHECK_GET("애태움")		;【굴복:33】애태움플레이
SIF PALAM:MASTER:윤활 >= 1000 && !TALENT:MASTER:남자
	CALL MARK_CHECK_GET("젖음")		;【굴복:62】젖음(윤활 1000 돌파)

;-------------------------------------------------
;절정에 의한굴복
;-------------------------------------------------
FOR LCOUNT, 0, 4
	SIF NOWEX:MASTER:LCOUNT
		CALL MARK_CHECK_GET(TOSTR(12 + LCOUNT))	;【굴복:12】【굴복:13】【굴복:14】【굴복:15】Ｘ절정
NEXT
SIF (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정) && GETBIT(TEQUIP:자위중, 0)
	CALL MARK_CHECK_GET("자위절정")		;【굴복:26】자위절정
SIF (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정) && TEQUIP:밧줄
	CALL MARK_CHECK_GET("구속절정")		;【굴복:34】구속절정
SIF ((NOWEX:MASTER:Ｃ절정 != 0) + (NOWEX:MASTER:Ｖ절정 != 0) + (NOWEX:MASTER:Ａ절정 != 0) + (NOWEX:MASTER:Ｂ절정 != 0)) > 1
	CALL MARK_CHECK_GET("다중절정")		;【굴복:37】다중절정(복수 개소동시절정)
SIF (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정) && (NOWEX:Ｃ절정 || NOWEX:Ｖ절정 || NOWEX:Ａ절정 || NOWEX:Ｂ절정)
	CALL MARK_CHECK_GET("동시절정")		;【굴복:38】동시절정(죠교대상, 죠교자동시절정)
SIF ((TEQUIP:성교중 && (NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정) && (NOWEX:사정 || NOWEX:Ｃ절정)) || (TEQUIP:성교봉사중 && (NOWEX:MASTER:사정 || NOWEX:MASTER:Ｃ절정) && (NOWEX:Ｖ절정 || NOWEX:Ａ절정)))
	CALL MARK_CHECK_GET("성교동시절정")	;【굴복:39】성교동시절정(성교중, 삽입자사정시에 피삽입자가 A/V절정)
SIF TFLAG:전회질싸 == 1 && NOWEX:MASTER:Ｖ절정
	CALL MARK_CHECK_GET("역질내Ｖ절정")	;【굴복:60】질내사정Ｖ절정(죠교자에질내사정당했다와 동시에죠교대상가Ｖ절정)
SIF NOWEX:MASTER:Ｃ절정 && NOWEX:MASTER:Ｖ절정 && NOWEX:MASTER:Ａ절정 && NOWEX:MASTER:Ｂ절정
	CALL MARK_CHECK_GET("사중절정")		;【굴복:61】사중절정

;-------------------------------------------------
;사정에 의한굴복
;-------------------------------------------------
;뽑지않고두발/뽑지않고세발의 조정@revkoishi(14/06/07)
SIF NOWEX:MASTER:사정 > 1
	CALL MARK_CHECK_GET("사정")			;【굴복:19】사정
SIF NOWEX:MASTER:사정 == 3
	CALL MARK_CHECK_GET("대량사정")		;【굴복:20】대량사정
SIF NOWEX:MASTER:사정 == 4
	CALL MARK_CHECK_GET("조루사정")		;【굴복:21】사정(조루)
SIF TFLAG:전회질싸 == 1
	CALL MARK_CHECK_GET("역질내사정")	;【굴복:22】질내사정당했다
SIF TFLAG:전회질싸 == 2
	CALL MARK_CHECK_GET("질내사정")		;【굴복:25】질내사정
;드라이사정는 연주한다. 일견 알기 어렵기 때문에@PENIS를+@/L
SIF PENIS(TARGET) && IS_NOWACTNAME("입으로 애무를 강요/이라마치오") && NOWEX:사정 > 1
	CALL MARK_CHECK_GET("역구강사정")	;【굴복:27】구강사정당했다(펠라/이라마중에죠교자가사정)
;드라이사정는 연주한다. 일견 알기 어렵기 때문에@PENIS를+@/L
SIF PENIS(MASTER) && (IS_NOWACTNAME("입으로 애무") || TEQUIP:죠교대상Ｃ사용 == 2) && NOWEX:MASTER:사정 > 1
	CALL MARK_CHECK_GET("구강사정")		;【굴복:28】구강사정(죠교자펠라중에사정)
SIF TFLAG:전회질싸 == 6
	CALL MARK_CHECK_GET("항내사정")		;【굴복:41】항문에사정
SIF TFLAG:전회질싸 == 5
	CALL MARK_CHECK_GET("역항내사정")	;【굴복:42】항문에사정 된다
;드라이사정에서의안빼고는 우선 인정하는 것으로 한다@/L
SIF IS_UNEXTRACTION(MASTER) && GROUPMATCH(TFLAG:전회질싸, 2, 4, 6, 8) > 0
	CALL MARK_CHECK_GET("뽑지않고두발")	;【굴복:43】뽑지않고두발(사정 후삽입를 중단하지 않고사정)
SIF NOWEX:MASTER:사정 == 1
	CALL MARK_CHECK_GET("드라이사정")		;【굴복:44】드라이사정
SIF NOWEX:MASTER:사정 == 5
	CALL MARK_CHECK_GET("전립선사정")	;【굴복:45】전립선사정
SIF IS_UNEXTRACTION(MASTER) > 1 && GROUPMATCH(TFLAG:전회질싸, 2, 4, 6, 8) > 0
	CALL MARK_CHECK_GET("뽑지않고세발")	;【굴복:55】뽑지않고세발(뽑지않고두발 후삽입를 중단하지 않고사정)

;-------------------------------------------------
;죠교대상치정에 의한굴복
;-------------------------------------------------
SIF !(TEQUIP:MASTER:코스프레 || TEQUIP:MASTER:하반신속옷 || TEQUIP:MASTER:상반신속옷 || TEQUIP:MASTER:하반신겉옷 || TEQUIP:MASTER:상반신겉옷 || TEQUIP:MASTER:전신겉옷)
	CALL MARK_CHECK_GET("전라")			;【굴복:1】전라로 벗겼다(MASTER의 의복이 없어진다)(※신발안등은 착의에 세지 않는다)
SIF CUP:MASTER:쾌Ａ
	CALL MARK_CHECK_GET("Ａ죠교")		;【굴복:9】항문죠교(쾌A발생)
SIF TEQUIP:야외플레이
	CALL MARK_CHECK_GET("야외")			;【굴복:16】야외플레이
SIF TEQUIP:거울
	CALL MARK_CHECK_GET("거울")			;【굴복:17】수치플레이
;리액션이 수용인 경우로 한정한다. 방뇨의 경우, 용서를 빌다는거부취급@/L
SIF IS_NOWACTNAME("방뇨") && IS_COMGRONAME("소극적으로 한다/태연하게 한다/적극적으로 한다/받아들인다")
	CALL MARK_CHECK_GET("방뇨")			;【굴복:18】방뇨시켰다(죠교대상가 따르는 계를선택) L
SIF TEQUIP:관장그릇＋플러그
	CALL MARK_CHECK_GET("관장")			;【굴복:35】관장
SIF TFLAG:실금
	CALL MARK_CHECK_GET("실금")			;【굴복:36】실금
SIF IS_NOWACTNAME("관장세트") && TFLAG:ACT파생 == 1
	CALL MARK_CHECK_GET("배설")			;【굴복:50】배설(관장그릇＋플러그해제시)

;-------------------------------------------------
;죠교대상고통에 의한굴복
;-------------------------------------------------
SIF CUP:MASTER:공포 || CUP:MASTER:고통
	CALL MARK_CHECK_GET("위압")			;【굴복:7】고통, 공포(하지만 발생)
SIF IS_NOWACTNAME("채찍")
	CALL MARK_CHECK_GET("채찍")			;【굴복:8】채찍으로 두드렸다
SIF TEQUIP:밧줄
	CALL MARK_CHECK_GET("구속")			;【굴복:10】구속했다
SIF TFLAG:징계·플래그 && (CUP:MASTER:공포 || CUP:MASTER:고통)
	CALL MARK_CHECK_GET("징계·")		;【굴복:48】징계·(징계·때, 공포/고통가 발생)
SIF TEQUIP:삼각목마
	CALL MARK_CHECK_GET("삼각목마")		;【굴복:52】삼각목마

;-------------------------------------------------
;결정적 상황에 의한굴복
;-------------------------------------------------
;리액션이 수용인 경우에 한정@/L
SIF IS_NOWACTNAME("키스") && IS_COMGRONAME("소극적으로 한다/태연하게 한다/적극적으로 한다/받아들인다/용서를 빌다")
	CALL MARK_CHECK_GET("키스")			;【굴복:11】키스
SIF TEQUIP:바이브
	CALL MARK_CHECK_GET("바이브")		;【굴복:23】바이브
SIF TEQUIP:항문바이브 || TEQUIP:항문비즈
	CALL MARK_CHECK_GET("Ａ도구")		;【굴복:24】항문바이브, 비즈
SIF TEQUIP:성교중 || TEQUIP:성교봉사중
	CALL MARK_CHECK_GET("성교")			;【굴복:30】성교
SIF NOWEX:MASTER:분유
	CALL MARK_CHECK_GET("분유")			;【굴복:40】분유
SIF TEQUIP:성교중 == 6
	CALL MARK_CHECK_GET("Ａ성교·수")	;【굴복:46】항문섹스(접수)
SIF TEQUIP:성교봉사중 == 6
	CALL MARK_CHECK_GET("Ａ성교·공")	;【굴복:47】항문섹스(공격)
SIF IS_NOWACTNAME("공중육변기플레이") && GROUPMATCH(TFLAG:ACT파생, 2, 3)
	CALL MARK_CHECK_GET("공중변소")		;【굴복:51】공중변소(공중육변기플레이수용시)
SIF IS_NOWACTNAME("피스트퍽/양쪽피스트")
	CALL MARK_CHECK_GET("Ｖ확장")		;【굴복:53】Ｖ확장(피스트/양쪽피스트시)
SIF IS_NOWACTNAME("애널피스트/양쪽피스트")
	CALL MARK_CHECK_GET("Ａ확장")		;【굴복:54】Ａ확장(A피스트/양쪽피스트시)
SIF NOWEX:MASTER:시오후키
	CALL MARK_CHECK_GET("시오후키")		;【굴복:59】시오후키

;-------------------------------------------------
;구상 독자적인굴복
;-------------------------------------------------
;구상독자실적용 리저브 X
;【굴복:112】【굴복:113】【굴복:114】【굴복:115】【굴복:116】【굴복:117】【굴복:118】【굴복:119】
;【굴복:120】【굴복:121】【굴복:122】【굴복:123】【굴복:124】【굴복:125】【굴복:126】【굴복:127】
TRYCALLFORM KOJO_CHECK_SURRENDER_K{NO:TARGET}

;굴복각인취득확인
CALL MARK_CHECK_SOURCE_SURRENDER_CHECK
RETURN RESULT

;그 외굴복조건은【굴복:\d*】로 검색 가능
;
;56실신는 EVENTCOMEND로
;57, 58처녀, 동정상실는 EXPCHECK로
;그 밖에도 있을지도, 입니다 라고? 있으면 곤란합니다(강간눈)


@MARK_CHECK_SOURCE_SURRENDER_CHECK
;각인를 MASTER 측에 적용
;CFLAG:MASTER:각인에 MASTER가 받은 모든각인
;CFLAG:TARGET:각인에 TARGET가 준 모든각인
;가 들어가는 일이 됩니다
CFLAG:MASTER:각인 |= CFLAG:각인
CFLAG:MASTER:각인2 |= CFLAG:TARGET:각인2	;어디서 사용하는지 모르지만 일단 추가@revkoishi(14/05/26)

;LOCAL:1에굴복포인트 총계를 넣고보정 한다
LOCAL:1 = CFLAG:굴복P1
SIF TALENT:MASTER:남자
	TIMES LOCAL:1, 1.10
;후타나리도 남자도 아니다(즉 여자)
SIF TALENT:MASTER:후타나리 !| TALENT:MASTER:남자
	TIMES LOCAL:1, 1.20
SIF TALENT:MASTER:겁쟁이
	TIMES LOCAL:1, 1.05
SIF TALENT:MASTER:굳셈
	TIMES LOCAL:1, 0.95
SIF TALENT:MASTER:반항적
	TIMES LOCAL:1, 0.95
SIF TALENT:MASTER:프라이드높음
	TIMES LOCAL:1, 1.05
SIF TALENT:MASTER:프라이드낮음
	TIMES LOCAL:1, 0.95
SIF TALENT:위압감
	TIMES LOCAL:1, 1.50

;보정치를 바탕으로굴복각인조건을 초과하고 있을까 산출
LOCAL = LOCAL:1 > (MARK:굴복각인 * 5 + MARK:굴복각인 * MARK:굴복각인 / 2)
;10 LV가 상한
SIF LOCAL && (MARK:굴복각인 < 10)
	RETURN ++MARK:굴복각인

RETURN 0

;────────────────────────────────────
;굴복각인취득함수 이벤트제입니다. 특정의 조건으로 턴 엔드 하면굴복포인트가 저점점 농락 되어 갑니다
;ARGS:0 굴복실적명 또는 캐릭터 라인 형식의굴복실적번호(0～127)
;결단적으로 신사양에 목수일(14/05/26). 성별로 유효 판정하도록(듯이) 가드@revkoishi(14/06/01)
;────────────────────────────────────
@MARK_CHECK_GET(ARGS:0)
LOCAL:1 = RESULT
LOCAL:0 = ISNUMERIC(ARGS:0) ? TOINT(ARGS:0) # GET_SURRENDER_NUM(ARGS:0)
IF IS_SURRENDER_ENABLE(LOCAL:0) && !(LOCAL:0 > 63 ? GETBIT(CFLAG:TARGET:각인2, LOCAL:0 - 64) # GETBIT(CFLAG:TARGET:각인, LOCAL:0))
;OriginalString : 【%GET_SURRENDER_STR_M(LOCAL:0)%】굴복ポイント+{GET_SURRENDER_POINT(LOCAL:0)}
	PRINTFORML 【%GET_SURRENDER_STR_M(LOCAL:0)%】굴복 포인트+{GET_SURRENDER_POINT(LOCAL:0)}
	CFLAG:TARGET:굴복P1 += GET_SURRENDER_POINT(LOCAL:0)
	SETBIT CFLAG:TARGET:\@ LOCAL:0 > 63 ? 각인2 # 각인 \@, LOCAL:0 > 63 ? LOCAL:0 - 64 # LOCAL:0
ENDIF
RETURN LOCAL:1
