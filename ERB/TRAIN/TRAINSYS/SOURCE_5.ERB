;────────────────────────────────────
;죠교자쾌감（cvab）의 소스와절정의 처리（쾌Ｃ, 쾌Ｖ, 쾌Ａ, 쾌Ｂ, 체력, 기력, 사정, 모유, 이성, 흥미）
;────────────────────────────────────
@CALC_SOURCE40

LOCALS = CALC_SOURCE40

;최초의 변동
CALLFORM %LOCALS%_TALENT

;쾌감（ＣＶＡＢ）의 상승
CALLFORM %LOCALS%_PLEASURE

;절정, 체력, 기력, 사정, 모유, 소변등의 처리
CALLFORM %LOCALS%_EX

;────────────────────────────────────
;최초의 변동
;────────────────────────────────────
@CALC_SOURCE40_TALENT
#DIM LCOUNT
;페니스밴드장비중은쾌Ｃ가쾌Ｖ에 변환
;변환율… 쾌Ｃ를 20％로 해, 나머지의 값에 v감각+1/c감각+1을 걸어 2로 나누어쾌Ｖ에 가산한다
IF TEQUIP:죠교자페니스밴드
	SOURCE:쾌Ｖ += (SOURCE:쾌Ｃ*8*(((ABL:Ｖ감각+1)*100)/(ABL:Ｃ감각+1)))/2000
	TIMES SOURCE:쾌Ｃ, 0.20
ENDIF

;ＣＶＡＢ부위에 대해서
FOR LCOUNT, 0, 4
	;민감와둔감는 최초로 처리
	;Ｘ민감
	SIF TALENT:(100 + LCOUNT * 2)
		TIMES SOURCE:LCOUNT, 1.50
	;Ｘ둔감
	SIF TALENT:(101 + LCOUNT * 2)
		TIMES SOURCE:LCOUNT, 0.70
	;죠교자의상태
	IF 0
	ELSEIF T_COND("피폐")
		TIMES SOURCE:LCOUNT, 0.80
	ELSEIF T_COND("쇠약")
		TIMES SOURCE:LCOUNT, 0.65
	ELSEIF T_COND("무기력")
		TIMES SOURCE:LCOUNT, 0.70
	ELSEIF T_COND("정욕")
		TIMES SOURCE:LCOUNT, 1.20
	ENDIF
NEXT

;────────────────────────────────────
;쾌감（ＣＶＡＢ）의 상승
;────────────────────────────────────
@CALC_SOURCE40_PLEASURE
#DIM LCOUNT

;욕정PALAM가[0→4, 1000→7, 2000→9, 3000→10, 4000→11, 5000→12]를 LOCAL에(4～12)
LOCAL = LIMIT(GET_REVISION_SQRT(PALAM:MASTER:욕정, 5000, 8) + 4, 4, 12)
FOR LCOUNT, 0, 4
	;연속보너스(연속횟수배에 증가하는 보너스)
	CUP:LCOUNT += SOURCE:LCOUNT * LOCAL * TFLAG:동일행동보너스 / 10
	;조교중에 집중적으로 책와 약해질 생각이 든다(쾌Ｘ보정처리)
	CUP:LCOUNT = CUP:LCOUNT * CFLAG:(30 + LCOUNT) / 50
NEXT

;────────────────────────────────────
;절정, 체력, 기력, 사정, 모유, 소변등의 처리
;────────────────────────────────────
@CALC_SOURCE40_EX
#DIM LCOUNT
#DIM DYNAMIC 절정강도, 4
#DIM DYNAMIC 쾌감부담
#DIM DYNAMIC 사정부담
#DIM DYNAMIC 분유부담
#DIM DYNAMIC 절정부담

;절정Ｃ
SIF (!PENIS(TARGET) || TCVAR:발기 > 1)
	절정강도:0 = CALC_SOURCE40_EX_P(0, CUP:쾌Ｃ + PALAM:쾌Ｃ)

;절정Ｖ
절정강도:1 = CALC_SOURCE40_EX_P(1, CUP:쾌Ｖ + PALAM:쾌Ｖ)

;절정Ａ
절정강도:2 = CALC_SOURCE40_EX_P(2, CUP:쾌Ａ + PALAM:쾌Ａ)

;절정Ｂ
절정강도:3 = CALC_SOURCE40_EX_P(3, CUP:쾌Ｂ + PALAM:쾌Ｂ)

;절정강도 합계로부터절정부담을 계산
절정부담 = 1000 * SUMARRAY(절정강도)

;쾌감에 의한체력기력이성소모
;쾌ＸSOURCE 합계가[0→0, 2000→44, 4000→63, 6000→77, 8000→89, 10000→100]을 쾌감 부담에
쾌감부담 = GET_REVISION_SQRT(SUMARRAY(SOURCE, 40, 44), 10000, 100)

;죠교의 정도에 의한 경감
;죠교자죠교레벨+욕망ABL가[0→100%, 2→91%, 4→83%, 6→75%, 8→67%, 10→59%, 12→51%]를 쾌감 부담에 곱셈(50～100%)
LOCAL = GET_REVISION_SQRT_PROP(CFLAG:죠교레벨 + ABL:욕망, 1, 5, 12, 50)
쾌감부담 -= 쾌감부담 * LIMIT(LOCAL, 50, 100) / 100

;페니스존재시
IF PENIS(TARGET)
	;절정결정시
	IF 절정강도:0 > 0
		;사정게이지가 없다
		IF BASE:사정 == 0
			STR:1044 = 드라이사정（죠교자）
			DOWNBASE:체력  += 100
			DOWNBASE:기력  += 80
			NOWEX:사정      = 1
			TCVAR:사정      = 1
			TCVAR:드라이사정    = 1
			TCVAR:드라이사정수 += 1
		;강절정
		ELSEIF 절정강도:0 == 2
			STR:1044 = 대량사정（죠교자）
			사정부담 = 500
			NOWEX:사정 = 3
			TCVAR:사정 = 3
		;통상의절정
		ELSE
			STR:1044 = 사정（죠교자）
			사정부담 = 300
			NOWEX:사정 = 2
			TCVAR:사정 = 2
		ENDIF
	ENDIF
	;하늘이 아닌사정시
	IF NOWEX:사정 > 1
		;정액더러움의 추가
		;────────────────────────────────────
		;ACT가 파생하면애무물이 바뀌는 경우가 있으므로@IS_NOWACTNAME는 아니고@TC_PLAYER로
		;하나 하나 ACT파생도 참조하는 것은 귀찮고!
		;────────────────────────────────────
		;죠교자의 페니스
		CALL SET_STAIN("페니스", "정액", TARGET)
		;죠교대상의
		;바기나(성교봉사시)
		SIF TC_PLAYER(1) == 4
			CALL SET_STAIN("바기나", "정액", MASTER)
		;항문
		SIF TC_PLAYER(1) == 5
			CALL SET_STAIN("항문", "정액", MASTER)
		;손
		SIF TC_PLAYER(1) == 2
			CALL SET_STAIN("손", "정액", MASTER)
		;입
		SIF TC_PLAYER(1) == 1
			CALL SET_STAIN("입", "정액", MASTER)
		;가슴
		SIF TC_PLAYER(1) == 6
			CALL SET_STAIN("가슴", "정액", MASTER)
		[SKIPSTART]
		Ｖ/Ａ는 질내사정 판정 이후에 처리를 이전
		;바기나
		SIF TC_PLAYER(1) == 4
			CALL SET_STAIN("바기나", "정액", MASTER)
		;항문
		SIF TC_PLAYER(1) == 5
			CALL SET_STAIN("항문", "정액", MASTER)
		[SKIPEND]
	ENDIF
ENDIF

;중/밖에사정
IF TEQUIP:성교중 && NOWEX:사정
	;질내사정 판정
	LOCAL = 0
	;안에 내면 안돼! 한편용서를 빌다실패
	IF IS_NOWCOMNAME("안에 내면 안돼!") && !IS_COMGRONAME("용서를 빌다")
		;트라우마가산
		SOURCE:MASTER:트라우마 += (BASE:사정 == 1 ? 200 # 500) - CFLAG:MASTER:죠교레벨 * 10
		;질내사정 확인
		LOCAL = 1
	;안에 내! 또는호의(M→T)가 충분히 존재
	ELSEIF IS_NOWCOMNAME("안에 내!") || CFLAG:M호의 > RAND:8000
		;질내사정 확인
		LOCAL = 1
	ENDIF
	;질내사정시
	IF LOCAL
		;Ｖ성교
		IF TEQUIP:성교중 < 6
			;더러운 추가(드라이사정시 제외함)
			STAIN:MASTER:바기나 |= (BASE:사정 == 1 ? 0 # 4)
			;질내사정기록(드라이사정정보도 기입)
			TFLAG:전회질싸 = (BASE:사정 == 1 ? 3 # 1)
			;죠교대상(주인님)의질내사정량을 늘린다
			CFLAG:M질정 += DOWNBASE:사정
			;죠교대상의바기나에정액더러움을 추가
			CALL SET_STAIN("바기나", "정액", MASTER)
		;Ａ성교
		ELSE
			;더러운 추가(드라이사정시 제외함)
			STAIN:MASTER:항문 |= (BASE:사정 == 1 ? 0 # 4)
			;질내사정기록(드라이사정정보도 기입)
			TFLAG:전회질싸 = (BASE:사정 == 1 ? 7 # 5)
			;죠교대상의항문에정액더러움을 추가
			CALL SET_STAIN("항문", "정액", MASTER)
		ENDIF
	ENDIF
ENDIF

;Ｂ절정시, 모유체질이면
IF 절정강도:3 && TALENT:모유체질
	;모유불발
	IF BASE:모유 < 50 + RAND:5 * 10
		STR:1045 = 하늘분유（죠교자）
		분유부담 = 50 + 절정강도:3 * 10
		DOWNBASE:체력 += 100
		DOWNBASE:기력 += 80
		NOWEX:분유 = 1
		TCVAR:분유 = 1
		;NOWEX:분유에서는 『하늘분유』라고통상의분유에 1을 할당해 버리고 있으므로, TCVAR:분유와번호가 어긋난다
		;현상 NOWEX의번호를 두등했다구 없기 때문에, TCVAR:분유만손당 해 두기로 한다@(2014/11/08)/L
	;강절정
	ELSEIF 절정강도:3 == 2
		STR:1045 = 대량분유（죠교자）
		분유부담 = 500
		NOWEX:분유 = 2
		TCVAR:분유 = 3
	;통상의절정
	ELSE
		STR:1045 = 분유（죠교자）
		분유부담 = 300
		NOWEX:분유 = 1
		TCVAR:분유 = 2
	ENDIF
	;죠교자가슴에모유더러움
	CALL SET_STAIN("가슴", "모유", TARGET)
	;죠교대상의
	;손
	SIF TB_PLAYER(1) == 2
		CALL SET_STAIN("손", "모유", MASTER)
	;입
	SIF TB_PLAYER(1) == 1
		CALL SET_STAIN("입", "모유", MASTER)
	;페니스
	SIF TB_PLAYER(1) == 3
		CALL SET_STAIN("페니스", "모유", MASTER)
ENDIF

;STR:1046(절정표시용 캐릭터 라인) 초기화
STR:1046 = 
;절정종을 순서에 기입
SPLIT "＆Ｃ/＆Ｖ/＆Ａ/＆Ｂ", "/", LOCALS
FOR LCOUNT, 0, 4
	SIF 절정강도:LCOUNT
		STR:1046 += LOCALS:LCOUNT
NEXT
;다중절정시
IF STRLENSU(STR:1046) > 2
	;서식을 정돈한다
	STR:1046 = %SUBSTRINGU(STR:1046, 1)%절정（죠교자）
	;문자수로절정종을 판정
	;사중절정
	IF STRLENSU(STR:1046) > 13
		;사중절정시에는 특별한 캐릭터 라인을 기입
		STR:1046 = 사중절정（죠교자）
		TIMES 절정부담, 2.00
		TIMES 사정부담, 1.40
		TIMES 분유부담, 1.40
		TIMES 쾌감부담, 1.50
	;삼중절정
	ELSEIF STRLENSU(STR:1046) > 11
		TIMES 절정부담, 1.65
		TIMES 사정부담, 1.20
		TIMES 분유부담, 1.20
		TIMES 쾌감부담, 1.30
	;이중절정
	ELSE
		TIMES 절정부담, 1.30
		TIMES 사정부담, 1.10
		TIMES 분유부담, 1.10
		TIMES 쾌감부담, 1.30
	ENDIF
;단절정, 무절정시
ELSE
	;캐릭터 라인 초기화
	STR:1046 = 
ENDIF

;절정는달성의 주된 소스의 1개
SOURCE:MASTER:달성 += 사정부담 + 분유부담 + 절정부담

;체력와기력와 정력의소모
DOWNBASE:체력 += 20 * 절정강도:0 + 40 * 절정강도:1 + 60 * 절정강도:2 + 20 * 절정강도:3 + 사정부담 / 10 + 분유부담 / 10 + 쾌감부담 * (TFLAG:동일행동보너스 + 1) / 2
DOWNBASE:기력 += 10 * 절정강도:0 + 20 * 절정강도:1 + 30 * 절정강도:2 + 10 * 절정강도:3 + 사정부담 / 15 + 분유부담 / 15 + 쾌감부담 * (TFLAG:동일행동보너스 + 1) * 4 / 10
DOWNBASE:사정 = 사정부담
;한계를 넘어 좁혀졌을 경우체력가 깎아진다
SIF DOWNBASE:사정 > BASE:사정
	DOWNBASE:체력 += 200 * (2 - TALENT:정력절륜 + TALENT:정력박약) / 2
;모유의소모
DOWNBASE:모유 += 분유부담
;한계를 넘어 좁혀졌을 경우체력가 깎아진다
SIF DOWNBASE:모유 > BASE:모유
	DOWNBASE:체력 += 200 * (2 - TALENT:정력절륜 + TALENT:정력박약) / 2

;이성
DOWNBASE:이성 += 쾌감부담 + (SUMARRAY(절정강도) + DOWNBASE:사정 / 300 + NOWEX:분유 + 5) * 20

;죠교부위의누적치(보정치 계산)
FOR LCOUNT, 0, 4
	SIF 절정강도:LCOUNT
		CFLAG:(30 + LCOUNT) = 50 + (CFLAG:(30 + LCOUNT) - 50) / 2
NEXT

;NOWEX에 데이터를 넣는다（절정시구상에 사용한다）
FOR LCOUNT, 0, 4
	NOWEX:LCOUNT = 절정강도:LCOUNT
NEXT

;NOWEX를 TCVAR절정계에 보존
TCVAR:Ｃ절정 = NOWEX:Ｃ절정
TCVAR:Ｖ절정 = NOWEX:Ｖ절정
TCVAR:Ａ절정 = NOWEX:Ａ절정
TCVAR:Ｂ절정 = NOWEX:Ｂ절정

;사정시, 관련플래그를 설정
IF NOWEX:사정
	;죠교자사정후경과턴참조계플래그를 리셋트
	VARSET TCVAR:0, 0, 90, 100
	;Ｖ성교시
	SIF V_SEX()
		TCVAR:질내사정후경과턴참조 = 1
	;Ａ성교시
	SIF A_SEX()
		TCVAR:장내사정후경과턴참조 = 1
	TCVAR:사정후경과턴참조 = 1
	CFLAG:사정횟수            += 1
ENDIF

;죠교자의발기

;쾌감계 소스를 받는지, 키스인가, 발핥기강제시.
SIF (SOURCE:쾌Ｃ || SOURCE:쾌Ｖ || SOURCE:쾌Ａ || SOURCE:쾌Ｂ || IS_NOWACTNAME("키스/발핥기강제/Ｗ키스")) && PENIS(TARGET)
	TCVAR:발기++
;팬티·성기 보이고탈의시.
SIF P_DATUICHECK(1) > 1 && PENIS(TARGET)
	TCVAR:발기++
;브라·가슴 보이고탈의시.
SIF B_DATUICHECK(1) > 1 && PENIS(TARGET)
	TCVAR:발기++
;죠교대상절정시.
SIF (NOWEX:MASTER:Ｃ절정 || NOWEX:MASTER:Ｖ절정 || NOWEX:MASTER:Ａ절정 || NOWEX:MASTER:Ｂ절정 || NOWEX:MASTER:사정 || NOWEX:MASTER:분유 || NOWEX:MASTER:시오후키 || NOWEX:MASTER:방뇨) && PENIS(TARGET)
	TCVAR:발기++
;새드의 경우, 가학계 ACT시.
SIF MENU(TFLAG:ACT분류) == "가학" && TALENT:새드 && PENIS(TARGET)
	TCVAR:발기++
;헌신적의 경우, 애무계 ACT시.
SIF MENU(TFLAG:ACT분류) == "애무" && TALENT:헌신적 && PENIS(TARGET)
	TCVAR:발기++
;수동적의 경우, 봉사계 ACT시.
SIF MENU(TFLAG:ACT분류) == "봉사" && TALENT:수동적 && PENIS(TARGET)
	TCVAR:발기++
;심술궂음의 경우, 수치계 ACT시.
SIF MENU(TFLAG:ACT분류) == "수치" && TALENT:심술궂음 && PENIS(TARGET)
	TCVAR:발기++
;광기의 경우, 이상계 ACT시.
SIF MENU(TFLAG:ACT분류) == "이상" && TALENT:광기 && PENIS(TARGET)
	TCVAR:발기++

;사정 하면 1에돌아가기
SIF NOWEX:사정
	TCVAR:발기 = 1

;사정시, 관련플래그를 설정
IF NOWEX:사정
	CFLAG:(250 + NOWEX:사정) += 1
	CFLAG:사정횟수 += 1
ENDIF
;절정횟수플래그를 설정
FOR LCOUNT, 0, 4
	SIF 절정강도:LCOUNT
		CFLAG:(256 + LCOUNT)++
NEXT

;절정횟수를 늘린다
FOR LCOUNT, 0, 4
	EX:LCOUNT += NOWEX:LCOUNT
NEXT
SIF PENIS(TARGET)
	EX:사정 += NOWEX:사정
EX:분유 += NOWEX:분유

;삶그렇게（잠정）
;가버려가 아니고, 페니스가 있으면발기 하고 있는지, 페니스가 없을 때, 쾌Ｃ가 존재하면
IF TCVAR:TARGET:가버려 == 0 && (!PENIS(TARGET) || TCVAR:발기 > 1) && CUP:쾌Ｃ > 0
	;C감각가 낮으면보다 낮은 값으로 갈 것 같게, 또이성저하로 C치에 관계없이 살 것 같게 된다.
	VARSET LOCAL
	;C자극 > C감각 판정
	LOCAL += (CUP:쾌Ｃ + PALAM:쾌Ｃ) >= MAX(GET_ABL(TARGET, "Ｃ감각") * 100, 1000)
	;기능 > 이성 판정
	LOCAL += GET_ABL(MASTER, "기교") - GET_ABL(TARGET, "기교") > RAND:(MAX(BASE:TARGET:이성 / 10, 1))
	;이성가 전혀 없으면 무조건으로가버려에
	LOCAL += BASE:TARGET:이성 == 0
	;어떤 것인가의 판정에 인소
	IF LOCAL
		TCVAR:TARGET:가버려 += 5
;OriginalString : %CALLNAME:TARGET%は가버려되었다…
		PRINTFORMW %CALLNAME:TARGET%(은)는 이 기초가 되었다…
	ENDIF
ENDIF

;절정강도 처리
;ARG:0 =쾌Ｘ종(ＣＶＡＢ)
;ARG:1 =이번 도달 강도
;RESULT = 절정강도
@CALC_SOURCE40_EX_P(ARG, ARG:1)
#FUNCTION
#DIM LCOUNT
#DIM 절정경계, 3
;절정경계 세트
절정경계 = 90
절정경계:1 = 10000
절정경계:2 = 20000
;LOCAL 초기화
VARSET LOCAL
;절정경계분만큼 루프 한다
FOR LCOUNT, 1, 3
	;절정경계를 넘으면, 절정강도가 1씩 오른다
	SIF ARG:1 < 절정경계:LCOUNT
		BREAK
	LOCAL++
NEXT
;가버려시, 쾌Ｃ가 있으면Ｃ절정가 반드시 일어난다
SIF (ARG == 0) && TCVAR:TARGET:가버려 && CUP:쾌Ｃ > 0
	LOCAL = MAX(LOCAL, 1)
;절정시
IF LOCAL >= 1
	;절정경계:N, 절정경계:0의 수치에 응해 PALAM 감소
	;(하한을 돌파하지 않게 조정)
	CDOWN:ARG = MIN(절정경계:LOCAL * 절정경계 / 100, ARG:1)
	;절정표시용 캐릭터 라인 초기화
	STR:(1040 + ARG) = 
	;강도가 2라면 "강"을 추가
	SIF LOCAL == 2
		STR:(1040 + ARG) += "강"
	;절정종을 기입
	STR:(1040 + ARG) += EXNAME:ARG + "（죠교자）"
	;CDOWN:쾌Ｘ로 내려도절정이상이라면
	;그 값-1이 되도록(듯이) 조정（10000으로절정라면 9999）
	SIF ARG:1 - CDOWN:ARG >= 절정경계:1
		CDOWN:ARG = ARG:1 - 절정경계:1 + 1
ENDIF
;절정강도을반환
RETURNF LOCAL

