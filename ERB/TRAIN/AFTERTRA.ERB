;────────────────────────────────────
;죠교종료의 처리
;────────────────────────────────────
@EVENTEND

;불끈불끈 리셋트
SIF TARGET > 0
	CFLAG:무라무라 = 0
SIF ASSI:1 > 0
	CFLAG:(ASSI:1):무라무라 = 0
SIF ASSI:2 > 0
	CFLAG:(ASSI:2):무라무라 = 0

;MASTER측의 CFLAG에 누구에게 얼마나 짜졌는지를기록
;1회로 최대 5000 정도?
CFLAG:MASTER:(2200 + NO:TARGET) += TFLAG:취득마력 * (5 + CFLAG:랭크) / 5
;ASSI는 TARGET의 1/5
FOR LOCAL, 0, 9
	IF ASSI:(LOCAL + 1) > 0
		CFLAG:MASTER:(2200 + NO:(ASSI:(LOCAL + 1))) += TFLAG:취득마력 * (5 + CFLAG:(ASSI:(LOCAL + 1)):랭크) / 25
	ENDIF
NEXT

;누적마력
SIF TARGET > 0
	CFLAG:누적마력 += TFLAG:취득마력

;죠교종료
FLAG:조교중 = 0
;OriginalString : 죠교は終わりま했다。
PRINTW 조교는 끝났습니다.

EXP:MASTER:성지식 += TFLAG:경과시간 / 10
EXP:성지식 += TFLAG:경과시간 / 10

;죠교시간를 체크해기록(최장·최단)
SIF TFLAG:경과시간 > FLAG:최장죠교시간기록
	FLAG:최장죠교시간기록 = TFLAG:경과시간
SIF TFLAG:경과시간 > 0 && (TFLAG:경과시간 < FLAG:최단죠교시간기록 || FLAG:최단죠교시간기록 == 0)
	FLAG:최단죠교시간기록 = TFLAG:경과시간

;ACT 장기보정치의 일괄 산입
CALL ACT_M_RESEARCH_L(1)

;데이타베이스 기입
CALL DATABASE_INPUT_T

;────────────────────────────────────
;모유의 가치의 계산
;────────────────────────────────────
SIF TALENT:MASTER:모유체질 && TFLAG:죠교시간 > 0
	CALL SELL_MILK

;────────────────────────────────────
;레벨 업. 많이경험치 모아 두어도 1회의죠교는 한 번만 레벨 올라갈 수 있는 사양입니다
;────────────────────────────────────
IF TFLAG:일회휴게 == 0
	IF CFLAG:MASTER:죠교경험 > CFLAG:MASTER:필요경험치
		CFLAG:MASTER:죠교레벨 += 1
		CFLAG:MASTER:죠교경험 -= CFLAG:MASTER:필요경험치
		CFLAG:MASTER:필요경험치 += 100 + CFLAG:MASTER:죠교레벨 * CFLAG:MASTER:죠교레벨 * 10
		;레벨 10이후필요경험치는 갑자기 오릅니다만, 이상경험로 경감할 수 있습니다
		LOCAL = 250 + (CFLAG:MASTER:죠교레벨 - 5) * CFLAG:MASTER:필요경험치 / 3 - EXP:MASTER:이상경험 * 20
		SIF LOCAL > 0 && CFLAG:MASTER:죠교레벨 > 10
			CFLAG:MASTER:필요경험치 += LOCAL
		PRINTFORMW %CALLNAME:MASTER%의 조교 레벨은[{CFLAG:MASTER:죠교레벨}](이)가 되었습니다
		MAXBASE:MASTER:체력 += 100 + 10 * RAND:6
		MAXBASE:MASTER:기력 += 50 + 10 * RAND:6
	ENDIF
	
	IF CFLAG:죠교경험 > CFLAG:필요경험치
		CFLAG:죠교레벨 += 1
		CFLAG:죠교경험 -= CFLAG:필요경험치
		CFLAG:필요경험치 += 100 + CFLAG:죠교레벨 * CFLAG:죠교레벨 * 10
		LOCAL = 250 + (CFLAG:죠교레벨 - 5) * CFLAG:필요경험치 / 3 - EXP:MASTER:이상경험 * 20
		SIF LOCAL > 0 && CFLAG:죠교레벨 > 10
			CFLAG:필요경험치 += LOCAL
		PRINTFORMW %CALLNAME:TARGET%의 조교 레벨은[{CFLAG:죠교레벨}](이)가 되었습니다
		MAXBASE:체력 += 100 + 10 * RAND:6
		MAXBASE:기력 += 50 + 10 * RAND:6
	ENDIF
	
	TFLAG:취득마력 = MAX(TFLAG:취득마력, 10)
	PRINTFORMW %CALLNAME:TARGET%(은)는 %CALLNAME:MASTER%(으)로부터 빨아 들인 정력을 {TFLAG:취득마력}의 마력으로 변환했다
	CFLAG:마력 += TFLAG:취득마력
	
	[SKIPSTART]
	IF CFLAG:호의 >= 500 && CFLAG:M연모 == 0
		CFLAG:M연모 = 1
		PRINTFORML %CALLNAME:TARGET%의 %CALLNAME:MASTER%를 보는 눈이 이상하다…
		PRINTFORMW %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%에 호의를 대기 시작한 것 같다
		IF CFLAG:호의 / 250 >= CFLAG:죠교레벨 - CFLAG:MASTER:죠교레벨
			CFLAG:M연모 = 2
			PRINTFORMW 그 뿐만 아니라, %CALLNAME:TARGET%에 %CALLNAME:MASTER%에의 친애의 정이 싹튼 것 같다
		ENDIF
	ENDIF
	IF CFLAG:호의 / 250 >= CFLAG:죠교레벨 - CFLAG:MASTER:죠교레벨 && CFLAG:M연모 == 1
		CFLAG:M연모 = 2
		PRINTFORML %CALLNAME:TARGET%의 %CALLNAME:MASTER%를 보는 눈이 이상하다…
		PRINTFORMW %CALLNAME:TARGET%에 %CALLNAME:MASTER%에의 친애의 정이 싹튼 것 같다
	ENDIF
	[SKIPEND]
ENDIF

;────────────────────────────────────
;평시아라이멘도의 변동
;────────────────────────────────────
LOCAL = 10 * CFLAG:아라이멘도 - CFLAG:평상시
LOCAL:1 = (CFLAG:SM성격 + CFLAG:츤데레 + 200) / 2
;성격로 배율이 정해진다.
;최소치 50(M데레), 최대치 150(S튼), 소질 변동하지 않는다/시키지 않는 캐릭터에서는 B = 100
CFLAG:평상시 += LOCAL * (LOCAL >= 0 ? 200 - LOCAL:1 # LOCAL:1) / 500
IF CFLAG:평상시 > 100 && MARK:5 == 0
	MARK:5 = 1
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 200 && MARK:5 == 1
	MARK:5 = 2
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 300 && MARK:5 == 2
	MARK:5 = 3
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 400 && MARK:5 == 3
	MARK:5 = 4
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 500 && MARK:5 == 4
	MARK:5 = 5
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 600 && MARK:5 == 5
	MARK:5 = 6
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 700 && MARK:5 == 6
	MARK:5 = 7
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 800 && MARK:5 == 7
	MARK:5 = 8
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 900 && MARK:5 == 8
	MARK:5 = 9
	CFLAG:반발제거 = 1
ELSEIF CFLAG:평상시 > 950 && MARK:5 == 9
	MARK:5 = 10
	CFLAG:반발제거 = 1
ENDIF
;────────────────────────────────────
;드라이사정보너스
;────────────────────────────────────
IF TCVAR:MASTER:드라이사정수 > 0 
	IF MAXBASE:MASTER:사정 < 7500 + CFLAG:MASTER:정력한계 * 500
		MAXBASE:MASTER:사정 = MIN(MAXBASE:MASTER:사정 + TCVAR:MASTER:드라이사정수 * 100, 7500 + CFLAG:MASTER:정력한계 * 500)
		PRINTFORML 한계를 넘어 짜졌기 때문에, %CALLNAME:MASTER%의 성적 기능이 강화되었습니다
	ELSE
		PRINTFORML 성적 기능의 강화가 한계에 이르고 있습니다
	ENDIF
	TCVAR:MASTER:드라이사정수 = 0
ENDIF

;────────────────────────────────────
;리셋트
;────────────────────────────────────
;소변게이지를 지운다
MAXBASE:MASTER:소변 = TALENT:MASTER:오줌싸개 ? MAXBASE:MASTER:소변 # 0
;죠교로 축적했다피폐를일상에 가산
CFLAG:MASTER:소모 = MIN(CFLAG:MASTER:소모 + MAX(TFLAG:63, 0), 20)
SIF TARGET >= 0
	CFLAG:TARGET:소모 += MAX(TFLAG:62, 0)

BEGIN TURNEND
