;────────────────────────────────────
;050, 자위를 시작한다（자위, 열락, 반항, 징계·포인트）
;────────────────────────────────────
@COM50
;자위처리
CALL ONANIE_APPLY
;열락, 굴종SOURCE 증가
SOURCE:MASTER:열락 += (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ) / 1000
SOURCE:MASTER:굴종 += 250 + (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ) * (GET_ABL(MASTER, "욕망") + 10) / 300
LOCAL = (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ) * (3 + TALENT:심술궂음) / 50
SIF LOCAL > 100
	LOCAL = 100
;-------------------------------------------------
;ACT가휴식시킨다, 혹은봉사계라면 REACT분류는거부취급에. 징계·포인트가산으로 COMGRO 받아들이고는 위화감이라고 하는 일도 포함한 처치
;한층 더 SET_COMGRO, SET_COMIMP를 REACT파생대입 개소와 통합@/L
;-------------------------------------------------
;죠교자가만족 하고 있다면징계·이 되지 않을지도 모릅니다
IF BASE:만족 / 100 + CFLAG:MASTER:죠교레벨 / 2 < BASE:초조함 / 100 + RAND:5 || MENUMATCH(TFLAG:ACT, "봉사") || IS_NOWACTNAME("휴식시킨다")
	;징계·포인트가산
	TFLAG:징계·포인트 += LOCAL
	;파생:승손에 자위 하지마
	TFLAG:REACT파생 = 1
	;REACT분류:거부를 세트
	CALL SET_COMGRO("거부")
	;REACT인상는 COM40, 거부한다에 준해악인상대
	CALL SET_COMIMP("악인상대")
	;의존도변화
	CALL ADD_DEPEND(-3, +2)
ELSE
	;파생:승손에 자위 하고 있지만 좋은가
	TFLAG:REACT파생 = 0
	;REACT분류:받아들이는 것을 세트
	CALL SET_COMGRO("받아들인다")
	;REACT인상는 나쁜인상
	CALL SET_COMIMP("나쁜인상")
	;의존도변화
	CALL ADD_DEPEND(-1, 0)
ENDIF
;참다플래그해소
TCVAR:MASTER:참다 = 0
RETURN 1


;────────────────────────────────────
;051, 도구를 제거한다（쾌Ｃ, 쾌Ａ, 달성역, 반항, 징계·포인트）
;────────────────────────────────────
@COM51
;달성SOURCE 감소
SOURCE:MASTER:달성 -= 100 + (CFLAG:M호의 - 500) * (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ) / 1000
;반항SOURCE 증가
SOURCE:MASTER:반항 += 250 + SOURCE:MASTER:쾌Ｖ / 2 + SOURCE:MASTER:쾌Ａ / 2 + (CFLAG:MASTER:죠교레벨 + 1) * (GET_ABL(MASTER, "기교") + 10) * CFLAG:M호의 / 100
;도구를 제거한다의우선 순서 판정. 한 번은 1개까지. 처음은 랜덤 1～3제외할 예정이었지만 왠지 귀찮아
;외했다도구의 TEQUIP번호가 REACT파생에 들어간다
IF TEQUIP:눈가리개
	TEQUIP:눈가리개 = 0
	TFLAG:REACT파생 = GETNUM(TEQUIP, "눈가리개")
ELSEIF TEQUIP:관장그릇＋플러그
	SOURCE:MASTER:쾌Ａ = 500 * TEQUIP:관장그릇＋플러그 * (100 + RAND:11 - RAND:11) / 100
	TEQUIP:관장그릇＋플러그 = 0
	;남자는Ａ로전립선자극
	SIF TALENT:MASTER:남자
		TCVAR:MASTER:전립선자극 += 1
	TFLAG:REACT파생 = GETNUM(TEQUIP, "관장그릇＋플러그")
ELSEIF TEQUIP:항문바이브
	TEQUIP:항문바이브 = 0
	;남자는Ａ로전립선자극
	SIF TALENT:MASTER:남자
		TCVAR:MASTER:전립선자극 += 1
	TFLAG:REACT파생 = GETNUM(TEQUIP, "항문바이브")
ELSEIF TEQUIP:바이브
	TEQUIP:바이브 = 0
	TCVAR:MASTER:전립선자극 += 1
	TFLAG:REACT파생 = GETNUM(TEQUIP, "바이브")
ELSEIF TEQUIP:오나홀
	SOURCE:MASTER:쾌Ｃ = 50 * (TALENT:MASTER:도구능숙 + 1) * GET_ABL(MASTER, "기교") * CFLAG:MASTER:죠교레벨 / 10
	TEQUIP:오나홀 = 0
	TFLAG:REACT파생 = GETNUM(TEQUIP, "오나홀")
ELSEIF TEQUIP:클리캡
	SOURCE:MASTER:쾌Ｃ = 50 * (TALENT:MASTER:도구능숙 + 1) * GET_ABL(MASTER, "기교") * CFLAG:MASTER:죠교레벨 / 10
	TEQUIP:클리캡 = 0
	TFLAG:REACT파생 = GETNUM(TEQUIP, "클리캡")
ELSEIF TEQUIP:볼개그
	TEQUIP:볼개그 = 0
	TFLAG:REACT파생 = GETNUM(TEQUIP, "볼개그")
ELSEIF TEQUIP:유두캡
	TEQUIP:유두캡 = 0
	TFLAG:REACT파생 = GETNUM(TEQUIP, "유두캡")
ELSEIF TEQUIP:페니스밴드
	TEQUIP:페니스밴드 = 0
	TCVAR:MASTER:전립선자극 += 1
	TFLAG:REACT파생 = GETNUM(TEQUIP, "페니스밴드")
ELSE
	SOURCE:MASTER:쾌Ａ = 50 * TEQUIP:항문비즈 + 50 * TEQUIP:항문비즈 + (TALENT:MASTER:도구능숙 + 1) * GET_ABL(MASTER, "기교") * CFLAG:MASTER:죠교레벨 * (TEQUIP:항문비즈 - 1) / 10
	TEQUIP:항문비즈 = 0
	;남자는Ａ로전립선자극
	SIF TALENT:MASTER:남자
		TCVAR:MASTER:전립선자극 += 2
	TFLAG:REACT파생 = GETNUM(TEQUIP, "항문비즈")
ENDIF
;죠교대상자위를 중단
TEQUIP:자위중 &= ~1
;징계·포인트가산
LOCAL = (BASE:초조함 / 100 + PALAM:MASTER:반항 / 1000) * (GET_TRAINLV(TARGET) + 5) + 5 + RAND:10
SIF LOCAL > 100
	LOCAL = 100
TFLAG:징계·포인트 += LOCAL
;날뛴 것처럼 설정
;COM 분류
CALL SET_COMGRO("날뛴다")
;COM 인상
CALL SET_COMIMP("악인상대")
;의존도변화
CALL ADD_DEPEND(-3, -10)
RETURN 1


;────────────────────────────────────
;052, 이루는대로（효과 없음. 실행할 수 있는 반응이 한 개도 없을 때의 구제, 필요인가 어떤가는 모릅니다만）
;필요가 되어 버렸으므로 개명해, 다소 내용을 정비.
;────────────────────────────────────
@COM52
;파성의 처리
;일회휴게
IF TFLAG:일회휴게 == 1
	TFLAG:REACT파생 = -1
;성교계 ACT·역강간
ELSEIF MENUMATCH(TFLAG:ACT, "성교") && !IS_NOWACTNAME("기승위") || IS_NOWACTNAME("역강간")
	;무저항에 범해진다
	TFLAG:REACT파생 = 1
	;COM 분류
	CALL SET_COMGRO("받아들인다")
	;의존도변화
	CALL ADD_DEPEND(+4, +10)
;성교봉사계 ACT·기승위
ELSEIF MENUMATCH(TFLAG:ACT, "성봉") && !IS_NOWACTNAME("역강간") || IS_NOWACTNAME("기승위")
	;빠진 것처럼허리를 흔든다
	TFLAG:REACT파생 = 2
	;COM 분류
	CALL SET_COMGRO("받아들인다")
	;의존도변화
	CALL ADD_DEPEND(+10, +10)
;의상변경·자위·방뇨·개장·이라마치오
ELSEIF IS_NOWACTNAME("의상 변경을 요구한다/자위/비패개장/방뇨/이라마치오")
	;말해지는 대로 천천히 따른다
	TFLAG:REACT파생 = 3
	;COM 분류
	CALL SET_COMGRO("받아들인다")
	;의존도변화
	CALL ADD_DEPEND(+4, +10)
;봉사계 ACT
ELSEIF MENUMATCH(TFLAG:ACT, "봉사")
	;매료되어진 것처럼봉사를 반복한다
	TFLAG:REACT파생 = 4
	;COM 분류
	CALL SET_COMGRO("받아들인다")
	;의존도변화
	CALL ADD_DEPEND(+8, +10)
;위협하는·기분나쁘게 웃다·매도·고함치다·징계·
ELSEIF IS_NOWACTNAME("엄하게 위협한다/징계한다고 위협한다/고함치다/기분나쁘게 웃다/매도")
	;반항적인 소질이 있어, 굴복각인 5 이하
	IF MARK:굴복각인 <= 5 && (TALENT:MASTER:굳셈 || TALENT:MASTER:프라이드높음 || TALENT:MASTER:건방짐 || TALENT:MASTER:반항적)
		;분한 듯이 노려본다
		TFLAG:REACT파생 = 5
		;의존도변화
		CALL ADD_DEPEND(0, -2)
	ELSE
		;무서워한 것처럼 응시한다
		TFLAG:REACT파생 = 6
		;의존도변화
		CALL ADD_DEPEND(+2, +10)
	ENDIF
	;COM 분류
	CALL SET_COMGRO("소극적으로 한다")
;가학계 ACT·또는고통가 크다
ELSEIF SOURCE:MASTER:고통 > 400 || MENUMATCH(TFLAG:ACT, "가학")
	;소리가 되지 않는 비명을 올려 몸부림쳐 뒹군다
	TFLAG:REACT파생 = 7
	;COM 분류
	CALL SET_COMGRO("소극적으로 한다")
	;의존도변화
	CALL ADD_DEPEND(+8, +10)
;쾌감이 있다
ELSEIF SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ > 10
	;회화계 ACT
	IF MENUMATCH(TFLAG:ACT, "회화")
		;자위중
		IF GETBIT(TEQUIP:자위중, 0)
			;오나니─에 열중해 듣지 않았다
			TFLAG:REACT파생 = 8
			;COM 인상
			CALL SET_COMIMP("나쁜인상")
			;COM 분류
			CALL SET_COMGRO("받아들인다")
			;의존도변화
			CALL ADD_DEPEND(-2, +2)
		ELSE
			;기분 좋아서 선대답
			TFLAG:REACT파생 = 9
			;COM 분류
			CALL SET_COMGRO("받아들인다")
			;의존도변화
			CALL ADD_DEPEND(+4, +2)
		ENDIF
	ELSE
		;두 진흰색 황홀상태
		TFLAG:REACT파생 = 10
		;COM 분류
		CALL SET_COMGRO("받아들인다")
		;의존도변화
		CALL ADD_DEPEND(+4, +10)
	ENDIF
;회화계 ACT
ELSEIF MENUMATCH(TFLAG:ACT, "회화")
	;스스로도 모르는 채 부끄러운 일을입 달린다
	TFLAG:REACT파생 = 11
	;COM 분류
	CALL SET_COMGRO("받아들인다")
	;의존도변화
	CALL ADD_DEPEND(+8, +4)
;어떤 것도 아니다
ELSE
	TFLAG:REACT파생 = 0
	;COM 분류
	CALL SET_COMGRO("받아들인다")
	;의존도변화
	CALL ADD_DEPEND(+4, +10)
ENDIF

;쾌감계보정
IF SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ > 10
	;쾌ＸSOURCE 증가
	TIMES SOURCE:MASTER:쾌Ｃ , 1.20
	TIMES SOURCE:MASTER:쾌Ｖ , 1.20
	TIMES SOURCE:MASTER:쾌Ａ , 1.20
	TIMES SOURCE:MASTER:쾌Ｂ , 1.20
	;이탈SOURCE 감소
	TIMES SOURCE:MASTER:이탈 , 0.70
	;트라우마화 가속
	TIMES SOURCE:MASTER:트라우마 , 1.30
	;굴종, 열락SOURCE 증가
	SOURCE:MASTER:굴종 += 50 + (MARK:MASTER:쾌락각인 + 1) * (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ) * CFLAG:MASTER:죠교레벨 / 200
	SOURCE:MASTER:열락 += 100 + SOURCE:MASTER:쾌Ｃ / 25 + SOURCE:MASTER:쾌Ｖ / 25 + SOURCE:MASTER:쾌Ａ / 25 + SOURCE:MASTER:쾌Ｂ / 25
ENDIF

;굴복계보정
;굴종SOURCE 증가
SOURCE:MASTER:굴종 += 50 + MARK:MASTER:고통각인 * 20 + GET_ABL(MASTER, "마조끼") * CFLAG:MASTER:죠교레벨 * 2

RETURN 1
