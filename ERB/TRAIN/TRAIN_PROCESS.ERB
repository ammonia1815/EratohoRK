;------------------------------------------------------------------------
;EDIT	001		kap			2011/10/14		STR의번호를 두등했다의 것에 대응 
;											행동예약를 TCVAR로 변경
;EDIT	002		KOremi		2011/11/09		탈의실행 처리의 분리 
;EDIT	003		KOremi		2014/05/20		조교중지판정을애니메─숀 처리로부터 낸다
;											또, 첫회만 판정하도록(듯이) 수정 
;EDIT	004		revkoishi	2014/05/28		쓸데없게 버튼화하는 것을 회피
;------------------------------------------------------------------------
;────────────────────────────────────
;메인죠교화면의 표시, 죠교자행동를 호출해, 그리고 COMABLE전의필요인 처리
;메인 죠교 화면의 표시, 죠교자 행동을 호출, 그리고 COMABLE 이전 필요 처리
;────────────────────────────────────
@SHOW_STATUS
#DIM LINE
#DIM PREV_BASE_M, 9
#DIM PREV_BASE_T, 9
#DIM NOW_BASE_M, 9
#DIM NOW_BASE_T, 9
#DIM PREV_B_M
#DIM PREV_B_T
#DIM NOW_B_M
#DIM NOW_B_T
#DIM 시간
시간 = 6
;조수 1을 메인 조수로 유지
ASSI = ASSI:1
IF IS_POLICY("금일", "야외")
	TEQUIP:야외플레이 = 1
	TFLAG:금일의방침 = 0
ENDIF

DRAWLINE
;-----------EDIT 004 START--------------;쓸데없게 버튼화하는 것을 회피@revkoishi(14/05/28)
PRINTPLAINFORM {DAY + 1}일째(%GET_TIME()%) 경과시간[{TFLAG:경과시간}] 
SIF TALENT:MASTER:독심능력 > 1
	PRINTPLAINFORM /징계·\@ TFLAG:징계·플래그 ? 모드 # 포인트[{TFLAG:징계·포인트}]\@
PRINTL 
;-----------EDIT 004 END  --------------
FOR LOCAL, 0, 9
	NOW_BASE_M:LOCAL = BASE:MASTER:LOCAL
	NOW_BASE_T:LOCAL = BASE:TARGET:LOCAL
NEXT
NOW_B_M = TCVAR:MASTER:발기도
NOW_B_T = TCVAR:TARGET:발기도
REDRAW 0
LINE = LINECOUNT
FOR LOCAL:1, TFLAG:경과시간 ? 0 # 시간, 시간 + 1
	CLEARLINE LINECOUNT - LINE
	SIF MESSKIP() || FLAG:PALAM애니메 == 2
		LOCAL:1 = 시간
	IF LOCAL:1 == 시간
		FOR LOCAL, 0 ,9
			BASE:MASTER:LOCAL = NOW_BASE_M:LOCAL
			BASE:TARGET:LOCAL = NOW_BASE_T:LOCAL
		NEXT
		TCVAR:MASTER:발기도 = NOW_B_M 
		TCVAR:TARGET:발기도 = NOW_B_T
	ELSE
		FOR LOCAL, 0, 9
			BASE:MASTER:LOCAL = (NOW_BASE_M:LOCAL * LOCAL:1 + PREV_BASE_M:LOCAL * (시간 - LOCAL:1)) / 시간
			BASE:TARGET:LOCAL = (NOW_BASE_T:LOCAL * LOCAL:1 + PREV_BASE_T:LOCAL * (시간 - LOCAL:1)) / 시간
		NEXT
		TCVAR:MASTER:발기도 = (NOW_B_M * LOCAL:1 + PREV_B_M * (시간 - LOCAL:1)) / 시간
		TCVAR:TARGET:발기도 = (NOW_B_T * LOCAL:1 + PREV_B_T * (시간 - LOCAL:1)) / 시간
	ENDIF
	CALL INFO_GAUGE_TRAINER
	;CALL INFO_GAUGE_TENTACLE
	DRAWLINE

	;복장의 표시
	CALL SHOW_EQUIP_3
	;수정전의자세를 표시
	CALL POSE_PRINT
	;사용중의도구를 표시
	CALL SHOW_EQUIP_1
	;상태를표시
	CALL SHOW_EQUIP_2
	DRAWLINE
	TWAIT 100, 1
NEXT
FOR LOCAL, 0, 9
	PREV_BASE_M:LOCAL = BASE:MASTER:LOCAL
	PREV_BASE_T:LOCAL = BASE:TARGET:LOCAL
NEXT
;-----------EDIT 003 START--------------
;-----------EDIT 001 START--------------
;조교중지의 판정 자체가 ACT를 결정했다 나중에
;그 후에 내용을 전부 잡는다든가 하고 있었으므로 독립시킨다
;조교 중 지수의 판정 자체가 ACT를 결정한 후에 그 후에 내용을 모두 망친다던지 때문에 독립시킨다
;---------------------------------------
;조교중지의 판정
;---------------------------------------
SIF TFLAG:경과시간 == 0
	CALL TRAIN_CANCEL
;-----------EDIT 001 END  --------------
;-----------EDIT 003 END  --------------
PREV_B_M = TCVAR:MASTER:발기도
PREV_B_T = TCVAR:TARGET:발기도
REDRAW 1
;-----------EDIT 001 START--------------
;조수함수에 인수를 마련하면서, TCVAR에플래그를 옮겨 처리를 간이화 해
;또, 메세지의 표시 타이밍 및구상의 표시 타이밍을
;모든 커맨드 결정 후로 변경
;조수 함수에 인수를 마련하면서 TCVAR에 플래그를 옮겨 처리를 간소화
;또한 메시지 표시 방법 및 구상 표시시기를
;모든 명령 결정 후 변경
;---------------------------------------
;죠교자행동의 표시
IF TFLAG:조교자행동제++ == 0 && !TFLAG:일회휴게
	;죠교자의행동를선택 합니다.
	CALL PLAYER_ACT
	;-----------EDIT 001 START--------------
	;현재 ACT명의취득를 함수를 통시킨다야 게
	;현재 ACT 이름의 취득을 함수를 통해시킨다 있도록
	;---------------------------------------
	TSTR:죠교지령 = %NOWACTNAME()%
	;-----------EDIT 001 END  --------------
	;행동 전판정
	CALL BEFORE_ACT
	;각죠교액션의누적치
	CFLAG:TARGET:(2000 + TFLAG:ACT) += 1
	CFLAG:MASTER:(2000 + TFLAG:ACT) += 1
	;선택당했다행동에 의해 MASTER의 몸의 자세와사용중의 몸의 부위를기록 합니다.
	CALL MASTER_POSE
	CALL PLAYER_ACT_EXTRA_LATER
	;전회의행동를 표시해 이번행동를 결정
	IF STRLENS(TSTR:전죠교지령) || (STRLENS(TSTR:죠교지령) && FLAG:디버그)
		IF FLAG:디버그
			SIF STRLENS(TSTR:전죠교지령)
				PRINTFORML 전회의 조교 지령:(%MENU(TFLAG:전ACT분류)%계)＜%TSTR:전죠교지령%＞
			PRINTFORML 이번 조교 지령:(%MENU(TFLAG:ACT분류)%계)＜%TSTR:죠교지령%＞
		ELSE
			PRINTFORML 전회의 조교 지령:＜%TSTR:전죠교지령%＞
		ENDIF
		DRAWLINE
	ENDIF
	CALL EQUIP_2USE
	CALL SP_CHECK
	;선택당했다행동가 파생하는 경우, 그플래그를 세웁니다
	CALL TRAIN_VARIATION
	IF TFLAG:ＳＰ행동 < 200
		;PALAM, 발기도등의 계산
		CALL ACTION_APPLY
	ELSE
		CALL SP_ACTION
	ENDIF
	SIF TFLAG:강제탈의 > 0
		CALL KYOUSEI_DATUI
	SIF TFLAG:자주탈의 > 0
		CALL JISHU_DATUI
	;접촉정보의취득, 지문 표시
	CALL TOUCH_SUCCESSION
	;----------------------------------------
	;조수의행동결정
	;----------------------------------------
	LOCAL = 1
	WHILE ASSI:LOCAL > 0
		;조수행동의 결정
		IF TCVAR:ASSI:행동예약 >= 0
			TCVAR:(ASSI:LOCAL):조수ACT = TCVAR:ASSI:행동예약
			TCVAR:ASSI:행동예약 = -1
		ELSE
			CALL ASSI_ACT, ASSI:LOCAL
		ENDIF
		;MASTER의 몸의 자세의 정보를 덧쓰기
		CALL MASTER_POSE_ASSI, ASSI:LOCAL
		CALL ASSI_ACT_APPLY, ASSI:LOCAL
		;파생 정보의취득
		CALL ASSI_TRAIN_VARIATION, ASSI:LOCAL, TCVAR:(ASSI:LOCAL):조수ACT
		;횟수카운트의 추가
		IF TCVAR:(ASSI:LOCAL):조수방침 >= 0 && GET_ASSIMENUNAME(TCVAR:(ASSI:LOCAL):조수방침) != "콤비 네이션"
			CFLAG:(ASSI:LOCAL):(2200 + TCVAR:(ASSI:LOCAL):조수ACT) += 1
			CFLAG:MASTER:(2200 + TCVAR:(ASSI:LOCAL):조수ACT) += 1
		ENDIF
		LOCAL++
	WEND
	;강제탈의의 성공 판정
	SIF TFLAG:강제탈의 > 0
		CALL KYOUSEI_DATUI_CHECK
	;----------------------------------------------
	;메세지 표시
	;----------------------------------------------
	;-----------EDIT 002 START--------------
	;탈의실행 처리와 지문, 구상표시 처리를 분리
	;---------------------------------------
	;강제·자주탈의의 실행, 또 지문 표시와 그구상
	SIF TFLAG:자주탈의 || TFLAG:강제탈의
		CALL DATUI_PROCESS
	;-----------EDIT 002 END  --------------
	;자세의 변경의 지문과 그구상
	CALL POSE_CHANGE
	;ACT의 지문
	CALL TRAIN_MESSAGE
	;죠교자액션의 표시와 그구상
	CALL KOJO_ACT
	;추가 커맨드의 메세지
	CALL PLAYER_ACT_EXTRA_MESSAGE
	;----------------------------------
	;조수의 테키스트 표시
	;----------------------------------
	LOCAL = 1
	WHILE ASSI:LOCAL > 0
		ALIGNMENT RIGHT
		;구상용으로 호환 처리
		ASSI = ASSI:LOCAL
		TFLAG:조수ACT = TCVAR:(ASSI:LOCAL):조수ACT
		TFLAG:조수ACT파생 = TCVAR:(ASSI:LOCAL):조수ACT파생
		CALL ASSI_TRAIN_MESSAGE, ASSI:LOCAL
		LOCAL++
		ALIGNMENT LEFT
	WEND
	ASSI = ASSI:1
	;미약·로션사용과 그구상
	SIF TFLAG:미약등사용
		CALL USE_MESSAGE
	CALL EQUIP_CHECK
	CALL CLOTHES_CHECK
	DRAWLINE
	;여기서 리액션 파생을 리셋트 해 둔다
	TFLAG:REACT파생 = 0
ELSE
	SIF STRLENS(TSTR:전죠교지령)
		PRINTFORML 전회의 조교 지령＜%TSTR:2%＞
	PRINTFORML 이번 조교 지령＜%TSTR:1%＞
	DRAWLINE
ENDIF
;-----------EDIT 001 END  --------------
SIF FLAG:디버그
	CALL SHOW_TOUCH
;COMABLE 처리
CALL COMABLE(-1)
IF FLAG:오토모드 > 0
	CALL AUTO_MODE
	LOCAL = RESULT
	DOTRAIN LOCAL
ENDIF
@AUTO_MODE, ARG
#DIMS AUTO, 100
;ARG가 진정한 때, 혹은 AUTO가 텅텅때, AUTO를 갱신한다
IF AUTO == "" || ARG
	VARSET AUTO
	SPLIT GLOBALS:@"오토모드등록{FLAG:선택세트}", "_", RESULTS
	SPLIT RESULTS:1, "/", AUTO
	SIF ARG
		RETURN 0
ENDIF
FOR LOCAL, 0, 100
	SIF GETNUM(TRAINNAME,AUTO:LOCAL) == -1
		CONTINUE
	CALL COMABLE(GETNUM(TRAINNAME,AUTO:LOCAL))
	SIF !RESULT
		CONTINUE
	RETURN GETNUM(TRAINNAME,AUTO:LOCAL)
	BREAK
NEXT
RETURN GETNUM(TRAINNAME, "이루는대로")
;────────────────────────────────────
;행동 전판정（EVETRAIN보다 이동）
;2011/10/14		휴식에 의한 종료를 떼어내 별함수로 한다
;────────────────────────────────────
@BEFORE_ACT
;죠교자행동 전구상
CALL BEFORE_ACT_MESSAGE
CALL KOJO_EVENT(12)
IF RESULT
	DRAWLINE
ELSE
	WAIT
ENDIF
;────────────────────────────────────
;행동 전판정（EVETRAIN보다 이동）
;2011/10/14		BEFORE_ACT로부터 잘라 독립했다함수로 변경
;────────────────────────────────────
@TRAIN_CANCEL
IF IS_POLICY("금일", "휴식")
	;죠교자행동 전구상· 포상에 의한 중지
	CALL KOJO_EVENT(12, 3)
	IF RESULT < 2
		SIF RESULT
			PRINTL 
		PRINTFORML %CALLNAME:TARGET%(은)는 오늘의 조교를 중지해,
		PRINTFORML %CALLNAME:MASTER%(와)과 쉬며 시간을 보냈다…
	ENDIF
	CALL _BEFORE_ACT_SUB1
ELSEIF CFLAG:소모 > 5
	;죠교자행동 전구상·죠교자의 피로에 의한 중지
	CALL KOJO_EVENT(12, 1)
	IF RESULT < 2
		SIF RESULT
			PRINTL 
		PRINTFORML 조교의 시간이 왔지만, 들어 온 %CALLNAME:TARGET%(은)는 어쩐지 피곤한 모습으로,
		PRINTFORML 이번은 휴일이라고 말해 나와 갔다…
	ENDIF
	CALL _BEFORE_ACT_SUB1
;SANDBOX 한정 처리 체력불발로조교중지
ELSEIF CFLAG:MASTER:소모 > 5 || (FLAG:모드 == 1 && BASE:MASTER:체력 * 100 / MAXBASE:MASTER:체력 < 30)
	;죠교자행동 전구상·죠교대상의 피로에 의한 중지
	CALL KOJO_EVENT(12, 2)
	IF RESULT < 2
		SIF RESULT
			PRINTL 
		PRINTFORML 조교의 시간이 왔지만, %CALLNAME:TARGET%(은)는 심한 모습의 %CALLNAME:MASTER%(을)를 봐 당분간 생각하면,
		PRINTFORML 이번은 쉬어도 좋다고 말해 나와 갔다…
	ENDIF
	CALL _BEFORE_ACT_SUB1
ENDIF

@_BEFORE_ACT_SUB1
DRAWLINE
CFLAG:TARGET:소모 = 0
CFLAG:MASTER:소모 = 0
TFLAG:조교자행동제 = 1
TFLAG:턴방침 = -1
TFLAG:ACT분류 = -1
TFLAG:ACT = -1
TFLAG:일회휴게 = 1


;────────────────────────────────────
;더러운 표시
;────────────────────────────────────
@STAIN_INFO
CALL _STAIN_INFO_SUB1(MASTER)
CALL _STAIN_INFO_SUB1(TARGET)
CALL _STAIN_INFO_SUB1(ASSI:1)
CALL _STAIN_INFO_SUB1(ASSI:2)
CALL _STAIN_INFO_SUB1(ASSI:3)
WAIT

@_STAIN_INFO_SUB1(ARG)
#LOCALSSIZE 20
SIF ARG < 0
	RETURN 0
SIF !STRLENS(LOCALS)
	SPLIT "입/손/Ｐ/Ｖ/Ａ/Ｂ/발/Ｖ/Ｐ/정령/Ａ/유/혈/점액", "/", LOCALS
FOR LOCAL, 0, 7
	;페니스가 없는 경우, Ｐ는 처리하지 않는다. 남자의 경우, Ｖ와Ｂ는 처리하지 않는다
	SIF (LOCAL == 2 && !PENIS(ARG)) || ((LOCAL == 3 || LOCAL == 5) && TALENT:ARG:남자)
		CONTINUE
	PRINTFORM %CALLNAME:ARG%의 %LOCALS:LOCAL%:
	FOR LOCAL:1, 0, 7
		PRINTFORM \@ GETBIT(STAIN:ARG:LOCAL, LOCAL:1) ? <%LOCALS:(7 + LOCAL:1)%> # \@
	NEXT
	PRINTL 
NEXT


;────────────────────────────────────
;상태를표시
;────────────────────────────────────
@SHOW_EQUIP_2
SIF TFLAG:누움
	PRINT  [앙향]
SIF TEQUIP:밀어넘어지는중
	PRINT  [압도]
SIF TEQUIP:안면승마 || TEQUIP:안면승마항문
	PRINT  [안면 승마]
SIF TEQUIP:야외플레이
	PRINT  [야외]
SIF TEQUIP:거울
	PRINT  [대거울]
SIF TCVAR:MASTER:애태움도 > 1
	PRINT  [초조하게 해]
SIF TEQUIP:자위중 == 1
	PRINTFORM  [%CALLNAME:MASTER%자위중]
SIF TEQUIP:자위중 == 2
	PRINTFORM  [%CALLNAME:TARGET%자위중]
SIF TEQUIP:페니스밴드
	PRINTFORM  [%CALLNAME:MASTER%페니스 밴드 장착]
SIF TEQUIP:죠교자페니스밴드
	PRINTFORM  [%CALLNAME:TARGET%페니스 밴드 장착]
SIF TEQUIP:자위중 == 3
	PRINT  [두 명 자위중]
SIF TEQUIP:성교중
	PRINTFORM  [%CALLNAME:TARGET%삽입중]
SIF TEQUIP:성교봉사중
	PRINTFORM  [%CALLNAME:MASTER%삽입중]
SIF TEQUIP:촉수조교중
	PRINT  [촉수] 
SIF !LINEISEMPTY()
	PRINTL 


@EQUIP_2USE
SIF TFLAG:저항불능
	PRINTFORML 기력을 다 써 버려, %CALLNAME:MASTER%(은)는 %CALLNAME:TARGET%에 몸을 맡기고 자르고 있다…
SIF TEQUIP:안면승마
	PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%의 안면에 걸쳐 음순을 문질러 비비고 있다…
SIF TEQUIP:안면승마항문
	PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%의 안면에 걸쳐 애널에의 봉사를 강요하고 있다…
IF TEQUIP:야외플레이
	PRINTDATAL
;OriginalString : あたりに人の気配はないようだ…
		DATA 근처에 사람의 기색은 없는 것 같다…
;OriginalString : 近くで誰かの발音が聞こえた…
		DATA 근처에서 누군가의 발소리가 들렸다…
;OriginalString : 背中から人の視線を感じる…
		DATA 등으로부터 사람의 시선을 느낀다…
;OriginalString : 
	ENDDATA
ENDIF
SIF TEQUIP:거울
	PRINTFORML 눈앞의 거울에는 %CALLNAME:TARGET%(와)과 %CALLNAME:MASTER%의 치태가 비치고 있다…
SIF TCVAR:MASTER:애태움도 > 1
	PRINTFORML %CALLNAME:MASTER%(은)는 %CALLNAME:TARGET%에 초조하게 해져 허덕이고 있다…
SIF TEQUIP:자위중 == 1
	PRINTFORML %CALLNAME:TARGET%(은)는 %CALLNAME:MASTER%(은)는 자위하는 모습을 웃음을 띄워 바라보고 있다…
SIF TEQUIP:자위중 == 2
	PRINTFORML %CALLNAME:TARGET%\@ TEQUIP:눈가리개 ? 의 손가락과 애액이 연주하는 추잡한 물소리가, 시야를 빼앗겼다%CALLNAME:MASTER%의 귓전에 닿는다 # 하%CALLNAME:MASTER%에 스스로의 치태를 과시하고 있다 \@…
SIF TEQUIP:자위중 == 3
	PRINTFORML %CALLNAME:MASTER%(와)과 %CALLNAME:TARGET%(은)는 서로 \@ TEQUIP:눈가리개 ? 마주보 # 치태를 보 \@면서 자위를 하고 있다…
SIF TEQUIP:밀어넘어지는중
	PRINTFORML %CALLNAME:MASTER%(은)는 %CALLNAME:TARGET%에 밀어 넘어뜨려져 동작이 잡히지 않는다…
SIF TEQUIP:성교중
	PRINTFORML %CALLNAME:TARGET%의\@ TEQUIP:죠교자페니스밴드 ? 모조품의 # \@ 페니스는 근원까지 %CALLNAME:MASTER%에 삽입되고 있다…
IF TEQUIP:성교봉사중
	IF TCVAR:MASTER:질내사정후경과턴 == 1 || TCVAR:MASTER:장내사정후경과턴 == 1
		PRINTFORML %CALLNAME:TARGET%의 \@ TEQUIP:성교봉사중 == 6 ? 장내 # 질내 \@(은)는 방금 전 가버린 %CALLNAME:MASTER%의 페니스로부터
		PRINTFORML 다시 정액을 짜기 시작하려 하고 있다…
	ELSE
		PRINTFORML %CALLNAME:TARGET%의 \@ TEQUIP:성교봉사중 == 6 ? 엉덩이 # 성기 \@는 \@ TEQUIP:페니스밴드 ? 모조품의 남성기 # %CALLNAME:MASTER%의 페니스 \@를 근원까지 물고 있다…
	ENDIF
ENDIF
SIF TEQUIP:촉수조교중
	PRINTFORML %CALLNAME:MASTER%(은)는 준구촉수에 걸고 빼앗기고 있다…
SELECTCASE TEQUIP:죠교대상Ｃ사용
	CASE 1
		IF PENIS(MASTER) || TEQUIP:페니스밴드
			PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%의 \@ TEQUIP:페니스밴드 ? 모형자지 # 페니스 \@(을)를 잡아 천천히 상하시키고 있다…
		ELSE
			PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%의 클리토리스에 손가락을 기게 한 계속 천천히 자극하고 있다…
		ENDIF
	CASE 2
		PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:MASTER%의 \@ PENIS(MASTER) ? 귀두 # 비순 \@에 혀를 기게 하고 있다…
ENDSELECT
SIF TCVAR:MASTER:가버려
	PRINTFORML %CALLNAME:MASTER%(은)는 이제 가버릴 것 같다…
SIF TCVAR:TARGET:가버려
	PRINTFORML %CALLNAME:TARGET%(은)는 이제 가버릴 것 같다…
SIF TEQUIP:밀어넘어지는중 || TEQUIP:안면승마 || TEQUIP:안면승마항문 || TEQUIP:야외플레이 || TEQUIP:거울 || TCVAR:MASTER:애태움도 > 1 || TEQUIP:자위중 || TEQUIP:성교중 || TEQUIP:성교봉사중 || TEQUIP:촉수조교중 || TCVAR:MASTER:가버려 || TCVAR:TARGET:가버려
	DRAWLINE

@POSE_PRINT(ARG)
#LOCALSSIZE 1
LOCALS = 현재의자세:
;LOCALS =현재의자세:

;죠교대상
SELECTCASE TCVAR:MASTER:자세
	CASE 1
		LOCALS += @"[%CALLNAME:MASTER%:누움]"
	CASE 2
		LOCALS += @"[%CALLNAME:MASTER%:앉아있다]"
	CASE 3
		LOCALS += @"[%CALLNAME:MASTER%:네발로 엎드림]"
	CASE 4
		LOCALS += @"[%CALLNAME:MASTER%:직립]"
	CASE 10
		LOCALS += @"[%CALLNAME:MASTER%:무릎으로 서있다]"
	CASEELSE
		LOCALS += @"[%CALLNAME:MASTER%:편하게 하고있다]"
ENDSELECT
;주인
SELECTCASE TCVAR:MASTER:위치상하
	CASE 1
		LOCALS += @"[%CALLNAME%:%CALLNAME:MASTER%의 위]"
	CASE 2
		LOCALS += @"[%CALLNAME%:%CALLNAME:MASTER%의 아래]"
	CASEELSE
		SELECTCASE TCVAR:MASTER:위치전후
			CASE 1
				LOCALS += @"[%CALLNAME%:%CALLNAME:MASTER%의 정면]"
			CASE 2
				LOCALS += @"[%CALLNAME%:%CALLNAME:MASTER%의 배후]"
			CASE 3
				LOCALS += @"[%CALLNAME%:%CALLNAME:MASTER%에 등을 돌리고 있다]"
			CASEELSE
				LOCALS += @"[%CALLNAME%:%CALLNAME:MASTER%를 관찰]"
		ENDSELECT
ENDSELECT
IF ARG
	CALL FADEIN(4, LOCALS)
ELSE
	PRINTSL LOCALS
ENDIF

;────────────────────────────────────
;사용중의도구를 표시
;────────────────────────────────────
@SHOW_EQUIP_1
IF TEQUIP:로션 || TEQUIP:미약 || TEQUIP:이뇨제 || TEQUIP:은방울꽃독 || TEQUIP:바이브 || TEQUIP:항문바이브 || TEQUIP:항문비즈 || TEQUIP:관장그릇＋플러그 || TEQUIP:클리캡 || TEQUIP:오나홀 || TEQUIP:유두캡 || TEQUIP:밧줄 || TEQUIP:눈가리개 || TEQUIP:볼개그 || TEQUIP:삼각목마
	PRINT 사용중 
	SIF TEQUIP:로션
		PRINTFORM [로션(%EFFECT(TEQUIP:로션, 1)%)]
	SIF TEQUIP:미약
		PRINTFORM [미약(%EFFECT(TEQUIP:미약)%)]
	SIF TEQUIP:이뇨제
		PRINTFORM [이뇨제(%EFFECT(TEQUIP:이뇨제)%)]
	SIF TEQUIP:은방울꽃독
		PRINTFORM [은방울꽃의 독(%EFFECT(TEQUIP:은방울꽃독)%)]
	SIF TEQUIP:바이브
		PRINTFORM [\@ TEQUIP:바이브 > 1 ? 특주 # \@바이브래이터]
	SIF TEQUIP:항문바이브
		PRINTFORM [\@ TEQUIP:항문바이브 > 1 ? 특주 # \@어널 바이브래이터]
	SIF TEQUIP:항문비즈
		PRINT [어널 비즈]
	SIF TEQUIP:관장그릇＋플러그
		PRINT [관장＋어널 플러그]
	SIF TEQUIP:클리캡
		PRINTFORM [\@ TEQUIP:클리캡 > 1 ? 특주 # \@구리 캡]
	SIF TEQUIP:오나홀
		PRINTFORM [\@ TEQUIP:오나홀 > 1 ? 특주 # \@오나홀]
	SIF TEQUIP:유두캡
		PRINTFORM [\@ TEQUIP:유두캡 > 1 ? 특주 # \@유두캡]
	SIF TEQUIP:밧줄
		PRINT [줄로 긴박]
	SIF TEQUIP:눈가리개
		PRINT [아이마스크]
	SIF TEQUIP:볼개그
		PRINT [볼 개그]
	SIF TEQUIP:삼각목마
		PRINT [삼각 목마 승마중]
	;코코보다↑에 TEQUIP에등록했다의 것을 기입한다
	PRINTL 
ENDIF


;────────────────────────────────────
;복장의 표시
;인수 1이라면죠교자, 2라면죠교대상만 표시. 생략이라면 양쪽 모두
;────────────────────────────────────
@SHOW_EQUIP_3(ARG)
IF ARG == 1 || !ARG
	PRINTFORM %CALLNAME:TARGET%의 의상:
	PRINTFORM \@ TEQUIP:전신겉옷 ? [%DRESS(TARGET)%] # \@\@ TEQUIP:상반신겉옷 ? [%OUTER_T(TARGET)%] # \@\@ TEQUIP:하반신겉옷 ? [%OUTER_B(TARGET)%] # \@\@ TEQUIP:상반신속옷 ? [%INNER_T(TARGET)%] # \@
	PRINTFORML \@ TEQUIP:하반신속옷 ? [%INNER_B(TARGET)%] # \@\@ TEQUIP:신발안 ? [%SOCKS(TARGET)%] # \@\@ TEQUIP:악세서리 ? [%GLOVES(TARGET)%] # \@\@ TEQUIP:코스프레 ? [%COSPLAY(TARGET)%] # \@
ENDIF
IF ARG == 2 || !ARG
	PRINTFORM %CALLNAME:MASTER%의 의상:
	PRINTFORM \@ TEQUIP:MASTER:전신겉옷 ? [%DRESS(MASTER)%] # \@\@ TEQUIP:MASTER:상반신겉옷 ? [%OUTER_T(MASTER)%] # \@\@ TEQUIP:MASTER:하반신겉옷 ? [%OUTER_B(MASTER)%] # \@\@ TEQUIP:MASTER:상반신속옷 ? [%INNER_T(MASTER)%] # \@
	PRINTFORML \@ TEQUIP:MASTER:하반신속옷 ? [%INNER_B(MASTER)%] # \@\@ TEQUIP:MASTER:신발안 ? [%SOCKS(MASTER)%] # \@\@ TEQUIP:MASTER:악세서리 ? [%GLOVES(MASTER)%] # \@\@ TEQUIP:MASTER:코스프레 ? [%COSPLAY(MASTER)%] # \@
ENDIF

;────────────────────────────────────
;행동전
;────────────────────────────────────
@BEFORE_ACT_MESSAGE
