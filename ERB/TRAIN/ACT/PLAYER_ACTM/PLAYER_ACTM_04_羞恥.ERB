;────────────────────────────────────
;수치 (자위 40/개장 41/보이고 42/거울 43/야외 44/방뇨 45/코스(미구현)46/방치 73)
;────────────────────────────────────
@ACT_M4(ARG = 0)
;1이상이라면 판정을 실시하지 않는다
#DIM LCOUNT, 1

;함수 초기화 ;함수 초기화
CALL ACT_M_INIT(4)

;처리 개시

LOCALS = ACT_ACTM4

CALL CHA_CRI_VAR, "기준치"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, "죠교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "죠교대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "죠교대상의 능력"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "죠교자의 능력"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "죠교대상의욕정"
CALLFORM %LOCALS%_DESIRE_M

CALL CHA_CRI_VAR, "죠교자의죄책감"
CALLFORM %LOCALS%_SIN_T

CALL CHA_CRI_VAR, "죠교자의 ALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "죠교대상의체력"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, "죠교대상의기력"
CALLFORM %LOCALS%_MENTAL_M

CALL CHA_CRI_VAR, "죠교자의흥미"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "죠교자의만족"
CALLFORM %LOCALS%_SATISFACTION_T

CALL CHA_CRI_VAR, @"죠교자의상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, @"죠교대상의상태(%CONDITION(TCVAR:MASTER:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_M

CALL CHA_CRI_VAR, "미약"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "이뇨제"
CALLFORM %LOCALS%_DIURETIC

CALL CHA_CRI_VAR, "장착도구"
CALLFORM %LOCALS%_TOOLS

CALL CHA_CRI_VAR, "밧줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "눈가리개"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "볼개그"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "삼각목마"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "야외플레이"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "거울"
CALLFORM %LOCALS%_T_MILLER

CALL CHA_CRI_VAR, "도구를은 두했다"
CALLFORM %LOCALS%_TAKE_OFF_TOOLS

CALL CHA_CRI_VAR, "죠교자자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "죠교대상자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "죠교대상의 의복"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "더 하드하게 해"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "용서를 빌다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "연속행동보정"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "구상보정치"
CALL ACT_ACTM_KOJO_CORRECTION_T, 4

CALL CHA_CRI_VAR, "ACT보정치"
CALLFORM %LOCALS%_CORRECTION(ARG)

;ARG가 1이상이라면 처리 중단
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;────────────────────────────────────
;최종판정
;────────────────────────────────────
CALL DEF_CRI_VAR("최대 전자")
LOCAL = GET_CRI_VAR("결정")
IF LOCAL == -1
	SIF FLAG:디버그
;OriginalString : (수치ACT분류に実行可能なACTが存在しません)
		PRINTL (수치ACT분류에 실행 가능한ACT하지만 존재하지 않습니다)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "수치")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;기준치
;-------------------------------------------------------------------------
@ACT_ACTM4_REFERENCE

;필요하지 않을지도라고 생각했으므로 시험삼아 무효화@이것 보고
[SKIPSTART]
CALL ADD_CRI_VAR("자위", 10)
CALL ADD_CRI_VAR("비패개장", 10)
CALL ADD_CRI_VAR("자위과시", 8)
CALL ADD_CRI_VAR("수치플레이", 8)
CALL ADD_CRI_VAR("야외플레이", 0)
CALL ADD_CRI_VAR("방뇨", 5)
CALL ADD_CRI_VAR("코스프레", 5)
CALL ADD_CRI_VAR("방치플레이", 0)
[SKIPEND]

;-------------------------------------------------------------------------
;죠교자의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_TALENT_T

;죠교자가프라이드높음/낮다
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 0 # -5)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 0 # 5)
ENDIF

;죠교자가자제심/충동적
IF TALENT:자제심
	CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 0 # -3)
	CALL ADD_CRI_VAR("야외플레이", -3)
ELSEIF TALENT:충동적
	CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 0 # 3)
	CALL ADD_CRI_VAR("야외플레이", 3)
ENDIF

;죠교자가무관심/호기심
IF TALENT:무관심
	CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 0 # -2)
	CALL ADD_CRI_VAR("비패개장", -3)
	CALL ADD_CRI_VAR("방치플레이", 3, 4)
ELSEIF TALENT:호기심
	CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 0 # 1, GETBIT(TEQUIP:자위중, 0) ? 0 # 3)
	CALL ADD_CRI_VAR("비패개장", 2, 3)
ENDIF

;죠교자가일선을넘지않음
SIF TALENT:일선을넘지않음 && !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", -5)

;죠교자가튀고싶어함 한편, 죠교자가자위 하고 있지 않다
SIF TALENT:튀고싶어함 && !GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("자위과시", 5, 5)

;죠교자가억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 0 # -3)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 0 # -5)
	CALL ADD_CRI_VAR("방뇨", -5)
	CALL ADD_CRI_VAR("코스프레", -5)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("자위과시", , GETBIT(TEQUIP:자위중, 1) ? 0 # 5)
	CALL ADD_CRI_VAR("야외플레이", , TEQUIP:야외플레이 ? 0 # 7)
	CALL ADD_CRI_VAR("방뇨", , 7)
	CALL ADD_CRI_VAR("코스프레", , 7)
ENDIF

;죠교자가수줍음/수치없음
IF TALENT:수줍음
	CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 0 # -7)
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:거울 ? 0 # -5)
ELSEIF TALENT:수치없음
	CALL ADD_CRI_VAR("자위과시", , GETBIT(TEQUIP:자위중, 1) ? 0 # 5)
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:거울 ? 0 # 5)
ENDIF

;죠교자가젖기쉬움
SIF TALENT:젖기쉬움
	CALL ADD_CRI_VAR("비패개장", , 2)

;죠교자가자위하기쉬움 한편, 죠교자가자위 하고 있지 않다
SIF TALENT:자위하기쉬움 && !GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("자위과시", 3, 1)

;죠교자가더러움무시, 냄새둔감/민감
IF TALENT:더러움무시 || TALENT:냄새둔감
	CALL ADD_CRI_VAR("방뇨", 1, 4)
ELSEIF TALENT:냄새민감
	CALL ADD_CRI_VAR("방뇨", 4)
ENDIF

;죠교자가쾌감에솔직/부정
IF TALENT:쾌감에솔직
	CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 0 # 3)
ELSEIF TALENT:쾌감을부정
	CALL ADD_CRI_VAR("자위과시", , GETBIT(TEQUIP:자위중, 1) ? 0 # -6)
ENDIF

;죠교자가남성혐오 한편, 죠교대상이남자
SIF TALENT:남성혐오 && TALENT:MASTER:남자
	CALL ADD_CRI_VAR("자위", -5)

;죠교자가새드
IF TALENT:새드
	CALL ADD_CRI_VAR("야외플레이", , TEQUIP:야외플레이 ? 0 # 4)
	CALL ADD_CRI_VAR("방뇨", 3, 3)
ENDIF

;죠교자가신중함 한편, 야외플레이중이 아니다
SIF TALENT:신중함 && !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", -3)

;죠교자가심술궂음
IF TALENT:심술궂음
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:거울 ? 0 # 3)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 0 # 1)
	CALL ADD_CRI_VAR("방뇨", 3)
	CALL ADD_CRI_VAR("방치플레이", 3)
ENDIF

;죠교자가유치
SIF TALENT:유치
	CALL ADD_CRI_VAR("방뇨", , 4)

;죠교자가매혹 한편, 죠교대상가자위 하고 있지 않다
SIF TALENT:매혹 && !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", , 6)

;-------------------------------------------------------------------------
;죠교대상의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_TALENT_M

;죠교대상가자위하기쉬움 한편, 죠교대상가자위 하고 있지 않다
SIF TALENT:MASTER:자위하기쉬움 && !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", 3, 1)

;────────────────────────────────────
;능력, 파라미터에 의한 변동
;────────────────────────────────────
;-------------------------------------------------------------------------
;죠교대상의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_ABL_M

;죠교대상의노출증. 죠교가 진행된상태로 보다하드인 메뉴를선택 하는경향
;가[0→2, 5→-5]를 가산(0～7)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(ABL:MASTER:노출증, "0→2, 5→-5, LIMIT/-5～2"))
;가[2→0, 5→2～8]을 가산(0～8)
CALL ADD_CRI_VAR("방뇨", LINE_CALC(ABL:MASTER:노출증, "2→0, 5→2, LIMIT/0～2"), LINE_CALC(ABL:MASTER:노출증, "2→0, 5→6, LIMIT/0～6"))
;가[1→0, 5→0～9]를 가산(0～9)
CALL ADD_CRI_VAR("코스프레", , LINE_CALC(ABL:MASTER:노출증, "1→0, 5→9, LIMIT/0～9"))
;가[2→0, 5→0～7]을 가산(0～7)
CALL ADD_CRI_VAR("방치플레이", , LINE_CALC(ABL:MASTER:노출증, "2→0, 5→7, LIMIT/0～7"))
;가[0→0, 2→0～4, 4→0]을 가산(0～4)
CALL ADD_CRI_VAR("자위", , LINE_CALC(ABL:MASTER:노출증, "0→0, 2→4, 4→0, LIMIT/0～4"))
;가[0→0, 2→2, 4→0]을 가산(0～2)
CALL ADD_CRI_VAR("자위과시", LINE_CALC(ABL:MASTER:노출증, "0→0, 2→2, 4→0, LIMIT/0～2"))
;※취소에보정는 붙지 않는다
;가[0→0/0, 4→0/3～5, 8→0/0]을 가산(0～5)
SIF !TEQUIP:거울
	CALL ADD_CRI_VAR("수치플레이", LINE_CALC(ABL:MASTER:노출증, "0→0, 4→3, 8→0, LIMIT/0～3"), LINE_CALC(ABL:MASTER:노출증, "0→0, 4→2, 8→0, LIMIT/0～2"))
;가[2→0/0, 5→0/4～8]을 가산(0～8)
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", LINE_CALC(ABL:MASTER:노출증, "2→0, 5→4, LIMIT/0～4"), LINE_CALC(ABL:MASTER:노출증, "2→0, 5→4, LIMIT/0～4"))

;-------------------------------------------------------------------------
;죠교자의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM4_ABL_T

;죠교자의이상가[0→0, 5→3～5]를 가산(0～5)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(ABL:이상, "0→0, 5→3, LIMIT/0～3"), LINE_CALC(ABL:이상, "0→0, 5→2, LIMIT/0～2"))

;-------------------------------------------------------------------------
;PALAM군
;여기는 개별적으로
;-------------------------------------------------------------------------
;----------------------------------
;욕정
;----------------------------------
@ACT_ACTM4_DESIRE_M

;잠정의 처리. 죠교자가심술궂음라면, 욕정했다죠교대상를…
IF TALENT:심술궂음
	;죠교대상가자위중
	IF GETBIT(TEQUIP:자위중, 0)
		;죠교자의욕정가[3000→0, 7500→5]를 가산(0～5)
		CALL ADD_CRI_VAR("자위", LINE_CALC(PALAM:MASTER:욕정, "3000→0, 7500→5, LIMIT/0～5"))
	;죠교대상는자위 하고 있지 않다
	ELSE
		;죠교자의욕정가[3000→0/2, 7500→5/7]을 가산(0～7)(죠교자자위에 의해 증가)
		CALL ADD_CRI_VAR("방치플레이", LINE_CALC(PALAM:MASTER:욕정, "3000→0, 7500→5, LIMIT/0～5") + GETBIT(TEQUIP:자위중, 1))
	ENDIF
ENDIF

;-------------------------------------
;죠교자의죄책감
;-------------------------------------
@ACT_ACTM4_SIN_T

;죄책감가[0→0, 100→-5～-2]를 가산(-5～0)
CALL ADD_CRI_VAR("방뇨", LINE_CALC(CFLAG:죄책감, "0→0, 100→-2, LIMIT/-2～0"), LINE_CALC(CFLAG:죄책감, "0→0, 100→-3, LIMIT/-3～0"))
;죄책감가[0→0, 100→-7～-4]를 가산(-7～0)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(CFLAG:죄책감, "0→0, 100→-4, LIMIT/-4～0"), LINE_CALC(CFLAG:죄책감, "0→0, 100→-3, LIMIT/-3～0"))
;※취소에보정는 붙지 않는다
;죄책감가[0→0, 100→0/-5～-2]를 가산(-5～0)
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", LINE_CALC(CFLAG:죄책감, "0→0, 100→-2, LIMIT/-2～0"), LINE_CALC(CFLAG:죄책감, "0→0, 100→-3, LIMIT/-3～0"))

;-------------------------------------
;죠교자의 ALI
;-------------------------------------
@ACT_ACTM4_ALI_T

;아라이멘도가[0→0, 50→0/3]을 가산(0～3)(죠교대상자위중는보정없음)
SIF !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", LINE_CALC(CFLAG:아라이멘도, "0→0, 50→3, LIMIT/0～3"))
;아라이멘도가[0→0, 50→0/3]을 가산(0～3)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(CFLAG:아라이멘도, "0→0, 50→3, LIMIT/0～3"))
;아라이멘도가[-50→3, 50→-3]을 가산(-3～3)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(CFLAG:아라이멘도, "-50→3, 50→-3, LIMIT/-3～3"))
;아라이멘도가[-50→3, 0→0]을 가산(0～3)(취소에는보정없음)
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", LINE_CALC(CFLAG:아라이멘도, "-50→3, 0→0, LIMIT/0～3"))
;아라이멘도가[-50→3, 0→0]을 가산(0～3)
CALL ADD_CRI_VAR("방뇨", LINE_CALC(CFLAG:아라이멘도, "-50→3, 0→0, LIMIT/0～3"))

;────────────────────────────────────
;게이지나상태에 의한 변동
;────────────────────────────────────
;-------------------------------------
;죠교대상의체력
;-------------------------------------
@ACT_ACTM4_PHYSICAL_M

;죠교자의체력가[0→-1, 500→0]을 가산(-1～0)
CALL ADD_CRI_VAR("수치플레이", LINE_CALC(BASE:체력, "0→-1, 500→0, LIMIT/-1～0"))
;죠교자의체력가[0→3/-3, 1500→0/0]을 가산(-3～3)(죠교자자위중는 정)
LOCAL = GETBIT(TEQUIP:자위중, 1) ? 1 # -1
CALL ADD_CRI_VAR("자위과시", LOCAL * LINE_CALC(BASE:체력, "0→3, 1500→0, LIMIT/0～3"))
;죠교자의체력가[0→3/-3, 1500→0/0]을 가산(-3～3)(실행중은 정)
LOCAL = TEQUIP:야외플레이 ? 1 # -1
CALL ADD_CRI_VAR("야외플레이", LOCAL * LINE_CALC(BASE:체력, "0→3, 1500→0, LIMIT/0～3"))

;-------------------------------------
;죠교대상의기력
;-------------------------------------
@ACT_ACTM4_MENTAL_M

;죠교자의기력가[0→-2, 500→0]을 가산(-2～0)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(BASE:기력, "0→-2, 500→0, LIMIT/-2～0"))
;죠교자의기력가[0→2, 500→0]을 가산(0～2)
CALL ADD_CRI_VAR("방치플레이", LINE_CALC(BASE:기력, "0→2, 500→0, LIMIT/0～2"))
;죠교자의기력가[0→5/-5, 500→0/0]을 가산(-5～5)(죠교자자위중는 정)
LOCAL = GETBIT(TEQUIP:자위중, 1) ? 1 # -1
CALL ADD_CRI_VAR("자위과시", LOCAL * LINE_CALC(BASE:기력, "0→5, 750→0, LIMIT/0～5"))
;죠교자의기력가[0→3/-3, 500→0/0]을 가산(-3～3)(실행중은 정)
LOCAL = TEQUIP:야외플레이 ? 1 # -1
CALL ADD_CRI_VAR("야외플레이", LOCAL * LINE_CALC(BASE:기력, "0→3, 500→0, LIMIT/0～3"))

;-------------------------------------
;죠교자의흥미
;-------------------------------------
@ACT_ACTM4_INTEREST_T

;죠교자의흥미가[500→1～2, 750→3～6]을 가산(-2～6)
CALL ADD_CRI_VAR("비패개장", LINE_CALC(BASE:흥미, "500→1, 750→3, LIMIT/-1～3"), LINE_CALC(BASE:흥미, "500→1, 750→3, LIMIT/-1～3"))
;죠교자의흥미가 500 미만이라면
IF BASE:흥미 < 500
	CALL ADD_CRI_VAR("코스프레", , 4)
	CALL ADD_CRI_VAR("방치플레이", 3)
ENDIF

;-------------------------------------
;죠교자의만족
;-------------------------------------
@ACT_ACTM4_SATISFACTION_T

;죠교자의만족가[500→3/-3, 750→5/-5]를 가산(-5～5)(실행중은 정)
LOCAL = TEQUIP:야외플레이 ? 1 # -1
CALL ADD_CRI_VAR("야외플레이", LOCAL * LINE_CALC(BASE:만족, "500→3, 750→5, LIMIT/0～5"))

;-------------------------------------
;죠교자의상태
;-------------------------------------
@ACT_ACTM4_CONDITION_T

;죠교자의상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울굴)
SELECTCASE TCVAR:상태변화
	CASE 1 TO 3
		CALL ADD_CRI_VAR("자위과시", GETBIT(TEQUIP:자위중, 1) ? 5 # -5)
		CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 5 # -5)
	CASE 7
		CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 5 # 0)
		CALL ADD_CRI_VAR("코스프레", TEQUIP:MASTER:코스프레 ? 5 # 0)
		CALL ADD_CRI_VAR("방치플레이", GETBIT(TEQUIP:자위중, 0) ? 0 # 5)
	CASE 8
		CALL ADD_CRI_VAR("방뇨", , 6)
ENDSELECT

;-------------------------------------
;죠교대상의상태
;-------------------------------------
@ACT_ACTM4_CONDITION_M

;죠교대상의상태가피폐/쇠약/무기력
IF 1 <= TCVAR:MASTER:상태변화 && TCVAR:MASTER:상태변화 <= 3
	CALL ADD_CRI_VAR("자위", (GETBIT(TEQUIP:자위중, 0) ? 1 # -1) * (TFLAG:소모도 * 2 + TCVAR:MASTER:상태변화 + 1))
	CALL ADD_CRI_VAR("수치플레이", (TEQUIP:거울 && TCVAR:MASTER:상태변화 == 3 ? 1 # -1) * (TFLAG:소모도 + 3))
	CALL ADD_CRI_VAR("야외플레이", (TEQUIP:야외플레이 ? 1 # -1) * (TFLAG:소모도 * 2 + TCVAR:MASTER:상태변화), (TEQUIP:야외플레이 ? 1 # -1) * 5)
ENDIF

;────────────────────────────────────
;전회의행동나 상황에 의한 변동
;────────────────────────────────────
;-------------------------------------
;미약
;-------------------------------------
@ACT_ACTM4_DRAG
SIF TEQUIP:미약
	CALL ADD_CRI_VAR("방치플레이", TALENT:심술궂음 * 3 - TALENT:마음씨착한 * 3, 4)

;-------------------------------------
;이뇨제
;-------------------------------------
@ACT_ACTM4_DIURETIC
SIF TEQUIP:이뇨제
	CALL ADD_CRI_VAR("방뇨", 3, 4)

;-------------------------------------
;장착도구류
;-------------------------------------
@ACT_ACTM4_TOOLS

;바이브/Ａ바이브/클리캡/오나홀/유두캡장착중에서, 죠교대상자위중가 아니다
SIF (TEQUIP:바이브 || TEQUIP:항문바이브 || TEQUIP:클리캡 || TEQUIP:오나홀 || TEQUIP:유두캡) && !GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("자위", 3 + TEQUIP:바이브 + TEQUIP:항문바이브 + TEQUIP:클리캡 + TEQUIP:오나홀 + TEQUIP:유두캡)

;-------------------------------------
;밧줄
;-------------------------------------
@ACT_ACTM4_ROPE
SIF TEQUIP:밧줄
	CALL ADD_CRI_VAR("방치플레이", 5)

;-------------------------------------
;눈가리개
;-------------------------------------
@ACT_ACTM4_EYEMASK
IF TEQUIP:눈가리개
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:거울 ? 0 # -10)
	CALL ADD_CRI_VAR("방치플레이", 3)
ENDIF

;-------------------------------------
;볼개그
;-------------------------------------
@ACT_ACTM4_GAG
SIF TEQUIP:볼개그
	CALL ADD_CRI_VAR("방치플레이", , 3)

;-------------------------------------
;삼각목마
;-------------------------------------
@ACT_ACTM4_HORSE
SIF TEQUIP:삼각목마
	CALL ADD_CRI_VAR("방치플레이", , 7)

;-------------------------------------
;야외플레이
;-------------------------------------
@ACT_ACTM4_OUT
SIF TEQUIP:야외플레이
	CALL MUL_CRI_VAR("방치플레이", 150)

;-------------------------------------
;거울
;-------------------------------------
@ACT_ACTM4_T_MILLER
IF TEQUIP:거울
	CALL ADD_CRI_VAR("자위", GETBIT(TEQUIP:자위중, 0) ? 0 # 3)
	CALL ADD_CRI_VAR("방뇨", , 4)
ENDIF

;-------------------------------------
;요구되어도구를은 두했다
;-------------------------------------
@ACT_ACTM4_TAKE_OFF_TOOLS

;취소에 응해장착를 주저
SIF !TEQUIP:야외플레이
	CALL ADD_CRI_VAR("야외플레이", -CANCEL_TEQUIP_COUNT("야외플레이") / 20)
SIF !TEQUIP:거울
	CALL ADD_CRI_VAR("수치플레이", -CANCEL_TEQUIP_COUNT("거울") / 20)

;-------------------------------------
;죠교자자위중
;-------------------------------------
@ACT_ACTM4_MASTURBATION_T

;죠교자만
IF TEQUIP:자위중 == 2
	CALL ADD_CRI_VAR("자위", 5)
	CALL ADD_CRI_VAR("방치플레이", , 10)
ENDIF

;-------------------------------------
;죠교대상자위중
;-------------------------------------
@ACT_ACTM4_MASTURBATION_M

;죠교대상만
IF TEQUIP:자위중 == 1
	CALL ADD_CRI_VAR("비패개장", -5)
	;죠교자의하반신겉옷로 방해되지 않을 때
	SIF CLOTHESTYPE(0, 1) != 2
		CALL ADD_CRI_VAR("자위과시", 5)
	CALL ADD_CRI_VAR("방뇨", -3)
ENDIF

;-------------------------------------
;죠교대상의 의복
;-------------------------------------
@ACT_ACTM4_CLOTH_M

;죠교대상의하반신겉옷로 방해될 때
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("자위", -5)
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:거울 ? 0 # -5)
	CALL ADD_CRI_VAR("방뇨", -10)
ENDIF

;-------------------------------------
;더 하드하게 해
;-------------------------------------
@ACT_ACTM4_PLEASE_HARD
IF IS_NOWCOMNAME("더 하드하게 해")
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:거울 ? -5 # 5)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? -5 # 5)
ENDIF

;-------------------------------------
;용서를 빌다
;-------------------------------------
@ACT_ACTM4_PLEASE_HELP
;허가를 청했는지 개, 죠교자의상태가울굴 이외
IF IS_NOWCOMNAME("용서를 빌다") && TCVAR:상태변화 < 8
	CALL ADD_CRI_VAR("수치플레이", TEQUIP:거울 ? 5 # -5)
	CALL ADD_CRI_VAR("야외플레이", TEQUIP:야외플레이 ? 5 # -5)
ENDIF

;────────────────────────────────────
;같은행동연속실행의 확률을 다운합니다
;────────────────────────────────────
@ACT_ACTM4_PREV
SELECTCASE TFLAG:전ACT
	CASE 40 TO 47, 73
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), -3 * TFLAG:동일행동보너스, -4 * TFLAG:동일행동보너스)
ENDSELECT

;────────────────────────────────────
;보정치 계산
;────────────────────────────────────
@ACT_ACTM4_CORRECTION(ARG)
#DIM LCOUNT

;ARG가 0이외라면보정치를 계산해, 이후의 처리를 실시하지 않는다
IF ARG
	;73에 관해서는 여기서 계산하지 않는다
	FOR LCOUNT, 40, 47
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "누계"))
	NEXT
ELSE
	;보정치를 판정치에 가산한다  ; 보정 값을 판정 값에 가산한다
	CALL ACT_ACTM_CORRECTION(4)
ENDIF

;────────────────────────────────────
;그 외 변동이나 실행 불가능의 판정
;────────────────────────────────────
@ACT_ACTM4_ABLE
#DIM LCOUNT

;불가능 판정
CALL ACT_ACTM_ABLE(4)
