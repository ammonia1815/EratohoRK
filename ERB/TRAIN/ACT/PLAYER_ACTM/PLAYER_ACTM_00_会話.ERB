;────────────────────────────────────
;회화 (기분 0/호의 1/지식 2/농락해라 3/위협하는 4/의상 5/합의 6/처분 7/고함치다 8/비웃는 9/매도 66)
;────────────────────────────────────
@ACT_M0(ARG = 0)
;1이상이라면 판정을 실시하지 않는다
#DIM LCOUNT, 1

;함수 초기화 ;함수 초기화
CALL ACT_M_INIT(0)

;처리 개시

LOCALS = ACT_ACTM0

CALL CHA_CRI_VAR, "기준치"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, @"턴방침(%POLICY("지금")%)"
CALLFORM %LOCALS%_MENU

CALL CHA_CRI_VAR, "죠교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "죠교대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "죠교자의 성별 의식"
CALLFORM %LOCALS%_GENDER_T

CALL CHA_CRI_VAR, "죠교대상의 능력"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "죠교대상의 쾌감"
CALLFORM %LOCALS%_PLEASURE_M

CALL CHA_CRI_VAR, "죠교대상의반항"
CALLFORM %LOCALS%_RESISTANCE_M

CALL CHA_CRI_VAR, "죠교대상의치정"
CALLFORM %LOCALS%_SHYNESS_M

CALL CHA_CRI_VAR, "죠교대상의공포"
CALLFORM %LOCALS%_FEAR_M

CALL CHA_CRI_VAR, "죠교대상의억울"
CALLFORM %LOCALS%_DEPRESSION_M

CALL CHA_CRI_VAR, "죠교대상의반발각인"
CALLFORM %LOCALS%_RESISTMARK_M

CALL CHA_CRI_VAR, "죠교대상의반발각인"
CALLFORM %LOCALS%_SEXUALINFO_M

CALL CHA_CRI_VAR, "죠교자의죄책감"
CALLFORM %LOCALS%_SIN_T

CALL CHA_CRI_VAR, "죠교자의 ALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "죠교자의이성"
CALLFORM %LOCALS%_REASON_T

CALL CHA_CRI_VAR, "죠교자의흥미"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "죠교자의초조함"
CALLFORM %LOCALS%_IRRITATION_T

CALL CHA_CRI_VAR, "죠교자의만족"
CALLFORM %LOCALS%_SATISFACTION_T

CALL CHA_CRI_VAR, @"죠교자의상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, "장착도구"
CALLFORM %LOCALS%_TOOLS

CALL CHA_CRI_VAR, "미약"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "밧줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "눈가리개"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "볼개그"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "삼각목마"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "야외플레이"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "죠교대상자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "죠교자자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "삽입보정"
CALLFORM %LOCALS%_INSERT

CALL CHA_CRI_VAR, "죠교대상의 복장"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "용서를 빌다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "기출의화제"
CALLFORM %LOCALS%_ALREADY_TALKED

CALL CHA_CRI_VAR, "징계·"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "연속행동보정"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "구상보정치"
CALL ACT_ACTM_KOJO_CORRECTION_T, 0

CALL CHA_CRI_VAR, "ACT보정치"
CALLFORM %LOCALS%_CORRECTION(ARG)


;ARG가 1이상이라면 처리 중단 ;ARG가 1 이상이면 처리 중단
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE
;실행판정
;────────────────────────────────────
;최종판정
;────────────────────────────────────
CALL DEF_CRI_VAR("최대 전자")
LOCAL = GET_CRI_VAR("결정")
IF LOCAL == -1
	SIF FLAG:디버그
;OriginalString : (회화ACT분류に実行可能なACTが存在しません)
		PRINTL (회화ACT분류에 실행 가능한ACT하지만 존재하지 않습니다)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "회화")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;기준치
;-------------------------------------------------------------------------
@ACT_ACTM0_REFERENCE

;필요하지 않을지도라고 생각했으므로 시험삼아 무효화@이것 보고
[SKIPSTART]
CALL ADD_CRI_VAR("지금 기분을 묻다", 10)
CALL ADD_CRI_VAR("자신에게의 마음을 묻다", 10)
CALL ADD_CRI_VAR("성에 관한 이야기를 함", 10)
CALL ADD_CRI_VAR("상냥하게 위로한다", 10)
CALL ADD_CRI_VAR("엄하게 위협한다", 10)
CALL ADD_CRI_VAR("의상 변경을 요구한다", 0)
CALL ADD_CRI_VAR("합의를 요구한다", 10)
CALL ADD_CRI_VAR("징계한다고 위협한다", 10)
CALL ADD_CRI_VAR("고함치다", 10)
CALL ADD_CRI_VAR("기분나쁘게 웃다", 10)
CALL ADD_CRI_VAR("매도", 0)
[SKIPEND]



SIF TFLAG:ACT분류 == 4
	CALL ADD_CRI_VAR("매도", 10)

;-------------------------------------------------------------------------
;죠교방침(1=휴식/2=소프트/3=노멀/4=하드/5=이상)
;-------------------------------------------------------------------------
@ACT_ACTM0_MENU

SELECTCASE TFLAG:턴방침
	CASE 2
		SIF !TFLAG:징계·플래그
			CALL ADD_CRI_VAR("상냥하게 위로한다", 3)
	CASE 3
		CALL ADD_CRI_VAR("지금 기분을 묻다", 2)
		SIF !TFLAG:징계·플래그
			CALL ADD_CRI_VAR("자신에게의 마음을 묻다", 1)
	CASE 4
		CALL ADD_CRI_VAR("매도", 10)
		SIF !TFLAG:징계·플래그
			CALL ADD_CRI_VAR("엄하게 위협한다", 3)
ENDSELECT

;-------------------------------------------------------------
;오늘의방침
;오늘의방침(0=언제나 대로/1=A개발/2=징계·/3=하드/4=노닥노닥/5=이상/6=야외/7=애만들기/8=휴식/9=안아/10= 봉사/11=귀여워해)
;-------------------------------------------------------------
@ACT_ACTM0_POLYCY
SELECTCASE POLICY("금일")
	CASE "귀여워해"
		CALL ADD_CRI_VAR("지금 기분을 묻다", -10)
		CALL ADD_CRI_VAR("성에 관한 이야기를 하다", -10)
		CALL ADD_CRI_VAR("상냥하게 위로한다", 5)
		CALL ADD_CRI_VAR("엄하게 위협한다", -10)
		CALL ADD_CRI_VAR("징계한다고 위협한다", -10)
		CALL ADD_CRI_VAR("고함치다", -10)
		CALL ADD_CRI_VAR("기분나쁘게 웃다", -10)
	CASEELSE
		;none
ENDSELECT

;-------------------------------------------------------------------------
;죠교자의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM0_TALENT_T
	
	
;죠교자가얌전한/건방짐
IF TALENT:얌전한
	CALL ADD_CRI_VAR("상냥하게 위로한다", , 1)
	CALL ADD_CRI_VAR("고함치다", -4, 2)
ELSEIF TALENT:건방짐
	CALL ADD_CRI_VAR("상냥하게 위로한다", , -2)
	CALL ADD_CRI_VAR("고함치다", 1, 1)
ENDIF

;죠교자가프라이드높음/낮다
IF TALENT:프라이드높음
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , -2)
	CALL ADD_CRI_VAR("상냥하게 위로한다", , -4)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", 2)
	CALL ADD_CRI_VAR("매도", , 2)
ELSEIF TALENT:프라이드낮음
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , 2)
	CALL ADD_CRI_VAR("상냥하게 위로한다", , 4)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", -4)
ENDIF

;죠교자가자제심/충동적
IF TALENT:자제심
	CALL ADD_CRI_VAR("고함치다", -5)
ELSEIF TALENT:충동적
	CALL ADD_CRI_VAR("고함치다", 4, 2)
ENDIF

;죠교자가무관심/호기심
IF TALENT:무관심
	CALL ADD_CRI_VAR("지금 기분을 묻다", -5)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", -5)
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", -5)
ELSEIF TALENT:호기심
	CALL ADD_CRI_VAR("지금 기분을 묻다", 5)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", 5)
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", 5)
ENDIF

;죠교자가감정결여/정서적
IF TALENT:감정결여
	CALL ADD_CRI_VAR("상냥하게 위로한다", -3, 4)
	CALL ADD_CRI_VAR("엄하게 위협한다", -4, 2)
	CALL ADD_CRI_VAR("고함치다", -6)
ELSEIF TALENT:정서적
	CALL ADD_CRI_VAR("상냥하게 위로한다", 4)
	CALL ADD_CRI_VAR("엄하게 위협한다", 3, 2)
	CALL ADD_CRI_VAR("고함치다", 3, 4)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", , 4)
ENDIF

;죠교자가낙관적/비관적
IF TALENT:낙관적
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", 3)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", , 2)
	CALL ADD_CRI_VAR("합의를 요구한다", 5)
ELSEIF TALENT:비관적
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", -2, 2)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", , -2)
	CALL ADD_CRI_VAR("합의를 요구한다", , -6)
ENDIF

;죠교자가일선을넘지않음
IF TALENT:일선을넘지않음
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", -3)
	CALL ADD_CRI_VAR("합의를 요구한다", -5)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", -5)
ENDIF

;죠교자가튀고싶어함
IF TALENT:튀고싶어함
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , 7)
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", 1, 5)
	CALL ADD_CRI_VAR("엄하게 위협한다", , 6)
	CALL ADD_CRI_VAR("징계한다고 위협한다", 3)
	CALL ADD_CRI_VAR("고함치다", , 4)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", , 4)
ENDIF

;죠교자가정조관념/정조무관심
IF TALENT:정조관념
	CALL ADD_CRI_VAR("합의를 요구한다", 7)
ELSEIF TALENT:정조무관심
	CALL ADD_CRI_VAR("합의를 요구한다", -7)
ENDIF

;죠교자가억압/해방
IF TALENT:억압
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", -10)
	CALL ADD_CRI_VAR("합의를 요구한다", -5)
ELSEIF TALENT:해방
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", , 10)
	CALL ADD_CRI_VAR("합의를 요구한다", , 5)
ENDIF

;죠교자가수줍음/수치없음
IF TALENT:수줍음
	CALL ADD_CRI_VAR("의상 변경을 요구한다", -5)
	CALL ADD_CRI_VAR("합의를 요구한다", -5)
ELSEIF TALENT:수치없음
	CALL ADD_CRI_VAR("의상 변경을 요구한다", 5)
	CALL ADD_CRI_VAR("합의를 요구한다", 5)
ENDIF

;죠교자가습득빠름/습득느림
IF TALENT:습득빠름
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", 5)
ELSEIF TALENT:습득느림
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", -5)
ENDIF

;죠교자가헌신적
SIF TALENT:헌신적
	CALL ADD_CRI_VAR("상냥하게 위로한다", 5)

;죠교자가변태적
IF TALENT:변태적
	CALL ADD_CRI_VAR("엄하게 위협한다", , 4)
	CALL ADD_CRI_VAR("징계한다고 위협한다", 4)
	CALL ADD_CRI_VAR("매도", , 4)
ENDIF

;죠교자가새드
IF TALENT:새드
	CALL ADD_CRI_VAR("상냥하게 위로한다", -3)
	CALL ADD_CRI_VAR("엄하게 위협한다", 5)
	CALL ADD_CRI_VAR("징계한다고 위협한다", 7)
	CALL ADD_CRI_VAR("매도", 7)
ENDIF

;죠교자가신중함/민감
IF TALENT:신중함
	CALL ADD_CRI_VAR("합의를 요구한다", , -5)
	CALL ADD_CRI_VAR("징계한다고 위협한다", -3)
	CALL ADD_CRI_VAR("고함치다", -5)
ELSEIF TALENT:민감
	CALL ADD_CRI_VAR("합의를 요구한다", , 5)
	CALL ADD_CRI_VAR("고함치다", 5)
ENDIF

;죠교자가심술궂음/마음씨착한
IF TALENT:심술궂음
	CALL ADD_CRI_VAR("상냥하게 위로한다", -5)
	CALL ADD_CRI_VAR("엄하게 위협한다", , 6)
	CALL ADD_CRI_VAR("징계한다고 위협한다", 5)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", 5)
	CALL ADD_CRI_VAR("매도", 5)
ELSEIF TALENT:마음씨착한
	CALL ADD_CRI_VAR("상냥하게 위로한다", 5)
	CALL ADD_CRI_VAR("엄하게 위협한다", -5)
	CALL ADD_CRI_VAR("징계한다고 위협한다", -5)
ENDIF

;죠교자가유치
IF TALENT:유치
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", -5)
	CALL ADD_CRI_VAR("합의를 요구한다", -3)
	CALL ADD_CRI_VAR("고함치다", , 7)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", , 4)
ENDIF

;죠교자가광기
IF TALENT:광기
	CALL ADD_CRI_VAR("고함치다", , 6)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", 1, 8)
ENDIF

;죠교자가위압감
SIF TALENT:위압감
	CALL ADD_CRI_VAR("엄하게 위협한다", , 5)

;죠교자가매혹
SIF TALENT:매혹
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , 5)

;죠교자가친절함/까다로움
IF TALENT:친절함
	CALL ADD_CRI_VAR("지금 기분을 묻다", 3)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", 3)
	CALL ADD_CRI_VAR("상냥하게 위로한다", 3)
ELSEIF TALENT:까다로움
	CALL ADD_CRI_VAR("지금 기분을 묻다", -3)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", -3)
	CALL ADD_CRI_VAR("상냥하게 위로한다", -3)
ENDIF

;죠교자가 공기를 읽는 정도의 능력
SIF TALENT:독심능력
	CALL ADD_CRI_VAR("지금 기분을 묻다", -5)

;죠교자가 사고를 읽는 정도의 능력… 은, 어떻게 봐도 사토리 같습니다. 정말로(ry
;구상측이 대응하면 오히려+보정했다 (분)편이 보다 재미있어질지도 모른다
SIF NO:TARGET == 61
	CALL ADD_CRI_VAR("지금 기분을 묻다", -10)

;-------------------------------------------------------------------------
;죠교대상의 소질을 참조
;-------------------------------------------------------------------------
@ACT_ACTM0_TALENT_M

;죠교대상가겁쟁이
SIF TALENT:MASTER:겁쟁이
	CALL ADD_CRI_VAR("상냥하게 위로한다", , 5)

;죠교대상가굳셈
SIF TALENT:MASTER:굳셈
	CALL ADD_CRI_VAR("엄하게 위협한다", , 5)

;죠교대상가감정결여/정서적
IF TALENT:MASTER:감정결여
	CALL ADD_CRI_VAR("지금 기분을 묻다", , 4)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , 2)
ELSEIF TALENT:MASTER:정서적
	CALL ADD_CRI_VAR("지금 기분을 묻다", , -4)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , -2)
ENDIF

;죠교대상가낙관적/비관적
IF TALENT:MASTER:낙관적
	CALL ADD_CRI_VAR("상냥하게 위로한다", , -4)
	CALL ADD_CRI_VAR("엄하게 위협한다", , 2)
ELSEIF TALENT:MASTER:비관적
	CALL ADD_CRI_VAR("상냥하게 위로한다", , 2)
	CALL ADD_CRI_VAR("엄하게 위협한다", , -4)
ENDIF

;-------------------------------------------------------------------------
;죠교자의 성별의 기호를 참조
;-------------------------------------------------------------------------
@ACT_ACTM0_GENDER_T
;죠교자가바이
IF TALENT:바이
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , (TALENT:남자 == TALENT:MASTER:남자) ? 2 # 0)
ELSE
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", (TALENT:남자 == TALENT:MASTER:남자) ? -6 # 0)
ENDIF

;죠교자가남성혐오 한편, 죠교대상이남자
IF TALENT:남성혐오 && TALENT:MASTER:남자
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", -5)
	CALL ADD_CRI_VAR("상냥하게 위로한다", -3)
	CALL ADD_CRI_VAR("합의를 요구한다", -5)
ENDIF
;-------------------------------------------------------------------------
;죠교대상의 능력을 참조
;-------------------------------------------------------------------------
@ACT_ACTM0_ABL_M

;죠교대상의순종가[0→0, 5→-5]를 가산(-5～0)
CALL ADD_CRI_VAR("엄하게 위협한다", LINE_CALC(ABL:MASTER:순종, "0→0, 5→-5, LIMIT/-5～0"))

;죠교대상의욕망가[0→0, 5→5]를 가산(0～7)
CALL ADD_CRI_VAR("자신에게의 마음을 묻다", LINE_CALC(ABL:MASTER:순종, "0→0, 5→5, LIMIT/0～7"))

;죠교대상의노출증가[0→0, 5→0～5]를 가산(0～5)
CALL ADD_CRI_VAR("자신에게의 마음을 묻다", , LINE_CALC(ABL:MASTER:순종, "0→0, 5→5, LIMIT/0～5"))

;────────────────────────────────────
;게이지나상태에 의한 변동
;────────────────────────────────────
;-------------------------------------
;죠교대상의 쾌감
;-------------------------------------
@ACT_ACTM0_PLEASURE_M

;쾌감과호의. 쾌ＣＶＡＢ가 높을 때에 자신을 좋아하는가 (듣)묻는… 그러한손입인가
;죠교대상의 쾌X합계가[0→0, 6000→3, 12000→4, 18000→5, 24000→6, 30000→7]을 가산(0～7)
LOCAL = GET_REVISION_SQRT(PALAM:MASTER:쾌Ｃ + PALAM:MASTER:쾌Ｖ + PALAM:MASTER:쾌Ａ + PALAM:MASTER:쾌Ｂ, 30000, 7)
CALL ADD_CRI_VAR("자신에게의 마음을 묻다", LIMIT(LOCAL, 0, 7))

;-------------------------------------
;죠교대상의반항
;-------------------------------------
@ACT_ACTM0_RESISTANCE_M

;죠교대상의반항가[1500→0, 3000→1, 4500→3, 6000→5, 7500→7]을 가산(0～7)
CALL ADD_CRI_VAR("엄하게 위협한다", LINE_CALC(PALAM:MASTER:반항, "3000→1, 7500→7, LIMIT/0～7"))
CALL ADD_CRI_VAR("징계한다고 위협한다", LINE_CALC(PALAM:MASTER:반항, "3000→1, 7500→7, LIMIT/0～7"))
;죠교대상의반항가[1500→0, 3000→0～2, 4500→0～4, 6000→0～6, 7500→0～8]을 가산(0～8)
CALL ADD_CRI_VAR("고함치다", , LINE_CALC(PALAM:MASTER:반항, "3000→2, 7500→8, LIMIT/0～8"))

;-------------------------------------
;죠교대상의치정
;-------------------------------------
@ACT_ACTM0_SHYNESS_M

;죠교대상의치정가[2000→0, 3000→2, 4000→4, 5000→6, 6000→8, 7000→10]을 가산(0～11)
CALL ADD_CRI_VAR("지금 기분을 묻다", , LINE_CALC(PALAM:MASTER:치정, "2000→0, 7000→10, LIMIT/0～11"))

;-------------------------------------
;죠교대상의공포
;-------------------------------------
@ACT_ACTM0_FEAR_M

;죠교대상의공포가[0→0, 2500→1, 5000→2, 7500→3]을 가산(0～3)
;(죠교자의마음씨착한경우는 배)
CALL ADD_CRI_VAR("상냥하게 위로한다", LINE_CALC(PALAM:MASTER:치정, "0→0, 7500→3, LIMIT/0～3") * (TALENT:마음씨착한 + 1))

;-------------------------------------
;죠교대상의억울
;-------------------------------------
@ACT_ACTM0_DEPRESSION_M

;죠교대상의억울가[0→0, 2500→1, 5000→2, 7500→3]을 가산(0～3)
;(죠교자의마음씨착한경우는 배)
CALL ADD_CRI_VAR("상냥하게 위로한다", LINE_CALC(PALAM:MASTER:억울, "0→0, 7500→3, LIMIT/0～3") * (TALENT:마음씨착한 + 1))

;-------------------------------------
;죠교대상의반발각인
;-------------------------------------
@ACT_ACTM0_RESISTMARK_M

;죠교대상의반발각인가[0→0, 2→0～1, 4→1～4, 6→2～7, 8→3～10, 10→4～13]을 가산(0～13)
CALL ADD_CRI_VAR("엄하게 위협한다", LINE_CALC(MARK:반항각인, "2→0, 10→4, LIMIT/0～4"), LINE_CALC(MARK:반항각인, "2→1, 10→9, LIMIT/0～9"))

;-------------------------------------
;죠교대상의성지식
;-------------------------------------
@ACT_ACTM0_SEXUALINFO_M

;죠교대상의성지식가[10→2～5, 15→1～3, 20→0～1, 25→0, 30→-5, 35→-10]을 가산(-10～6)
CALL ADD_CRI_VAR("성에 관한 이야기를 하다", LINE_CALC(EXP:MASTER:성지식, "10→2, 20→0, 25→0, 35→-10, LIMIT/-10～2"), LINE_CALC(EXP:MASTER:성지식, "10→3, 25→0, LIMIT/0～4"))

;-------------------------------------
;죠교자의죄책감
;-------------------------------------
@ACT_ACTM0_SIN_T

;죠교자의죄책감가[20→0, 40→1, 60→2, 80→3]을 가산(0～3)
CALL ADD_CRI_VAR("상냥하게 위로한다", LINE_CALC(CFLAG:죄책감, "20→0, 80→3, LIMIT/0～3"))
;죠교자의죄책감가[0→0, 20→-1, 40→-2, 60→-3, 80→-4, 100→-5]를 가산(-5～0)
CALL ADD_CRI_VAR("엄하게 위협한다", LINE_CALC(CFLAG:죄책감, "0→0, 100→-5, LIMIT/-5～0"))
CALL ADD_CRI_VAR("징계한다고 위협한다", LINE_CALC(CFLAG:죄책감, "0→0, 100→-5, LIMIT/-5～0"))

;-------------------------------------
;죠교자의 ALI
;-------------------------------------
@ACT_ACTM0_ALI_T

;죠교자의 ALI가[-50→-6～-3, 0→0, 50→3～6]을 가산(-6～6)
CALL ADD_CRI_VAR("상냥하게 위로한다", LINE_CALC(CFLAG:아라이멘도, "-50→-3, 0→0, 50→3, LIMIT/-3～3"), LINE_CALC(CFLAG:아라이멘도, "-50→-3, 0→0, 50→3, LIMIT/-3～3"))
;죠교자의 ALI가[-50→3～6, 0→0, 50→-6～-3]을 가산(-6～6)
CALL ADD_CRI_VAR("엄하게 위협한다", LINE_CALC(CFLAG:아라이멘도, "-50→3, 0→0, 50→-3, LIMIT/-3～3"), LINE_CALC(CFLAG:아라이멘도, "-50→3, 0→0, 50→-3, LIMIT/-3～3"))
;죠교자의 ALI가[-20→0, 0→3, 20→0]을 가산(0～3)
CALL ADD_CRI_VAR("지금 기분을 묻다", LINE_CALC(CFLAG:아라이멘도, "-20→0, 0→3, 20→0, LIMIT/0～3"))
;죠교자의 ALI가[-10→0, 0→2, 10→0]을 가산(0～2)
CALL ADD_CRI_VAR("지금 기분을 묻다", LINE_CALC(CFLAG:아라이멘도, "-10→0, 0→2, 10→0, LIMIT/0～2"))

;-------------------------------------
;죠교자의이성
;-------------------------------------
@ACT_ACTM0_REASON_T

;죠교자의이성가[0→4～8, 250→2～4, 500→0, 750→-2, 1000→-4]를 가산(-4～8)
CALL ADD_CRI_VAR("고함치다", LINE_CALC(BASE:이성, "0→4, 1000→-4, LIMIT/-4～4"), LINE_CALC(BASE:이성, "0→4, 500→0, LIMIT/0～4"))
;죠교자의이성가[0→0～8, 250→0～4, 500→0, 750→-4～0]을 가산(-4～8)
CALL ADD_CRI_VAR("기분나쁘게 웃다", , LINE_CALC(BASE:이성, "0→8, 750→-4, LIMIT/-4～8"))

;-------------------------------------
;죠교자의흥미
;-------------------------------------
@ACT_ACTM0_INTEREST_T

;죠교자의흥미가[100→-5, 200→-4, 300→-3, 400→-2, 500→-1, 600→0]을 가산(-5～0)
CALL ADD_CRI_VAR("지금 기분을 묻다", LINE_CALC(BASE:흥미, "100→-5, 600→0, LIMIT/-5～0"))
CALL ADD_CRI_VAR("자신에게의 마음을 묻다", LINE_CALC(BASE:흥미, "100→-5, 600→0, LIMIT/-5～0"))

;-------------------------------------
;죠교자의초조함
;-------------------------------------
@ACT_ACTM0_IRRITATION_T

;죠교자의초조함가[0→0, 200→1～2, 400→2～4, 600→3～6, 800→4～8, 1000→5～10]을 가산(0～10)
CALL ADD_CRI_VAR("고함치다", LINE_CALC(BASE:초조함, "0→0, 1000→5, LIMIT/0～5"), LINE_CALC(BASE:초조함, "0→0, 1000→5, LIMIT/0～5"))
;죠교자의초조함가[0→0, 200→1, 400→2, 600→3, 800→4, 1000→5]를 가산(0～6)
CALL ADD_CRI_VAR("징계한다고 위협한다", LINE_CALC(BASE:초조함, "0→0, 1000→5, LIMIT/0～6"))

;-------------------------------------
;죠교자의만족
;-------------------------------------
@ACT_ACTM0_SATISFACTION_T

;죠교자의만족가[0→0, 1000→-3]을 가산(-3～0)
CALL ADD_CRI_VAR("징계한다고 위협한다", LINE_CALC(BASE:만족, "0→0, 1000→-3, LIMIT/-3～0"))
CALL ADD_CRI_VAR("매도", LINE_CALC(BASE:만족, "0→0, 1000→-3, LIMIT/-3～0"))
;죠교자의만족가[0→0, 1000→-5]를 가산(-5～0)
CALL ADD_CRI_VAR("엄하게 위협한다", LINE_CALC(BASE:만족, "0→0, 1000→-5, LIMIT/-5～0"))
CALL ADD_CRI_VAR("고함치다", LINE_CALC(BASE:만족, "0→0, 1000→-5, LIMIT/-5～0"))

;-------------------------------------
;죠교자의상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울굴)
;-------------------------------------
@ACT_ACTM0_CONDITION_T
SELECTCASE GET_CONDITION(TARGET)
	;CASE "피폐"
	;CASE "쇠약"
	CASE "무기력"
		CALL ADD_CRI_VAR("고함치다", -5)
	CASE "몽롱"
		CALL ADD_CRI_VAR("상냥하게 위로한다", , -4)
		CALL ADD_CRI_VAR("엄하게 위협한다", , -4)
		CALL ADD_CRI_VAR("고함치다", , -4)
	CASE "정욕"
		CALL ADD_CRI_VAR("성에 관한 이야기를 하다", 5)
		CALL ADD_CRI_VAR("합의를 요구한다", 6)
	CASE "분노"
		CALL ADD_CRI_VAR("자신에게의 마음을 묻다", -3)
		CALL ADD_CRI_VAR("성에 관한 이야기를 하다", -3)
		CALL ADD_CRI_VAR("상냥하게 위로한다", -5)
		CALL ADD_CRI_VAR("엄하게 위협한다", , 2)
		CALL ADD_CRI_VAR("징계한다고 위협한다", 3)
		CALL ADD_CRI_VAR("고함치다", 5)
		CALL ADD_CRI_VAR("매도", 3)
	CASE "싫증"
		CALL ADD_CRI_VAR("고함치다", , 4)
		CALL ADD_CRI_VAR("기분나쁘게 웃다", 6)
		CALL ADD_CRI_VAR("매도", , 4)
	CASE "울굴"
		CALL ADD_CRI_VAR("고함치다", , 4)
		CALL ADD_CRI_VAR("기분나쁘게 웃다", 6)
		CALL ADD_CRI_VAR("매도", , 4)
ENDSELECT

;────────────────────────────────────
;전회의행동나 상황에 의한 변동
;────────────────────────────────────
;-------------------------------------
;장착도구류(바이브, 항문바이브, 항문비즈, 관장＋항문플러그, 클리캡, 오나홀, 유두캡)
;-------------------------------------
@ACT_ACTM0_TOOLS

LOCAL = TEQUIP:바이브 + TEQUIP:항문바이브 + TEQUIP:항문비즈 + (TEQUIP:관장그릇＋플러그 * 3) + TEQUIP:클리캡 + TEQUIP:오나홀 + TEQUIP:유두캡
;장착도구류에 의한보정치가[0→0, 2→1, 4→2, 6→3, 8→4, 10→5]를 가산(0～6)
CALL ADD_CRI_VAR("지금 기분을 묻다", LINE_CALC(LOCAL, "0→0, 10→5, LIMIT/0～6"))

;-------------------------------------
;미약
;-------------------------------------
@ACT_ACTM0_DRAG

IF TEQUIP:미약
	CALL ADD_CRI_VAR("지금 기분을 묻다", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", TEQUIP:미약)
ENDIF

;-------------------------------------
;ＳＭ계도구(밧줄, 눈가리개, 볼개그, 삼각목마)
;-------------------------------------
@ACT_ACTM0_ROPE
IF TEQUIP:밧줄
	CALL ADD_CRI_VAR("지금 기분을 묻다", 1)
	CALL ADD_CRI_VAR("엄하게 위협한다", 2)
ENDIF

@ACT_ACTM0_EYEMASK
IF TEQUIP:눈가리개
	CALL ADD_CRI_VAR("지금 기분을 묻다", 3)
	CALL ADD_CRI_VAR("엄하게 위협한다", 2)
ENDIF

@ACT_ACTM0_GAG
IF TEQUIP:볼개그
	CALL ADD_CRI_VAR("지금 기분을 묻다", -5)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", -10)
	CALL ADD_CRI_VAR("합의를 요구한다", -10)
ENDIF

@ACT_ACTM0_HORSE
IF TEQUIP:삼각목마
	CALL ADD_CRI_VAR("지금 기분을 묻다", (TALENT:새드 || TALENT:심술궂음) ? 3 # 0)
	CALL ADD_CRI_VAR("상냥하게 위로한다", -5)
	CALL ADD_CRI_VAR("엄하게 위협한다", 3)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", 5)
ENDIF

;-------------------------------------
;야외플레이중
;-------------------------------------
@ACT_ACTM0_OUT
IF TEQUIP:야외플레이
	CALL ADD_CRI_VAR("지금 기분을 묻다", 3)
	CALL ADD_CRI_VAR("엄하게 위협한다", , 4)
ENDIF

;-------------------------------------
;죠교대상가자위중
;-------------------------------------
@ACT_ACTM0_MASTURBATION_M
IF GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("지금 기분을 묻다", (TALENT:새드 || TALENT:심술궂음) ? 8 # 2)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", -5)
ENDIF

;-------------------------------------
;죠교자가자위중
;-------------------------------------
@ACT_ACTM0_MASTURBATION_T
IF GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", (TALENT:변태적 || TALENT:광기) ? 5 # 0)
	CALL ADD_CRI_VAR("상냥하게 위로한다", -3)
	CALL ADD_CRI_VAR("엄하게 위협한다", -3)
	CALL ADD_CRI_VAR("징계한다고 위협한다", -5)
	CALL ADD_CRI_VAR("고함치다", -5)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", -5)
ENDIF

;-------------------------------------
;죠교자삽입중
;-------------------------------------
@ACT_ACTM0_INSERT
IF TEQUIP:성교중
	CALL ADD_CRI_VAR("지금 기분을 묻다", (TALENT:새드 || TALENT:심술궂음) ? 5 # 0)
	CALL ADD_CRI_VAR("징계한다고 위협한다", -5)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", -5)
ENDIF

;-------------------------------------
;의상관련, 아직도 조정하는필요가 있습니다…
;회화계는 방해도는 아니고, 입고 있을까에서의 판단
;-------------------------------------
;죠교대상의 복장
;-------------------------------------
@ACT_ACTM0_CLOTH_M
;겉옷
;죠교대상가 상하 양쪽 모두의겉옷를 입고 있을 때
IF CLOTHESTYPE(1, 1) && CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", 12)
	;죠교자가억압/해방
	IF TALENT:억압
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -4)
	ELSEIF TALENT:해방
		CALL ADD_CRI_VAR("의상 변경을 요구한다", 4)
	ENDIF
;죠교대상가 상하 어느 쪽인가의겉옷를 입고 있을 때
ELSEIF CLOTHESTYPE(1, 1) || CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", 10)
	;죠교자가억압/해방
	IF TALENT:억압
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -3)
	ELSEIF TALENT:해방
		CALL ADD_CRI_VAR("의상 변경을 요구한다", 3)
	ENDIF
ENDIF

;속옷
;죠교대상가 상하 양쪽 모두의속옷를 입고 있을 때
IF CLOTHESTYPE(1, 0) && CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", 8)
	;죠교자가억압/해방
	IF TALENT:억압
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -4)
	ELSEIF TALENT:해방
		CALL ADD_CRI_VAR("의상 변경을 요구한다", 4)
	ENDIF
;죠교대상가 상하 어느 쪽인가의속옷를 입고 있을 때
ELSEIF CLOTHESTYPE(1, 0) || CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", 6)
	;죠교자가억압/해방
	IF TALENT:억압
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -3)
	ELSEIF TALENT:해방
		CALL ADD_CRI_VAR("의상 변경을 요구한다", 3)
	ENDIF
ENDIF

;죠교대상가 몇개의겉옷/속옷류를 입고 있을 때
IF CLOTHESTYPE(1, 1) || CLOTHESTYPE(1, 3) || CLOTHESTYPE(1, 0) || CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("의상 변경을 요구한다", 2, 4)
	;죠교자가일선을넘지않음
	SIF TALENT:일선을넘지않음
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -5)
	;죠교자가변태적
	SIF TALENT:변태적
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -3, 4)
	;죠교자가바이가 아니고, 한편죠교대상가 동성
	SIF !TALENT:바이 && !(TALENT:남자 ^^ TALENT:MASTER:남자)
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -3)
	;죠교자가남성혐오 한편, 죠교대상이남자
	SIF TALENT:남성혐오 && TALENT:MASTER:남자
		CALL ADD_CRI_VAR("의상 변경을 요구한다", -5)
ENDIF

;죠교대상가코스프레중. 질릴 때까지의상변경하지 않도록
IF TEQUIP:MASTER:코스프레
	;죠교자의흥미
	SELECTCASE BASE:흥미
		CASE IS > 750
			CALL ADD_CRI_VAR("의상 변경을 요구한다", 0)
		CASE IS > 500
			CALL ADD_CRI_VAR("의상 변경을 요구한다", -10)
		CASE IS > 250
			CALL ADD_CRI_VAR("의상 변경을 요구한다", -5)
		CASEELSE
			CALL ADD_CRI_VAR("의상 변경을 요구한다", 5)
	ENDSELECT
ENDIF

;-------------------------------------
;허가를 청했는지 개, 죠교자의상태가울굴 이외
;-------------------------------------
@ACT_ACTM0_PLEASE_HELP

IF IS_NOWCOMNAME("용서를 빌다") && TCVAR:상태변화 < 8
	CALL ADD_CRI_VAR("엄하게 위협한다", -5)
	CALL ADD_CRI_VAR("고함치다", -5)
	CALL ADD_CRI_VAR("매도", -5)
ENDIF

;-------------------------------------
;그런 이야기는 벌써 끝난플래그(1=호의, 2=지식, 4=위로해, 8=위협하는, 16=합의, 32=비웃는, 64=매도)
;-------------------------------------
@ACT_ACTM0_ALREADY_TALKED

LOCAL = LIMIT(ABL:회화 / 2 - 5, -5, 0)
SIF TFLAG:기출의화제 & 1
	CALL ADD_CRI_VAR("자신에게의 마음을 묻다", LOCAL)
SIF TFLAG:기출의화제 & 2
	CALL ADD_CRI_VAR("성에 관한 이야기를 하다", LOCAL)
SIF TFLAG:기출의화제 & 4
	CALL ADD_CRI_VAR("상냥하게 위로한다", LOCAL)
SIF TFLAG:기출의화제 & 8
	CALL ADD_CRI_VAR("엄하게 위협한다", LOCAL)
SIF TFLAG:기출의화제 & 16
	CALL ADD_CRI_VAR("합의를 요구한다", LOCAL)
SIF TFLAG:기출의화제 & 32
	CALL ADD_CRI_VAR("기분나쁘게 웃다", LOCAL)
SIF TFLAG:기출의화제 & 64
	CALL ADD_CRI_VAR("매도", LOCAL)

;-------------------------------------
;징계·모드
;-------------------------------------
@ACT_ACTM0_OSHIOKI

IF TFLAG:징계·플래그
	CALL ADD_CRI_VAR("징계한다고 위협한다", 8, 5)
	CALL ADD_CRI_VAR("기분나쁘게 웃다", , 7)
	CALL ADD_CRI_VAR("매도", 6, 7)
ENDIF

;────────────────────────────────────
;같은행동연속실행의 확률을 다운합니다
;────────────────────────────────────
@ACT_ACTM0_PREV

SELECTCASE TFLAG:전ACT
	CASE 0 TO 9, 66
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:전ACT), -3 * TFLAG:동일행동보너스, -4 * TFLAG:동일행동보너스)
ENDSELECT

;────────────────────────────────────
;보정치 계산
;────────────────────────────────────
@ACT_ACTM0_CORRECTION(ARG)
#DIM LCOUNT

;ARG가 0이외라면보정치를 계산해, 이후의 처리를 실시하지 않는다
IF ARG
	;66에 관해서는 여기서 계산하지 않는다
	FOR LCOUNT, 0, 10
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "누계"))
	NEXT
ELSE
	;보정치를 판정치에 가산한다  ; 보정 값을 판정 값에 가산한다
	CALL ACT_ACTM_CORRECTION(0)
ENDIF

;────────────────────────────────────
;실행 불가능의 판정
;────────────────────────────────────
@ACT_ACTM0_ABLE
#DIM LCOUNT

;불가능 판정
CALL ACT_ACTM_ABLE(0)

