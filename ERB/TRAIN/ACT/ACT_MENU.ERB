;────────────────────────────────────
;죠교유별의 판정(회화 0/애무 1/도구 2/성교 3/수치 4/봉사 5/가학 6/이상 7/사역 8/성봉 10)
;────────────────────────────────────
@ACT_MENU
#DIM LCOUNT

;변수를 초기화 ;변수 초기화
CALL SET_CRI_VAR
FOR LCOUNT, 0, 11
	;휴식를 튕긴다
	;사역는 처리하지 않는다
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	CALL SET_CRI_VAR, MENU(LCOUNT), 1
NEXT

LOCALS = ACT_MENU

CALL CHA_CRI_VAR, "줄거리"
CALLFORM %LOCALS%_STORY_T

CALL CHA_CRI_VAR, "상황 판단"
CALLFORM %LOCALS%_ESTIMATE_T

CALL CHA_CRI_VAR, "죠교자의 소질"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "죠교대상의 소질"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "죠교자가임신"
CALLFORM %LOCALS%_MATERNITY_T

CALL CHA_CRI_VAR, "죠교자가처녀"
CALLFORM %LOCALS%_MAIDEN_T

CALL CHA_CRI_VAR, "죠교대상가처녀"
CALLFORM %LOCALS%_MAIDEN_M

CALL CHA_CRI_VAR, "죠교대상가동정"
CALLFORM %LOCALS%_CHERRY_M

CALL CHA_CRI_VAR, "죠교자의 능력"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "죠교대상의 능력"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "성별 관계"
CALLFORM %LOCALS%_SEX

CALL CHA_CRI_VAR, @"오늘의방침(%POLICY("금일")%)"
CALLFORM %LOCALS%_POLICY

CALL CHA_CRI_VAR, @"이번방침(%POLICY("지금")%)"
CALLFORM %LOCALS%_COURCE

CALL CHA_CRI_VAR, "죠교대상의윤활"
CALLFORM %LOCALS%_MOIST_M

CALL CHA_CRI_VAR, "죠교대상의욕정"
CALLFORM %LOCALS%_DESIRE_M

CALL CHA_CRI_VAR, "죠교대상의굴복"
CALLFORM %LOCALS%_YIELD_M

CALL CHA_CRI_VAR, "죠교대상의공순"
CALLFORM %LOCALS%_ALLEGIANCE_M

CALL CHA_CRI_VAR, "죠교대상의사정게이지"
CALLFORM %LOCALS%_ECSTASY_M

IF TCVAR:MASTER:요강제발기
	CALL CHA_CRI_VAR, "요강제발기"
ELSE
	CALL CHA_CRI_VAR, "발기도"
ENDIF
CALLFORM %LOCALS%_ERECTION_M

CALL CHA_CRI_VAR, "죠교자의 쾌감"
CALLFORM %LOCALS%_PREASURE_T

CALL CHA_CRI_VAR, "죠교자의체력"
CALLFORM %LOCALS%_PHYSICAL_T

CALL CHA_CRI_VAR, "죠교자의기력"
CALLFORM %LOCALS%_MENTAL_T

CALL CHA_CRI_VAR, "죠교자의이성"
CALLFORM %LOCALS%_REASON_T

CALL CHA_CRI_VAR, "죠교자의흥미"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "죠교자의초조함"
CALLFORM %LOCALS%_IRRITATION_T

CALL CHA_CRI_VAR, "죠교자의호감도"
CALLFORM %LOCALS%_LIKE_T

CALL CHA_CRI_VAR, "죠교대상의호감도"
CALLFORM %LOCALS%_LIKE_M

CALL CHA_CRI_VAR, @"죠교자의상태(%CONDITION(TCVAR:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, @"죠교대상의상태(%CONDITION(TCVAR:MASTER:상태변화)%)"
CALLFORM %LOCALS%_CONDITION_M

CALL CHA_CRI_VAR, "죠교대상의 피로"
CALLFORM %LOCALS%_TIRED_M

CALL CHA_CRI_VAR, "첫손회화보정"
CALLFORM %LOCALS%_FIRST

CALL CHA_CRI_VAR, "거울소지 보너스"
CALLFORM %LOCALS%_MILLER

CALL CHA_CRI_VAR, "더 하드하게 해"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "용서를 빌다"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "기분 좋게 해"
CALLFORM %LOCALS%_PLEASE_PREASURE

CALL CHA_CRI_VAR, "날뛴다"
CALLFORM %LOCALS%_VIOLENT

CALL CHA_CRI_VAR, "도구해제"
CALLFORM %LOCALS%_UNEQUIP

CALL CHA_CRI_VAR, "밀어넘어짐연속애무보정"
CALLFORM %LOCALS%_PETTING_CONTINUE

CALL CHA_CRI_VAR, "식스나인 유발"
CALLFORM %LOCALS%_69

CALL CHA_CRI_VAR, "죠교자의 복장"
CALLFORM %LOCALS%_CLOTH_T

CALL CHA_CRI_VAR, "죠교대상의 복장"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "미약"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "바이브"
CALLFORM %LOCALS%_VIBE

CALL CHA_CRI_VAR, "항문바이브"
CALLFORM %LOCALS%_AVIBE

CALL CHA_CRI_VAR, "항문비즈"
CALLFORM %LOCALS%_ABEADS

CALL CHA_CRI_VAR, "관장＋항문플러그"
CALLFORM %LOCALS%_APLUG

CALL CHA_CRI_VAR, "클리캡"
CALLFORM %LOCALS%_CRICAP

CALL CHA_CRI_VAR, "오나홀"
CALLFORM %LOCALS%_ONAHOLE

CALL CHA_CRI_VAR, "유두캡"
CALLFORM %LOCALS%_NIPPLECAP

CALL CHA_CRI_VAR, "밧줄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "눈가리개"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "볼개그"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "삼각목마"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "야외플레이"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "거울"
CALLFORM %LOCALS%_T_MILLER

CALL CHA_CRI_VAR, "도구해제 제한"
CALLFORM %LOCALS%_KILL_UNEQUIP

CALL CHA_CRI_VAR, "죠교자자위중"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "죠교대상자위중"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "징계·"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "삽입보정"
CALLFORM %LOCALS%_INSERT

CALL CHA_CRI_VAR, "애태움중"
CALLFORM %LOCALS%_IRRITATE

CALL CHA_CRI_VAR, "Ａ섹스회피"
CALLFORM %LOCALS%_AGUARD

CALL CHA_CRI_VAR, "Ａ섹스체위 제한"
CALLFORM %LOCALS%_ASEX_LIMITATION

CALL CHA_CRI_VAR, "구상보정"
CALLFORM %LOCALS%_KOJO_CORRECTION_T

CALL CHA_CRI_VAR, "실행 판정"
CALLFORM %LOCALS%_ABLE

;가능한ACT계열수리셋트
;※히트치의보정에 사용하므로 여기서 계산
TFLAG:가능한ACT계열수 = 0
;가능한ACT계열수세트
FOR LCOUNT, 0, 11
	;휴식를 튕긴다
	;사역는 처리하지 않는다
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	;-98이상을 가능이라고 판단한다(아마 지금은 괜찮다고 생각하지만)@이것 보고
	SIF GET_CRI_VAR(MENU(LCOUNT) + "누계") > -99
		TFLAG:가능한ACT계열수++
NEXT

CALL CHA_CRI_VAR, "히트치"
CALLFORM %LOCALS%_HEAT

CALL CHA_CRI_VAR, "ACT보정치"
CALLFORM %LOCALS%_CORRECTION

;──────────────────────────────
;최종판정
;──────────────────────────────
CALL DEF_CRI_VAR("최대우선지정-애무/성봉/성교/도구/봉사/수치/가학/사역/이상/회화")
CALLF GET_CRI_VAR("누계")
CALL ACT_MENU_RESULT()
CALL ANA_CRI_VAR()

;──────────────────────────────
;ACT분류판정
;갈라 놓을 때의우선 순서는애무 1＞성봉 10＞성교 3＞도구 2＞봉사 5＞수치 4＞가학 6＞사역 8＞이상 7＞회화 0 ;무승부시의 우선 순서는 애무
;
;ARG = 0:기준치 보존 1:분류 호출 2:ARGS로 지정했다분류를 무효화
;ARG = 0 : 기준치 저장 1 : 분류 호출 2 : ARGS로 지정했다 분류를 비활성화
;──────────────────────────────
@ACT_MENU_RESULT(ARG = 0, ARGS)
#DIM LCOUNT
#DIM LTEMP, 2
#DIMS LTEMPS

;ARG가 0이라면
IF ARG == 0
	;결과를 보존
	FOR LCOUNT, 0, 9
		LOCAL:LCOUNT = RESULT:LCOUNT
	NEXT
ENDIF
;분류명 read
;※사역를 처리하도록(듯이)되었다등 추가한다
SPLIT "회화/애무/도구/성교/수치/봉사/가학/이상/성봉", "/", LOCALS
;ARG가 1이라면
IF ARG == 1
	;우선 순서 read
	LTEMPS = 애무/성봉/성교/도구/봉사/수치/가학/사역/이상/회화
	;최우선분류를 찾는다
	LTEMP = -99
	LTEMP:1 = -1
	FOR LCOUNT, 0, 9
		;수치가 작은, 무효인 경우는 무시
		IF LOCAL:LCOUNT < LTEMP || LOCAL:LCOUNT <= -99
			CONTINUE
		;수치가 같은 경우는우선 순서에 따라 변환
		ELSEIF LOCAL:LCOUNT == LTEMP
			SIF STRFIND(LTEMPS, LOCALS:LCOUNT) > STRFIND(LTEMPS, LOCALS:(LTEMP:1))
				CONTINUE
		;물론 수치가 큰 경우도 변환
		ENDIF
		;변환시는 분류 포인터를 움직인다
		LTEMP = LOCAL:LCOUNT
		LTEMP:1 = LCOUNT
	NEXT
	;최우선분류가 존재하지 않으면 1을반환
	SIF LTEMP:1 == -1
		RETURN 1
	;최우선분류를 이번 분류로 지정한다
	TFLAG:ACT분류 = MENUNUM(LOCALS:(LTEMP:1))
	RETURN 0
;ARG가 2라면
ELSEIF ARG == 2
	;지정당했다분류를 찾는다
	LTEMP = FINDELEMENT(LOCALS, ARGS)
	;존재하지 않으면 날린다
	SIF LTEMP == -1
		RETURN -1
	;존재하면 분류를 판정대상로부터 제외한다
	LOCAL:LTEMP = -99
	;이번 분류가 지정당했다의 것이라면, 미선택에 되돌린다
	SIF TFLAG:ACT분류 == MENUNUM(LOCALS:LTEMP)
		TFLAG:ACT분류 = -1
ENDIF

;-------------------------------------------------------------------------
;줄거리보정 ;줄거리 보정
;-------------------------------------------------------------------------
@ACT_MENU_STORY_T

	;실험 코드
	;CALL ADD_CRI_VAR("성교", 999)
	;CALL ADD_CRI_VAR("성봉", 999)
	
	
[SKIPSTART]
;엑스트라모드 이외에서는
IF FLAG:모드 != 9
	;성교, 성봉에 마이나스보정(20일경과로 완전하게 해소)
	CALL ADD_CRI_VAR("성교", MIN(DAY / 2 - 10, 0))
	CALL ADD_CRI_VAR("성봉", MIN(DAY / 2 - 10, 0))
ENDIF
[SKIPEND]

;-------------------------------------------------------------------------
;상황 판단
;-------------------------------------------------------------------------
@ACT_MENU_ESTIMATE_T
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	CALL ADD_CRI_VAR(MENU(LCOUNT), EST_TO_ACTM("취득", MENU(LCOUNT)))
NEXT

;-------------------------------------------------------------------------
;죠교자의 소질을 참조
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자가프라이드높음/낮다
IF TALENT:LOCAL:프라이드높음
	CALL ADD_CRI_VAR("봉사",  9)
ELSEIF TALENT:LOCAL:프라이드낮음
	CALL ADD_CRI_VAR("봉사", -9)
ENDIF

;죠교자가무관심/호기심
IF TALENT:LOCAL:무관심
	CALL ADD_CRI_VAR("회화", -15)
	CALL ADD_CRI_VAR("애무",  -6)
	CALL ADD_CRI_VAR("도구", , -9)
ELSEIF TALENT:LOCAL:호기심
	CALL ADD_CRI_VAR("회화", 3, 18)
	CALL ADD_CRI_VAR("애무", 6)
	CALL ADD_CRI_VAR("도구", 3,  9)
	CALL ADD_CRI_VAR("성봉", 3,  6)
ENDIF

;죠교자가억압/해방
IF TALENT:LOCAL:억압
	CALL ADD_CRI_VAR("도구",  -6)
	CALL ADD_CRI_VAR("성교",  -6)
	CALL ADD_CRI_VAR("수치", -12)
	CALL ADD_CRI_VAR("가학",  -3)
	CALL ADD_CRI_VAR("이상",  -9)
	CALL ADD_CRI_VAR("성봉",  -3)
ELSEIF TALENT:LOCAL:해방
	CALL ADD_CRI_VAR("도구",   6)
	CALL ADD_CRI_VAR("성교",   6)
	CALL ADD_CRI_VAR("수치",  12)
	CALL ADD_CRI_VAR("가학",   3)
	CALL ADD_CRI_VAR("이상",   9)
	CALL ADD_CRI_VAR("성봉",   3)
ENDIF

;죠교자가수줍음/수치없음
IF TALENT:LOCAL:수줍음
	CALL ADD_CRI_VAR("수치", -9)
	CALL ADD_CRI_VAR("성봉", -6)
ELSEIF TALENT:LOCAL:수치없음
	CALL ADD_CRI_VAR("수치",  9)
	CALL ADD_CRI_VAR("성봉",  6)
ENDIF

;죠교자가혀기술
SIF TALENT:LOCAL:혀기술
	CALL ADD_CRI_VAR("애무", 9)

;죠교자가바느질(바늘소지시)
SIF TALENT:LOCAL:바느질 && ITEM:바늘
	CALL ADD_CRI_VAR("가학", 9)

;죠교자가거친혀
SIF TALENT:LOCAL:거친혀
	CALL ADD_CRI_VAR("애무", -9)

;죠교자가 요령 있는 손가락
SIF TALENT:LOCAL:손가락기술
	CALL ADD_CRI_VAR("애무", 9)

;죠교자가허리기술
IF TALENT:LOCAL:허리기술
	CALL ADD_CRI_VAR("성교", 9)
	CALL ADD_CRI_VAR("성봉", 6)
ENDIF

;죠교자가긴박능숙(밧줄소지시)
SIF TALENT:LOCAL:긴박능숙 && ITEM:밧줄
	CALL ADD_CRI_VAR("가학", 9)

;죠교자가도구능숙
SIF TALENT:LOCAL:도구능숙
	CALL ADD_CRI_VAR("도구", 9)

;죠교자가자위하기쉬움
SIF TALENT:LOCAL:자위하기쉬움
	CALL ADD_CRI_VAR("수치", 9)

;죠교자가헌신적
SIF TALENT:LOCAL:헌신적
	CALL ADD_CRI_VAR("봉사", -9)

;죠교자가수동적
SIF TALENT:LOCAL:수동적
	CALL ADD_CRI_VAR("봉사", 9)

;죠교자가쾌감에솔직/부정
IF TALENT:LOCAL:쾌감에솔직
	CALL ADD_CRI_VAR("성교",  9)
	CALL ADD_CRI_VAR("봉사",  6)
	CALL ADD_CRI_VAR("성봉",  9)
ELSEIF TALENT:LOCAL:쾌감을부정
	CALL ADD_CRI_VAR("성교", -9)
	CALL ADD_CRI_VAR("봉사", -6)
	CALL ADD_CRI_VAR("성봉", -6)
ENDIF

;죠교자가변태적
IF TALENT:LOCAL:변태적
	CALL ADD_CRI_VAR("가학", 3)
	CALL ADD_CRI_VAR("이상", 3)
ENDIF

;죠교자가마음씨착한
SIF TALENT:LOCAL:마음씨착한
	CALL ADD_CRI_VAR("가학", -9)

;죠교자가유치
IF TALENT:LOCAL:유치
	CALL ADD_CRI_VAR("애무", , 15)
	CALL ADD_CRI_VAR("수치", -9)
	CALL ADD_CRI_VAR("가학", -3)
	CALL ADD_CRI_VAR("이상", , 12)
ENDIF

;죠교자가광기
SIF TALENT:LOCAL:광기
	CALL ADD_CRI_VAR("이상", , 20)
	
;-------------------------------------------------------------------------
;죠교대상의 소질을 참조
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;죠교대상가위압감
IF TALENT:LOCAL:위압감
	CALL ADD_CRI_VAR("봉사", -3)
	CALL ADD_CRI_VAR("가학", -3)
ENDIF

;죠교대상가매혹
IF TALENT:LOCAL:매혹
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("성교", , 4)
	CALL ADD_CRI_VAR("성봉", , 5)
ENDIF

;죠교대상가친절함/까다로움
IF TALENT:LOCAL:친절함
	CALL ADD_CRI_VAR("회화",  3)
	CALL ADD_CRI_VAR("도구",   , -2)
	CALL ADD_CRI_VAR("봉사",   ,  2)
	CALL ADD_CRI_VAR("성봉",   ,  2)
ELSEIF TALENT:LOCAL:까다로움
	CALL ADD_CRI_VAR("회화", -3)
	CALL ADD_CRI_VAR("도구",   ,  2)
	CALL ADD_CRI_VAR("봉사",   , -2)
	CALL ADD_CRI_VAR("성봉",   , -2)
ENDIF

;-------------------------------------
;죠교자가임신중
;-------------------------------------
@ACT_MENU_MATERNITY_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG
;임신중
IF TALENT:LOCAL:임신
	CALL ADD_CRI_VAR("가학",  -5)
	CALL ADD_CRI_VAR("이상", -10)
	CALL ADD_CRI_VAR("성봉",  -5)
ENDIF

;-------------------------------------------------------------------------
;동정·처녀관계
;인수는죠교자·죠교대상
;죠교대상가처녀때, 함부로성교 하지 않는 사양입니다. 동정도 같은 처리를 실시하지만 효과는 약간 낮습니다.
;죠교대상가처녀로합의미취득 한편, 죠교대상가여자
;-------------------------------------------------------------------------
@ACT_MENU_MAIDEN_T, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;죠교자가처녀로, 죠교대상에페니스가 있다
IF TALENT:LOCAL:처녀 && PENIS(LOCAL:1)
	;죠교자가정조관념/정조무관심
	LOCAL:1 = CFLAG:LOCAL:호의 + CFLAG:LOCAL:M호의 / 2
	IF TALENT:LOCAL:정조관념
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-12, 1200→0, LIMIT/-24～0"))
		CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→-12, 2400→0, LIMIT/-24～0"))
	ELSEIF TALENT:LOCAL:정조무관심
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-4, 400→0, LIMIT/-8～0"))
		CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→-4, 800→0, LIMIT/-8～0"))
	ELSE
		CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "0→-8, 800→0, LIMIT/-16～0"))
		CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→-8, 1600→0, LIMIT/-16～0"))
	ENDIF
	;죠교자가겁쟁이
	IF TALENT:LOCAL:겁쟁이
		CALL ADD_CRI_VAR("성교", -4)
		CALL ADD_CRI_VAR("성봉", -2)
	ENDIF
	;죠교자가일선을넘지않음
	IF TALENT:LOCAL:일선을넘지않음
		CALL ADD_CRI_VAR("성교", -4)
		CALL ADD_CRI_VAR("성봉", -2)
	ENDIF
ENDIF

;-----------------------------------------
;죠교대상가처녀
;-----------------------------------------
@ACT_MENU_MAIDEN_M, ARG = -1, ARG:1 = 1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
IF TALENT:(LOCAL:1):처녀 && !CFLAG:LOCAL:합의 && !TALENT:(LOCAL:1):후타나리
	CALL ADD_CRI_VAR("성교", -5, -2)
	;죠교자가겁쟁이/자제심/일선을넘지않음/신중함
	SIF TALENT:LOCAL:겁쟁이 || TALENT:LOCAL:자제심 || TALENT:LOCAL:일선을넘지않음 || TALENT:LOCAL:신중함
		CALL ADD_CRI_VAR("성교", -3)
	;죠교자가굳셈/충동적/민감, 또는죠교자의상태가정욕/분노/싫증/광기의 언젠가
	SIF TALENT:LOCAL:굳셈 || TALENT:LOCAL:충동적 || TALENT:LOCAL:민감 || TCVAR:상태변화 > 4
		CALL ADD_CRI_VAR("성교", , 6)
	;뒤를 사용할 수 있는상태라면 패널티를 경감(죠교대상의Ａ경험 3 이상)
	SIF EXP:(LOCAL:1):Ａ경험 > 2
		CALL ADD_CRI_VAR("성교", 3)
	;죠교자의죠교Lv가 1이상
	SIF CFLAG:LOCAL:죠교레벨 > 0
		CALL ADD_CRI_VAR("성교", GET_TRAINLV(LOCAL) / 3)
	;죠교자에의호의가 1000 초과
	SIF CFLAG:LOCAL:M호의 > 1000
		CALL ADD_CRI_VAR("성교", CFLAG:LOCAL:M호의 / 500)
ENDIF

@ACT_MENU_CHERRY_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;죠교대상가동정로합의미취득 한편, 죠교대상이남자 또는후타나리처녀
IF TALENT:(LOCAL:1):동정 && !CFLAG:합의 && (TALENT:(LOCAL:1):남자 || TALENT:(LOCAL:1):처녀)
	CALL ADD_CRI_VAR("성교", -3, -1)
	;죠교자가겁쟁이/자제심/일선을넘지않음/신중함
	SIF TALENT:LOCAL:겁쟁이 || TALENT:LOCAL:자제심 || TALENT:LOCAL:일선을넘지않음 || TALENT:LOCAL:신중함
		CALL ADD_CRI_VAR("성교", -2)
	;죠교자가굳셈/충동적/민감, 또는죠교자의상태가정욕/분노/싫증/광기의 언젠가
	SIF TALENT:LOCAL:굳셈 || TALENT:LOCAL:충동적 || TALENT:LOCAL:민감 || TCVAR:상태변화 > 4
		CALL ADD_CRI_VAR("성교", , 3)
	;뒤를 사용할 수 있는상태라면 패널티를 경감(죠교대상의Ａ경험 2 이상)
	SIF EXP:(LOCAL:1):Ａ경험 > 1
		CALL ADD_CRI_VAR("성교", 2)
	;죠교자의죠교Lv가 1이상
	SIF CFLAG:LOCAL:죠교레벨 > 0
		CALL ADD_CRI_VAR("성교", GET_TRAINLV(LOCAL) / 3)
	;죠교자에의호의가 1000 초과
	SIF CFLAG:LOCAL:M호의 > 1000
		CALL ADD_CRI_VAR("성교", CFLAG:LOCAL:M호의 / 500)
ENDIF

;-------------------------------------------------------------------------
;죠교자의 능력을 참조
;-------------------------------------------------------------------------
@ACT_MENU_ABL_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;이미 있는 것을 살릴 때에 사용해,
;신규 개척시는 부호를 반대로 해 사용하는 것（지금은 좋아）
[SKIPSTART]

;기본적인 능력보정
FOR LCOUNT, 0, 11
	;죠교종별로부터 대응하는 ABL를 산출해, ABL보정치의 것 1/5을 가산
	CALL ADD_CRI_VAR(MENU(LCOUNT), GET_ABL(LOCAL, ABLNAME:(20 + LCOUNT)) * 2 / 10)
NEXT

;ＣＶ감각의 합계치가[0→0, 1→1, 4→2, 7→3]을 가산(0～3)
CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1, 7→3, LIMIT/0～3"))

;죠교자의Ｖ감각
CALL ADD_CRI_VAR("성봉", GET_ABL(LOCAL, "Ｖ감각") / 20)

;죠교자의노출증
SIF ABL:LOCAL:노출증
	CALL ADD_CRI_VAR("수치", GET_ABL(LOCAL, "노출증") / 20)

[SKIPEND]

;-------------------------------------------------------------------------
;죠교대상의 능력을 참조
;-------------------------------------------------------------------------
@ACT_MENU_ABL_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

;죠교대상의순종와봉사정신의 합계치가[0→0, 1→1, 4→2, 7→3]을 가산(0～3)
CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:순종 + ABL:LOCAL:봉사정신, "1→1, 7→3, LIMIT/0～3"))
;죠교대상의순종와봉사정신의 합계치가[1→0, 4→1, 7→2]를 가산(0～2)
CALL ADD_CRI_VAR("성봉", LINE_CALC(ABL:LOCAL:순종 + ABL:LOCAL:봉사정신, "4→1, 7→2, LIMIT/0～2"))

;죠교대상의ＣＶ감각의 합계치가[0→0, 1→0～1, 4→0～2, 7→0～3]을 가산(0～3)
CALL ADD_CRI_VAR("도구", , LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1, 7→3, LIMIT/0～3"))
CALL ADD_CRI_VAR("성교", , LINE_CALC(ABL:LOCAL:Ｃ감각 + ABL:LOCAL:Ｖ감각, "1→1, 7→3, LIMIT/0～3"))

;죠교대상의Ａ감각가[0→0～1, 1→0～2, 3→0～4, 5→0～6]을 가산(0～6)
CALL ADD_CRI_VAR("도구", , LINE_CALC(ABL:LOCAL:Ａ감각, "1→2, 5→6, LIMIT/1～6"))
;죠교대상의Ａ감각가[0→0, 1→0～1, 3→0～3, 5→0～5]를 가산(0～5)
CALL ADD_CRI_VAR("이상", , LINE_CALC(ABL:LOCAL:Ａ감각, "1→1, 5→5, LIMIT/0～5"))
;죠교대상의Ａ감각가[0→0, 3→1, 5→2]를 가산(0～2)
CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:Ａ감각, "3→1, 5→2, LIMIT/0～2"))

;죠교대상의Ｂ감각가[0→0, 3→0～1, 5→0～2]를 가산(0～2)
CALL ADD_CRI_VAR("애무", , LINE_CALC(ABL:LOCAL:Ｂ감각, "3→1, 5→2, LIMIT/0～2"))
;죠교대상의Ｂ감각가[0→0, 1→0, 3→0～1]을 가산(0～1)
CALL ADD_CRI_VAR("봉사", , LINE_CALC(ABL:LOCAL:Ｂ감각, "1→0, 3→1, LIMIT/0～1"))

;죠교대상의 마조 상관 능력(마조끼/고통중독/구속중독의 합계)
;※왠지레즈끼를 보고 있었으므로마조끼로 변경
;가[0→0, 3→0～2, 6→0～4, 9→0～6, 12→0～8]을 가산(0～8)
CALL ADD_CRI_VAR("가학", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "0→0, 12→8, LIMIT/0～8"))
;가[0→0, 3→0, 6→0～2, 9→0～4, 12→0～6]을 가산(0～6)
CALL ADD_CRI_VAR("이상", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "3→0, 12→6, LIMIT/0～6"))
;가[0→0, 9→0, 12→0～2]를 가산(0～2)
CALL ADD_CRI_VAR("사역", , LINE_CALC(ABL:LOCAL:마조끼 + ABL:LOCAL:고통중독 + ABL:LOCAL:구속중독, "9→0, 12→2, LIMIT/0～2"))

;-------------------------------------------------------------------------
;성별 관계
;인수는죠교자·죠교대상
;동성으로 대응의 능력이나 소질이 없는 경우는 몸의접촉를 피하도록(듯이)
;-------------------------------------------------------------------------
@ACT_MENU_SEX, ARG = -1, ARG:1 = -1
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1

;죠교자에바이가 없고, 남자끼리
IF TALENT:LOCAL:남자 && TALENT:(LOCAL:1):남자 && !TALENT:LOCAL:바이
	;죠교자의ＢＬ끼가[0→-2, 2→-1, 4→0]을 가산(-2～0)
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-2, 4→0, LIMIT/-2～0"))
	;죠교자의ＢＬ끼가[0→-4, 1→-3, 2→-2, 3→-1, 4→0]을 가산(-4～0)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-4, 4→0, LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:ＢＬ끼, "0→-4, 4→0, LIMIT/-4～0"))
ENDIF
;죠교자에바이가 없고, 여자끼리
IF !TALENT:LOCAL:남자 && !TALENT:(LOCAL:1):남자 && !TALENT:LOCAL:바이
	;죠교자의레즈끼가[0→-2, 2→-1, 4→0]을 가산(-2～0)
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:레즈끼, "0→-2, 4→0, LIMIT/-2～0"))
	;죠교자의레즈끼가[0→-4, 1→-3, 2→-2, 3→-1, 4→0]을 가산(-4～0)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4, 4→0, LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4, 4→0, LIMIT/-4～0"))
	CALL ADD_CRI_VAR("성봉", LINE_CALC(ABL:LOCAL:레즈끼, "0→-4, 4→0, LIMIT/-4～0"))
	
	;죠교자의레즈중독가[0→0, 1→1, 3→2, 5→3]을 가산(0～3)
	CALL ADD_CRI_VAR("애무", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
	CALL ADD_CRI_VAR("성교", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
	CALL ADD_CRI_VAR("성봉", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
	CALL ADD_CRI_VAR("봉사", LINE_CALC(ABL:LOCAL:레즈중독, "1→1, 5→3, LIMIT/0～3"))
ENDIF

;죠교자가남성혐오 한편, 죠교대상이남자
IF TALENT:LOCAL:남성혐오 && TALENT:(LOCAL:1):남자
	CALL ADD_CRI_VAR("회화",  2)
	CALL ADD_CRI_VAR("애무", -3)
	CALL ADD_CRI_VAR("성교", -4)
	CALL ADD_CRI_VAR("봉사", -2)
	CALL ADD_CRI_VAR("가학",  1)
	CALL ADD_CRI_VAR("성봉", -4)
ENDIF

[SKIPSTART]
/////////////////////////////////////
오늘의방침
	0=통상         (언제나처럼)
	1=항문       (항문을괴롭혀)
	2=징계·     (징계해)
	3=하드       (격렬하게)
	4=노닥노닥 (응석부리게)
	5=이상         (망가질때까지)
	6=야외         (밖에서해)
	7=애만들기       (임신해)
	8=휴식         (쉬게)
	9=안아       (안기고 싶다)
	10= 봉사      (봉사하게)
	11=귀여워해  (귀여워해)
/////////////////////////////////////
[SKIPEND]
@ACT_MENU_POLICY
SELECTCASE POLICY("금일")
	CASE "항문"
		;죠교자가후타나리일까페니스밴드소지의 경우는성교계를 유발하도록(듯이)
		IF (CFLAG:M호의 + CFLAG:호의 >= 10000) && (TALENT:TARGET:후타나리 || ITEM:페니스밴드)
			CALL ADD_CRI_VAR("회화", -10)
			CALL ADD_CRI_VAR("애무",  -8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  10)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  -6)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성봉", -10)
		;그 외의 경우
		ELSE
			CALL ADD_CRI_VAR("애무",  10)
			CALL ADD_CRI_VAR("도구",  10)
			CALL ADD_CRI_VAR("가학", ITEM:관장세트 ? 10 # -10)
			CALL ADD_CRI_VAR("성봉", -10)
		ENDIF
	CASE "징계·"
		;징계·때는징계·플래그가 설 뿐
		;어떠한손 넣고를 해도 되는 것인지도 모른다
	CASE "하드"
		CALL ADD_CRI_VAR("도구",   5)
		CALL ADD_CRI_VAR("봉사",   8)
		CALL ADD_CRI_VAR("가학",  10)
	CASE "노닥노닥"
		;죠교대상가 여자인가개페니스밴드소지의 경우는성봉계를 유발하도록(듯이)
		IF (CFLAG:M호의 + CFLAG:호의 >= 10000) && (ITEM:페니스밴드 && !PENIS(MASTER))
			CALL ADD_CRI_VAR("회화",  -7)
			CALL ADD_CRI_VAR("애무",  -8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  -5)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  -3)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성봉",  10)
		ELSE
			CALL ADD_CRI_VAR("애무",   8)
			CALL ADD_CRI_VAR("도구", -10)
			CALL ADD_CRI_VAR("성교",  -5)
			CALL ADD_CRI_VAR("수치", -10)
			CALL ADD_CRI_VAR("봉사",  10)
			CALL ADD_CRI_VAR("가학", -10)
			CALL ADD_CRI_VAR("이상", -10)
			CALL ADD_CRI_VAR("성봉",  -5)
		ENDIF
	CASE "이상"
		CALL ADD_CRI_VAR("가학",  10)
		CALL ADD_CRI_VAR("이상",  10, 10)
		CALL ADD_CRI_VAR("사역",  10)
	CASE "애만들기"
		CALL ADD_CRI_VAR("회화",  -8)
		CALL ADD_CRI_VAR("애무",  -8)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  -4)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉",  10)
	CASE "안아"
		CALL ADD_CRI_VAR("회화", -10)
		CALL ADD_CRI_VAR("애무",  -9)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교",  10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  -2)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉", -10)
	CASE "봉사"
		CALL ADD_CRI_VAR("회화", -10)
		CALL ADD_CRI_VAR("애무", -10)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("성교", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사",  10)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉",  -5)
	CASE "귀여워해"
		CALL ADD_CRI_VAR("회화",   5)
		CALL ADD_CRI_VAR("애무",  10)
		CALL ADD_CRI_VAR("도구", -10)
		CALL ADD_CRI_VAR("수치", -10)
		CALL ADD_CRI_VAR("봉사", -10)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉", -10)
		;죠교대상이남자가 아닌 경우, 조건을 채우고 있으면성교계에+보정
		SIF !TALENT:MASTER:남자 && (CFLAG:M호의 + CFLAG:호의 >= 10000)
			CALL ADD_CRI_VAR("성교", 5)
	CASEELSE
		;none
ENDSELECT

;------------------------------------------
;이번죠교방침
;------------------------------------------
@ACT_MENU_COURCE

SELECTCASE POLICY("지금")
	CASE "소프트"
		CALL ADD_CRI_VAR("회화",   7)
		CALL ADD_CRI_VAR("애무",   5)
		CALL ADD_CRI_VAR("성교",  -3)
		CALL ADD_CRI_VAR("가학", -10)
		CALL ADD_CRI_VAR("이상", -10)
		CALL ADD_CRI_VAR("성봉",  -3)
	CASE "노멀"
		CALL ADD_CRI_VAR("가학",  -5)
		CALL ADD_CRI_VAR("이상",  -5)
	CASE "하드"
		CALL ADD_CRI_VAR("회화",  -5)
		CALL ADD_CRI_VAR("애무",  -5)
		CALL ADD_CRI_VAR("봉사",  -3)
	CASE "이상"
		CALL ADD_CRI_VAR("회화",  -5)
		CALL ADD_CRI_VAR("애무",  -5)
		CALL ADD_CRI_VAR("봉사",  -3)
		CALL ADD_CRI_VAR("가학",  10)
		CALL ADD_CRI_VAR("이상",  10, 10)
		CALL ADD_CRI_VAR("사역",  10)
;	CASE "SM"
;		CALL ADD_CRI_VAR("회화", -10)
;		CALL ADD_CRI_VAR("애무", -10)
;		SIF ! TALENT:MASTER:남자
;			CALL ADD_CRI_VAR("성교", -20)
;		CALL ADD_CRI_VAR("봉사", , -10)
;		CALL ADD_CRI_VAR("가학", 15)
;		CALL ADD_CRI_VAR("이상", 10)
;		CALL ADD_CRI_VAR("성봉", -20)
ENDSELECT

;-------------------------------------------------------------------------
;PALAM군
;여기는 개별적으로
;-------------------------------------------------------------------------
;------------------------------------------
;윤활
;------------------------------------------
@ACT_MENU_MOIST_M, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상의윤활PALAM가[0→-6, 1000→-4, 2000→-2, 3000→0]을 가산(-6～0)
LOCAL:1 = LINE_CALC(PALAM:LOCAL:윤활, "0→-6, 3000→0, LIMIT/-6～0")
;죠교자가신중함라면 보다 감산
IF TALENT:신중함
	LOCAL:1 = LOCAL:1 * 8 / 5
;죠교자가민감라면 경감
ELSEIF TALENT:민감
	LOCAL:1 = LOCAL:1 * 2 / 5
ENDIF
;도구는성교보다 감산량이 작다
CALL ADD_CRI_VAR("성교", LOCAL:1)
CALL ADD_CRI_VAR("도구", LOCAL:1 * 2 / 5)

;----------------------------------
;욕정
;----------------------------------
@ACT_MENU_DESIRE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상의욕정PALAM가[0→3～6, 1000→2～4, 2000→1～2, 3000→0, 4000→-2, 5000→-4]를 가산(-4～6)
CALL ADD_CRI_VAR("애무", LINE_CALC(PALAM:LOCAL:욕정, "0→3, 3000→0, 5000→-4, LIMIT/-4～3"), LINE_CALC(PALAM:LOCAL:욕정, "0→3, 3000→0, LIMIT/0～3"))
;죠교대상의욕정PALAM가[0→-2, 1000→-1, 2000→2, 3000→4, 4000→6, 5000→4]를 가산(-2～6)
CALL ADD_CRI_VAR("성교", LINE_CALC(PALAM:LOCAL:욕정, "0→-2, 2000→0, 2000→2, 4000→6, 5000→4, LIMIT/-2～6"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(PALAM:LOCAL:욕정, "0→-2, 2000→0, 2000→2, 4000→6, 5000→4, LIMIT/-2～6"))
;죠교대상의욕정PALAM가[0→1, 1000→2, 2000→3, 3000→4, 4000→3, 5000→2]를 가산(1～4)
CALL ADD_CRI_VAR("도구", LINE_CALC(PALAM:LOCAL:욕정, "0→1, 3000→4, 5000→2, LIMIT/1～4"))

;----------------------------------
;굴복
;----------------------------------
@ACT_MENU_YIELD_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상의굴복PALAM가[4000→-2～-1, 5000→-4～-2, 6000→-5～-2, 7000→-7～-3, 8000→-8～-3]을 가산(-8～0)
CALL ADD_CRI_VAR("가학", LINE_CALC(PALAM:LOCAL:굴복, "4000→-1, 8000→-3, LIMIT/-3～0"), LINE_CALC(PALAM:LOCAL:굴복, "4000→-1, 8000→-5, LIMIT/-5～0"))
;죠교대상의굴복PALAM가[4000→-2～0, 5000→-4～0, 6000→-6～0, 7000→-8～0, 8000→-10～0]을 가산(-10～0)
CALL ADD_CRI_VAR("사역", , LINE_CALC(PALAM:LOCAL:굴복, "4000→-2, 8000→-10, LIMIT/-10～0"))

;----------------------------------
;공순
;----------------------------------
@ACT_MENU_ALLEGIANCE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교대상의공순PALAM가[4000→0, 8000→3]을 가산(0～3)
CALL ADD_CRI_VAR("애무", LINE_CALC(PALAM:LOCAL:공순, "4000→0, 8000→3, LIMIT/0～3"))
;죠교대상의공순PALAM가[4000→1, 5000→2, 6000→3, 7000→4, 8000→5]를 가산(0～6)
CALL ADD_CRI_VAR("성교", LINE_CALC(PALAM:LOCAL:공순, "4000→1, 8000→5, LIMIT/0～6"))
CALL ADD_CRI_VAR("봉사", LINE_CALC(PALAM:LOCAL:공순, "4000→1, 8000→5, LIMIT/0～6"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(PALAM:LOCAL:공순, "4000→1, 8000→5, LIMIT/0～6"))
;죠교대상의공순PALAM가[4000→0, 5000→-1, 6000→-2, 7000→-3, 8000→-4]를 가산(-4～0)
CALL ADD_CRI_VAR("가학", LINE_CALC(PALAM:LOCAL:공순, "4000→0, 8000→-4, LIMIT/-4～0"))

;----------------------------------
;죠교대상의사정게이지
;----------------------------------
@ACT_MENU_ECSTASY_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;죠교대상의사정BASE가[0%→-3, 100%→0]을 가산(-3～0)
SIF PENIS(LOCAL) && !TEQUIP:성교봉사중
	CALL ADD_CRI_VAR("성봉", LINE_CALC(10 * BASE:LOCAL:사정 / MAXBASE:LOCAL:사정, "0→-3, 10→0, LIMIT/-3～0"))

;----------------------------------
;발기도
;
;요강부디기모드시는성봉의 패널티 없음
;에 일단 해 본다
;---------------------------------
@ACT_MENU_ERECTION_M
;발기도(요강부디기모드)
IF TCVAR:MASTER:요강제발기
	;죠교대상의발기도가[400→5, 1000→0]을 가산(0～5)
	CALL ADD_CRI_VAR("애무", LINE_CALC(TCVAR:MASTER:발기도, "400→5, 1000→0, LIMIT/0～5"))
ELSE
	;죠교대상의발기도가[400→-2, 1000→-1]을 가산(-3～0)
	CALL ADD_CRI_VAR("성봉", LINE_CALC(TCVAR:MASTER:발기도, "400→-2, 1000→-1, LIMIT/-3～0"))
ENDIF

;-------------------------------------------------------------------------
;죠교자의 베이스를 참조
;-------------------------------------------------------------------------
;----------------------------------
;죠교자의 쾌감(쾌Ｃvab의 합계)
;---------------------------------
@ACT_MENU_PREASURE_T, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자쾌감의 합계를 계산
LOCAL:1 = PALAM:쾌Ｃ + PALAM:쾌Ｖ + PALAM:쾌Ａ + PALAM:쾌Ｂ
;합계가[0→0, 600→1, 1200→2, 1800→3, 2400→4]를 가산(0～4)
CALL ADD_CRI_VAR("봉사", LINE_CALC(LOCAL:1, "0→0, 2400→4, LIMIT/0～4"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(LOCAL:1, "0→0, 2400→4, LIMIT/0～4"))
;합계가[600→0, 1200→1, 1800→2, 2400→3]을 가산(0～4)
CALL ADD_CRI_VAR("성교", LINE_CALC(LOCAL:1, "600→0, 2400→3, LIMIT/0～4"))
;합계가[1200→0, 1800→-1, 2400→-2]를 가산(-2～0)
CALL ADD_CRI_VAR("애무", LINE_CALC(LOCAL:1, "1200→0, 2400→-2, LIMIT/-2～0"))
CALL ADD_CRI_VAR("도구", LINE_CALC(LOCAL:1, "1200→0, 2400→-2, LIMIT/-2～0"))

;-------------------------------------
;죠교자의체력
;-------------------------------------
@ACT_MENU_PHYSICAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자의체력가[0→4, 500→2, 1000→0, 1500→-2]를 가산(-2～4)
CALL ADD_CRI_VAR("회화", LINE_CALC(BASE:LOCAL:체력, "0→4, 1500→-2, LIMIT/-2～4"))
;죠교자의체력가[0→2, 500→1, 1000→0]을 가산(0～2)
CALL ADD_CRI_VAR("봉사", LINE_CALC(BASE:LOCAL:체력, "0→2, 1000→0, LIMIT/0～2"))
;죠교자의체력가[0→-2, 500→-1, 1000→0]을 가산(-2～0)
CALL ADD_CRI_VAR("애무", LINE_CALC(BASE:LOCAL:체력, "0→-2, 1000→0, LIMIT/-2～0"))
CALL ADD_CRI_VAR("가학", LINE_CALC(BASE:LOCAL:체력, "0→-2, 1000→0, LIMIT/-2～0"))
;죠교자의체력가[0→-3, 500→-2, 1000→-1, 1500→0]을 가산(-3～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(BASE:LOCAL:체력, "0→-3, 1500→0, LIMIT/-3～0"))

;-------------------------------------
;죠교자의기력
;-------------------------------------
@ACT_MENU_MENTAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자의기력가[0→3, 250→2, 500→1, 1000→0]을 가산(0～3)
CALL ADD_CRI_VAR("봉사", LINE_CALC(BASE:LOCAL:기력, "0→3, 500→1, 1000→0, LIMIT/0～3"))
;죠교자의기력가[0→2, 250→1, 500→0, 1000→-1]을 가산(-1～2)
CALL ADD_CRI_VAR("사역", LINE_CALC(BASE:LOCAL:기력, "0→2, 500→0, 1000→-1, LIMIT/-1～2"))
;죠교자의기력가[0→-2, 250→-1, 500→0, 1000→2]를 가산(-2～2)
CALL ADD_CRI_VAR("애무", LINE_CALC(BASE:LOCAL:기력, "0→-2, 1000→2, LIMIT/-2～2"))
CALL ADD_CRI_VAR("성교", LINE_CALC(BASE:LOCAL:기력, "0→-2, 1000→2, LIMIT/-2～2"))
;죠교자의기력가[0→-2, 250→-1, 500→0, 1000→1]을 가산(-2～1)
CALL ADD_CRI_VAR("도구", LINE_CALC(BASE:LOCAL:기력, "0→-2, 500→0, 1000→1, LIMIT/-2～1"))
;죠교자의기력가[0→-2, 250→-1, 500→0]을 가산(-2～0)
CALL ADD_CRI_VAR("수치", LINE_CALC(BASE:LOCAL:기력, "0→-2, 500→0, LIMIT/-2～0"))
CALL ADD_CRI_VAR("성봉", LINE_CALC(BASE:LOCAL:기력, "0→-2, 500→0, LIMIT/-2～0"))
;죠교자의기력가[0→-3, 250→-2, 500→-1, 1000→1]을 가산(-3～1)
CALL ADD_CRI_VAR("가학", LINE_CALC(BASE:LOCAL:기력, "0→-3, 1000→1, LIMIT/-3～1"))
CALL ADD_CRI_VAR("이상", LINE_CALC(BASE:LOCAL:기력, "0→-3, 1000→1, LIMIT/-3～1"))

;-------------------------------------
;죠교자의이성
;-------------------------------------
@ACT_MENU_REASON_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자의이성가[0→0～4, 250→0～2, 500→0]을 가산(0～4)
FOR LCOUNT, 0, 11
	CALL ADD_CRI_VAR, MENU(LCOUNT), , LINE_CALC(BASE:LOCAL:이성, "0→4, 500→0, LIMIT/0～4")
NEXT
;죠교자의이성가 750을 초과하고 있으면-4～0을 가산
SIF BASE:LOCAL:이성 > 750
	CALL ADD_CRI_VAR("이상", , -4)

;-------------------------------------
;죠교자의흥미
;-------------------------------------
@ACT_MENU_INTEREST_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자의흥미를취득 한다
LOCAL:1 = BASE:LOCAL:흥미
FOR LCOUNT, 0, 11
	;이것들은[0→3, 250→1, 500→0, 750→-2, 1000→-3](-3～3)
	SIF MENU(LCOUNT) == "사역"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→3, 1000→-3, LIMIT/-3～3"))
	;이것들은[0→-3, 250→-2, 500→0, 750→1, 1000→3](-3～3)
	SIF GROUPMATCH(MENU(LCOUNT), "회화", "애무", "성교")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3, 1000→3, LIMIT/-3～3"))
	;이것들은[0→-2, 250→-1, 500→0, 750→1, 1000→2](-2～2)
	SIF GROUPMATCH(MENU(LCOUNT), "도구", "봉사")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-2, 1000→2, LIMIT/-2～2"))
	;이것들은[0→-3, 250→-2, 500→-1, 750→0, 1000→1](-3～1)
	SIF MENU(LCOUNT) == "성봉"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3, 1000→1, LIMIT/-3～1"))
NEXT

;-------------------------------------
;죠교자의초조함
;-------------------------------------
@ACT_MENU_IRRITATION_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자의초조함를취득 한다
LOCAL:1 = BASE:LOCAL:초조함
FOR LCOUNT, 0, 11
	;이것들은[200→2, 400→3, 600→4, 800→5](2～5)
	SIF MENU(LCOUNT) == "가학"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "200→2, 800→5, LIMIT/2～5"))
	;이것들은[0→1, 200→2, 400→3, 600→4, 800→5](1～5)
	SIF MENU(LCOUNT) == "사역"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→1, 800→5, LIMIT/1～5"))
	;이것들은[400→0, 600→-2, 800→-3](-3～0)
	SIF GROUPMATCH(MENU(LCOUNT), "도구", "성교")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→0, 800→-3, LIMIT/-3～0"))
	;이것들은[200→0, 400→-1, 600→-3, 800→-5](-5～0)
	SIF GROUPMATCH(MENU(LCOUNT), "회화", "애무")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→-1, 800→-5, LIMIT/-5～0"))
	;이것들은[0→0, 200→-1, 400→-2, 600→-3, 800→-4](-4～0)
	SIF MENU(LCOUNT) == "성봉"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→0, 800→-4, LIMIT/-4～0"))
NEXT

;-------------------------------------
;죠교자의호의
;-------------------------------------
@ACT_MENU_LIKE_T

;죠교자의호의가[-2000→-6, 2000→0]을 가산(-6～0)
CALL ADD_CRI_VAR("성봉", LINE_CALC(CFLAG:호의, "-2000→-6, 2000→0, LIMIT/-6～0"))
;죠교자의호의가[-2000→-5, 2000→0]을 가산(-5～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(CFLAG:호의, "-2000→-5, 2000→0, LIMIT/-5～0"))
;죠교자의호의가[-2000→-4, 2000→4]를 가산(-4～4)
CALL ADD_CRI_VAR("애무", LINE_CALC(CFLAG:호의, "-2000→-4, 2000→4, LIMIT/-4～4"))

;-------------------------------------
;죠교대상의호의
;주인공으로부터죠교자에의호의는죠교자자신이 보유 하고 있는데 주의
;-------------------------------------
@ACT_MENU_LIKE_M

;죠교대상의호의가[-2000→-10, 2000→0]을 가산(-10～0)
CALL ADD_CRI_VAR("성봉", LINE_CALC(CFLAG:M호의, "-2000→-10, 2000→0, LIMIT/-10～0"))
;죠교대상의호의가[-2000→-12, 2000→0]을 가산(-12～0)
CALL ADD_CRI_VAR("성교", LINE_CALC(CFLAG:M호의, "-2000→-12, 2000→0, LIMIT/-12～0"))
;죠교대상의호의가[-2000→-4, 2000→4]를 가산(-4～4)
CALL ADD_CRI_VAR("봉사", LINE_CALC(CFLAG:M호의, "-2000→-4, 2000→4, LIMIT/-4～4"))

;-------------------------------------
;죠교자의상태
;-------------------------------------
@ACT_MENU_CONDITION_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;죠교자의상태(0=통상/1=피폐/2=쇠약/3=무기력/4=몽롱/5=정욕/6=분노/7=싫증/8=울굴)
SELECTCASE TCVAR:상태변화
	;피폐·쇠약
	CASE 1, 2
		;죠교자의 피로
		CALL ADD_CRI_VAR("회화",   TCVAR:상태변화 * 2 + TFLAG:조교자소모도)
		CALL ADD_CRI_VAR("성교", -(TCVAR:상태변화 * 3 + TFLAG:조교자소모도))
		CALL ADD_CRI_VAR("가학", -(TCVAR:상태변화 * 2 + TFLAG:조교자소모도))
		CALL ADD_CRI_VAR("성봉", -(TCVAR:상태변화 * 3 + TFLAG:조교자소모도))
	;무기력
	CASE 3
		CALL ADD_CRI_VAR("애무", -3)
		CALL ADD_CRI_VAR("도구", -1)
		CALL ADD_CRI_VAR("성교", -5)
		CALL ADD_CRI_VAR("봉사",  5)
		CALL ADD_CRI_VAR("가학", -3)
		CALL ADD_CRI_VAR("이상", -1)
		CALL ADD_CRI_VAR("사역",  3)
	;몽롱
	CASE 4
		CALL ADD_CRI_VAR("애무", -3)
		CALL ADD_CRI_VAR("도구", -7)
		CALL ADD_CRI_VAR("성교", -3)
		CALL ADD_CRI_VAR("수치",   , 6)
		CALL ADD_CRI_VAR("봉사",   , 3)
		CALL ADD_CRI_VAR("가학",   , 2)
		CALL ADD_CRI_VAR("이상",   , 2)
		CALL ADD_CRI_VAR("성봉",   , 2)
	;정욕
	CASE 5
		CALL ADD_CRI_VAR("애무",  2)
		CALL ADD_CRI_VAR("도구",  1)
		CALL ADD_CRI_VAR("성교", TALENT:충동적 ? 7 # 4, 5)
		CALL ADD_CRI_VAR("수치", -3)
		CALL ADD_CRI_VAR("봉사",  2, 3)
		CALL ADD_CRI_VAR("가학",   , 2)
		CALL ADD_CRI_VAR("이상",   , 2)
		CALL ADD_CRI_VAR("성봉", TALENT:충동적 ? 7 # 4, 5)
	;분노
	CASE 6
		;TALENT:충동적 = 충동적  TALENT:자제심 = 자제심
		CALL ADD_CRI_VAR("가학", , 7 + TALENT:충동적 * 5 - TALENT:자제심 * 3)
	;싫증
	CASE 7
		CALL ADD_CRI_VAR("회화", -5)
		CALL ADD_CRI_VAR("이상",   , 5)
		CALL ADD_CRI_VAR("사역",   , 5)
	;울굴
	CASE 8
		CALL ADD_CRI_VAR("애무", ,  5)
		CALL ADD_CRI_VAR("도구", ,  5)
		CALL ADD_CRI_VAR("성교", ,  5)
		CALL ADD_CRI_VAR("수치", ,  5)
		CALL ADD_CRI_VAR("봉사", ,  5)
		CALL ADD_CRI_VAR("가학", ,  8)
		CALL ADD_CRI_VAR("이상", , 11)
		CALL ADD_CRI_VAR("사역", ,  8)
		CALL ADD_CRI_VAR("성봉", ,  5)
ENDSELECT

;-------------------------------------
;죠교대상의상태
;욕정 하고 있는죠교대상를 봐죠교자가 어떻게 생각할까
;즉 소질의 참조처는죠교자
;-------------------------------------
@ACT_MENU_CONDITION_M, ARG = -1
#DIM DYNAMIC TGT
TGT = ARG == -1 ? TARGET # ARG

;죠교대상의상태가정욕
IF IS_CONDITION(TARGET, "정욕")
	;죠교자가반항적/자제심/쾌감을부정/새드/심술궂음중 2개 이상 소지
	IF TRUECHECK(TALENT:TGT:반항적, TALENT:TGT:자제심, TALENT:TGT:쾌감을부정, TALENT:TGT:새드, TALENT:TGT:심술궂음) > 1
		CALL ADD_CRI_VAR("수치", 2)
		CALL ADD_CRI_VAR("봉사", 1)
		CALL ADD_CRI_VAR("성봉", 4)
	ELSE
		CALL ADD_CRI_VAR("성교", 3)
		CALL ADD_CRI_VAR("성봉", 4)
	ENDIF
ENDIF

;-------------------------------------
;죠교대상의 피로
;TFLAG:소모도 =피로도
;성봉가 없는 것은 위화감
;-------------------------------------
@ACT_MENU_TIRED_M

;죠교대상의소모플래그(피로)
IF TFLAG:소모도
	CALL ADD_CRI_VAR("성교", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("봉사", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("가학", -TFLAG:소모도)
	CALL ADD_CRI_VAR("이상", -TFLAG:소모도 / 2)
	CALL ADD_CRI_VAR("사역", -TFLAG:소모도 / 2)
ENDIF

;-------------------------------------
;첫손회화보정
;-------------------------------------
@ACT_MENU_FIRST

;첫손로 높은 확률회화
SIF !TFLAG:경과시간
	CALL ADD_CRI_VAR("회화", 10)

;-------------------------------------
;거울보너스
;-------------------------------------
@ACT_MENU_MILLER
SIF ITEM:거울
	CALL ADD_CRI_VAR("수치", , 3)

;──────────────────────────────
;상황이나전회의커맨드에 의한 변동 ;상황과 전회의 명령에 의한 변동
;직전의죠교대상의 반응 ;이전 죠교 대상의 반응
;-------------------------------------
;더 하드하게 해
;-------------------------------------
@ACT_MENU_PLEASE_HARD

IF IS_NOWCOMNAME("더 하드하게 해")
	CALL ADD_CRI_VAR("애무", -2)
	CALL ADD_CRI_VAR("성교", -1)
	CALL ADD_CRI_VAR("봉사", -1)
	CALL ADD_CRI_VAR("가학",  4)
	CALL ADD_CRI_VAR("이상",  2)
ENDIF

;-------------------------------------
;용서를 빌다
;새드OR심술궂음
;-------------------------------------
@ACT_MENU_PLEASE_HELP

IF IS_NOWCOMNAME("용서를 빌다")
	;죠교대상가울굴 이외
	IF TCVAR:상태변화 < 8
		CALL ADD_CRI_VAR("가학", -5, (TALENT:새드 || TALENT:심술궂음) ? 4 # 0)
		CALL ADD_CRI_VAR("이상", -5, (TALENT:새드 || TALENT:심술궂음) ? 4 # 0)
	ENDIF
ENDIF

;-------------------------------------
;기분 좋게 해
;-------------------------------------
@ACT_MENU_PLEASE_PREASURE

IF IS_NOWCOMNAME("기분좋게 해")
	CALL ADD_CRI_VAR("애무", 2)
	CALL ADD_CRI_VAR("도구", 3)
	CALL ADD_CRI_VAR("성교", 2)
	CALL ADD_CRI_VAR("성봉", 3)
ENDIF

;-------------------------------------
;날뛴다
;-------------------------------------
@ACT_MENU_VIOLENT

IF IS_NOWCOMNAME("날뛴다")
	CALL ADD_CRI_VAR("애무", -2)
	CALL ADD_CRI_VAR("성교", -5)
	CALL ADD_CRI_VAR("가학",  5)
	CALL ADD_CRI_VAR("사역",  5)
	CALL ADD_CRI_VAR("성봉", -6)
ENDIF

;-------------------------------------
;도구해제
;-------------------------------------
@ACT_MENU_UNEQUIP

IF IS_NOWCOMNAME("도구를 제거한다")
	CALL ADD_CRI_VAR("가학", 10)
ENDIF

;-------------------------------------
;밀어넘어짐시의연속애무보정
;-------------------------------------
@ACT_MENU_PETTING_CONTINUE

;밀어넘어지는중로, 전의 턴의 메뉴가애무
IF TEQUIP:밀어넘어지는중 && TFLAG:전ACT분류 == 1
	CALL ADD_CRI_VAR("애무", 20)
ENDIF

;-------------------------------------
;식스나인 유발
;-------------------------------------
@ACT_MENU_69

;식스나인이 되는경향(TFLAG:전ACT분류 =전의 턴의죠교메뉴)
SELECTCASE TFLAG:전ACT분류
	CASE 1
		CALL ADD_CRI_VAR("봉사", 1, 2)
	CASE 5
		CALL ADD_CRI_VAR("애무", 1, 2)
ENDSELECT

;--------------------------------------
;죠교자의 복장
;-------------------------------------
@ACT_MENU_CLOTH_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;복장
;LOCAL:3（성교）, LOCAL:10（성교봉사）에영향
;죠교자가하반신겉옷를 입고 있다
IF TEQUIP:LOCAL:하반신겉옷
	CALL ADD_CRI_VAR("성교", -2)
	CALL ADD_CRI_VAR("성봉", -2)
ENDIF
;죠교자가상반신겉옷를 입고 있다
IF TEQUIP:LOCAL:상반신겉옷
	CALL ADD_CRI_VAR("성교", -1)
	CALL ADD_CRI_VAR("성봉", -1)
ENDIF
;죠교자가전신겉옷를 입고 있다
IF TEQUIP:LOCAL:전신겉옷
	CALL ADD_CRI_VAR("성교", -3)
	CALL ADD_CRI_VAR("성봉", -3)
ENDIF

;--------------------------------------
;죠교대상의 복장
;대상의 복장은 2개소로 정의되고 있었다
;그 중 1개소를 comment out
;-------------------------------------
@ACT_MENU_CLOTH_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG   == -1 ? TARGET # ARG
LOCAL = ARG:1 == -1 ? MASTER # ARG:1

;;죠교대상가하반신겉옷를 입고 있다
;IF TEQUIP:(LOCAL:1):하반신겉옷
;	;죠교자에벽통과가 없다
;	SIF ! TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -2)
;	CALL ADD_CRI_VAR("성봉", -2)
;ENDIF
;죠교대상가상반신겉옷를 입고 있다
;IF TEQUIP:(LOCAL:1):상반신겉옷
;	;죠교자에벽통과가 없다
;	SIF ! TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -1)
;	CALL ADD_CRI_VAR("성봉", -1)
;ENDIF
;죠교대상가전신겉옷를 입고 있다
;IF TEQUIP:(LOCAL:1):전신겉옷
;	;죠교자에벽통과가 없다
;	SIF ! TALENT:LOCAL:벽통과
;		CALL ADD_CRI_VAR("성교", -3)
;	CALL ADD_CRI_VAR("성봉", -3)
;ENDIF
;겉옷
;죠교대상의하반신겉옷로 방해될 때
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("회화", 1, 4)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", -2, -3)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", -1, -5)
	CALL ADD_CRI_VAR("수치", , -7)
	CALL ADD_CRI_VAR("성봉", -1, -5)
;죠교대상의하반신겉옷가 있을 때
ELSEIF CLOTHESTYPE(1, 1)
	CALL ADD_CRI_VAR("회화", , 3)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -1)
	CALL ADD_CRI_VAR("도구", -1, -2)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", , -1)
	CALL ADD_CRI_VAR("성봉", , -1)
ENDIF
;죠교대상의상반신겉옷가 있을 때
IF CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("회화", , 2)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", , -2)
	CALL ADD_CRI_VAR("수치", , -4)
ENDIF

;속옷
;죠교대상의하반신속옷가 있을 때
IF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("회화", , 1)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -1)
	CALL ADD_CRI_VAR("도구", -1, -1)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("성교", , -4)
	CALL ADD_CRI_VAR("성봉", , -4)
ENDIF
;죠교대상의상반신속옷가 있을 때
IF CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("회화", , 2)
	;죠교자에벽통과가 없다
	SIF !TALENT:LOCAL:벽통과
		CALL ADD_CRI_VAR("애무", , -2)
	CALL ADD_CRI_VAR("도구", , -4)
	CALL ADD_CRI_VAR("수치", , -2)
ENDIF

;----------------------------------------------------------
;TEQUIP 관련
;---------------------------------------------------------
;-------------------------------------
;미약
;-------------------------------------
@ACT_MENU_DRAG

IF TEQUIP:미약
	CALL ADD_CRI_VAR("회화", , 4)
	CALL ADD_CRI_VAR("애무", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("도구", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("성교", TEQUIP:미약 / 2)
	CALL ADD_CRI_VAR("수치", TEQUIP:미약 / 3)
	CALL ADD_CRI_VAR("성봉", TEQUIP:미약 / 2)
ENDIF

;-------------------------------------
;바이브
;조수의 A감각가 높으면성교했다?
;-------------------------------------
@ACT_MENU_VIBE

;바이브
IF TEQUIP:바이브
	CALL ADD_CRI_VAR("애무", ,  3)
	CALL ADD_CRI_VAR("도구", ,  6)
	CALL ADD_CRI_VAR("성교", , -9)
	SIF ASSI >= 0
		CALL ADD_CRI_VAR("성교", GET_ABL(ASSI, "Ａ감각"))
	CALL ADD_CRI_VAR("수치", 7)
	CALL ADD_CRI_VAR("가학", , 2)
ENDIF

;-------------------------------------
;A바이브
;-------------------------------------
@ACT_MENU_AVIBE

;항문바이브
IF TEQUIP:항문바이브
	CALL ADD_CRI_VAR("애무", , 3)
	CALL ADD_CRI_VAR("도구", , 6)
	;바이브가 없는 경우섹스했다
	CALL ADD_CRI_VAR("성교", TEQUIP:바이브 ? 0 # 8)
	CALL ADD_CRI_VAR("수치", 8)
	CALL ADD_CRI_VAR("가학", , 5)
	CALL ADD_CRI_VAR("이상", , 4)
ENDIF

;-------------------------------------
;항문비즈
;-------------------------------------
@ACT_MENU_ABEADS

;항문비즈
IF TEQUIP:항문비즈
	CALL ADD_CRI_VAR("애무", , 5)
	CALL ADD_CRI_VAR("도구", , 8)
	CALL ADD_CRI_VAR("성교", TEQUIP:바이브 ? 0 # 2)
	CALL ADD_CRI_VAR("수치", 5)
	CALL ADD_CRI_VAR("가학", , 3)
	CALL ADD_CRI_VAR("이상", , 1)
ENDIF

;-------------------------------------
;항문플러그
;-------------------------------------
@ACT_MENU_APLUG

;관장＋항문플러그
IF TEQUIP:관장그릇＋플러그
	CALL ADD_CRI_VAR("회화", , 5)
	CALL ADD_CRI_VAR("애무", , 3)
	CALL ADD_CRI_VAR("도구", , 3)
	CALL ADD_CRI_VAR("성교", , TEQUIP:바이브 ? 0 # 1)
	CALL ADD_CRI_VAR("수치", 8)
	CALL ADD_CRI_VAR("가학", , 7)
	CALL ADD_CRI_VAR("이상", , 7)
ENDIF

;-------------------------------------
;클리캡
;-------------------------------------
@ACT_MENU_CRICAP

;클리캡
IF TEQUIP:클리캡
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4)
ENDIF

;-------------------------------------
;오나홀
;-------------------------------------
@ACT_MENU_ONAHOLE

IF TEQUIP:오나홀
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4)
	CALL ADD_CRI_VAR("이상", 1)
ENDIF

;-------------------------------------
;유두캡
;-------------------------------------
@ACT_MENU_NIPPLECAP

IF TEQUIP:유두캡
	CALL ADD_CRI_VAR("애무", , 2)
	CALL ADD_CRI_VAR("도구", , 2)
	CALL ADD_CRI_VAR("수치", 4 + TALENT:MASTER:남자)
	CALL ADD_CRI_VAR("이상", , TALENT:MASTER:남자 ? 5 # 0)
ENDIF

;-------------------------------------
;밧줄
;-------------------------------------
@ACT_MENU_ROPE

IF TEQUIP:밧줄
	CALL ADD_CRI_VAR("회화",  3)
	CALL ADD_CRI_VAR("성교", , -1)
	CALL ADD_CRI_VAR("봉사", -7)
	CALL ADD_CRI_VAR("가학",  3)
	CALL ADD_CRI_VAR("이상",  , 2)
	CALL ADD_CRI_VAR("성봉", -3)
ENDIF

;-------------------------------------
;눈가리개
;-------------------------------------
@ACT_MENU_EYEMASK

IF TEQUIP:눈가리개
	CALL ADD_CRI_VAR("회화",   , 4)
	CALL ADD_CRI_VAR("수치",  3, 4)
	CALL ADD_CRI_VAR("봉사", -5)
	CALL ADD_CRI_VAR("가학",  1)
	CALL ADD_CRI_VAR("이상",   , 3)
ENDIF

;-------------------------------------
;볼개그
;-------------------------------------
@ACT_MENU_GAG

IF TEQUIP:볼개그
	CALL ADD_CRI_VAR("회화", -10)
	CALL ADD_CRI_VAR("봉사",  -9)
	CALL ADD_CRI_VAR("가학",   2)
	CALL ADD_CRI_VAR("이상", , 4)
ENDIF

;-------------------------------------
;삼각목마
;-------------------------------------
@ACT_MENU_HORSE

IF TEQUIP:삼각목마
	CALL ADD_CRI_VAR("회화",   , 6)
	CALL ADD_CRI_VAR("애무", -6)
	CALL ADD_CRI_VAR("도구", -6)
	CALL ADD_CRI_VAR("수치", -3)
	CALL ADD_CRI_VAR("가학",  7, 2)
	CALL ADD_CRI_VAR("이상",  3, 7)
ENDIF

;-------------------------------------
;야외플레이
;-------------------------------------
@ACT_MENU_OUT

SIF TEQUIP:야외플레이
	CALL ADD_CRI_VAR("수치", , 6)

;-------------------------------------
;거울
;-------------------------------------
@ACT_MENU_T_MILLER

SIF TEQUIP:거울
	CALL ADD_CRI_VAR("수치", , 8)

;-------------------------------------
;도구해제 제한
;체력가 있는데 풀 아머 하고 있다면도구메뉴를 선택하지 않도록 (도구해제가 되기 때문에)
;조금 처리를 바꾸어 보는 테스트. 변변치않았으면 미안해요.
;·남자라면Ｖ계 없음에서도 풀 아머로 간주하도록(듯이)
;-------------------------------------
@ACT_MENU_KILL_UNEQUIP, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

IF (BASE:LOCAL:체력 + BASE:LOCAL:기력) > 500
	SIF (TALENT:LOCAL:남자 || TEQUIP:바이브 || TEQUIP:페니스밴드) && TEQUIP:항문바이브 && (TEQUIP:클리캡 || TEQUIP:오나홀) && TEQUIP:유두캡
		CALL ADD_CRI_VAR("도구", -15)
ENDIF

;-------------------------------------
;죠교자자위중
;-------------------------------------
@ACT_MENU_MASTURBATION_T

;죠교자자위중
IF GETBIT(TEQUIP:자위중, 1)
	CALL ADD_CRI_VAR("회화",  , 4)
	CALL ADD_CRI_VAR("도구",  , 4)
	CALL ADD_CRI_VAR("성교",  , 4)
	CALL ADD_CRI_VAR("수치", 2, 4)
	CALL ADD_CRI_VAR("봉사", 2, 4)
	CALL ADD_CRI_VAR("성봉",  , 4)
ENDIF

;-------------------------------------
;죠교대상자위중
;-------------------------------------
@ACT_MENU_MASTURBATION_M

;자위중
IF GETBIT(TEQUIP:자위중, 0)
	CALL ADD_CRI_VAR("회화", 2, 4)
	CALL ADD_CRI_VAR("도구", 2, 4)
	CALL ADD_CRI_VAR("성교", ,  4)
	CALL ADD_CRI_VAR("수치", 2, 4)
	CALL ADD_CRI_VAR("봉사",  , 4)
	CALL ADD_CRI_VAR("성봉",  -10)
ENDIF

;-------------------------------------
;징계·안
;-------------------------------------
@ACT_MENU_OSHIOKI

;징계·플래그
IF TFLAG:징계·플래그
	CALL ADD_CRI_VAR("가학", , 4)
	CALL ADD_CRI_VAR("이상", , 2)
	CALL ADD_CRI_VAR("사역", , 1)
	CALL ADD_CRI_VAR("봉사", -999)
ENDIF

;──────────────────────────────
;실행 불가능의 판정
;──────────────────────────────
;-------------------------------------
;삽입
;우선, 현행의삽입속행 사양(pa0903판 시점)에 맞춘다(COMABLE로 속행하지 않는 커맨드를 차는 것 낸다)
;라고 할까 이 사양이 있는 경우, 하손에 허가되지 않는 것에 떨어뜨리면 커맨드 전멸로 에러가 될 것
;
;삽입속행 제한가능으로 실행 가능한 커맨드가 없는 것은 이 시점에서 찬다
;보정는성교(성봉) > 애무 > 회화·가학
;분노보정는 이 사양이라면 해제에 향하기 쉬워질 것도 아니기 때문에 놓아둔다
;로시에삽입해제라든지 그러한 별이벤트를 사이에 두어 주는 편이내림?
;페니반시는 커맨드 금지에 의한 조치가 움직이지 않기 때문에, 추가로 속행시키기 쉽게 하기 위해서보정를 준다
;-------------------------------------
@ACT_MENU_INSERT

;성교/성교봉사중
IF TEQUIP:성교중 || TEQUIP:성교봉사중
	;페니스밴드삽입
	IF TEQUIP:성교중 && TEQUIP:죠교자페니스밴드
		CALL ADD_CRI_VAR("성교", 5)
	;페니스밴드삽입 이외
	ELSE
		CALL ADD_CRI_VAR("도구", -999)
		CALL ADD_CRI_VAR("수치", -999)
		CALL ADD_CRI_VAR("봉사", -999)
		CALL ADD_CRI_VAR("가학", , 2)
		CALL ADD_CRI_VAR("이상", -999)
		CALL ADD_CRI_VAR("사역", -999)
	ENDIF
	CALL ADD_CRI_VAR("회화", , 2)
	CALL ADD_CRI_VAR("애무", , 4)
	;성교중
	IF TEQUIP:성교중
		CALL ADD_CRI_VAR("성교", 5, 5)
		SIF !TEQUIP:죠교자페니스밴드
			CALL ADD_CRI_VAR("성봉", -999)
	;성교봉사중
	ELSE
		CALL ADD_CRI_VAR("성봉", 5, 5)
		CALL ADD_CRI_VAR("성교", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;애태움중, Ｃ자극을 중단하지 않도록
;----------------------------------------------------------
@ACT_MENU_IRRITATE

;애태움중
IF TCVAR:MASTER:애태움도
	;애태움종료의 순간은 거기에 집중. 혹은성교봉사중가 아닌 경우
	;(성교봉사에 관해서는삽입보정의 내용으로 문제 없는 것이 아닐까)
	IF TCVAR:MASTER:애태움도 == 1 || !TEQUIP:성교봉사중
		;향후 재고의필요유리카
		CALL ADD_CRI_VAR("회화", -999)
		CALL ADD_CRI_VAR("도구", -999)
		CALL ADD_CRI_VAR("성교", -999)
		CALL ADD_CRI_VAR("수치", -999)
		CALL ADD_CRI_VAR("봉사", -999)
		CALL ADD_CRI_VAR("가학", -999)
		CALL ADD_CRI_VAR("이상", -999)
		CALL ADD_CRI_VAR("사역", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;A섹스회피
;----------------------------------------------------------
@ACT_MENU_AGUARD

;죠교방침가이상의 경우,50%의 확률로 판정을 실시한다
IF IS_POLICY("지금", "이상") && RAND:2
	;ＡＳ가능이지만, 익숙해지지 않기 때문에 가드 한다. 다만 50%(?!)
	SIF EXP:MASTER:Ａ경험 < 2 && !TEQUIP:항문바이브 && !TEQUIP:항문비즈 && !TEQUIP:관장그릇＋플러그
		CALL ADD_CRI_VAR("성교", -999)
ENDIF

;----------------------------------------------------------
;항문섹스중, 소프트방침로 다른 체위로 변경하지 않도록
;----------------------------------------------------------
@ACT_MENU_ASEX_LIMITATION

IF IS_POLICY("지금", "소프트")
	SIF TEQUIP:성교중 == 6
		CALL ADD_CRI_VAR("성교", -999)
	SIF TEQUIP:성교봉사중 == 6
		CALL ADD_CRI_VAR("성봉", -999)
ENDIF

;----------------------------------------------------------
;실행 판정
;----------------------------------------------------------
@ACT_MENU_ABLE

;불가능 판정
FOR LOCAL, 0, 11
	CALL ACTM_ABLE(LOCAL)
	SIF !RESULT
		CALL DIM_CRI_VAR(MENU(LOCAL), -999)
NEXT

;-------------------------------------
;히트치
;-------------------------------------
@ACT_MENU_HEAT
#LOCALSIZE 1
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	LOCAL = TFLAG:(@"%MENU(LCOUNT)%히트") * LIMIT(TFLAG:가능한ACT계열수 - 1, 0, 4) / 4
	CALL ADD_CRI_VAR(MENU(LCOUNT), -LOCAL)
NEXT

;-------------------------------------
;ACT보정치
;-------------------------------------
@ACT_MENU_CORRECTION
#DIM LCOUNT

FOR LCOUNT, 0, 11
	;보정치를 가산
	CALL ACT_M_RES_MENU(LCOUNT)
	CALL ADD_CRI_VAR(MENU(LCOUNT), RESULT)
NEXT
