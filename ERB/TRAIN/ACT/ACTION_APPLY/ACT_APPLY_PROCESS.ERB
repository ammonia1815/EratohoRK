;------------------------------------------------------------------------
;EDIT	001		KOremi		2011/11/10		지문을 ACT_MESSAGE_OTHERS.ERB 로 분리
;											탈의실행 처리를 여기에 이동
;------------------------------------------------------------------------

;────────────────────────────────────
;거절 처리(리액션에 의해죠교가취소 된다)
;────────────────────────────────────

;────────────────────────────────────
;성교의취소（쾌Ｃ, 쾌Ｖ, 쾌Ａ, 접촉, 애정, 성행위, 고통, 이탈, 중독충족, 트라우마）
;────────────────────────────────────
@CANCEL_INSERT_TO_M
;항문섹스선택시, 항문섹스상태이면
IF IS_NOWACTNAME("항문섹스") || TEQUIP:성교중 == 6
	;쾌Ａ/쾌ＣSOURCE 감쇠
	SOURCE:MASTER:쾌Ａ /= 3
	SOURCE:쾌Ｃ /= 5
;항문섹스 이외선택시, 항문섹스 이외의성교상태이면
ELSEIF TEQUIP:성교중 < 6
	;쾌Ｖ/쾌ＣSOURCE 감쇠
	SOURCE:MASTER:쾌Ｖ /= 3
	SOURCE:쾌Ｃ /= 5
ENDIF
;접촉, 성행위, 고통, 이탈, 트라우마SOURCE는 감쇠
;애정SOURCE는 감산, 중독충족SOURCE는 말소
SOURCE:MASTER:접촉 /= 3
SOURCE:MASTER:애정 -= 500
SOURCE:MASTER:성행위 /= 3
SOURCE:MASTER:고통 /= 3
SOURCE:MASTER:이탈 /= 3
SOURCE:MASTER:중독충족 = 0
SOURCE:MASTER:트라우마 /= 3
;죠교자, 죠교대상상호의삽입를 해제
TEQUIP:성교중     = 0
TEQUIP:성교봉사중 = 0
TFLAG:안빼고      = 0
;────────────────────────────────────
;성교봉사의취소（쾌Ｃ, 쾌Ｖ, 쾌Ａ, 접촉, 애정, 성행위, 고통, 이탈, 중독충족, 트라우마）
;────────────────────────────────────
@CANCEL_INSERT_TO_T
;항문섹스상태이면
IF TEQUIP:성교봉사중 == 6
	;쾌Ｃ/쾌ＡSOURCE 감쇠
	SOURCE:MASTER:쾌Ｃ /= 3
	SOURCE:쾌Ａ /= 5
;항문섹스 이외의성교상태이면
ELSE
	;쾌Ｃ/쾌ＶSOURCE 감쇠
	SOURCE:MASTER:쾌Ｃ /= 3
	SOURCE:쾌Ｖ /= 5
ENDIF
;접촉, 성행위, 고통, 이탈, 트라우마SOURCE는 감쇠
;애정SOURCE는 감산, 중독충족SOURCE는 말소
SOURCE:MASTER:접촉 /= 3
SOURCE:MASTER:애정 -= 500
SOURCE:MASTER:성행위 /= 3
SOURCE:MASTER:고통 /= 3
SOURCE:MASTER:이탈 /= 3
SOURCE:MASTER:중독충족 = 0
SOURCE:MASTER:트라우마 /= 3
;죠교자, 죠교대상상호의삽입를 해제
TEQUIP:성교중     = 0
TEQUIP:성교봉사중 = 0
TFLAG:안빼고      = 0
;────────────────────────────────────
;가학, 노출플레이의취소（고통, 구속, 노출, 이탈）
;────────────────────────────────────
;구속취소
@CANCEL_TEQUIP_RESTRAINT
;발생 카운트
CALLF CANCEL_TEQUIP_COUNT("구속", "발생")
;구속SOURCE 감쇠
SOURCE:MASTER:구속 /= 3
;밧줄, 눈가리개, 볼개그해제
TEQUIP:밧줄 = 0
TEQUIP:눈가리개 = 0
TEQUIP:볼개그 = 0

;삼각목마취소
@CANCEL_TEQUIP_W_HORSE
;발생 카운트
CALLF CANCEL_TEQUIP_COUNT("삼각목마", "발생")
;고통, 구속SOURCE 감쇠
SOURCE:MASTER:고통 /= 2
SOURCE:MASTER:구속 /= 3
;삼각목마중단
TEQUIP:삼각목마 = 0

;야외플레이취소
@CANCEL_TEQUIP_OUTDOOR
;발생 카운트
CALLF CANCEL_TEQUIP_COUNT("야외플레이", "발생")
;노출, 이탈SOURCE 감쇠
SOURCE:MASTER:노출 /= 2
SOURCE:MASTER:이탈 /= 3
;야외플레이중단
TEQUIP:야외플레이 = 0

;거울취소
@CANCEL_TEQUIP_MIRROR
;발생 카운트
CALLF CANCEL_TEQUIP_COUNT("거울", "발생")
;노출SOURCE 감쇠
SOURCE:MASTER:노출 /= 3
;거울를 치운다
TEQUIP:거울 = 0

;코스프레취소
@CANCEL_TEQUIP_COSPLAY
;발생 카운트
CALLF CANCEL_TEQUIP_COUNT("코스프레", "발생")
;노출, 이탈SOURCE 감쇠
SOURCE:MASTER:노출 /= 3
SOURCE:MASTER:이탈 /= 3
;코스프레를 벗는다
TEQUIP:MASTER:코스프레 = 0
;디폴트의 복장에돌아가기
TEQUIP:MASTER:악세서리 = EQUIP:MASTER:악세서리
TEQUIP:MASTER:신발안 = EQUIP:MASTER:신발안
TEQUIP:MASTER:하반신속옷 = EQUIP:MASTER:하반신속옷
TEQUIP:MASTER:상반신속옷 = EQUIP:MASTER:상반신속옷
;욕정가 높은상태라면겉옷를 입지 않는다
IF PALAM:MASTER:욕정 < 5000
	TEQUIP:MASTER:하반신겉옷 = EQUIP:MASTER:하반신겉옷
	TEQUIP:MASTER:상반신겉옷 = EQUIP:MASTER:상반신겉옷
	TEQUIP:MASTER:전신겉옷 = EQUIP:MASTER:전신겉옷
ENDIF

;취소이력관리
@CANCEL_TEQUIP_COUNT(ARGS = "", ARGS:1 = "")
#FUNCTION
IF ARGS != ""
	IF ARGS:1 == "발생"
		IF CFLAG:@"%ARGS%취소보정" <= 33
			CFLAG:@"%ARGS%취소보정" = 100
		ELSE
			CFLAG:@"%ARGS%취소보정" *= 3
		ENDIF
	ELSE
		RETURNF CFLAG:@"%ARGS%취소보정"
	ENDIF
ELSE
	SPLIT "구속/삼각목마/야외플레이/거울/코스프레", "/", LOCALS
	FOR LOCAL, 0, RESULT
		CFLAG:@"%LOCALS:LOCAL%취소보정" -= 5 + CFLAG:@"%LOCALS:LOCAL%취소보정" / 100
	NEXT
ENDIF
RETURNF 0

;────────────────────────────────────
;사용 검토 처리(죠교자가도구나소모품의 사용 미사용을 결정한다)
;────────────────────────────────────

;────────────────────────────────────
;미약와로션의 사용（윤활, 소변）
;
;불려 가면 현재의 상황을 감안해,
;필요라고 판단되면미약/로션가 사용된다
;────────────────────────────────────
;실은미약의 사용도거부당했다저항당했다의 것이 보통이라고 생각하지만, 처리가 귀찮은 것으로 사용하면 절대 성공이야 게섬했다
@LOVEPOTION_USE

VARSET LOCAL

;지금의 약력 잔량을 체크
SELECTCASE TEQUIP:미약
	CASE IS < 1
		LOCAL += 20
	CASE IS < 3
		LOCAL += 10
	CASE IS < 5
		LOCAL += 5
	CASE IS < 7
		LOCAL += 1
	CASE IS < 9
		LOCAL -= 5
	CASE IS < 11
		LOCAL -= 10
	CASEELSE
		LOCAL -= 15
ENDSELECT
;욕정가 벌써 높아지고 있는상태라면 사용하지 않아 좋아
SELECTCASE PALAM:MASTER:욕정
	CASE IS > 6999
		LOCAL -= 15
	CASE IS > 5199
		LOCAL -= 8
	CASE IS > 3799
		LOCAL -= 2
	CASE IS > 2599
		LOCAL += 1
	CASE IS > 1599
		LOCAL += 3
	CASE IS > 699
		LOCAL += 6
	CASE IS > 299
		LOCAL += 10
	CASEELSE
		LOCAL += 15
ENDSELECT
;히트값을 공제합니다. 히트치와는 계산의 개념으로 실제는 약의 잔량이라고 인지해 주세요
;(연속사용을 억제한다)
LOCAL -= FLAG:미약히트

;사용하는 경우
IF LOCAL > 0
	;약효 추가
	;죠교자배합지식소질에 응해 약효 증대
	TEQUIP:미약 += 5 + TALENT:배합지식 * 4 + (1 + RAND:4) * (TALENT:배합지식 + 2)
	;조수가손 탔을 경우, 조수배합지식, 기교에 의해 약효 증대
	TEQUIP:미약 += TFLAG:조수ACT == 71 ? 3 + (TALENT:ASSI:배합지식 * 2 + RAND:3) * (ABL:ASSI:기교 + RAND:2) # 0
	;히트치에 가산
	;죠교자배합지식소질, 죠교레벨, 기교, 금단의지식소질에 의해 경감
	LOCAL:1 = 40 - (TALENT:배합지식 * 5 + GET_TRAINLV(TARGET) + 1) * (4 + ABL:기교 + TALENT:금단의지식 * 2) / 8
	;조수가손 탔을 경우, 조수배합지식, 기교에 의해 경감
	LOCAL:1 -= TFLAG:조수ACT == 71 ? LOCAL:1 / (4 - TALENT:ASSI:배합지식) + (TALENT:ASSI:배합지식 * 3 + RAND:2) * (ABL:ASSI:기교 + 1 + RAND:3) # 0
	;현재미약히트가 정이면, 난수로 2-4를 추가
	LOCAL:1 += FLAG:미약히트 > 0 ? 2 + RAND:3 # 0
	;미약히트에 가산, 이 때 0 이하이면 난수로 1-3을 가산
	FLAG:미약히트 += LOCAL:1 < 1 ? 1 + RAND:3 # LOCAL:1
	;난수에 응해소변추가
	BASE:MASTER:소변 += 100 + RAND:5 * 50
	;플래그세트
	TFLAG:미약등사용 |= 2
	TFLAG:미약등사용구상 |= 2
	TFLAG:미약등사용지문스킵 |= 2
ENDIF


@LOTION_USE

VARSET LOCAL

;지금의 약력 잔량을 체크
SELECTCASE TEQUIP:로션
	CASE IS < 1
		LOCAL += 20
	CASE IS < 3
		LOCAL += 10
	CASE IS < 5
		LOCAL += 5
	CASE IS < 7
		LOCAL -= 1
	CASE IS < 9
		LOCAL -= 5
	CASE IS < 11
		LOCAL -= 10
	CASEELSE
		LOCAL -= 15
ENDSELECT
;윤활가 벌써 높아지고 있는상태라면 사용하지 않아 좋아
SELECTCASE PALAM:MASTER:윤활
	CASE IS > 5999
		LOCAL -= 15
	CASE IS > 4799
		LOCAL -= 8
	CASE IS > 3799
		LOCAL -= 2
	CASE IS > 2499
		LOCAL += 1
	CASE IS > 1199
		LOCAL += 3
	CASE IS > 499
		LOCAL += 6
	CASE IS > 299
		LOCAL += 10
	CASEELSE
		LOCAL += 15
ENDSELECT
;히트값을 공제합니다
;(연속사용을 억제한다)
LOCAL -= FLAG:로션히트

;겉옷가 아직 남아 있다(한편강제탈의의 설정이나밧줄장착가 없다)라면강제탈의를 호출해
IF (TEQUIP:MASTER:하반신겉옷 || TEQUIP:MASTER:상반신겉옷 || TEQUIP:MASTER:전신겉옷) && !TFLAG:강제탈의 && !TEQUIP:밧줄
	;반반의 확률로 상＋하겉옷, 악세서리－까지강제탈의
	TFLAG:강제탈의 = 3 + (RAND:2 * 4)
	;강제탈의최적화
	CALL KYOUSEI_DATUI
ENDIF
;그런데도겉옷가 아직 남아 있는 경우는로션사용을 단념
SIF TEQUIP:MASTER:하반신겉옷 || TEQUIP:MASTER:상반신겉옷 || TEQUIP:MASTER:전신겉옷
	LOCAL = 0

;사용하는 경우
IF LOCAL > 0
	;약효 추가
	;죠교자배합지식소질에 응해 약효 증대
	TEQUIP:로션 += 5 + TALENT:배합지식 * 4 + (1 + RAND:4) * (TALENT:배합지식 + 2)
	;조수가손 탔을 경우, 조수배합지식, 기교에 의해 약효 증대
	TEQUIP:로션 += TFLAG:조수ACT == 71 ? 3 + (TALENT:ASSI:배합지식 * 2 + RAND:3) * (ABL:ASSI:기교 + RAND:2) # 0
	;히트치에 가산
	;죠교자배합지식소질, 죠교레벨, 기교, 금단의지식소질에 의해 경감
	LOCAL:1 = 40 - (TALENT:배합지식 * 5 + GET_TRAINLV(TARGET) + 1) * (4 + ABL:기교 + TALENT:금단의지식 * 2) / 8
	;조수가손 탔을 경우, 조수배합지식, 기교에 의해 경감
	LOCAL:1 -= TFLAG:조수ACT == 71 ? LOCAL:1 / (4 - TALENT:ASSI:배합지식) + (TALENT:ASSI:배합지식 * 3 + RAND:2) * (ABL:ASSI:기교 + 1 + RAND:3) # 0
	;현재로션히트가 정이면, 난수로 2-4를 추가
	LOCAL:1 += FLAG:로션히트 > 0 ? 2 + RAND:3 # 0
	;로션히트에 가산, 이 때 0 이하이면 난수로 1-3을 가산
	FLAG:로션히트 += LOCAL:1 < 1 ? 1 + RAND:3 # LOCAL:1
	;윤활추가
	PALAM:MASTER:윤활 += 200
	;플래그세트
	TFLAG:미약등사용 |= 1
	TFLAG:미약등사용구상 |= 1
	TFLAG:미약등사용지문스킵 |= 1
ENDIF

;────────────────────────────────────
;효과 계산 처리(죠교효과의 계산을 실시한다)
;────────────────────────────────────

;────────────────────────────────────
;윤활불발시계산처리
;────────────────────────────────────
;인수에는 『/』단락으로 캐릭터 라인을 대입
;제 0 블록:충분히윤활치
;	윤활불발를 빼앗기지 않게 되는 PALAM:MASTER:윤활를 지정
;제 1 블록 이후:『,』단락으로 캐릭터 라인을 대입
;	제 0 블록:보정SOURCE
;		보정를 실시하는 SOURCE를 지정
;	제 1 블록:보정법
;		보정계산의 방법을 지정
;	제 2 블록:지정치
;		보정법에 따라 취급은 다르다
;
;지정예:"500/M쾌Ａ, T, 50/M고통, A, 500"
;　　　　"2000/M쾌Ｖ, T, 80/M쾌Ａ, T, 80/M고통, A, 600"
@SHORTAGE_LUBRICATION(ARGS)
#LOCALSSIZE 100
;COUNT 변수
#DIM LCOUNT
;충분히윤활치:PALAM:MASTER:윤활가 이 값에 이르면보정가 없어진다
#DIM LMAX
;지정치:계산방법에 따라 취급은 다르다
#DIM LSPEC
;대상자:SOURCE 지정대상자
#DIM LCHA
;범용 캐릭터 라인 변수
#DIMS LTEMPS, 3

;LOCALS, LCOUNT의 초기화
VARSET LOCALS
VARSET LCOUNT

;ARGS를 새겨 LOCALS에 대입
SPLIT ARGS, "/", LOCALS

;충분히윤활치를 설정
LMAX = TOINT(LOCALS)

DO
	;LTEMPS 초기화
	VARSET LTEMPS
	;LOCALS를 새겨 LTEMPS에 대입
	SPLIT LOCALS:(LCOUNT + 1), ",", LTEMPS
	;보정SOURCE 지정으로부터 SOURCE대상자를 뽑아낸다(M=MASTER, T=TARGET)
	SELECTCASE SUBSTRINGU(LTEMPS, 0, 1)
		CASE "M"
			LCHA = MASTER
		CASE "T"
			LCHA = TARGET
		CASEELSE
			CALLF PRINT_ERRORMESSAGE(@"@SHORTAGE_LUBRICATION 에 무효인 SOURCE대상자(%SUBSTRINGU(LTEMPS, 0, 1)%)가 도 사라질 수 있는 했다")
			LCHA = MASTER
	ENDSELECT
	LTEMPS = %SUBSTRINGU(LTEMPS, 1)%
	;지정치를 설정
	LSPEC = TOINT(LTEMPS:2)
	;Times보정법을 채용
	IF LTEMPS:1 == "T"
		;Times보정법
		;대상에 1 이하의 값을 들여 감소시킨다.
		;치는 『지정치%』로부터 시작된다(0 초과, 100 미만의 값을 지정한다. 특히 0 이하는 지정 금지)
		;PALAM:MASTER:윤활의 상승에 응해 1에 가까워져 가지만, 가까워지는 속도는 늦게 되어 간다
		;
		;보정결과 일람(대상에[X%]를 건다)
		;
		;지정치  충분히윤활치를 100%로했다시의 PALAM:MASTER:윤활
		;           0%  20%  40%  60%  80% 100%
		;
		;40     [  40%  66%  77%  86%  93% 100%]
		;50     [  50%  72%  81%  88%  94% 100%]
		;60     [  60%  77%  85%  90%  95% 100%]
		;70     [  70%  83%  88%  93%  96% 100%]
		;80     [  80%  88%  92%  95%  97% 100%]
		;
		LOCAL = LSPEC + GET_REVISION_SQRT(PALAM:MASTER:윤활, LMAX, 100 - LSPEC)
		SOURCE:LCHA:LTEMPS = SOURCE:LCHA:LTEMPS * LIMIT(LOCAL, 0, 100) / 100
	;Add보정법을 채용
	ELSEIF LTEMPS:1 == "A"
		;Add보정법
		;대상에 0이상의 값을 가산한다
		;치는 『지정치』로부터 시작된다(0 초과의 값을 지정한다)
		;PALAM:MASTER:윤활의 상승에 응해 0에 가까워져 가지만, 가까워지는 속도는 늦게 되어 간다
		;
		;보정결과 일람(대상에[X]를발)
		;
		;지정치  충분히윤활치를 100%로했다시의 PALAM:MASTER:윤활
		;           0%  20%  40%  60%  80% 100%
		;
		; 250   [ 250  140   92   57   27    0 ]
		; 500   [ 500  280  185  115   55    0 ]
		; 750   [ 750  420  277  172   82    0 ]
		;1000   [1000  560  370  230  110    0 ]
		;1500   [1500  840  555  345  165    0 ]
		;3000   [3000 1680 1110  690  330    0 ]
		;
		LOCAL = LSPEC - GET_REVISION_SQRT(PALAM:MASTER:윤활, LMAX, LSPEC)
		SOURCE:LCHA:LTEMPS += LIMIT(LOCAL, 0, LSPEC)
	ENDIF
	;카운트 가산
	LCOUNT++
;처리하는 것이 없어지면 탈출
LOOP LOCALS:(LCOUNT + 1) != ""

;────────────────────────────────────
;자위관련 처리
;────────────────────────────────────

;────────────────────────────────────
;자위의 실행 함수
;────────────────────────────────────
@ONANIE_APPLY
;부위보정치
#DIM L_C
#DIM L_V
#DIM L_A
#DIM L_B
;노출(Exposure)보정치
#DIM L_E

;죠교대상욕망, 기교, 죠교레벨, 손가락기술소질에 응해 C, B부위보정가격인상승
;그 외보정치는 0에 초기화
L_C = 20 + GET_ABL(MASTER, "욕망") * (GET_ABL(MASTER, "기교") + 10) * CFLAG:MASTER:죠교레벨 / 50 + GET_ABL(MASTER, "기교") * 25 * (TALENT:MASTER:손가락기술 + 1) / 10
L_V = 0
L_A = 0
L_B = L_C
L_E = 0

;바이브사용
IF TEQUIP:바이브
	;죠교대상욕망, 죠교레벨, 도구능숙소질, 기교, 도구강화에 응해 V부위보정가격인상승
	L_V = 50 + GET_ABL(MASTER, "욕망") * (CFLAG:MASTER:죠교레벨 + 1) / 2 + (TALENT:MASTER:도구능숙 + 1) * GET_ABL(MASTER, "기교") * (ITEM:9 + 1) * 3
	;C부위보정치는 감소
	TIMES L_C, 0.80
ENDIF

;항문바이브사용
IF TEQUIP:항문바이브 && ABL:MASTER:Ａ감각 > 1
	;죠교대상욕망, 죠교레벨, 도구능숙소질, 기교, 도구강화에 응해 A부위보정가격인상승
	L_A = (GET_ABL(MASTER, "욕망") + 10) * (CFLAG:MASTER:죠교레벨 + 1) * 6 + (TALENT:MASTER:도구능숙 + 1) * GET_ABL(MASTER, "기교") * (ITEM:9 + 1) * 25 / 10
	;B부위보정치는 크게 감소
	TIMES L_B, 0.60
ENDIF

;적극적으로 한다라면 부위보정치에 보너스
IF IS_COMGRONAME("적극적으로 한다")
	TIMES L_C, 1.20
	TIMES L_V, 1.10
	TIMES L_A, 1.10
	TIMES L_B, 1.20
ENDIF

;부위보정치에 응해 쾌Ｘ발생
;(남자이면쾌Ｂ는 발생하지 않는다)
SOURCE:MASTER:쾌Ｃ += L_C * 2
SOURCE:MASTER:쾌Ｖ += L_V
SOURCE:MASTER:쾌Ａ += L_A
SOURCE:MASTER:쾌Ｂ += TALENT:MASTER:남자 ? 0 # L_B * 2

;죠교대상기교, 죠교레벨, 바이브, 항문바이브장착에 응해성행위SOURCE 증가
SOURCE:MASTER:성행위 += 50 + GET_ABL(MASTER, "기교") * 2 + CFLAG:MASTER:죠교레벨 * 10 + TEQUIP:바이브 * 50 + TEQUIP:항문바이브 * 50
;죠교자죠교레벨, 야외플레이중, 거울사용중, 부위보정치에 응해노출보정가격인상승
L_E = 150 + GET_TRAINLV(TARGET) * 10 + TEQUIP:야외플레이 * 200 + TEQUIP:거울 * 150 + L_C / 2 + L_V / 3 + L_A / 3 + L_B / 2
;노출보정치에 응해노출SOURCE 증가
SOURCE:MASTER:노출 += L_E
;노출보정치에 응해 증가해, 노출증, 죠교대상죠교레벨에 응해 감소하는 값이 정이면이탈SOURCE에 가산
LOCAL = L_E / 2 - GET_ABL(MASTER, "노출증") * 2 - CFLAG:MASTER:죠교레벨 * 10
SOURCE:MASTER:이탈 += LOCAL > 0 ? LOCAL # 0
;보정치 총계, 자위중독에 응해중독충족SOURCE 증가
SOURCE:MASTER:중독충족 += (L_C + L_V + L_A + L_B + L_E) * (50 + GET_ABL(MASTER, "자위중독")) / 500

;죠교대상자위중를 세트
TEQUIP:자위중 |= 1
;────────────────────────────────────
;자위절정처리
;────────────────────────────────────
@ONANIE_EX
;자위의 실행 함수 호출
CALL ONANIE_APPLY

;────────────────────────────────────
;탈의관련 처리
;────────────────────────────────────

;────────────────────────────────────
;자주탈의/강제탈의최적화(탈의대상가 존재하지 않는 경우, 탈의지정을 변경한다)
;ARG	탈의대상
;────────────────────────────────────
;구명에서의 호출을 처리
@KYOUSEI_DATUI
CALL DATUI_OPTIMIZE(MASTER)

@JISHU_DATUI
CALL DATUI_OPTIMIZE(TARGET)

;처리 본체
@DATUI_OPTIMIZE(ARG)

;탈의대상가 MASTER이면강제탈의, 그 이외라면자주탈의
;(현재 이 2개 이외는 처리할 수 없습니다)
LOCAL = (ARG == MASTER ? 102 # 103)

;현재의 복장에 의한플래그변동（예를 들어상반신겉옷를 벗기도록(듯이) 지정했다가 벌써 벗어졌을 경우, 브래지어를 벗기도록(듯이)）
;상반신겉옷탈의시, 상반신겉옷와전신겉옷가 존재하지 않는다
IF TFLAG:LOCAL == 1 && !TEQUIP:ARG:상반신겉옷 && !TEQUIP:ARG:전신겉옷
	;상반신속옷가 존재하면상반신전부탈의, 그 이외는탈의 하지 않는다
	TFLAG:LOCAL = TEQUIP:ARG:상반신속옷 ? 4 # 0
;하반신겉옷탈의시, 하반신겉옷와전신겉옷가 존재하지 않는다
ELSEIF TFLAG:LOCAL == 2 && !TEQUIP:ARG:하반신겉옷 && !TEQUIP:ARG:전신겉옷
	;하반신속옷가 존재하면하반신전부탈의, 그 이외는탈의 하지 않는다
	TFLAG:LOCAL = TEQUIP:ARG:하반신속옷 ? 5 # 0
;상＋하겉옷탈의시, 하반신겉옷와상반신겉옷, 전신겉옷가 존재하지 않는다
ELSEIF TFLAG:LOCAL == 3 && !TEQUIP:ARG:하반신겉옷 && !TEQUIP:ARG:상반신겉옷 && !TEQUIP:ARG:전신겉옷
	;상반신속옷/하반신속옷가 존재하면악세서리－까지탈의, 그 이외는탈의 하지 않는다
	TFLAG:LOCAL = TEQUIP:ARG:상반신속옷 || TEQUIP:ARG:하반신속옷 ? 7 # 0
;상반신전부탈의시, 상반신속옷와상반신겉옷, 전신겉옷가 존재하지 않는다
ELSEIF TFLAG:LOCAL == 4 && !TEQUIP:ARG:상반신속옷 && !TEQUIP:ARG:상반신겉옷 && !TEQUIP:ARG:전신겉옷
	;탈의 하지 않는다
	TFLAG:LOCAL = 0
;하반신전부탈의시, 하반신속옷와하반신겉옷, 전신겉옷가 존재하지 않는다
ELSEIF TFLAG:LOCAL == 5 && !TEQUIP:ARG:하반신속옷 && !TEQUIP:ARG:하반신겉옷 && !TEQUIP:ARG:전신겉옷
	;탈의 하지 않는다
	TFLAG:LOCAL = 0
;악세서리─탈의시, 악세서리와신발안가 존재하지 않는다
ELSEIF TFLAG:LOCAL == 6 && !TEQUIP:ARG:악세서리 && !TEQUIP:ARG:신발안
	;탈의 하지 않는다
	TFLAG:LOCAL = 0
;악세서리－까지탈의시
ELSEIF TFLAG:LOCAL == 7
	;악세서리와신발안가 존재하지 않는 경우, 전라까지탈의
	SIF !TEQUIP:ARG:악세서리 && !TEQUIP:ARG:신발안
		TFLAG:LOCAL = 8
	;하반신속옷와상반신속옷, 하반신겉옷, 상반신겉옷, 전신겉옷가 존재하지 않는 경우, 탈의 하지 않는다
	SIF !TEQUIP:ARG:하반신속옷 && !TEQUIP:ARG:상반신속옷 && !TEQUIP:ARG:하반신겉옷 && !TEQUIP:ARG:상반신겉옷 && !TEQUIP:ARG:전신겉옷
		TFLAG:LOCAL = 0
;전라까지탈의시, 하반신속옷와상반신속옷, 하반신겉옷, 상반신겉옷, 전신겉옷가 존재하지 않는다
ELSEIF TFLAG:LOCAL == 8 && !TEQUIP:ARG:하반신속옷 && !TEQUIP:ARG:상반신속옷 && !TEQUIP:ARG:하반신겉옷 && !TEQUIP:ARG:상반신겉옷 && !TEQUIP:ARG:전신겉옷
	;악세서리와신발안가 존재하는 경우악세서리─탈의, 그 이외는탈의 하지 않는다
	TFLAG:LOCAL = TEQUIP:ARG:악세서리 || TEQUIP:ARG:신발안 ? 6 # 0
;속옷만탈의시, 하반신속옷와상반신속옷가 존재하지 않는다
ELSEIF TFLAG:LOCAL == 9 && !TEQUIP:ARG:하반신속옷 && !TEQUIP:ARG:상반신속옷
	;탈의 하지 않는다
	TFLAG:LOCAL = 0
ENDIF

;────────────────────────────────────
;강제탈의성공 여부 판정
;────────────────────────────────────
@KYOUSEI_DATUI_CHECK

;강제탈의를 하지 않는 경우, 판정을 실시하지 않는다
SIF TFLAG:강제탈의 <= 0
	RETURN 0

;탈의 성공할까의 판정
;LOCAL가 높을 정도 실패하기 쉬워집니다

;소질등에 의한보정

;죠교대상체력, 기력, 반항강도, 반항각인, 굴복각인, 반항, 굴복, 공순에따라증감
LOCAL = BASE:MASTER:체력 / 500 + BASE:MASTER:기력 / 200 + CFLAG:MASTER:반항강도 + MARK:반항각인 - MARK:굴복각인 + PALAM:MASTER:반항 / 1000 - PALAM:MASTER:굴복 / 1200 - PALAM:MASTER:공순 / 1200
;죠교대상굳셈, 겁쟁이, 반항적, 솔직, 건방짐, 얌전한, 장신, 작은체형소질에따라증감
LOCAL += TALENT:MASTER:굳셈 * 2 - TALENT:MASTER:겁쟁이 * 2 + TALENT:MASTER:반항적 * 5 - TALENT:MASTER:솔직 * 5 + TALENT:MASTER:건방짐 * 3 - TALENT:MASTER:얌전한 * 3 + TALENT:MASTER:장신 * 4 - TALENT:MASTER:작은체형 * 4
;죠교자죠교레벨, 기교, 장신, 작은체형소질, 체력, 기력에따라증감
LOCAL -= GET_TRAINLV(TARGET) + ABL:기교 + TALENT:장신 * 3 - TALENT:작은체형 * 3 + BASE:체력 / 600 + BASE:기력 / 300
;이번죠교종별의죠교자기교에 응해 감소
LOCAL -= ABL:(TFLAG:ACT분류 + 20) * 3 / 2

;목표 파츠에 의한보정

;상반신겉옷강제탈의(+0～+1)
IF TFLAG:강제탈의 == 1
	LOCAL += RAND:2
;하반신겉옷강제탈의(+0～)
ELSEIF TFLAG:강제탈의 == 2
	;하반신겉옷를 봐, 벗기기 어려운 것만큼 크게 가산(+0～)
	LOCAL += TEQUIP:MASTER:하반신겉옷
	;전신겉옷는 일률+2
	SIF TEQUIP:MASTER:전신겉옷
		LOCAL += 2
;상＋하겉옷강제탈의(+1～)
ELSEIF TFLAG:강제탈의 == 3
	;하반신겉옷를 봐, 벗기기 어려운 것만큼 크게 가산(+1～)
	LOCAL += 1 + TEQUIP:MASTER:하반신겉옷
	;전신겉옷는 일률+3
	SIF TEQUIP:MASTER:전신겉옷
		LOCAL += 3
;상반신전부강제탈의(-2～+2)
ELSEIF TFLAG:강제탈의 == 4
	;2를 감산
	LOCAL -= 2
	;상반신겉옷나전신겉옷가 존재하면 일률+4
	SIF TEQUIP:MASTER:상반신겉옷 || TEQUIP:MASTER:전신겉옷
		LOCAL += 4
;하반신전부강제탈의(-2～)
ELSEIF TFLAG:강제탈의 == 5
	;2를 감산
	LOCAL -= 2
	;하반신겉옷가 존재하면 벗기기 어려운 것만큼 크게 가산(+4～)
	SIF TEQUIP:MASTER:하반신겉옷
		LOCAL += 4 + TEQUIP:MASTER:하반신겉옷
	;전신겉옷가 존재하면 일률+5
	SIF TEQUIP:MASTER:전신겉옷
		LOCAL += 5
;악세서리─강제탈의(-3)
ELSEIF TFLAG:강제탈의 == 6
	LOCAL -= 3
;악세서리－까지/전라까지강제탈의(+2～)
ELSEIF TFLAG:강제탈의 == 7 || TFLAG:강제탈의 == 8
	;하반신겉옷를 봐, 벗기기 어려운 것만큼 크게 가산(+2～)
	LOCAL += 2 + TEQUIP:MASTER:하반신겉옷
	;상반신겉옷가 존재하면 일률+2
	SIF TEQUIP:MASTER:상반신겉옷
		LOCAL += 2
	;전신겉옷가 존재하면 일률+4
	SIF TEQUIP:MASTER:전신겉옷
		LOCAL += 4
;속옷만강제탈의(-2～)
ELSEIF TFLAG:강제탈의 == 9
	;하반신겉옷를 봐, 벗기기 어려운 것만큼 크게 가산(-2～)
	LOCAL += TEQUIP:MASTER:하반신겉옷 - 2
	;전신겉옷가 존재하면 일률+1
	SIF TEQUIP:MASTER:전신겉옷
		LOCAL += 1
ENDIF

;그 외의보정

;눈가리개장착시는 2를 감산
SIF TEQUIP:눈가리개
	LOCAL -= 2
;조금 질질 끄고라고 있지만 이 처리가 없으면 여러 가지 큰 일
;밧줄구속시, 가학, 이상, 사역계죠교시는 LOCAL를 0에
SIF TEQUIP:밧줄 || TFLAG:ACT분류 == 6 || TFLAG:ACT분류 == 7 || TFLAG:ACT분류 == 8
	LOCAL = 0

;성공 여부 판정
;(실행 실패시는 TFLAG:강제탈의를-1에, 실행 불가능시는 0으로 한다)

;목마에 의한 실행 불가능 판정
IF TEQUIP:삼각목마
	TFLAG:강제탈의 = 0
;조수의손 타는 것으로 반드시 성공에, 반항의 정도에 의해구속의 소스 약간 들어갑니다
ELSEIF (ASSI:1 >= 0 && TCVAR:(ASSI:1):조수ACT == GET_ASSIACTNUM("탈의")) || (ASSI:2 >= 0 && TCVAR:(ASSI:2):조수ACT == GET_ASSIACTNUM("탈의"))
	SIF LOCAL > 0
		SOURCE:MASTER:구속 += (LOCAL + RAND:3) * 20
;밧줄에 의한 실행 불가능 판정(예외로서악세서리─, 속옷만탈의는 가능)
ELSEIF TEQUIP:밧줄 && TFLAG:강제탈의 != 6 && TFLAG:강제탈의 != 9
	TFLAG:강제탈의 = 0
;난수로 성공 여부 판정(4이상으로 절대 실패, 0 이하로 절대 성공)
ELSEIF LOCAL > RAND:4
	;보정치, 죠교대상반항강도, 반항적, 솔직소질, 죠교레벨, 순종에 응해반항SOURCE 증감
	SOURCE:MASTER:반항 += 80 * (2 + LOCAL) * (4 + CFLAG:MASTER:반항강도 + TALENT:MASTER:반항적 * 3 - TALENT:MASTER:솔직 * 3) / (5 + CFLAG:MASTER:죠교레벨 + ABL:MASTER:순종)
	;죠교자초조함에 응해징계·포인트증가
	TFLAG:징계·포인트 += 2 + RAND:5 + BASE:초조함 / 150
	;강제탈의실패
	TFLAG:강제탈의 = -1
ENDIF

;강제탈의성공시
IF TFLAG:강제탈의 > 0
	;야외플레이, 거울사용시, 강제탈의내용, 죠교대상수줍음, 수치없음소질, 죠교레벨에 응해노출SOURCE 증감
	SOURCE:MASTER:노출 += 100 * (3 + TEQUIP:야외플레이 * 3 + TEQUIP:거울 * 2) * (3 + TFLAG:강제탈의 + TALENT:MASTER:수줍음 * 2 - TALENT:MASTER:수치없음 * 2) / (5 + CFLAG:MASTER:죠교레벨)
	;강제탈의내용, 죠교대상트라우마(각인), 죠교레벨, 순종에 응해트라우마SOURCE 증감
	SOURCE:MASTER:트라우마 += 50 * (TFLAG:강제탈의 + 5) * (5 + MARK:MASTER:트라우마) / (10 + CFLAG:MASTER:죠교레벨 + ABL:MASTER:순종 * 2)
	;악세서리─/속옷만강제탈의시는노출, 트라우마SOURCE 경감
	IF TFLAG:강제탈의 == 6
		TIMES SOURCE:MASTER:노출 , 0.10
		TIMES SOURCE:MASTER:트라우마 , 0.25
	ELSEIF TFLAG:강제탈의 == 9
		TIMES SOURCE:MASTER:노출 , 0.75
	ENDIF
	;조수가손 탔을 경우노출SOURCE 증가
	SIF TFLAG:조수ACT == 70
		TIMES SOURCE:MASTER:노출 , 1.20
ENDIF

;-----------EDIT 001 START--------------
;지문 분리의 일환으로서 독립시킨다
;내부 처리를 비트 연산으로 변경
;장래적으로강제탈의, 자주탈의를 비트로 취급할 수 있도록(듯이)
;---------------------------------------
;────────────────────────────────────
;강제탈의, 자주탈의시의탈의처리
;────────────────────────────────────
;※전제로서자주탈의/강제탈의최적화, 강제탈의성공 여부 판정을 하고 있는필요가 있다
@DATUI_PROCESS

;LOCAL 초기화
VARSET LOCAL

;죠교대상, 죠교자탈의부위 확인
FOR LOCAL, 1, 3
	;강제탈의, 자주탈의에 의해 분기
	SELECTCASE TFLAG:(101 + LOCAL)
		;상반신겉옷
		CASE 1
			;상반신겉옷/전신겉옷
			SETBIT LOCAL:LOCAL, 5
			SETBIT LOCAL:LOCAL, 6
		;하반신겉옷
		CASE 2
			;하반신겉옷/전신겉옷
			SETBIT LOCAL:LOCAL, 4
			SETBIT LOCAL:LOCAL, 6
		;상＋하겉옷
		CASE 3
			;하반신겉옷/상반신겉옷/전신겉옷
			SETBIT LOCAL:LOCAL, 4
			SETBIT LOCAL:LOCAL, 5
			SETBIT LOCAL:LOCAL, 6
		;상반신전부
		CASE 4
			;상반신속옷/상반신겉옷/전신겉옷
			SETBIT LOCAL:LOCAL, 3
			SETBIT LOCAL:LOCAL, 5
			SETBIT LOCAL:LOCAL, 6
		;하반신전부
		CASE 5
			;하반신속옷/하반신겉옷/전신겉옷
			SETBIT LOCAL:LOCAL, 2
			SETBIT LOCAL:LOCAL, 4
			SETBIT LOCAL:LOCAL, 6
		;악세서리─
		CASE 6
			;악세서리/신발안
			SETBIT LOCAL:LOCAL, 0
			SETBIT LOCAL:LOCAL, 1
		;악세서리－까지
		CASE 7
			;하반신속옷/상반신속옷/하반신겉옷/상반신겉옷/전신겉옷
			SETBIT LOCAL:LOCAL, 2
			SETBIT LOCAL:LOCAL, 3
			SETBIT LOCAL:LOCAL, 4
			SETBIT LOCAL:LOCAL, 5
			SETBIT LOCAL:LOCAL, 6
		;전라까지
		CASE 8
			;악세서리/신발안/하반신속옷/상반신속옷/하반신겉옷/상반신겉옷/전신겉옷
			SETBIT LOCAL:LOCAL, 0
			SETBIT LOCAL:LOCAL, 1
			SETBIT LOCAL:LOCAL, 2
			SETBIT LOCAL:LOCAL, 3
			SETBIT LOCAL:LOCAL, 4
			SETBIT LOCAL:LOCAL, 5
			SETBIT LOCAL:LOCAL, 6
		;속옷만
		CASE 9
			;하반신속옷/상반신속옷
			SETBIT LOCAL:LOCAL, 2
			SETBIT LOCAL:LOCAL, 3
	ENDSELECT
NEXT

;탈의대상최적화 처리(탈의지정 개소에 착의가 존재하지 않으면 지정을 해제)
FOR LOCAL, 0, 7
	SIF GETBIT(LOCAL:1, LOCAL) && !TEQUIP:MASTER:LOCAL
		CLEARBIT LOCAL:1, LOCAL
	SIF GETBIT(LOCAL:2, LOCAL) && !TEQUIP:TARGET:LOCAL
		CLEARBIT LOCAL:2, LOCAL
NEXT

;탈의직전에서의 지문/구상표시
CALL DATUI_MESSAGE(LOCAL:1, LOCAL:2)

;탈의처리(실제의탈의는 여기서 행해진다)
FOR LOCAL, 0, 7
	SIF GETBIT(LOCAL:1, LOCAL)
		TEQUIP:MASTER:LOCAL = 0
	SIF GETBIT(LOCAL:2, LOCAL)
		TEQUIP:TARGET:LOCAL = 0
NEXT

;착의노출해소
IF TFLAG:강제탈의 > 0
	;죠교대상하반신속옷가 없으면성기착의노출해소
	SIF TEQUIP:MASTER:하반신속옷 == 0
		TEQUIP:착의노출 &= ~4
	;죠교대상상반신속옷, 상반신겉옷, 전신겉옷가 없으면가슴착의노출해소
	SIF TEQUIP:MASTER:상반신속옷 == 0 && TEQUIP:MASTER:상반신겉옷 == 0 && TEQUIP:MASTER:전신겉옷 == 0
		TEQUIP:착의노출 &= ~8
ENDIF
IF TFLAG:자주탈의 > 0
	;죠교자하반신속옷가 없으면성기착의노출해소
	SIF TEQUIP:하반신속옷 == 0
		TEQUIP:착의노출 &= ~1
	;죠교자상반신속옷, 상반신겉옷, 전신겉옷가 없으면가슴착의노출해소
	SIF TEQUIP:상반신속옷 == 0 && TEQUIP:상반신겉옷 == 0 &&  TEQUIP:전신겉옷 == 0
		TEQUIP:착의노출 &= ~2
ENDIF

;탈의직후에서의 지문/구상표시
CALL DATUI_MESSAGE2(LOCAL:1, LOCAL:2)

RETURN RESULT
;-----------EDIT 001 END  --------------

[SKIPSTART]
;────────────────────────────────────
;보정치 대입 처리(원래의 값을 사용하면 증가가 직선적이고 사용하기 어려운 경우에 이용)
;※모두 GET_ABL, GET_TRAINLV, ACT_FROM, ACT_TO에 옮겨놓았을 것!
;  인 것으로 이하는 모두 구기록술이 됩니다@이것 보고
;────────────────────────────────────
;@ABL_REVISION
#DIM LCOUNT

kapnaluqa씨에 의한 해석을 이것 보고가 수정. 수정 개소는 『』안

·ABL에 의한보정계수를 결정하는 함수
『A:1-29  TARGET의 ABL:1-29의 계수
  B:1-29  MASTER의 ABL:1-29의 계수
  C:1-29  ASSI의 ABL:1-29의 계수』

ABL:1-29는 100 - 500/(ABL + 5)로보정치가 정의된다
즉
Lv0   0		Lv6  55
Lv1  17		Lv7  59
Lv2  29		Lv8  62
Lv3  38		Lv9  65
Lv4  45		Lv10 67
Lv5  50

아무튼 즉, 레벨이 오를 때 마다보정치의 성장이 나빠지는 타입의 식입니다

A:30, C:30은 ABL는 아니고, 죠교레벨에 의한보정입니다
이쪽은 50 - 2000(죠교레벨+40)으로 정의됩니다

『덧붙여순종의보정치는 B(MASTER) 마셔 존재해, B:30에 격납됩니다
  본래 B:0에 격납되어야 할 것이 그대로 B:30에 들어가 있다고 생각해 주세요』

실제로 이 계수를 사용하고 있는 개소는
20, 회화  21, 애무  22, 도구  23, 성교  24, 수치
25, 봉사  26, 가학  27, 이상  28, 사역

이것들의 ABL의보정치의 것 1/5이 메뉴 결정 시에 기초치에 가산됩니다（성봉는성교의보정를 이용한다）
Lv0 때 0 제산할 것 같은 것으로 하고 있지 않은 근처, 나에게도 잘 모르는 수수께끼가 있을 것 같다

뒤는 상당히 여러가지 개소에서 드문드문 사용하고 있습니다만 일례를 내 두면
;────────────────────────────────────
;010, 애무/수음 한다（Ｓ/Ｎ/Ｈ）（쾌Ｃ, 쾌Ｂ, 접촉, 애정, 성행위, 불결）
;────────────────────────────────────
ELSEIF IS_NOWACTNAME("손으로 애무")
	SOURCE:MASTER:쾌Ｃ = 75 + S:1 * (2 + TALENT:손가락기술) / 4
	;탈의의 처리
	;벽통과가 있으면 3/4로 무시한다
	IF ! TALENT:벽통과 || ! RAND:4
		IF TALENT:억압 == 0 && TALENT:수줍음 == 0 && TALENT:착의플레이선호 == 0
			IF TALENT:해방 * 4 + A:1 / 10 + A:30 / 5 > 3 + RAND:3
				TFLAG:강제탈의 = 8
				SIF RAND:3 > 0
					TFLAG:강제탈의 = 7
			ELSEIF RAND:3 > 1
				TFLAG:강제탈의 = 3
			ENDIF
		ENDIF
	ENDIF
TFLAG:강제탈의 는  3으로속옷를 모두 벗긴다. 7으로악세서리─까지 전부 벗긴다. 8으로전라까지 벗긴다
이 때는 A:1, 즉 TARGET의욕망의보정치와 A:30, 즉죠교레벨에 의한보정치로 벗김의 대성공율을보정 하고 있습니다
그 밖에 소스의 추가 따위에도 꽤 사용해지고 있는 것 같다
호출이 많은 것은 1문자가 글로벌인 관계로 내용의 보전이 불안함 텅텅 있고
항상 재계산하는 방식인 것으로 호출이 빈번해도 내용은 괜찮아

S배열은쾌락를 주는 커맨드때의보정치입니다
S:1－4   CVAB애무  S5－6 VA성교  S7-8 VA성교봉사  S:11-14  CVAB도구의 TARGET→MASTER의보정치
S:21-24  ASSI→MASTER의보정치가 됩니다
S:41-48  MASTER→TARGET의보정치가 됩니다
S:51-58  MASTER→ASSI의보정치가 됩니다
※MASTER→는 S:11-14의도구가 없기 때문에 배열이 적습니다

보정치는 우선 기초치를 이하의 식에서 산출합니다
LOCAL:1 = MAX(애무 하는 측의죠교레벨  * 10 / (애무를 받는 측의죠교레벨 + 1), MIN(애무실행자에게로의호의/ 200, 30), 10)
받는 측의죠교레벨가 높으면죠교레벨부분은 성장하지 않게 되기 때문에, 실질적으로는호의캡의보정치 30이 캡 같은 생각이 듭니다

다음에기교-감각에 의한보정치를 이하의 식에서 산출합니다
LOCAL = MAX(2 * 애무실행자의기교 - 피애무자의감각, 굴복각인)

마지막에(LOCAL + 10) * (LOCAL + 10) * LOCAL:1  / 10으로보정치를 확정합니다
덧붙여서(LOCAL + 10) * (LOCAL + 10) 부분은 이하대로라고 합니다
;기본치		기교－감각
;100		0
;121		1
;144		2
;169		3
;196		4
;225		5
;256		6
;289		7
;324		8
;361		9
;400		10

사용의 실례를 나타내면
SOURCE:MASTER:쾌Ｃ = 75 + S:1 * (2 + TALENT:손가락기술) / 4（애무의 C감각소스）
※TALENT:57은손가락기술
보정치가 30*400/10라면 1200으로,75+600그리고 675가 됩니다

그리고 아무튼, 이후에 처리 뒤쫓아 가 본 것입니다만
SOURCE:MASTER:0을 UP:0으로 변환하기까지, C감각 보고 있는 모습을 찾아낼 수 없었기 때문에
C감각는, 오르면 C로 예민해지는 것은 아니고, 느끼기 어려워지는 것 같다
이것은 원래의 Rev로부터 그래했다인가

덧붙여 1문자야 리카등 탈각하기 위해서 식 안의 함수를 만들고 있어, 여유가 있으면 옮겨놓아 가고 싶다든가 그런 응 같아?

;────────────────────────────────────
;ABL, CFLAG:**:죠교레벨 보정
;향후는 GET_ABL, GET_TRAINLV 를 사용하는 것
;────────────────────────────────────

;원래의 조건에서는 TARGET, ASSI > 0이었지만,
;죠교자 및조수가 『당신』의 때라도 처리되어야 한다고 생각했으므로 수정

;원래의 REPEAT에서는 1-30의 범위에서 돌리고 있었지만,
;30그리고 대입했다치가 직후에 덧쓰기되어 쓸데없게 되어 있었으므로 1-29로 변경(쓸데없는 구애됨)

;죠교자가 존재한다
IF TARGET > -1
	;A:1-29에 ABL:TARGET:1-29의보정치를 대입
	FOR LCOUNT, 1, 30
		A:LCOUNT = (100 - 500/(ABL:LCOUNT + 5))
	NEXT
	;A:30에 CFLAG:TARGET:죠교레벨의보정치를 대입
	A:30 = 50 - 2000 / (CFLAG:죠교레벨 + 40)
ENDIF
;B:1-29에 ABL:MASTER:1-29의보정치를 대입
FOR LCOUNT, 1, 30
	B:LCOUNT = (100 - 500/(ABL:MASTER:LCOUNT + 5))
NEXT
;B:30에 ABL:MASTER:0의보정치를 대입
B:30 = (100 - 500/(ABL:MASTER:순종 + 5))
;조수가 존재한다
;(조수 2인째 이후는 고려되지 않지만,
; 원래 GET_ABL, GET_TRAINLV 를 사용해야 하는 것으로 무시)
IF ASSI > -1
	;C:1-29에 ABL:ASSI:1-29의보정치를 대입
	FOR LCOUNT, 1, 30
	C:LCOUNT = (100 - 500/(ABL:ASSI:LCOUNT + 5))
	NEXT
	;C:30에 CFLAG:ASSI:죠교레벨의보정치를 대입
	C:30 = 50 - 2000 / (CFLAG:ASSI:죠교레벨 + 40)
ENDIF

;────────────────────────────────────
;애무효과보정
;향후는 ACT_FROM, ACT_TO 를 사용하는 것
;────────────────────────────────────

;큰폭으로 단축하는 것은 가능하지만,
;그 결과가 ACT_FROM, ACT_TO 와 다름없기 때문에 그 쪽을 참조

;TARGET애무(TARGET 존재 시계산)
IF TARGET > 0
	;죠교자죠교레벨가 과잉인 경우 증가하는 값,
	;죠교대상로부터죠교자에의호의가 큰 경우 증가하는 값(최대 30)
	;10…… 중에서 최대의 값을 선택한다(10-1000)
	LOCAL:1 = MAX(CFLAG:죠교레벨  * 10 / (CFLAG:MASTER:죠교레벨 + 1), MIN(CFLAG:M호의 / 200, 30), 10)
	LOCAL:1 = LIMIT(LOCAL:1, 10, 1000)
	
	;애무계
	;죠교자기교 + 애무 로부터 죠교대상대응감각를 당긴 것과굴복각인를 비교해,
	;큰 것에 10을발 해 제곱 해, LOCAL:1을 걸쳐 10으로 나눈다
	;C애무
	LOCAL = MAX(ABL:기교 + ABL:애무 - ABL:MASTER:Ｃ감각, MARK:굴복각인)
	S:1 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;V애무
	LOCAL = MAX(ABL:기교 + ABL:애무 - ABL:MASTER:Ｖ감각, MARK:굴복각인)
	S:2 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A애무
	LOCAL = MAX(ABL:기교 + ABL:애무 - ABL:MASTER:Ａ감각, MARK:굴복각인)
	S:3 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;B애무
	LOCAL = MAX(ABL:기교 + ABL:애무 - ABL:MASTER:Ｂ감각, MARK:굴복각인)
	S:4 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	
	;성교계
	;죠교자기교 + 성교 로부터 죠교대상대응감각를 당긴 것과굴복각인를 비교해,
	;큰 것에 10을발 해 제곱 해, LOCAL:1을 걸쳐 10으로 나눈다
	;V성교
	LOCAL = MAX(ABL:기교 + ABL:성교- ABL:MASTER:Ｖ감각, MARK:굴복각인)
	S:5 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A성교
	LOCAL = MAX(ABL:기교 + ABL:성교 - ABL:MASTER:Ａ감각, MARK:굴복각인)
	S:6 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	
	;성교봉사계
	;죠교자기교 + 성교 + 죠교자대응감각 로부터 죠교대상대응감각를 당긴 것과굴복각인를 비교해,
	;큰 것에 10을발 해 제곱 해, LOCAL:1을 걸쳐 10으로 나눈다
	;V성교봉사
	LOCAL = MAX(ABL:기교 + ABL:성교 + ABL:Ｖ감각 - ABL:MASTER:Ｃ감각, MARK:굴복각인)
	S:7 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A성교봉사
	LOCAL = MAX(ABL:기교 + ABL:성교 + ABL:Ａ감각 - ABL:MASTER:Ｃ감각, MARK:굴복각인)
	S:8 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	
	;도구계
	;죠교자기교 + 도구 로부터 죠교대상대응감각를 당긴 것과굴복각인를 비교해,
	;큰 것에 10을발 해 제곱 해, LOCAL:1을 걸쳐 10으로 나눈다
	;C도구
	LOCAL = MAX(ABL:기교 + ABL:도구 - ABL:MASTER:Ｃ감각, MARK:굴복각인)
	S:11 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;V도구
	LOCAL = MAX(ABL:기교 + ABL:도구 - ABL:MASTER:Ｖ감각, MARK:굴복각인)
	S:12 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A도구
	LOCAL = MAX(ABL:기교 + ABL:도구 - ABL:MASTER:Ａ감각, MARK:굴복각인)
	S:13 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;B도구
	LOCAL = MAX(ABL:기교 + ABL:도구 - ABL:MASTER:Ｂ감각, MARK:굴복각인)
	S:14 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
ENDIF

;이후, 기본적으로는 TARGET애무와 같은 것으로 상세약어

;ASSI애무(ASSI 존재 시계산)
IF ASSI > 0
	LOCAL:1 = MAX(CFLAG:ASSI:죠교레벨  * 10 / (CFLAG:MASTER:죠교레벨 + 1), MIN(CFLAG:ASSI:M호의 / 200, 30), 10)
	LOCAL:1 = LIMIT(LOCAL:1, 10, 1000)
	
	;C애무
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:애무 - ABL:MASTER:Ｃ감각, MARK:ASSI:굴복각인)
	S:21 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;V애무
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:애무 - ABL:MASTER:Ｖ감각, MARK:ASSI:굴복각인)
	S:22 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A애무
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:애무 - ABL:MASTER:Ａ감각, MARK:ASSI:굴복각인)
	S:23 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;B애무
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:애무 - ABL:MASTER:Ｂ감각, MARK:ASSI:굴복각인)
	S:24 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10

	;V성교
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:성교 - ABL:MASTER:Ｖ감각, MARK:ASSI:굴복각인)
	S:25 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A성교
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:성교 - ABL:MASTER:Ａ감각, MARK:ASSI:굴복각인)
	S:26 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	
	;V성교봉사
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:성교 + ABL:ASSI:Ｖ감각 - ABL:MASTER:Ｃ감각, MARK:ASSI:굴복각인)
	S:27 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A성교봉사
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:성교 + ABL:ASSI:Ａ감각 - ABL:MASTER:Ｃ감각, MARK:ASSI:굴복각인)
	S:27 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10

	;C도구
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:도구 - ABL:MASTER:Ｃ감각, MARK:굴복각인)
	S:31 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;V도구
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:도구 - ABL:MASTER:Ｖ감각, MARK:굴복각인)
	S:32 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;A도구
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:도구 - ABL:MASTER:Ａ감각, MARK:굴복각인)
	S:33 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
	;B도구
	LOCAL = MAX(ABL:ASSI:기교 + ABL:ASSI:도구 - ABL:MASTER:Ｂ감각, MARK:굴복각인)
	S:34 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 / 10
ENDIF

;이후 V애무 등 일부보정계산을 하지 않지만 무시
;(향후 이용하는 경우는 여기의 계산을필요로 하지 않기 때문에)

;애무계 따위 계열 ABL가 존재하지 않기 때문에기교의 값을 2배로 해 대응하고 있다

;MASTER→TARGET애무
IF TARGET > 0
	LOCAL:1 = MAX(CFLAG:MASTER:죠교레벨  * 10 / (CFLAG:죠교레벨 + 1), MIN(CFLAG:호의 / 200, 30), 10)
	LOCAL:1 = LIMIT(LOCAL:1, 10, 1000)
	
	;C애무
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:Ｃ감각, MARK:굴복각인)
	S:41 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;A애무
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:Ａ감각, MARK:굴복각인)
	S:43 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;B애무
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:Ｂ감각, MARK:굴복각인)
	S:44 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	
	;V성교
	LOCAL = MAX(ABL:MASTER:기교 + ABL:MASTER:Ｖ감각 - ABL:Ｃ감각, MARK:굴복각인)
	S:45 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;A성교
	LOCAL = MAX(ABL:MASTER:기교 + ABL:MASTER:Ａ감각 - ABL:Ｃ감각, MARK:굴복각인)
	S:46 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	
	;V성교봉사
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:Ｖ감각, MARK:굴복각인)
	S:47 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;A성교봉사
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:Ａ감각, MARK:굴복각인)
	S:48 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
ENDIF

;MASTER→ASSI애무
IF ASSI > 0
	LOCAL:1 = MAX(CFLAG:MASTER:죠교레벨  * 10 / (CFLAG:ASSI:죠교레벨 + 1), MIN(CFLAG:호의 / 200, 30), 10)
	LOCAL:1 = LIMIT(LOCAL:1, 10, 1000)
	;C애무
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:ASSI:Ｃ감각, MARK:ASSI:굴복각인)
	S:51 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;A애무
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:ASSI:Ａ감각, MARK:ASSI:굴복각인)
	S:53 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;B애무
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:ASSI:Ｂ감각, MARK:ASSI:굴복각인)
	S:54 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	
	;V성교
	LOCAL = MAX(ABL:MASTER:기교 + ABL:MASTER:Ｖ감각 - ABL:ASSI:Ｃ감각, MARK:ASSI:굴복각인)
	S:55 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;A성교
	LOCAL = MAX(ABL:MASTER:기교 + ABL:MASTER:Ａ감각 - ABL:ASSI:Ｃ감각, MARK:ASSI:굴복각인)
	S:56 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	
	;V성교봉사
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:ASSI:Ｖ감각, MARK:ASSI:굴복각인)
	S:57 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
	;A성교봉사
	LOCAL = MAX(2 * ABL:MASTER:기교 - ABL:ASSI:Ａ감각, MARK:ASSI:굴복각인)
	S:58 = (LOCAL + 10) * (LOCAL + 10) * LOCAL:1 * (FLAG:3185 + 100) / 1000
ENDIF

;기본치		기교－감각
;100		0
;121		1
;144		2
;169		3
;196		4
;225		5
;256		6
;289		7
;324		8
;361		9
;400		10
[SKIPEND]

